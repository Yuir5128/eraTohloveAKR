;キャラ名称表示を本名表示に切り替えるテスト
@CHARANAME_PRINT_NATION
;本名表示設定
IF FLAG:43
	FOR COUNT, 0, CHARANUM
		;あなたは除外（名前変更されてるかもしれないので）
		SIF NO:COUNT == 0
			CONTINUE
		;子供、ＳＰキャラは除外
		SIF (NO:COUNT >= 890 && NO:COUNT <= 899) || CFLAG:COUNT:0
			CONTINUE
		;存在しないキャラは除外
		SIF !EXISTCSV(NO:COUNT, 0)
			CONTINUE
		LOCAL = NO:COUNT
		;本名の長い方（アスルード･ローランド）
		NAME:COUNT = %NAME_NATION(LOCAL)%
		;本名の短い方（アスルード）
		CALLNAME:COUNT = %CALLNAME_NATION(LOCAL)%
	NEXT
;現在の名前表示設定
ELSE
	;CSVに設定してあるキャラ名の呼び出し
	FOR COUNT, 0, CHARANUM
		;あなたは除外（名前変更されてるかもしれないので）
		SIF NO:COUNT == 0
			CONTINUE
		;子供、ＳＰキャラは除外
		SIF (NO:COUNT >= 890 && NO:COUNT <= 899) || CFLAG:COUNT:0
			CONTINUE
		;存在しないキャラは除外
		SIF !EXISTCSV(NO:COUNT, 0)
			CONTINUE
		LOCAL = NO:COUNT
		CSVNAME LOCAL, 0
		NAME:COUNT = %RESULTS%
		CSVCALLNAME LOCAL, 0
		CALLNAME:COUNT = %RESULTS%
	NEXT
ENDIF

@NATION_NAME(ARG)
IF FLAG:43
	NAME:ARG = %NAME_NATION(NO:ARG)%
	CALLNAME:ARG = %CALLNAME_NATION(NO:ARG)%
ENDIF

;-------------------------------------------------
;関数名:NAME_NATION
;概　要:本名（フルネーム）呼び出し関数
;引　数:キャラクター番号(NOの方)
;戻り値:本名（フルネーム）
;備　考:
;使用例：
;-------------------------------------------------
@NAME_NATION(ARG)
#FUNCTIONS

SELECTCASE ARG
	;サンプル
	;CASE 3
	;	RETURNF "三日月宗近"
	CASEELSE
		CSVNAME ARG, 0
		RETURNF RESULTS
ENDSELECT

;-------------------------------------------------
;関数名:CALLNAME_NATION
;概　要:本名（あだ名）呼び出し関数
;引　数:キャラクター番号(NOの方)
;戻り値:本名（あだ名）
;備　考:
;使用例：
;-------------------------------------------------
@CALLNAME_NATION(ARG)
#FUNCTIONS

SELECTCASE ARG
	;サンプル
	;CASE 3
	;	RETURNF "じじい"
	CASEELSE
		CSVCALLNAME ARG, 0
		RETURNF RESULTS
ENDSELECT

;-------------------------------------------------
;関数名:NATION_OR_H(ARG)
;概　要:人名⇔国名表示切り替え対応第三者名呼び出し関数
;引　数:キャラクター番号(NOの方)
;戻り値:名前
;備　考:
;使用例：PRINTFORML 「%NATION_OR_H([[ライナ]])%！」
;	現名表示の時 = 「ライナ！」
;	本名表示の時 = 「フェルナ！」
;使用例２：PRINTFORML 「%NATION_OR_H(2,1)%」
;	現名時の時「ロヴィーノ・ヴァルガス」
;-------------------------------------------------
@NATION_OR_H(ARG,ARG:1)
#FUNCTIONS
;子供の場合、仲間にいるなら名前を呼ぶ
LOCAL = NO2(ARG)
SIF LOCAL > -1 && ((ARG >= 890 && ARG <= 899) || CFLAG:LOCAL:0)
	RETURNF CALLNAME:LOCAL
IF FLAG:43
	RETURNF CALLNAME_NATION(ARG)
ELSE
	IF ARG:1
		CSVNAME ARG, 0
	ELSE
		CSVCALLNAME ARG, 0
	ENDIF
	RETURNF RESULTS
ENDIF

