;=============================================================================
;売却後エピソード関連の関数
;=============================================================================
;エンディング時に売却したキャラのエピソードを流す
;エンディング後に呼ばれる。戻った後にゲーム終了ＯＲ強くてニューゲーム
;-------------------------------------------------
;エンディング後エピソードを表示させるか選択
;-------------------------------------------------
@SHOW_AFTERWARDS
;FLAG:500 = エピソードを表示する奴隷数
;一人もエピソードがないなら何もせず終了
SIF FLAG:500 == 0
	RETURN 0

;選択肢
PRINTFORML 売却された%CALLNAME%のその後を……
PRINTC [0]見る
PRINTC [1]見ない
$INPUT_LOOP
INPUT
IF RESULT == 1
	GOTO AFTERWARDS_RESET
ELSEIF RESULT != 0
	GOTO INPUT_LOOP
ENDIF

;記録できる人数を超えた場合、古いものから順に表示するようにしたかったらしい
A = FLAG:500
IF A > 10
	A = 10
	W:1 = FLAG:500 % 10
ELSE
	W:1 = 0
ENDIF

REPEAT A
	W = 501 + (COUNT + W:1) % 10
	X = FLAG:W % 1000
	Z = FLAG:W / 1000
	CALL SHOW_SLAVE_AFTERWARDS
REND

$AFTERWARDS_RESET
;エピソードフラグ消去
W = 501
REPEAT 10
	FLAG:W = 0
	W += 1
REND
;エピソードを表示する奴隷数リセット
FLAG:500 = 0
RETURN 0

;-------------------------------------------------
;売却時の処理
;-------------------------------------------------
@SET_AFTERWARDS
;売却キャラエピソードをセット
X = NO:TARGET
Z = 0
CALL SET_SLAVE_AFTERWARDS
;表示しない場合は記録しない
SIF Z == 0
	RETURN 0
;%10+581の意味：エピソード関連で使用する変数がFLAG:501から10人分だから
Y:0 = FLAG:500 % 10 + 501
Y:1 = Z * 1000 + NO:TARGET
FLAG:Y = Y:1
;エピソードを表示する奴隷数をカウント
FLAG:500 += 1

RETURN 0

