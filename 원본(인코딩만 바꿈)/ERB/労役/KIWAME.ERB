;====================================================================
;KIWAME.ERB　極修行
;修行に出したキャラは4日拘束される
;====================================================================
@KIWAME
;修行へ行く男士の登録番号
#DIM DYNAMIC TOKU = -1

DRAWLINE
PRINTL 【 極修行 】
PRINTL 
PRINTL 奴隷を極になるための修行へ行かせる
PRINTL ４日後に帰還
PRINTL
PRINTL 今の能力・経験などを引き継いだままへ極へと進化します
PRINTL しかし以下は極のもので上書きされます
PRINTL 名前・あだ名・刀種・素質
PRINTL ※後天的に取得した素質、個体差で得た素質は引き継がれます
PRINTL 
DRAWLINE
PRINTL 

;--------------------------------------------------------
;修行へ行かせる奴隷を選択
;--------------------------------------------------------
PRINTL
PRINTL 修行へ行かせる男士を選択してください

REPEAT CHARANUM
	;修行できるフラグが立っていない男士は表示しない
	SIF (CFLAG:COUNT:151 & 512) == 0
		CONTINUE
	
	PRINTFORML [{COUNT}]%CALLNAME:COUNT%
REND
PRINTL [999] - 戻る
$INPUT_LOOP
INPUT
IF RESULT == 999
	RETURN 0
ELSEIF (CFLAG:RESULT:151 & 512) == 0
	GOTO INPUT_LOOP
ELSE
	;選択された男士の登録番号を代入
	TOKU = RESULT
	PRINTFORML %CALLNAME:RESULT%を修行へ行かせますか？
	PRINTL [0] はい
	PRINTL [999] 戻る
	$INPUT_LOOP_2
	INPUT
	IF RESULT == 0
		
	ELSEIF RESULT == 999
		TOKU = -1
		RETURN 0
	ELSE
		GOTO INPUT_LOOP_2
	ENDIF
ENDIF

;--------------------------------------------------------
;修行へ行かせる
;--------------------------------------------------------
PRINTFORML %CALLNAME:TOKU%が、修行へ旅立ちました

;労役フラグを修行に設定
CFLAG:TOKU:12 = 6

;帰還日時を保持しておく
CFLAG:TOKU:13 = DAY + 4

;地の文、口上呼び出しのために一時的に現在の調教対象を退避
LOCAL = TARGET
TARGET = TOKU
TFLAG:13 = 86
;イベント口上（修行出発時）
CALL KOJO_MESSAGE_EVENT
TARGET = LOCAL

;修行へ行った男士が調教対象や助手の場合は、調教対象や助手から外す
SIF TOKU == TARGET
	TARGET = -1
SIF TOKU == ASSI
	ASSI = -1

;内番開始済みフラグ（修行=8）を立てる
FLAG:161 |= 8

PRINTL 

;====================================================================
;期間時の処理
;====================================================================
@END_KIWAME
;修行へ行った男士の登録番号（TOKUもKIWAMEも同じものが入る）
#DIM DYNAMIC TOKU = -1
#DIM DYNAMIC KIWAME = -1

REPEAT CHARANUM
	IF CFLAG:COUNT:12 == 6
		TOKU = COUNT
		KIWAME = COUNT
	ENDIF
REND

DRAWLINE
PRINTFORMW %CALLNAME:TOKU%の修行が終了しました


;地の文、口上呼び出しのために一時的に現在の調教対象を退避
LOCAL = TARGET
TARGET = TOKU
TFLAG:13 = 86
;イベント口上の表示（修行帰還時）
;ここで呼ばれる口上は特男士のものが呼ばれる
;支障があるようなら極男士のもの呼ぶように処理変えます
TFLAG:13 = 87
CALL KOJO_MESSAGE_EVENT
PRINTL 
TARGET = LOCAL

;極男士のキャラ追加部分
;新たに個体差は付かない
CALL KIWAME_SYUGYOU(KIWAME)

;内番開始済みフラグ（修行=8）を落とす
SIF FLAG:161 & 8
	FLAG:161 -= 8

;フラグのリセット
CFLAG:TOKU:12 = 0

DRAWLINE

;-------------------------------------------------
;関数名:KIWAME_SYUGYOU
;概　要:特男士の能力・経験・珠・刻印・CFLAG・一部の素質などを
;極男士に引き継がせる
;引　数:ARG == 購入した極男士の登録番号
;戻り値:特男士の登録番号
;備　考:特男士の固有番号を変更してデータを引継がせる
;名前　あだ名　刀種フラグ　素質のみ極男士のCSVのもので上書きする
;※特で後天的に取得した素質・個体差の素質は引継ぐ

;特のNOを極のもので上書きして処理してます
;-------------------------------------------------
@KIWAME_SYUGYOU(ARG)
#DIM DYNAMIC TOKU
#DIM DYNAMIC KIWAME
#DIM DYNAMIC TOKU_NO
#DIM DYNAMIC KIWAME_NO


;各種変数の代入
;特男士のキャラ固有番号を格納
TOKU_NO = NO:ARG

;極男士のキャラ固有番号を格納
;髭切極
IF (NO:ARG) == 107
	KIWAME_NO = 111
;膝丸極
ELSEIF (NO:ARG) == 112
	KIWAME_NO = 115
;それ以外は特の固有番号+1が極の固有番号
ELSE
	KIWAME_NO = NO:ARG + 1
ENDIF

;極男士の登録番号を格納
;処理を分かりやすくするためTOKUとKIWAMEに分けていますが、同じ値が入っています
TOKU = NO2(TOKU_NO)
KIWAME = NO2(TOKU_NO)

;特男士の登録番号に入ってるキャラの固有番号を、極の固有番号に変更する
;(NO:KIWAME = KIWAME_NOと処理的には同じ)
NO:TOKU = KIWAME_NO

;名前　あだ名に極のものを代入する
NAME:KIWAME = %CSVNAME(KIWAME_NO)%
CALLNAME:KIWAME = %CSVCALLNAME(KIWAME_NO)%

;刀種フラグを元の値に+10して、極のものに変更する
CFLAG:KIWAME:刀種 += 10

;素質
;ループに入る前の時点だと、極男士は特男士の素質をそのまま引き継いでいる
;極男士のCSVに設定されている素質は（特男士と重複してないかぎり）持っていない
;COUNT = 素質の番号
REPEAT 205
	;特男士のCSVに設定されていて、極男士のCSVに設定されていない素質は削除する
	SIF CSVTALENT(TOKU_NO, COUNT,0) == 1 && CSVTALENT(KIWAME_NO, COUNT,0) == 0
		TALENT:KIWAME:COUNT = 0
	
	;特男士のCSVに設定されていなくて、極男士のCSVに設定されている素質は取得する
	SIF CSVTALENT(TOKU_NO, COUNT,0) == 0 && CSVTALENT(KIWAME_NO, COUNT,0) == 1
		TALENT:KIWAME:COUNT = 1
	
	;特男士・極男士のCSVに重複して設定されている素質は何も処理しない
	;特男士・極男士のCSVのどちらにも設定されていない素質は何も処理しない
		;個体差で得た素質は残せているはず
REND

;該当の極男士以外のキャラに対する処理
;COUNT = キャラ登録番号
REPEAT CHARANUM
	;該当の極男士は弾く
	SIF COUNT == KIWAME
		CONTINUE
	
	;思慕畏怖対象記録フラグ　CFLAG:41
	SIF CFLAG:COUNT:41 == TOKU_NO
		CFLAG:COUNT:41 = KIWAME_NO
	;産む子の父親が誰か　CFLAG:111
	SIF CFLAG:COUNT:111 == TOKU_NO
		CFLAG:COUNT:111 = KIWAME_NO
	;子育て中の実子の固有番号　CFLAG:114　ここに男士の固有番号が入ることはない？
	SIF CFLAG:COUNT:114 == TOKU_NO
		CFLAG:COUNT:114 = KIWAME_NO
	;育ててもらっている母OR養母の固有番号　CFLAG:115
	SIF CFLAG:COUNT:115 == TOKU_NO
		CFLAG:COUNT:115 = KIWAME_NO
	;調教者を限定した汎用喘ぎ設定、の調教者記録用　CFLAG:634
	IF CFLAG:COUNT:634 /10 ==  TOKU_NO
		CFLAG:COUNT:634 = CFLAG:COUNT:634 - (TOKU_NO * 10)
		CFLAG:COUNT:634 = CFLAG:COUNT:634 + (KIWAME_NO * 10)
	ENDIF
	;父親のキャラ固有番号　CFLAG:1000
	SIF CFLAG:COUNT:1000 == TOKU_NO
		CFLAG:COUNT:1000 = KIWAME_NO
	;母親のキャラ固有番号　CFLAG:1001
	SIF CFLAG:COUNT:1001 == TOKU_NO
		CFLAG:COUNT:1001 = KIWAME_NO
	;父の父のキャラ固有番号　CFLAG:1002
	SIF CFLAG:COUNT:1002 == TOKU_NO
		CFLAG:COUNT:1002 = KIWAME_NO
	;父の母のキャラ固有番号　CFLAG:1003
	SIF CFLAG:COUNT:1003 == TOKU_NO
		CFLAG:COUNT:1003 = KIWAME_NO
	;母の父のキャラ固有番号　CFLAG:1004
	SIF CFLAG:COUNT:1004 == TOKU_NO
		CFLAG:COUNT:1004 = KIWAME_NO
	;母の母のキャラ固有番号　CFLAG:1005
	SIF CFLAG:COUNT:1005 == TOKU_NO
		CFLAG:COUNT:1005 = KIWAME_NO
	;助手経験保管(キャラ固有番号+1099)　CFLAG:1099～2098
	CFLAG:COUNT:(KIWAME_NO + 1099) = CFLAG:COUNT:(TOKU_NO + 1099)
	CFLAG:COUNT:(TOKU_NO + 1099) = 0
	;助手面識フラグ(キャラ固有番号+2099)　CFLAG:2099～3098 
	CFLAG:COUNT:(KIWAME_NO + 2099) = CFLAG:COUNT:(TOKU_NO + 2099)
	CFLAG:COUNT:(TOKU_NO + 2099) = 0
	
	;相手から特キャラへの相性が100以上　かつ　CSVでは100より小さい場合
	;後天的に相性が上がったとき、これで引き継げるはず
	IF RELATION:COUNT:(TOKU_NO) > 100 && CSVRELATION(NO:COUNT, NO:TOKU, 0) < 100
		RELATION:COUNT:(KIWAME_NO) = RELATION:COUNT:(TOKU_NO)
	ENDIF
REND


;特の購入フラグを復活させる
ITEMSALES:(199 + TOKU_NO) = 1

;極の購入フラグを2に
ITEMSALES:(199 + KIWAME_NO) = 2


;即落ち関連フラグの処理
FLAG:(1099 + KIWAME_NO) = FLAG:(1099 + TOKU_NO)
FLAG:(1099 + TOKU_NO) = 0

;特男士の登録番号（=極男士の登録番号）を戻り値とする（一応）
RETURN TOKU




