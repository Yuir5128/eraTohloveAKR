;SHOP.ERB는 완전하게 각 함수를 호출하는 엔트렌스화
;원의 처리는 SHOP2.ERB 및 ITEM.ERB에 이동
;지금후는 빈번하게 만질 필요가 없어질 것
@EVENTSHOP
#PRI
DRAWLINE

@SHOW_SHOP
CALL VERSION_UP

;아이템 구입 처리용
SIF TFLAG:0 > 0
	TFLAG:0 = 0
IF TFLAG:0 == 0
	DRAWLINE
	REPEAT 200
		ITEMSALES:COUNT = 0
	REND
	DRAWLINE
	;미션시에 표시
	IF FLAG:5 == 8
		PRINTFORM FREE MISSION {FLAG:24}　
	ENDIF
	PRINTV DAY
	PRINT 일째
	;주
	IF TIME == 0
		SETCOLOR 0xDBA901
			PRINTFORM 낮
		RESETCOLOR
	ELSE
		SETCOLOR 0x8080ff
			PRINTFORM 밤
		RESETCOLOR
	ENDIF

	;캘린더를 랜덤에 작성합니다
	IF FLAG:80 == 0
		D = RAND:12
		FLAG:80 = D + 1
		D = RAND:31
		SIF FLAG:80 == 2
			D = RAND:28
		SIF FLAG:80 == 4 || FLAG:80 == 6 || FLAG:80 == 9 || FLAG:80 == 11
			D = RAND:30
		FLAG:81 = D + 1
		D = RAND:3
		FLAG:83 = D
	ENDIF
	;월령과 가상 연호 취득해 본다
	CALL LUNAR_AGE
	;윤년용
	D = FLAG:83 % 4
	IF (FLAG:80 == 2 && FLAG:81 == 29 && D != 0) || (FLAG:80 == 2 && FLAG:81 == 30 && D == 0) || ((FLAG:80 == 4 || FLAG:80 == 6 || FLAG:80 == 9 || FLAG:80 == 11) && FLAG:81 == 31) || FLAG:81 == 32
		FLAG:81 = 1
		FLAG:80 += 1
		SIF FLAG:80 == 13
			FLAG:80 = 1
		;월령 계산으로 하는 김에 가상 연호도 취득하고 있으므로 플러스
		SIF FLAG:80 == 1 && FLAG:81 == 1
			FLAG:83 += 1
	ENDIF
	SIF FLAG:82 == 7
		FLAG:82 = 0

	PRINTFORM 　{FLAG:80}월{FLAG:81}일
	SIF FLAG:82 == 0
		PRINT (일)
	SIF FLAG:82 == 1
		PRINT (월)
	SIF FLAG:82 == 2
		PRINT (화)
	SIF FLAG:82 == 3
		PRINT (수)
	SIF FLAG:82 == 4
		PRINT (목)
	SIF FLAG:82 == 5
		PRINT (금)
	SIF FLAG:82 == 6
		PRINT (토)
	PRINTFORML 　(소지 코우슈우금{MONEY}고슈금)
	LOCALS = %CALLNAME:MASTER%(주인)
	PRINTFORML %LOCALS, 20, LEFT%
	MAXBASE:MASTER:사정 = 10000
	IF ASSI >= 0
		LOCALS = %CALLNAME:ASSI%(조수)
		PRINTFORML %LOCALS, 20, LEFT%
		MAXBASE:ASSI:사정 = 10000
	ENDIF
	IF TARGET > 0
		LOCALS = %CALLNAME%(조교중)
		PRINTFORM %LOCALS, 20, LEFT%
		CALL PRINT_BASE_SHOP(TARGET)
		MAXBASE:사정 = 15000
	ENDIF
	SETCOLOR 140, 140, 140
	DRAWLINE
	RESETCOLOR
	CALL STATUS
	
	;현조수＋원조수의 캐릭터수의 확인
	S = 0
	T = 0
	REPEAT CHARANUM
		;육아 방으로 이동하고 있는 캐릭터는 제외
		C = COUNT
		CALL CHECK_CHILD_CARE
		SIF RESULT == 1
			CONTINUE
		SIF CFLAG:COUNT:1 > 1
			S += 1
		SIF CFLAG:COUNT:1 == 1 || CFLAG:COUNT:1 == 2
			T += 1
	REND
	;TIME에 맞은 조교 캐릭터가 있을까
	E = 0
	REPEAT CHARANUM
		;주인과 노예와 노역중의 캐릭터는 배제
		SIF COUNT == MASTER
			CONTINUE
		SIF COUNT == TARGET
			CONTINUE
		;공범자, 마스터는 배제
		SIF CFLAG:COUNT:1 > 2
			CONTINUE
		SIF CFLAG:COUNT:12 > 0
			CONTINUE
		;육아 방으로 이동하고 있는 캐릭터는 제외
		C = COUNT
		CALL CHECK_CHILD_CARE
		SIF RESULT == 1
			CONTINUE
		E += 1
	REND
	SETCOLOR 140, 140, 140
	IF FLAG:5 == 8
		CALL FREE_MISSION_CONFIRM
	ELSEIF !FLAG:5
		PRINTFORML 목표:100일 이내에 코우슈우금 1000000고슈금 벌어,[납금]해
	ENDIF
	CALL SHOW_TIPS
	RESETCOLOR
	SETCOLOR 140, 140, 140
	DRAWLINE
	RESETCOLOR
	LOCAL:1 = 0
	FOR LOCAL, 0, 23
		LOCALS = 
		SELECTCASE LOCAL
			CASE 0
				SIF TARGET >= 0
					LOCALS = [200] 조교
			CASE 1
				SIF CHARANUM > 0
					LOCALS = [210] 도장
			CASE 2
				LOCALS = [220] 휴게
			CASE 3
				SIF CHARANUM < 800 && (FLAG:5 == 0 || FLAG:5 == 9)
					LOCALS = [300] 노예 구입
			CASE 4
				LOCALS = [310] 만물상
			;CASE 5
			;	LOCALS = [320] 부띠크
			;CASE 6
			;	LOCALS = [330] 약방
			;CASE 7
			;	LOCALS = [340] 비밀 숍
			CASE 8
				LOCALS = [400] 노역 지시
			CASE 9
				LOCALS = [450] 노역 지시(성의 중심 건물)
			CASE 10
				SIF E >= 1
					LOCALS = [500] 조교 대상 변경
			CASE 11
				SIF S >= 1
					LOCALS = [510] 주인의 변경
			CASE 12
				SIF S >= 1
					LOCALS = [520] 조수의 변경
			CASE 13
				LOCALS = [550] 능력 상승
			CASE 14
				LOCALS = [600] 콘피그
			CASE 15
				LOCALS = [610] 캐라콘피그
			CASE 16
				LOCALS = [700] 헬프
			;CASE 17
			;	LOCALS = [710] 캐릭터 도감
			;CASE 18
			;	LOCALS = [777] 디버그 메뉴
			CASE 19
				LOCALS = [800] 세이브
			CASE 20
				LOCALS = [900] 로드
			CASE 21
				SIF MONEY >= 1000000 && FLAG:5 == 0 && CFLAG:MASTER:1 == 3
					LOCALS = [910] 납금
			CASE 22
				SIF FLAG:40 == 10
					LOCALS = [920] 강해서 뉴 게임
		ENDSELECT
		IF STRLENS(LOCALS)
			PRINTFORM %LOCALS, 26, LEFT%
			LOCAL:1++
			SIF (LOCAL:1 % 3) == 0
				PRINTL 
		ENDIF
	NEXT
ENDIF
IF TFLAG:0 < 0
	SIF TFLAG:0 == -2
		CALL SHOP_ITEM_LIST(TFLAG:1)
	PRINTBUTTON "[999]돌아온다　　　", 999
	PRINTL 
ENDIF

@USERSHOP
SIF TFLAG:0 == 0
	TFLAG:1 = 0
;조교
IF RESULT == 200 && TARGET >= 0
	BEGIN TRAIN
	RETURN 1
;도장
ELSEIF RESULT == 210 && CHARANUM > 0
	CALL SHOW_CHARADATA
;휴게(일상 이벤트)
ELSEIF RESULT == 220
	;휴게 플래그를 세운다(여기는 특별)
	FLAG:0 = 2
	CALL DAILY_LIFE
	BEGIN TURNEND
	RETURN 1
;노예 구입
ELSEIF RESULT == 300 && CHARANUM < 800 && (FLAG:5 == 0 || FLAG:5 == 9) && TFLAG:0 == 0
	K = 0
	CALL CHARA_BUY
;만물상
ELSEIF RESULT == 310
	TFLAG:1 = 1
;ELSEIF RESULT == 320
;	TFLAG:1 = 1
;ELSEIF RESULT == 330
;	TFLAG:1 = 1
;ELSEIF RESULT == 340
;	TFLAG:1 = 1
;노역 지시
ELSEIF RESULT == 400 && TFLAG:0 == 0
	CALL CHARA_WORKING
;노역 지시(성의 중심 건물)
ELSEIF RESULT == 450 && TFLAG:0 == 0
	CALL CHARA_WORKING_HONMARU
;조교 대상 변경
ELSEIF RESULT == 500 && E >= 1 && TFLAG:0 == 0
	CALL CHANGE_TARGET
;주인의 변경 조교자 변경 기능확장
ELSEIF RESULT == 510 && S >= 1 && TFLAG:0 == 0
	CALL SELECT_MASTER
;조수의 변경
ELSEIF RESULT == 520 && S >= 1 && TFLAG:0 == 0
	CALL SELECT_ASSI
;능력 상승
ELSEIF RESULT == 550
	CALL SHOP_ABLUP(TARGET)
;콘피그
ELSEIF RESULT == 600 && TFLAG:0 == 0
	CALL OPTION
;캐라콘피그
ELSEIF RESULT == 610 && TFLAG:0 == 0
	CALL OPTION_CHARA
;헬프
ELSEIF RESULT == 700
	CALL DICTIONARY_MENU
;캐릭터 도감
ELSEIF RESULT == 710
	CALL CHARA_DICTIONARY_MENU
;디버그 메뉴
ELSEIF RESULT == 777
	CALL DEBUG_MENU_SHOP
;세이브
ELSEIF RESULT == 800 && TFLAG:0 == 0
	SAVEGAME
;로드
ELSEIF RESULT == 900 && TFLAG:0 == 0
	LOADGAME
;엔딩 처리
ELSEIF RESULT == 910 && MONEY >= 1000000 && FLAG:5 == 0 && FLAG:23 <= 0 && TFLAG:0 == 0 && CFLAG:MASTER:1 == 3
	CALL ENDING_CHECK
;강해서 뉴 게임
ELSEIF RESULT == 920 && FLAG:40 == 10
	CALL TSUYOKUTE_GAME

ELSEIF RESULT == 930 && TFLAG:0!= 0
	TFLAG:0 = 0
ELSEIF RESULT == 999
	TFLAG:0 = 0
	RETURN 0
ENDIF
SIF TFLAG:1 == 0
	RETURN 0
CALL SHOP_ITEM_LIST(TFLAG:1)
;-------------------------------------------------------
;TIPS
;-------------------------------------------------------
@SHOW_TIPS
VARSET LOCAL
SIF !FLAG:154
	RETURN 0
LOCALS = 
SETCOLOR 0x8888FF

FOR LOCAL, 0, CHARANUM
	SIF LOCAL == MASTER
		CONTINUE
	SIF 10 * BASE:LOCAL:체력 / MAXBASE:LOCAL:체력 < 8
		LOCAL:1++
	SIF CFLAG:LOCAL:1 == 3
		LOCAL:2++
	SIF CFLAG:LOCAL:1 >= 2
		LOCAL:3++
	SIF CFLAG:LOCAL:1 >= 1
		LOCAL:4++
	SIF KANRAKU2(LOCAL) == 1
		LOCAL:5++
	SIF KANRAKU2(LOCAL) == 2
		LOCAL:6++
	SIF KANRAKU2(LOCAL) == 3
		LOCAL:7++
	SIF CFLAG:LOCAL:1 < 3
		LOCAL:8++
	LOCAL:9++
NEXT

IF !GETBIT(FLAG:154, 1) && CHARANUM > 0
	LOCALS = [210] 도장으로부터, 캐릭터의 소질이나 능력을 볼 수가 있습니다.
	SETBIT FLAG:154, 1
ENDIF
IF !GETBIT(FLAG:154, 2) && TARGET >= 0 && LOCALS == ""
	LOCALS = [200] 조교를 개시해, 목표의 달성을 목표로 합시다.
	SETBIT FLAG:154, 2
ENDIF
IF !GETBIT(FLAG:154, 3) && !LOCAL:9 && LOCALS == ""
	LOCALS = [300] 우선은 노예를 구입해 봅시다.
	SETBIT FLAG:154, 3
ENDIF
IF !GETBIT(FLAG:154, 4) && MONEY >= 1000000 && !FLAG:5 && FLAG:23 <= 0 && !TFLAG:0 && CFLAG:MASTER:1 == 3 && LOCALS == ""
	LOCALS = [910] 자금이 모였습니다![납금]하면 엔딩입니다.
	SETBIT FLAG:154, 4
ENDIF
IF !GETBIT(FLAG:154, 5) && FLAG:40 == 10 && LOCALS == ""
	LOCALS = [920] 자, 유리한 보너스를 얻어 다음의 주회를 개시합시다.
	SETBIT FLAG:154, 5
ENDIF
IF !GETBIT(FLAG:154, 6) && MONEY >= 1000000 && !FLAG:5 && FLAG:23 <= 0 && !TFLAG:0 && CFLAG:MASTER:1 < 3 && LOCALS == ""
	LOCALS =원래의 주인공이나 공범자가 아니면[납금]할 수 없습니다.
	SETBIT FLAG:154, 6
ENDIF
IF !GETBIT(FLAG:154, 7) && LOCAL:1 && LOCALS == ""
	LOCALS = [220] 휴게를 취하는 것으로, 자동적으로 도검의손 넣고를 합니다.
	SETBIT FLAG:154, 7
ENDIF
IF !GETBIT(FLAG:154, 8) && TARGET > -1 && JUEL:부정 >= 100 && LOCALS == ""
	LOCALS =무리한 조교는부정를 더해가반발를 낳아 버립니다.
	SETBIT FLAG:154, 8
ENDIF
IF !GETBIT(FLAG:154, 9) && LOCAL:2 && LOCALS == ""
	LOCALS = [510] 가끔 씩은 공범자에게 조교를 맡겨 보는 것도 좋을지도 모릅니다.
	SETBIT FLAG:154, 9
ENDIF
IF !GETBIT(FLAG:154, 10) && LOCAL:3 && LOCALS == ""
	LOCALS = [520] 조교에 조수를 데리고 가면, 새로운 커맨드를 사용할 수 있을지도 모릅니다.
	SETBIT FLAG:154, 10
ENDIF
IF !GETBIT(FLAG:154, 11) && LOCAL:8 > 1 && LOCALS == ""
	LOCALS = [500] 다른 노예를 조교하고 싶으면, 조교 대상 변경을 선택합시다.
	SETBIT FLAG:154, 11
ENDIF
IF !GETBIT(FLAG:154, 12) && ITEM:비디오카메라 && !ITEM:비디오테이프 && LOCALS == ""
	LOCALS = [310] 비디오테이프를 구입해 촬영하면, AV를 팔아 자금을 얻을 수 있습니다.
	SETBIT FLAG:154, 12
ENDIF
IF !GETBIT(FLAG:154, 13) && !LOCAL:4 && LOCAL && MONEY <= 1000 && ((FLAG:4 == 1 && DAY >= 20) || (FLAG:4 == 2 && DAY >= 15) || (FLAG:4 == 3 && DAY >= 10)) && LOCALS == ""
	LOCALS = [400] 어떻게 하지나 없어지면, 노예를 헐값으로 팔아 버리는 것도 1손입니다.
	SETBIT FLAG:154, 13
ENDIF
IF !GETBIT(FLAG:154, 14) && LOCAL:4 && LOCALS == ""
	LOCALS = [400] 노예를[매각]하면, 상당한 자금을 얻을 수 있습니다.
	SETBIT FLAG:154, 14
ENDIF
IF !GETBIT(FLAG:154, 15) && (LOCAL:5 + LOCAL:6 + LOCAL:7) > 0 && LOCALS == ""
	LOCALS = [400] %CALLNAME%를 일하게 해 자금을 얻을 수 있습니다.
	SETBIT FLAG:154, 15
ENDIF
IF !GETBIT(FLAG:154, 16) && LOCAL:5 && LOCALS == ""
	LOCALS = [400] 포장마차나 뢰무는, 확실히 밑바닥 시키는 것이 돈을 버는 요령입니다.
	SETBIT FLAG:154, 16
ENDIF
IF LOCALS == ""
	IF !RAND:8
		LOCALS = %CALLNAME:MASTER%의기교가 오르면, 보다 조교는 용이하게 되겠지요.
	ELSEIF !RAND:7
		LOCALS =커맨드 필터로부터, 필요없는 커맨드를 지울 수가 있습니다.
	ELSEIF !RAND:6
		LOCALS = 기력가 0이 되면, 조교 효율이 현격히 떨어져 버립니다.
	ELSEIF !RAND:5
		LOCALS = [220] 휴게로부터, 모두의 평상시의 모습이 보여집니다.
	ELSEIF !RAND:4
		LOCALS = [700] 헬프로부터 eraToLove의 용어 해설, 공략의 힌트를 볼 수 있습니다.
	ELSEIF !RAND:3
		LOCALS = [310] 새로운 도구를 얻으면, 조교의 효율이 좋아지겠지요.
	ELSEIF RAND:2
		LOCALS = [310] 약의 과한 사용것은 정신에 심각한 데미지를 줍니다.
	ELSE
		LOCALS = [310] 조교에 유리한 기능 따위, 통상은 살 수 없는 것이 놓여져 있습니다.
	ENDIF
ENDIF

SIF LOCALS != ""
	PRINTFORML ＜Tips: %LOCALS%＞
RESETCOLOR

