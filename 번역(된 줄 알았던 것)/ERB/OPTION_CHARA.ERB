;--------------------------------------------------
;옵션 설정 처리
;--------------------------------------------------
@OPTION_CHARA
;현재의 조교 대상을 보존해 둔다
LOCAL = TARGET
PRINTL 
PRINTL 
PRINTL 
PRINTL 캐릭터마다의 옵션 설정입니다
PRINTL 설정 항목을 선택해 주세요
PRINTL [0] - 별명 설정
PRINTL [1] - 의상의 독자 설정
PRINTL [2] - 범용 허덕여 설정
PRINTL [999] -돌아온다
$INPUT_LOOP
INPUT
IF RESULT == 0
	CALL CHARA_CALLNAME_SETTING
	;보유 해 둔 현재의 조교 대상을 격납
	TARGET = LOCAL
ELSEIF RESULT == 1
	CALL CHARA_CLOTHES_SETTING
	;보유 해 둔 현재의 조교 대상을 격납
	TARGET = LOCAL
ELSEIF RESULT == 2
	CALL CHARA_PANT_VOICE_SETTING
	;보유 해 둔 현재의 조교 대상을 격납
	TARGET = LOCAL
ELSEIF RESULT == 999
	RETURN 0
ELSE
	CLEARLINE 1
	GOTO INPUT_LOOP
ENDIF
RESTART

;--------------------------------------------------
;별명 설정 일람 화면
;--------------------------------------------------
@CHARA_CALLNAME_SETTING
PRINTL 어느 캐릭터의 별명을 설정합니까?
FOR COUNT, 0, CHARANUM, 1
	PRINTFORML [{COUNT}] %CSVNAME(NO:COUNT, 0)% (별명:%CALLNAME:COUNT%)
NEXT
	PRINTL [999] 돌아온다
$INPUT_LOOP
INPUT
IF RESULT <= CHARANUM && RESULT != 999
	CALL CALLNAME_SETTING(RESULT)
ELSEIF RESULT == 999
	RETURN 0
ELSE
	CLEARLINE 1
	GOTO INPUT_LOOP
ENDIF

;--------------------------------------------------
;별명 설정 설정 화면
;--------------------------------------------------
@CALLNAME_SETTING(ARG)
PRINTFORML %CSVNAME(NO:ARG, 0)%의 별명을 설정합니다
PRINTFORML 현재의 별명:%CALLNAME:ARG%
PRINTL
PRINTL [0] 디폴트에 되돌린다
PRINTL [1] 설정한다
PRINTL [999] 돌아온다
$INPUT_LOOP
INPUT
IF RESULT == 0 || RESULT == 1
	IF RESULT == 0
		LOCALS = %CSVCALLNAME(NO:ARG, 0)%
	ELSEIF RESULT == 1
		PRINTL 별명을 입력해 주세요
		INPUTS
		LOCALS = %RESULTS%
	ENDIF
	
	PRINTFORML 「%LOCALS%」로 좋습니까?
	PRINTL [0] 네
	PRINTL [1] 아니오
	$INPUT_LOOP_DEFAULT
	INPUT
	IF RESULT == 0
		CALLNAME:ARG = %LOCALS%
		PRINTFORML %CSVNAME(NO:ARG, 0)%의 별명을 「%LOCALS%」로 설정했습니다
	ELSEIF RESULT == 1
		RESTART
	ELSE
		CLEARLINE 1
		GOTO INPUT_LOOP_DEFAULT
	ENDIF
ELSEIF RESULT == 999
	RETURN 0
ELSE
	CLEARLINE 1
	GOTO INPUT_LOOP
ENDIF


;--------------------------------------------------
;의상의 독자 설정
;기분이 내키면 고칩니다
;--------------------------------------------------
@CHARA_CLOTHES_SETTING
PRINTL 어느 캐릭터의 의상을 독자적으로 설정합니까?
PRINTL ※설정을 반영하려면 착의 초기설정을【독자 설정】으로 해 주세요
PRINTL 사양상, 전라는 설정할 수 없습니다
FOR COUNT, 1, CHARANUM, 1
	;공범자는 제외
	SIF CFLAG:COUNT:1!= 3
		PRINTFORML [{COUNT}] %CALLNAME:COUNT%
NEXT
	PRINTL [999] 돌아온다
$INPUT_LOOP
INPUT
IF RESULT <= CHARANUM && RESULT != 999
	TARGET = RESULT
	
	;호출하기 전에 독자 의상을 입혀 둔다
	REPEAT 11
		SELECTCASE COUNT
			CASE 0 TO 4
				LOCAL = COUNT
				;N형목을 요구하고 싶을 때는 10의 N승의 나머지를 10의 N-1승으로 나누면 좋다
				;예) 8, 7자리수째를 요구하고 싶을 때는 COUNT=LOCAL= 3이 되어, 10의 8승(=(3+1)*2)의 나머지를 10의 6승(=3*2)으로 나눈다
				LOCAL:1 = CFLAG:133 % POWER(10, (LOCAL+1)*2) / POWER(10, LOCAL*2)
				
				TEQUIP:COUNT = LOCAL:1
			CASE 5 TO 9
				LOCAL = COUNT - 5
				;처리는 위와 같이
				LOCAL:1 = CFLAG:134 % POWER(10, (LOCAL+1)*2) / POWER(10, LOCAL*2)
				
				TEQUIP:COUNT = LOCAL:1
			;악세사리─
			CASEELSE
				TEQUIP:10 = CFLAG:135
		ENDSELECT
	REND
	
	;조교중이 아니라고 알 수 있듯이, 통상 들어가지 않는 값을 PREVCOM에 대입
	PREVCOM = -2
	
	CALL CLOTHES_CHANGE
		;[900]돌아오지만 압하되면 Z에 RESULT가 격납된다
		IF Z == 900
			REPEAT 8
				D:COUNT = TEQUIP:COUNT
			REND
			D:9 = TEQUIP:9
			
			;CFLAG:133에 TEQUIP:0~TEQUIP:4(특수, 신발하, 속옷（아래）, 속옷（위）, 하의)
			;CFLAG:134에 TEQUIP:5~TEQUIP:9(상의, 전신상의, 머리장식, 안경, 신발)
			;CFLAG:135에 TEQUIP:10(악세사리─)을 기록
			;지금후의 일을 생각해, TEQUOP0~9는 1개에 대해 2자리수분 범위를 취해 둔다
			
			;3개로 나눈 것은 값의 상한의 관계
			CFLAG:133 = D:0 + (D:1 * 100) + (D:2 * 10000) + (D:3 * 1000000) + (D:4 * 100000000)
			CFLAG:134 = D:5 + (D:6 * 100) + (D:7 * 10000) + (D:8 * 1000000) + (D:9 * 100000000)
			CFLAG:135 = D:10

			SIF CFLAG:133 > 0 || CFLAG:134 > 0 || CFLAG:135 > 0
				PRINTW 설정했습니다
		ENDIF
		IF FLAG:1!= 99
			PRINTL 착의 초기설정을【독자 설정】으로 합니까?
			PRINTL [0]네
			PRINTL [1]아니오
			INPUT
			$INPUT_LOOP_2
			IF RESULT == 0
				PRINTW 초기의 착의를 독자 설정으로 설정했습니다
				FLAG:1 = 99
			ELSEIF RESULT == 1
			ELSE
				GOTO INPUT_LOOP_2
			ENDIF
		ENDIF
		
		;초기치에 되돌려 둔다
		PREVCOM = -1
		
ELSEIF RESULT == 999
	RETURN 0
ELSE
	CLEARLINE 1
	GOTO INPUT_LOOP
ENDIF

;--------------------------------------------------
;범용 허덕여 설정
;--------------------------------------------------
@CHARA_PANT_VOICE_SETTING
PRINTL 어느 캐릭터의 범용천를 설정합니까?
FOR COUNT, 1, CHARANUM, 1
	;공범자는 제외
	SIF CFLAG:COUNT:1!= 3
		PRINTFORML [{COUNT}] %CALLNAME:COUNT%
NEXT
	PRINTL [999] 돌아온다
$INPUT_LOOP
INPUT
IF RESULT <= CHARANUM && RESULT != 999
	TARGET = RESULT
	;이하의 함수는 「PANT_SET.ERB」에 있습니다
	CALL PANT_VOICE_SETTING
ELSEIF RESULT == 999
	CALL OPTION_CHARA
ELSE
	CLEARLINE 1
	GOTO INPUT_LOOP
ENDIF


