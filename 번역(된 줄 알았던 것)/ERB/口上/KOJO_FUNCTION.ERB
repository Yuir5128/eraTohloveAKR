;;≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡
;입 향상될 수 있는 함수 파일
;많게는 eraSQR로부터 이식하도록 해 받았습니다. 감사
;≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡
;-------------------------------------------------
;미독판정 함수@FIRSTTIME
;식중에서 사용하는 함수입니다. 대략 VARSIZE(LOCALS)^2개의 이벤트를 등록 할 수 있습니다
;ARGS는 이벤트 번호,ARG는 진이라면 첫회 판정을 갱신하지 않는,ARG:1은 캐릭터 등록 번호(생략 하면 TARGET,-1이라면 MASTER)
;{이벤트 번호}/ 그렇다고 하는 캐릭터 라인을 CSTR:20에 처넣어 갑니다
;-------------------------------------------------
@FIRSTTIME(ARGS,ARG,ARG:1)
#FUNCTION
;ARG:1의 변환
ARG:1 = ARG:1 ? ARG:1 # TARGET
ARG:1 = ARG:1 == -1 ? MASTER # ARG:1
SIF CSTR:(ARG:1):20 == ""
	CSTR:(ARG:1):20 = /

LOCALS = /%ARGS%/
IF STRCOUNT(CSTR:(ARG:1):20,LOCALS)
	RETURNF 0
ELSEIF ARG
	RETURNF 1
ENDIF
;첫회 판정 갱신
CSTR:(ARG:1):20 = %CSTR:(ARG:1):20%%ARGS%/
RETURNF 1

;-------------------------------------------------
;미독판정 함수@ONCE
;식중에서 사용하는 함수입니다
;ARGS는 이벤트 번호,ARG는 진이라면 첫회 판정을 갱신하지 않는,ARG:1은 진이라면 플래그의 올 리셋트를 실시하는 ARG:2는 캐릭터 등록 번호(생략 하면 TARGET,-1이라면 MASTER)
;{이벤트 번호}/ 그렇다고 하는 캐릭터 라인을 CSTR:21에 처넣어 갑니다
;-------------------------------------------------
@ONCE(ARGS,ARG,ARG:1,ARG:2)
#FUNCTION
;ARG:2의 변환
ARG:2 = ARG:2 ? ARG:2 # TARGET
ARG:2 = ARG:2 == -1 ? MASTER # ARG:2
IF ARG:1
	CSTR:(ARG:2):21 = 
	RETURNF 0
ENDIF
SIF CSTR:(ARG:2):21 == ""
	CSTR:(ARG:2):21 = /

LOCALS = /%ARGS%/
IF STRCOUNT(CSTR:(ARG:2):21,LOCALS)
	RETURNF 0
ELSEIF ARG
	RETURNF 1
ENDIF
;첫회 판정 갱신
CSTR:(ARG:2):21 = %CSTR:(ARG:2):21%%ARGS%/
RETURNF 1
;플래그 리셋트 처리
@EVENTTRAIN
FOR LOCAL,0,CHARANUM
	CSTR:LOCAL:21 = 
NEXT

;-------------------------------------------------
;페니스판정 함수@PENIS
;식중에서 사용하는 함수입니다. 페니스가 있는 경우 1을 돌려줍니다
;인수 0은 대상의 등록 번호. 시시한 내용
;-------------------------------------------------
@PENIS(ARG)
#FUNCTION
RETURNF TALENT:ARG:반음반양 || TALENT:ARG:후타나리 || TALENT:ARG:남자 || TALENT:ARG:쉬메일

;-------------------------------------------------
;페니스판정 함수 2@PENIS2
;식중에서 사용하는 함수입니다. 페니스(페니스밴드 가능)가 있는 경우 1을 돌려줍니다
;인수 0은 대상의 등록 번호. 시시한 내용
;-------------------------------------------------
@PENIS2(ARG)
#FUNCTION
RETURNF TALENT:ARG:반음반양 || TALENT:ARG:후타나리 || TALENT:ARG:남자 || TALENT:ARG:쉬메일 || ITEM:PBAND

;-------------------------------------------------
;바기나판정 함수@VAGINA
;식중에서 사용하는 함수입니다. 바기나가 있는 경우 1을 돌려줍니다
;인수 0은 대상의 등록 번호. 시시한 내용
;-------------------------------------------------
@VAGINA(ARG)
#FUNCTION
RETURNF !TALENT:ARG:남자 && !TALENT:ARG:쉬메일

;-------------------------------------------------
;가슴판정 함수@BREAST
;식중에서 사용하는 함수입니다. 젖가슴이 있는 경우 1을 돌려줍니다
;인수 0은 대상의 등록 번호. 시시한 내용
;-------------------------------------------------
@BREAST(ARG)
#FUNCTION
RETURNF MALE(ARG) == 0 && !TALENT:ARG:절벽

;-------------------------------------------------
;장머리카락판정 함수@RONGE
;식중에서 사용하는 함수입니다. 긴머리카락를 가지고 있는 경우 1을 돌려줍니다
;인수 0은 대상의 등록 번호. 헤어잡로 사용합니다만 그 밖에 용도 있어?
;-------------------------------------------------
@RONGE(ARG)
#FUNCTION
SELECTCASE NO:ARG
	;소여우,이마노츠루기,에 빌려 염주환,나마즈미,란,무라마사,톤보키리,대반야,소용,강설,소우산,사야,카슈우,안정,카네상,하치스가,우라시마,부동,시시오,코가라스마루,타로,지로,치요가네마루,네네키리마루,챠탄나키리,고금,닛코,칙종,희학,후쿠시마,칠성검
	CASE 5,11,17,19,27,45,63,65,75,77,79,81,83,85,87,91,101,103,120,122,124,132,134,156,162,170,182,188,198,202,204,206
		RETURNF 1
	;극
	;이마노츠루기극,에 빚극,나마즈꼬리극,무라마사극,톤보키리극,강설극,소우산극,사야극,카슈우극,안정극,카네상극,하치스가극,부동극,시시오극,치요가네마루극
	CASE 12,20,44,64,66,80,82,84,86,88,92,102,121,123,157
		RETURNF 1
	CASEELSE
		RETURNF 0
ENDSELECT

;-------------------------------------------------
;확률 판정 함수@PERCENT
;식중에서 사용하는 함수입니다. 인수%의 확률 판정을 실시합니다. 통과하면 1을 돌려줍니다
;-------------------------------------------------
@PERCENT(ARG)
#FUNCTION
RETURNF (ARG - RAND:100) > 0

;-------------------------------------------------
;함수명:BIND_CHECK_A
;개요:구속 체크 함수,도구에 의하는 것만
;인수:ARG… 부위(0. 사지/1. 목/2. 입/??. 코/??. 귀/??. 손/??. 발)
;반환값:해당 부위가 구속되어 사용 불가가 되어 있는지 어떤지
;비  고:식 나카노세키수
;구속을 체크하는 함수. 조금 시작품
;구속손단을후로부터 추가하는 경우에,그때마다입위쪽을 갱신하는 것이 싫은 것으로 작성
;텐프레로 사용할지 어떨지는 당분간 관망
;-------------------------------------------------
@BIND_CHECK_A(ARG)
#FUNCTION
SELECTCASE ARG
	;사지
	CASE 0
		;밧줄,촉수구속
		RETURNF TEQUIP:밧줄
	;시각
	CASE 1
		;아이마스크
		RETURNF TEQUIP:아이마스크
	;입
	CASE 2
		;볼개그,촉수구욕,조수입욕
		RETURNF TEQUIP:볼개그 || TEQUIP:촉수구욕
ENDSELECT
RETURNF 0

;-------------------------------------------------
;함수명:BIND_CHECK_B
;개요:구속 체크 함수,사지 결손을 포함한다
;인수:ARG… 부위(0. 사지/1. 목/2. 입/??. 코/??. 이/5. 손/6. 발)
;반환값:해당 부위가 구속되어 사용 불가가 되어 있는지 어떤지
;비  고:식 나카노세키수
;구속을 체크하는 함수. 조금 시작품
;구속손단을후로부터 추가하는 경우에,그때마다입위쪽을 갱신하는 것이 싫은 것으로 작성
;텐프레로 사용할지 어떨지는 당분간 관망
;-------------------------------------------------
@BIND_CHECK_B(ARG)
#FUNCTION
SELECTCASE ARG
	;사지
	CASE 0
		;밧줄,촉수구속,달마
		RETURNF TEQUIP:밧줄 || TALENT:달마
	;시각
	CASE 1
		;아이마스크
		RETURNF TEQUIP:아이마스크
	;입
	CASE 2
		;볼개그,촉수구욕,조수입욕
		RETURNF TEQUIP:볼개그 || TEQUIP:촉수구욕
	;손
	CASE 5
		;밧줄,촉수구속,달마,양팔결손
		RETURNF TEQUIP:밧줄 || TALENT:달마 || TALENT:양팔결손
	;각
	CASE 6
		;밧줄,촉수구속,달마,양팔결손
		RETURNF TEQUIP:밧줄 || TALENT:달마 || TALENT:양다리결손
ENDSELECT
RETURNF 0

;-------------------------------------------------
;함수명:CCHECK
;개요:C에 기분이 좋은 것이 붙어 있을까 판정
;인수:ARG… 캐릭터 등록 번호
;반환값:
;비  고:식 나카노세키수
;-------------------------------------------------
@CCHECK(ARG)
#FUNCTION
RETURNF TEQUIP:ARG:클리캡 || TEQUIP:ARG:전동오나홀 || TEQUIP:ARG:음핵클립 || TEQUIP:ARG:카테터 || TEQUIP:ARG:요도벌룬

;-------------------------------------------------
;함수명:VCHECK
;개요:V에 기분이 좋은 것이 붙어 있을까 판정
;인수:ARG… 캐릭터 등록 번호
;반환값:
;비  고:식 나카노세키수
;-------------------------------------------------
@VCHECK(ARG)
#FUNCTION
RETURNF TEQUIP:ARG:바이브 || TEQUIP:ARG:로터삽입 || TEQUIP:ARG:촉수질내산란

;-------------------------------------------------
;함수명:ACHECK
;개요:A에 기분이 좋은 것이 붙어 있을까 판정
;인수:ARG… 캐릭터 등록 번호
;반환값:
;비  고:식 나카노세키수
;-------------------------------------------------
@ACHECK(ARG)
#FUNCTION
RETURNF TEQUIP:ARG:애널바이브 || TEQUIP:ARG:애널비즈 || TEQUIP:ARG:로터A삽입 || TEQUIP:ARG:확장벌룬 || TEQUIP:ARG:촉수장내산란

;-------------------------------------------------
;함수명:BCHECK
;개요:B에 기분이 좋은 것이 붙어 있을까 판정
;인수:ARG… 캐릭터 등록 번호
;반환값:
;비  고:식 나카노세키수
;-------------------------------------------------
@BCHECK(ARG)
#FUNCTION
RETURNF TEQUIP:ARG:니플캡 || TEQUIP:ARG:유두클립 || TEQUIP:ARG:착유기 || TEQUIP:ARG:촉수유선삽입


;-------------------------------------------------
;함수명:ALLCHECK
;개요:CVAB 어떤 것인가에 기분이 좋은 것이 붙어 있을까 판정
;인수:ARG… 캐릭터 등록 번호
;반환값:
;비  고:식 나카노세키수
;-------------------------------------------------
@ALLCHECK(ARG)
#FUNCTION
RETURNF CCHECK(ARG) || VCHECK(ARG) || ACHECK(ARG) || BCHECK(ARG)


;-------------------------------------------------
;함수명:ZUIKI
;개요:가려움증약가 효과를 발휘하는 장소에 도착해 있을까 체크
;가슴,페니스,바기나,질내,애널의 어딘가
;인수:ARG… 캐릭터 등록 번호
;반환값:
;비  고:식 나카노세키수
;-------------------------------------------------
@ZUIKI(ARG)
#FUNCTION
RETURNF STAIN:ARG:가슴 & 1024 || STAIN:ARG:페니스 & 1024 || STAIN:ARG:바기나 & 1024 || STAIN:ARG:질내 & 1024 || STAIN:ARG:애널 & 1024


;-------------------------------------------------
;변의나뇨의가 있을까 체크
;인수:ARG… 종류(0. 양쪽 모두/1. 뇨의만/2. 변의만)
;반환값:노예가변의나뇨의를 느끼고 있을까
;비  고:식 나카노세키수
;-------------------------------------------------
@BASE46(ARG)
#FUNCTION
SELECTCASE ARG
	;어느 쪽인가
	CASE 0
		RETURNF BASE:뇨의 >= 60 || BASE:변의 >= 60
	;뇨의만
	CASE 1
		RETURNF BASE:뇨의 >= 60
	;변의만
	CASE 2
		RETURNF BASE:변의 >= 60
	;양쪽 모두
	CASE 3
		RETURNF BASE:뇨의 >= 60 && BASE:변의 >= 60
ENDSELECT
RETURNF 0

;-------------------------------------------------
;함수명:A_BIND_CHECK
;개요:변의가 있는 상태로 A가 막히고 있는지 어떤지 본다(배변 가능한가 아닌가가 판정 가능)
;인수:ARG… 캐릭터 등록 번호
;반환값:
;비  고:식 나카노세키수
;-------------------------------------------------
@A_BIND_CHECK(ARG)
#FUNCTION
RETURNF BASE:ARG:변의 >= 60 && (TEQUIP:ARG:애널비즈 || TEQUIP:ARG:애널바이브 || TEQUIP:ARG:확장벌룬)

;-------------------------------------------------
;함수명:U_BIND_CHECK
;개요:뇨의가 있는 상태로 U가 막히고 있는지 어떤지 본다(배뇨 가능한가 아닌가가 판정 가능)
;인수:ARG… 캐릭터 등록 번호
;반환값:
;비  고:식 나카노세키수
;-------------------------------------------------
@U_BIND_CHECK(ARG)
#FUNCTION
RETURNF BASE:ARG:뇨의 >= 60 && TEQUIP:ARG:요도벌룬


;-------------------------------------------------
;함수명:BOTTOMS
;개요:하의의 옷자락의 형상을 판정.
;	5는 바지 상태의 것. 손가 들어가지 않고,걷어 붙이는 일도 할 수 없다.
;	4는 짧은 바지 상태의 것. 손가 들어가지 않고,걷어 붙이는 일도 할 수 없지만,짧기 때문에 허벅지까지 닿는다.
;	3은 본테이지스트 한정. 지퍼로 개폐할 수 있다.
;	2는 사이비 스커트. 손가 들어가,걷어 붙일 수도 있지만,스커트는 아니다.
;	1은 스커트. 손가 들어가,걷어 붙일 수도 있다.
;인수:ARG… 캐릭터 등록 번호
;반환값:
;비  고:식 나카노세키수
;-------------------------------------------------
@BOTTOMS(ARG)
#FUNCTION
;원래하의를 입지 않으면 판정하지 않는다
IF TEQUIP:ARG:하의 == 0
	RETURNF 0
ELSE
	SELECTCASE TEQUIP:ARG:하의
		;바지
		;바지,운동복,하카마,올인원
		CASE 12,17,18,25
			RETURNF 5
		;숏팬츠
		;숏팬츠,하프 팬츠,부르마,스패츠
		CASE 13,14,15,16
			RETURNF 4
		;본테이지스트
		CASE 10
			RETURNF 3
		;사이비 스커트
		;차이나 드레스,유카타,기모노,무녀옷
		CASE 3,7,8,9
			RETURNF 2
		;스커트
		;원피스,칵테일 드레스,웨딩 드레스,메이드복,널스옷,스커트
		CASE 1,2,4,5,6,11
			RETURNF 1
	ENDSELECT
ENDIF

;-------------------------------------------------
;함수명:TOPS
;개요:전신상의의 열림의 형상을 판정. 1이라면 전은 열지 않는다
;인수:ARG… 캐릭터 등록 번호
;반환값:
;비  고:식 나카노세키수
;-------------------------------------------------
@TOPS(ARG)
#FUNCTION
;원래전신상의를 입지 않으면 판정하지 않는다
IF TEQUIP:ARG:전신상의 == 0
	RETURNF 0
ELSE
	SELECTCASE TEQUIP:ARG:전신상의
		CASE 3,4,5,6
			RETURNF 1
	ENDSELECT
ENDIF

;-------------------------------------------------
;함수명:KIMONO
;개요:입고 있는 것이 일본식 옷인가 어떤가 판정 1이라면 일본식 옷,0이라면 양복
;인수:ARG… 캐릭터 등록 번호
;반환값:
;비  고:식 나카노세키수
;무녀옷,유카타,기모노,작업복,법의,하카마,일본식 옷,사냥할 때 입는 옷,조끼
;-------------------------------------------------
@KIMONO(ARG)
#FUNCTION
;원래상의,하의 어느쪽이나 입지 않으면 판정하지 않는다
IF TEQUIP:ARG:상의 == 0 && TEQUIP:ARG:하의 == 0
	RETURNF 0
ELSE
	SELECTCASE TEQUIP:ARG:상의
		;유카타,기모노,무녀옷,일본식 옷,작업복,사냥할 때 입는 옷
		CASE 7,8,9,18,20,21
			RETURNF 1
	ENDSELECT
	SELECTCASE TEQUIP:ARG:하의
		;유카타,기모노,무녀옷,하카마
		CASE 7,8,9,18
			RETURNF 1
	ENDSELECT
ENDIF

;-------------------------------------------------
;함수명:CODE_CHECK
;개요:입고 있는상의가 외출 가능한가 어떤가 판정
;인수:ARG… 캐릭터 등록 번호
;반환값:
;비  고:식 나카노세키수
;-------------------------------------------------
@CODE_CHECK(ARG)
#FUNCTION
SIF TEQUIP:ARG:상의
	RETURNF 1
SELECTCASE TEQUIP:ARG:속옷（위）
	;이너,탱크 톱,셔츠,T셔츠,큰 셔츠
	CASE 7,8,9,10,11
		RETURNF 1
ENDSELECT
SELECTCASE TEQUIP:ARG:전신상의
	;윗도리,파커,로브,카속크,법의
	CASE 1,2,7,8,9
		RETURNF 1
ENDSELECT

;-------------------------------------------------
;함수명:TEQUIP_FEMALE
;개요:입고 있는 것이 남자용품인가,여자용품이나 판정. 여자용품이라면 1을 돌려준다
;인수:ARG… 캐릭터 등록 번호
;반환값:
;비  고:식 나카노세키수
;-------------------------------------------------
@TEQUIP_FEMALE(ARG)
#FUNCTION
;원래상의,하의 어느쪽이나 입지 않으면 판정하지 않는다
IF TEQUIP:ARG:상의 == 0 && TEQUIP:ARG:하의 == 0
	RETURNF 0
ELSE
	SELECTCASE TEQUIP:ARG:상의
		;원피스,칵테일 드레스,차이나 드레스,웨딩 드레스,메이드복,널스옷,무녀옷
		CASE 1,2,3,4,5,6,9
			RETURNF 1
	ENDSELECT
	SELECTCASE TEQUIP:ARG:하의
		;원피스,칵테일 드레스,차이나 드레스,웨딩 드레스,메이드복,널스옷,무녀옷,스커트
		CASE 1,2,3,4,5,6,9,11
			RETURNF 1
	ENDSELECT
ENDIF



;-------------------------------------------------
;함수명:MASTER_SEARCH2
;개요:인수 0의 캐릭터의 사모·공포 대상이 인수 1인가 어떤가를 판정하는 함수
;인수:ARG:0,ARG:1… 캐릭터 등록 번호
;반환값:
;비  고:식 나카노세키수
;-------------------------------------------------
@MASTER_SEARCH2(ARG:0,ARG:1)
#FUNCTION
SIF CFLAG:(ARG:0):40 && CFLAG:(ARG:0):41 == NO:(ARG:1)
	RETURNF 1
RETURNF 0

;-------------------------------------------------
;함수명:KANRAKU
;개요:함락 루트를 조사하는 함수
;인수:ARG:0… 캐릭터 등록 번호
;반환값:0=미함락 1=연모루트 2=음란루트 3=복종루트
;4=연모함락으로 사모 대상 의외로의 조교/피조교 상태 5=복종함락으로 공포 대상 의외로의 조교/피조교 상태
;비  고:식 나카노세키수
;-------------------------------------------------
@KANRAKU(ARG)
#FUNCTION
IF CFLAG:ARG:41 < 0 && CFLAG:ARG:39 != 4 && CFLAG:ARG:39 != 7
	RETURNF 0
ELSEIF TALENT:ARG:붕괴
	RETURNF 0
ELSE
	SELECTCASE CFLAG:ARG:39
		CASE 3,6,165
			IF !TALENT:ARG:연모 && !TALENT:ARG:친애
				RETURNF 4
			ELSE
				RETURNF 1
			ENDIF
		CASE 4,7
			RETURNF 2
		CASE 5,8
			IF !TALENT:ARG:복종 && !TALENT:ARG:예속
				RETURNF 5
			ELSE
				RETURNF 3
			ENDIF
	ENDSELECT
ENDIF
RETURNF 0

;-------------------------------------------------
;함수명:KANRAKU2
;개요:함락 루트를 조사하는 함수(조교자가 사모·공포 대상인가는 묻지 않는다)
;인수:ARG:0… 캐릭터 등록 번호
;반환값:0=미함락 1=연모루트 2=음란루트 3=복종루트
;비  고:식 나카노세키수
;		eraelevenGO로 추가한 함수입니다
;-------------------------------------------------
@KANRAKU2(ARG)
#FUNCTION
IF CFLAG:ARG:41 < 0 && CFLAG:ARG:39 != 4 && CFLAG:ARG:39 != 7
	RETURNF 0
ELSEIF TALENT:ARG:붕괴
	RETURNF 0
ELSE
	SELECTCASE CFLAG:ARG:39
		CASE 3,6,165
			RETURNF 1
		CASE 4,7
			RETURNF 2
		CASE 5,8
			RETURNF 3
	ENDSELECT
ENDIF
RETURNF 0

;-------------------------------------------------
;함수명:SEXPLAY
;개요:조교 대상이 하고 있는 섹스
;인수:ARG:0… 캐릭터 등록 번호
;반환값:1=섹스 2=A섹스 3=역섹스중 4=역A섹스
;비  고:식 나카노세키수
;-------------------------------------------------
@SEXPLAY(ARG)
#FUNCTION
SELECTCASE SELECTCOM
	;섹스,혼란 모란,삽입 G스포,삽입 자궁입,이륜 찔러,스팽킹섹스,스팽킹애널
	CASE 100 TO 106,506 TO 508,511,513,517
		RETURNF 1
	;애널섹스
	CASE 110 TO 116,512,514,518
		RETURNF 2
	;역섹스
	CASE 260,261,262
		RETURNF 3
	;역애널섹스
	CASE 263,264,265
		RETURNF 4
	;승마 유방자위,역에서 파는 도시락(TFLAG:705가 0이라면 V,1이라면 A)
	CASE 509,510
		RETURNF TFLAG:705 + 1
	;샌드위치,연결,샌드위치 시키는,조수를 샌드위치
	CASE 515,519,520,521
		IF TFLAG:40 == 1
			RETURNF 1
		ELSEIF TFLAG:40 == 2
			RETURNF 2
		ELSE
			RETURNF 0
		ENDIF
	CASEELSE
		RETURNF 0
ENDSELECT

;-------------------------------------------------
;함수명:DOUSEI
;개요:두 명이 동성이나 판정하는 함수
;인수:ARG,ARG:1… 캐릭터 등록 번호
;반환값:동성이라면 1,이성이라면 0
;비  고:식 나카노세키수
;-------------------------------------------------
@DOUSEI(ARG,ARG:1)
#FUNCTION
SIF (MALE(ARG) && !MALE(ARG:1)) || (!MALE(ARG) && MALE(ARG:1))
	RETURNF 0
RETURNF 1

;-------------------------------------------------
;함수명:COMMON_KOJO_EX
;개요:범용 커맨드대사의 분류
;인수:
;반환값:1=2=3=4=
;비  고:
;-------------------------------------------------
@COMMON_KOJO_EX
SELECTCASE SELECTCOM
	;아무것도안한다
	CASE 2,310 TO 312,
		;쾌락계 도구가 붙어 있을 때 수치계
		IF CCHECK(TARGET) || VCHECK(TARGET) || ACHECK(TARGET) || BCHECK(TARGET)
			RETURN 7
		;그 외
		ELSE
			RETURN 0
		ENDIF
	;약주
	CASE 301 TO 306
		RETURN 13
	;장소 이동
	CASE 313,315,320,323,207,321
		;장소 이동(방으로 돌아간다)
		IF TFLAG:54
			RETURN 19
		;장소 이동(방 이외에 이동)
		ELSE
			RETURN 18
		ENDIF
	;구속
	CASE 131 TO 134
		;도구를 제외한다(구속)
		IF TFLAG:54
			RETURN 12
		;붙어 있지 않으면 그 외
		ELSE
			RETURN 0
		ENDIF
	;밧줄,촉수구속
	CASE 130,351
		;도구를 제외한다(구속)
		IF TFLAG:54
			RETURN 12
		;쾌락계 도구가 붙어 있을 때고통쾌락계
		ELSEIF CCHECK(TARGET) || VCHECK(TARGET) || ACHECK(TARGET) || BCHECK(TARGET)
			RETURN 10
		;붙어 있지 않으면 가학계
		ELSE
			RETURN 8
		ENDIF
	;주인마조계
	CASE 280 TO 284
		RETURN 14
	;스카계
	CASE 180 TO 185,285,286,352,527
		RETURN 15
	;엽기
	CASE 156,383,390 TO 392
		RETURN 16
	;굴욕
	CASE 85 TO 87,224
		RETURN 17
	;C쾌감
	CASE 0,20 TO 23,30 TO 35,240 TO 242,246,342,343,380,400,401
		;도구를 제외한다(쾌감)
		IF TFLAG:54
			RETURN 11
		ELSE
			RETURN 1
		ENDIF
	;V쾌감
	CASE 40 TO 43,344,362,363,403,405,500
		;도구를 제외한다(쾌감)
		IF TFLAG:54
			RETURN 11
		ELSE
			RETURN 2
		ENDIF
	;A쾌감
	CASE 50 TO 54,344,364,404,406,501
		;도구를 제외한다(쾌감)
		IF TFLAG:54
			RETURN 11
		ELSE
			RETURN 3
		ENDIF
	;B쾌감
	CASE 10 TO 14,17,18,341,360,402,502
		;도구를 제외한다(쾌감)
		IF TFLAG:54
			RETURN 11
		ELSE
			RETURN 4
		ENDIF
	;성교계
	CASE 100 TO 119,506 TO 510
		RETURN 5
	;역성교계
	CASE 260 TO 265
		RETURN 6
	;수치
	CASE 3 TO 8,36,226,227,228,270,271,314,410,441,505
		RETURN 7
	;가학
	CASE 72,140 TO 145,153 TO 155,157
		;쾌락계 도구가 붙어 있을 때고통쾌락계
		IF CCHECK(TARGET) || VCHECK(TARGET) || ACHECK(TARGET) || BCHECK(TARGET)
			RETURN 10
		;붙어 있지 않으면 가학계
		ELSE
			RETURN 8
		ENDIF
	;봉사 쾌락
	CASE 73 TO 76,84,243,245,317,421,422,503,504,528
		RETURN 9
	;고통쾌락
	CASE 15,16,24,25,44,55,56,60 TO 62,160 TO 163,322,361,365 TO 374,381,382,511 TO 514,517,518
		RETURN 10
	;봉사
	CASE 70,71,77,80 TO 83,220 TO 223,225,420,516
		;쾌락계 도구가 붙어 있을 때 봉사 쾌락계
		IF CCHECK(TARGET) || VCHECK(TARGET) || ACHECK(TARGET) || BCHECK(TARGET)
			RETURN 9
		;그 외
		ELSE
			RETURN 20
		ENDIF
	CASEELSE
		RETURN 0
ENDSELECT

;====================================================================================
;@ACTIVECOM
;조교 대상이 능동적으로 실시하는 커맨드인가 어떤가를 판정하는 함수입니다. 개별 범용천의 분기용. 분류는 독단과 편견에 근거하고 있습니다.
;인수:ARG:0… 캐릭터 등록 번호
;반환값:1=문답 무용 2=촉수계 3=조수명령계 4=역강간계 5=봉사 쾌락계 6=스카계(하는 (분)편) 7=자위계·방치계 8=봉사계·기학계 9=연모계
;비  고:식 나카노세키수
;네등 넣고 귀도대사으로부터 빌렸습니다
;====================================================================================
@ACTIVECOM(ARG)
#FUNCTION
SELECTCASE ARG
	;문답 무용
	CASE 0,1,10 TO 18,20 TO 25,30 TO 36,40 TO 44,50 TO 56,60 TO 62,72,81,83,84,100 TO 105,110 TO 115,130 TO 145,151,153 TO 163,181,240 TO 242,246,314,318,322,380 TO 383,390 TO 392,410,412,500 TO 502,505 TO 508,510 TO 515,517,518,527
		RETURNF 1
	;촉수계
	CASE 340 TO 374
		RETURNF 2
	;조수명령계
	CASE 400 TO 407
		RETURNF 3
	;역강간계
	CASE 260 TO 265
		RETURNF 4
	;봉사 쾌락계
	CASE 76,106,116,243 TO 245,422,430,431,503,504,509,528
		RETURNF 5
	;스카계(하는 (분)편)
	CASE 180,182,183
		RETURNF 6
	;자위계·방치계
	CASE 2 TO 8,226,227,228,270,271,285,286,300 TO 304,310 TO 313,315,316,320,321,323,411,440,441
		RETURNF 7
	;봉사계·기학계
	CASE 70,71,73 TO 75,77,80,82,85 TO 87,150,152,184,185,220 TO 225,280 TO 284,317,420,421,516
		RETURNF 8
	;커뮤니케이션계
	CASE 200 TO 207,319
		RETURNF 9
	CASEELSE
		RETURNF 0
ENDSELECT

