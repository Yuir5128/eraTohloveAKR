;A감각의 가부
@ISABLUP5
;연모, 친애, 음란, 창부는 MAXLV10
IF ABL:A감각 >= 10
	RETURN 0
ELSEIF ABL:A감각 >= 5 && TALENT:연모 == 0 && TALENT:친애 == 0 && TALENT:음란 == 0 && TALENT:창부 == 0
	RETURN 0
ELSEIF TALENT:공
	RETURN 0
ENDIF

CALL DECIDE_ABLUP5

IF Z == 0
	RETURN 0
ELSE
	RETURN 1
ENDIF


;A감각
@ABLUP5
DRAWLINE
;연모, 친애, 음란, 창부는 MAXLV10
IF ABL:A감각 >= 10
	PRINTW 이미 MAX입니다
	RETURN 0
ELSEIF ABL:A감각 >= 5 && TALENT:연모 == 0 && TALENT:친애 == 0 && TALENT:음란 == 0 && TALENT:창부 == 0
	PRINTW 이미 MAX입니다
	RETURN 0
ENDIF

CALL DECIDE_ABLUP5

PRINTFORM [0] - %PALAMNAME:2%의 구슬×{A}, %EXPNAME:1%{B}이상……
IF I == 0
	PRINT OK
ELSE
	SIF I & 1
		PRINT 구슬부족 
	SIF I & 2
		PRINT 경험부족 
ENDIF
PRINTL 

PRINTL [100] - 그만둔다


INPUT
IF (RESULT < 0 || RESULT > 0) && RESULT != 100
	RESTART
ELSEIF I != 0 && RESULT == 0
	PRINTL 조건을 채우고 있지 않습니다
	RESTART
ELSEIF RESULT == 100
	RETURN 0
ENDIF

ABL:A감각 += 1

IF RESULT == 0
	JUEL:쾌A -= A
ENDIF

PRINTFORMW %ABLNAME:5%의 레벨이{ABL:A감각}가 되었습니다

;A감각LvUP 판정
@DECIDE_ABLUP5
;조건별로 OK인가 안 되는가를 기록한다
I = 0
;능력 업 가능하면 1으로 한다
Z = 0

IF ABL:A감각 == 0
	A = 1
	B = 2
ELSEIF ABL:A감각 == 1
	A = 50
	B = 10
	SIF EXP:A경험 >= EXPLV:3
		A = 20
ELSEIF ABL:A감각 == 2
	A = 400
	B = 30
	SIF EXP:A경험 >= EXPLV:4
		A = 100
ELSEIF ABL:A감각 == 3
	A = 5000
	B = 75
	SIF EXP:A경험 >= EXPLV:5
		A = 4000
ELSEIF ABL:A감각 == 4
	A = 15000
	B = 150
	SIF EXP:A경험 >= EXPLV:5
		A = 8000
ELSEIF ABL:A감각 == 5
	A = 30000
	B = 230
	SIF EXP:A경험 >= EXPLV:5
		A = 24000
ELSEIF ABL:A감각 == 6
	A = 40000
	B = 270
ELSEIF ABL:A감각 == 7
	A = 50000
	B = 360
ELSEIF ABL:A감각 == 8
	A = 60000
	B = 420
ELSEIF ABL:A감각 == 9
	A = 70000
	B = 500
ENDIF

;음란
IF TALENT:음란
	TIMES A , 0.80
	TIMES B , 0.80
ENDIF
;창부
IF TALENT:창부
	TIMES A , 0.50
	TIMES B , 0.50
ENDIF

;0(이)가 되었을 경우는 1으로 한다
SIF A == 0
	A = 1

;쾌A의 구슬로 올린다
SIF JUEL:쾌A < A
	I |= 1

SIF EXP:A경험 < B
	I |= 2

IF I == 0
	Z = 1
ENDIF

RETURN 1

