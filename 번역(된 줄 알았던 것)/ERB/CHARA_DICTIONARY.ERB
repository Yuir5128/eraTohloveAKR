;=============================================================================
;캐릭터 도감과 소질 검색
;=============================================================================
@CHARA_DICTIONARY_MENU
DRAWLINE
PRINTLC [ 1] 캐릭터 도감
PRINTLC [ 2] 소질 검색
PRINTL 
PRINTL [100]그만둔다
DRAWLINE
$INPUT_LOOP
INPUT
IF RESULT == 100
	RETURN 0
ELSEIF RESULT == 1
	CALL CHARA_DICTIONARY_LIST
ELSEIF RESULT == 2
	CALL DICTIONARY_SEARCH_TALENT
ELSE
	GOTO INPUT_LOOP
ENDIF
RESTART

;=============================================================================
;캐릭터 도감
;=============================================================================
;--------------------------------------------------
;표시하는 캐릭터의 선택
;--------------------------------------------------
@CHARA_DICTIONARY_LIST
VARSET Y, 0, 0, FLAG:44
;現在ページ
P = 0
;표시 캐릭터수
Q = 0
FOR COUNT, 0, FLAG:44
	;CSV가 존재하지 않는 캐릭터는 제외
	SIF !EXISTCSV(COUNT, 0)
		CONTINUE
	;아이는 제외
	SIF GROUPMATCH(CSVNAME(COUNT, 0), "아이1", "아이2", "아이3", "아이4", "아이5", "아이6", "아이7", "아이8", "아이9", "아이10")
		CONTINUE
	;캐릭터마다의 표시 가부
	Y:COUNT = 1
	;몇번째가 누군가
	X:Q = COUNT
	Q +=1
NEXT
;최종 페이지수
P:1 = Q / 60
;페이지를 넘겼을 때 등은 여기로부터
$PRINT_LIST_PAGE
DRAWLINE
PRINTFORML 선택한 캐릭터의 초기 능력을 열람할 수 있습니다. ＜page. {P+1}/{P:1+1}＞
DRAWLINE
U = 0
FOR COUNT, 0, Q
	SIF COUNT == 0 && P
		COUNT = P * 60
	SIF COUNT >= (P + 1) * 60
		BREAK
	S = STRLENS(CSVNAME(X:COUNT, 0)) + 6
	IF U >= 5 || (U >= 3 && S > 26 + 13) || (U >= 4 && S > 26)
		PRINTL 
		U = 0
	ENDIF
	IF S <= 26
		PRINTFORM [{X:COUNT, 3}] %CSVNAME(X:COUNT, 0), 20, LEFT%
		U += 2
	ELSEIF S <= 26 + 13
		PRINTFORM [{X:COUNT, 3}] %CSVNAME(X:COUNT, 0), 20 + 13, LEFT%
		U += 3
	ELSEIF S <= 26 + 26
		PRINTFORM [{X:COUNT, 3}] %CSVNAME(X:COUNT, 0), 20 + 26, LEFT%
		U += 4
	ELSE
		PRINTFORM [{X:COUNT, 3}] %CSVNAME(X:COUNT, 0)%
		U += 6
	ENDIF
NEXT
PRINTL 
DRAWLINE
PRINTFORMLC \@(P <= 0) ? %" " * 16% # [1001]전의 페이지\@
PRINTLC [1000]돌아온다
PRINTFORMLC \@(P >= P:1) ? %" " * 16% # [1009]차의 페이지\@
PRINTL 
$INPUT_LOOP
INPUT
IF RESULT == 1000
	RETURN 0
ELSEIF RESULT == 1001 && P > 0
	P -= 1
ELSEIF RESULT == 1009 && P < P:1
	P += 1
ELSEIF RESULT < 0 || RESULT > FLAG:44 || !Y:RESULT
	GOTO INPUT_LOOP
ELSE
	;캐릭터 도감 능력 표시부의 함수에
	CALL CHARA_DICTIONARY_MAIN, RESULT
ENDIF

GOTO PRINT_LIST_PAGE

;--------------------------------------------------
;캐릭터 도감 능력 표시부
;--------------------------------------------------
@CHARA_DICTIONARY_MAIN,ARG
$PRINT_PAGE
;존재하지 않는 캐릭터의 경우,돌아온다
SIF !EXISTCSV(ARG , 0)
	RETURN 0

DRAWLINE
DRAWLINE
CALL CHARA_MANUAL(ARG, 2)
DRAWLINE

PRINTFORM 캐릭터 번호：{ARG}　이름：%CSVNAME(ARG, 0)% \@ CSVNAME(ARG, 0) != CSVCALLNAME(ARG, 0) ? （%CSVCALLNAME(ARG, 0)%） # \@　
;PRINTFORML 성별：\@((CSVTALENT(ARG, 140, 0)) || (CSVTALENT(ARG, 137, 0))) ? ♂ # ♀\@　%SHOW_RACE(ARG)%
PRINTFORML 체력：{CSVBASE(ARG, 0, 0), 4}　기력：{CSVBASE(ARG, 1, 0), 4}

DRAWLINE
;초기 소질
CALL DICTIONARY_CHARA_TALENT, ARG
;초기 능력
CALL DICTIONARY_CHARA_ABL, ARG
;초기 경험
CALL DICTIONARY_CHARA_EXP, ARG
;초기 궁합
CALL DICTIONARY_CHARA_RELATION, ARG

$INPUT_LOOP
PRINTL [998]초기 의상을 표시하는　　　　[999]외 캐릭터로부터의 초기 궁합을 표시한다
PRINTFORMLC \@(ARG > 0 && EXISTCSV(ARG-1, 0) > 0) ? [1001]전의 페이지 # %" " * 16%\@
PRINTLC [1000]리스트에 돌아온다
PRINTFORMLC \@(ARG < 199 && EXISTCSV(ARG+1, 0) > 0) ? [1009]차의 페이지 # %" " * 16%\@
PRINTL 
INPUT
IF RESULT == 1000
	RETURN 0
ELSEIF RESULT == 998
	CALL DICTIONARY_CHARA_CLOTHES, ARG
	GOTO INPUT_LOOP
ELSEIF RESULT == 999
	CALL DICTIONARY_CHARA_BACK_RELATION, ARG
	GOTO INPUT_LOOP
ELSEIF RESULT == 1001 && ARG > 0 && EXISTCSV(ARG-1, 0) > 0
	ARG = ARG - 1
ELSEIF RESULT == 1009 && ARG < FLAG:44 && EXISTCSV(ARG+1, 0) > 0
	ARG = ARG + 1
ELSE
	CLEARLINE 3
	GOTO INPUT_LOOP
ENDIF
GOTO PRINT_PAGE

;………………………………………………
;초기 능력
;………………………………………………
@DICTIONARY_CHARA_ABL, ARG
PRINTL □ 초기 능력
U = 0
REPEAT VARSIZE("ABL")
	SIF ABLNAME:COUNT == ""
		CONTINUE
	SIF !CSVABL(ARG, COUNT, 0)
		CONTINUE
	PRINTFORM %ABLNAME:COUNT, 12%：LV{CSVABL(ARG, COUNT, 0), 2}　
	;개행
	SIF U++ % 5 == 4
		PRINTL 
REND

IF U == 0
	PRINTL 없음
;최후의 루프로 개행하고 있지 않았으면,여기서 개행해 둔다
ELSEIF U % 5 != 0
	PRINTL 
ENDIF
DRAWLINE

;………………………………………………
;초기 경험
;………………………………………………
@DICTIONARY_CHARA_EXP, ARG
PRINTL □ 초기 경험
U = 0
REPEAT VARSIZE("EXP")
	SIF EXPNAME:COUNT == ""
		CONTINUE
	SIF !CSVEXP(ARG, COUNT, 0)
		CONTINUE
	PRINTFORM %EXPNAME:COUNT, 12%：{CSVEXP(ARG, COUNT, 0), 8}　
	;개행
	SIF U++ % 3 == 2
		PRINTL 
REND

IF U == 0
	PRINTL 없음
;최후의 루프로 개행하고 있지 않았으면,여기서 개행해 둔다
ELSEIF U % 3 != 0
	PRINTL 
ENDIF
DRAWLINE

;………………………………………………
;초기 소질
;………………………………………………
@DICTIONARY_CHARA_TALENT, ARG
PRINTL □ 초기소질
FOR COUNT, 0, VARSIZE("TALENT")
	SIF TALENTNAME:COUNT == ""
		CONTINUE
	SIF !CSVTALENT(ARG, COUNT, 0)
		CONTINUE
	PRINTFORM [%TALENTNAME:COUNT%] 
NEXT
PRINTL 
DRAWLINE

;………………………………………………
;초기 궁합
;………………………………………………
@DICTIONARY_CHARA_RELATION, ARG
#LOCALSSIZE 10
PRINTL □ 초기 궁합
U = 0
REPEAT FLAG:44
	SIF !EXISTCSV(COUNT, 0)
		CONTINUE
	SIF !CSVRELATION(ARG, COUNT, 0)
		CONTINUE
	PRINTFORM %CSVCALLNAME(COUNT, 0), 16%×{CSVRELATION(ARG, COUNT, 0), 3}％　
	SIF U++ % 3 == 2
		PRINTL 
REND
IF U == 0
	PRINTL 특별히 이루어
ELSEIF U % 3 != 0
	PRINTL 
ENDIF
DRAWLINE

;………………………………………………
;초기 의상
;………………………………………………
@DICTIONARY_CHARA_CLOTHES, ARG

;일시적으로 캐릭터 추가해 끝나면 삭제한다
C = ARG
VARSET T, 0, 0, 11
ADDCHARA ARG

;현재 의상 서정을 탈피
LOCAL = FLAG:1

;전착
FLAG:1 = 0
CALL CLOTHES_SET(CHARANUM - 1)
PRINTL
PRINTL □ 초기 착의(통상)
CALL DICTIONARY_CHARA_CLOTHES_PRINT

;내번
FLAG:1 = 10
CALL CLOTHES_SET3(CHARANUM - 1)
PRINTL
PRINTL □ 초기 착의(내번)
CALL DICTIONARY_CHARA_CLOTHES_PRINT

;경장
FLAG:1 = 9
CALL CLOTHES_SET4(CHARANUM - 1)
PRINTL
PRINTL □ 초기 착의(경장)
CALL DICTIONARY_CHARA_CLOTHES_PRINT

;현재의 착의 설정을 되돌린다
FLAG:1 = LOCAL

DELCHARA CHARANUM - 1

PRINTL 
DRAWLINE


;착의 설정 표시용
@DICTIONARY_CHARA_CLOTHES_PRINT
;착의를 표시
REPEAT 10
	IF COUNT < 7
		A = 6 - COUNT
	ELSE
		A = COUNT
	ENDIF
	IF T:A
		PRINT [
		CALL CLOTHES_NAME(CHARANUM - 1)
		PRINT ]
	ENDIF
REND
;악세사리를 표시
REPEAT 11
	A = COUNT + 1
	IF GETBIT(T:10, COUNT)
		PRINT [
		CALL ACCESSORY_NAME
		PRINT ]
	ENDIF
REND

;………………………………………………
;타캐릭터로부터의 초기 궁합
;………………………………………………
@DICTIONARY_CHARA_BACK_RELATION, ARG
PRINTL
PRINTL □ 외 캐릭터로부터의 초기 궁합
U = 0
REPEAT FLAG:44
	SIF !EXISTCSV(COUNT, 0)
		CONTINUE
	SIF !CSVRELATION(COUNT, ARG, 0)
		CONTINUE
	PRINTFORM %CSVCALLNAME(COUNT, 0), 20%→○○：{CSVRELATION(COUNT, ARG, 0), 3}％　
	SIF U++ % 2 == 1
		PRINTL 
REND
IF U == 0
	PRINTL 특별히 이루어
;최후의 루프로 개행하고 있지 않았으면,여기서 개행해 둔다
ELSEIF U % 2 != 0
	PRINTL 
ENDIF
DRAWLINE


;=============================================================================
;조교 사전――소질 검색
;=============================================================================
;--------------------------------------------------
;소질 검색 메인
;--------------------------------------------------
@DICTIONARY_SEARCH_TALENT
P = 0
Q = 0
VARSET Y, 0, 0,300
;표시시키는 소질을 추출
FOR COUNT, 0, 300
	;존재하지 않는 소질은 배제
	SIF TALENTNAME:COUNT == ""
		CONTINUE
	SELECTCASE COUNT
		;재생처녀、재생처녀,각종 함락 소질,부위 개발계 소질,양성 구유계,수공낙인임신육아중실의기절결손은 비표시에
		CASE 1, 3 TO 9, 73 TO 78, 137 TO 139, 146, 153 TO 154, 157 TO 159, 162 TO 165, 200 TO 205
			CONTINUE
		CASEELSE
			;표시 가부
			Y:COUNT = 1
			;몇번째가 무엇인가
			X:Q = COUNT
			Q +=1
	ENDSELECT
NEXT
P:1 = Q / 60

$PRINT_LIST
DRAWLINE
PRINTFORML 어느 소질을 초기에 소유하는 캐릭터를 검색합니까? ＜page. {P+1}/{P:1+1}＞
DRAWLINE
;각 페이지를 표시
FOR COUNT, 0, Q
	SIF COUNT == 0 && P
		COUNT = P * 60
	SIF COUNT >= (P + 1) * 60
		BREAK
	PRINTFORM [{X:COUNT, 3}] %TALENTNAME:(X:COUNT), 18, LEFT%
	;개행
	SIF COUNT % 3 == 2
		PRINTL 
NEXT
PRINTL 
DRAWLINE
PRINTFORMLC \@(P <= 0) ? %" " * 16% # [1001]전의 페이지\@
PRINTLC [1000]돌아온다
PRINTFORMLC \@(P >= P:1) ? %" " * 16% # [1009]차의 페이지\@
PRINTL 
$INPUT_LOOP
INPUT
IF RESULT == 1000
	RETURN 0
ELSEIF RESULT == 1001 && P > 0
	P -= 1
	GOTO PRINT_LIST
ELSEIF RESULT == 1009 && P < P:1
	P += 1
	GOTO PRINT_LIST
ELSEIF RESULT < 0 || RESULT >= 300 || Y:RESULT == 0
	GOTO INPUT_LOOP
ENDIF
CALL SEARCH_CHARA_TALENT, RESULT
GOTO PRINT_LIST

;--------------------------------------------------
;검색 결과 표시 처리
;--------------------------------------------------
@SEARCH_CHARA_TALENT, ARG
U = 0
PRINTFORML 【%TALENTNAME:ARG%】를 가지는 캐릭터
PRINTL 
FOR COUNT, 0, FLAG:44
	;CSV가 존재하지 않는 캐릭터는 제외
	SIF !EXISTCSV(COUNT, 0)
		CONTINUE
	;아이는 제외
	SIF GROUPMATCH(CSVNAME(COUNT, 0), "아이1", "아이2", "아이3", "아이4", "아이5", "아이6", "아이7", "아이8", "아이9", "아이10")
		CONTINUE
	;그 소질을 가지지 않는 경우는 날린다
	SIF !CSVTALENT(COUNT, ARG, 0)
		CONTINUE
	PRINTFORM %CSVCALLNAME(COUNT,0), 18, LEFT%　
	SIF U++ % 4 == 3
		PRINTL 
NEXT
SIF U % 4 != 0
	PRINTL 
PRINTL 
PRINTFORMW \@ U ? 이상의{U}인이 초기 단계에서[%TALENTNAME:ARG%]를 보유하고 있는 # 초기 상태로 보유하고 있는 캐릭터는 없습니다\@

