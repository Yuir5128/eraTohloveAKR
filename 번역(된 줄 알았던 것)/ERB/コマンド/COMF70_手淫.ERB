;-------------------------------------------------
;수음
;봉사계 커맨드
;-------------------------------------------------
@COM70

;-------------------------------------------------
;특수파생 처리
;-------------------------------------------------
;전회가모유마시기로 노예가모유체질의 경우, 수유손코키로 난다
IF PREVCOM == 18 && TALENT:모유체질
	CALL COM_ABLE528
	SIF RESULT == 1
		JUMP COM528
ENDIF

PRINTL 수음
STR:0 = 수음

;-------------------------------------------------
;실행 판정 처리
;-------------------------------------------------
;모든 명령에 공통의 요소를 고려
;(순종가 높다고 명령에 따르기 쉬운 등)
CALL COM_ORDER

;실행 판정 조작
CALL KOJO_DENIAL_PLUS

;합계를 표시(14이상으로 실행)
PRINT  = 
PRINTV A

V = 14
SIF A < V
	PRINT  < 
SIF A == V
	PRINT  = 
SIF A > V
	PRINT  > 
PRINT 실행치
PRINTV V

;실행 불가대사/실행 판정 조작
SIF A < V
	CALL KOJO_DENIAL

WAIT

;실행 할 수 없다
SIF A < V
	RETURN 0

CALL KOJO_MESSAGE_COM


;실행자가 노예인 플래그
TFLAG:28 = 1



;-------------------------------------------------
;사정게이지 체크
;-------------------------------------------------
B = 600

CALL SHOOT_GAUGE_CHECK_MT

;재장전 처리중은사정게이지는 20분의 1에
SIF CFLAG:PLAYER:9 > 0
	B /= 20

SIF PENIS(PLAYER)
	BASE:PLAYER:사정 += B


;-------------------------------------------------
;소스 추가 처리
;-------------------------------------------------
;소비체력, 기력
LOSEBASE:체력 += 5
LOSEBASE:기력 += 30

SOURCE:애정 += 50
;스킨십선호라면애정의 소스 추가
SIF TALENT:스킨십선호
	SOURCE:애정 += 50

SOURCE:성행동 += 250
SOURCE:성취감 += 50
SOURCE:굴복 += 350
SOURCE:일탈 += 100
SOURCE:반발 += 100

;위 쪽으로 계산한 더러운 데이터
SOURCE:불결 += Y*10 + 60


;-------------------------------------------------
;특수소스 처리
;-------------------------------------------------
;억압, 저항소유에는, 억울소스 추가
SIF TALENT:억압 || TALENT:저항
	SOURCE:울굴 += 200


;-------------------------------------------------
;사정체크
;-------------------------------------------------
CALL SAMEN_CHECK(PLAYER, 1)

;사정시의 처리
IF E >= 1
	;사정처 타입의 설정
	TFLAG:703 = 3
	;사정처 선택의 가능·불가
	TFLAG:704 = 1
	;범용적인 처리는 이 함수로 실시한다
	;(사정게이지의 재계산이나사정경험의 상승과 공통 부분의 표시)
	CALL SAMEN_SHOOT
ENDIF

;-------------------------------------------------
;더러움의 처리
;-------------------------------------------------
CALL COM_STAIN


;-------------------------------------------------
;경험의 처리
;-------------------------------------------------
;게이경험라든지 가산
CALL COM_EXP(4)

;의존도 벡터
TFLAG:33 = 1

;의존도 보정치(주인 또한연모, 음란, 복종로 1. 친애, 창부, 예속로 2)
IF (TALENT:연모 || TALENT:음란 || TALENT:복종) && ASSIPLAY == 0
	TFLAG:30 += 1
ELSEIF (TALENT:친애 || TALENT:창부 || TALENT:예속) && ASSIPLAY == 0
	TFLAG:30 += 2
ENDIF

;봉사 쾌락 플래그
TFLAG:100 = 1


RETURN 1


