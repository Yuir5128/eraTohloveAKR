;-------------------------------------------------
;기승위한다
;주인수계 커맨드
;-------------------------------------------------
@COM262

;-------------------------------------------------
;특수 파생 처리
;-------------------------------------------------
;전회와 이번 조교자가 같음
IF (ASSIPLAY && TFLAG:50) || (ASSIPLAY == 0 && TFLAG:50 == 0)
	;양쪽 모두 삽입하고 있으면 「이륜 꽂아 시킨다」
	IF PREVCOM == 270
		CALL COM_ABLE522
		IF RESULT == 1
			TFLAG:40 = 0
			TFLAG:41 = 6
			TFLAG:49 = 6
			JUMP COM522
		ENDIF
	;조수가 A에 삽입하고 있으면 「샌드위치 시킨다(두 구멍 삽입)」
	ELSEIF PREVCOM == 271
		CALL COM_ABLE520
		IF RESULT == 1
			TFLAG:40 = 0
			TFLAG:41 = 7
			TFLAG:49 = 6
			JUMP COM520
		ENDIF
	;전회 커맨드가 「이륜 꽂아 시킨다」인가 「샌드위치 시킨다」
	ELSEIF PREVCOM == 522 || PREVCOM == 520
		IF TFLAG:41 == 6
			CALL COM_ABLE522
			IF RESULT == 1
				TFLAG:40 = 0
				TFLAG:41 = 6
				TFLAG:49 = 6
				JUMP COM522
			ENDIF
		ELSE
			CALL COM_ABLE520
			IF RESULT == 1
				TFLAG:40 = 0
				TFLAG:41 = 7
				TFLAG:49 = 6
				JUMP COM520
			ENDIF
		ENDIF
	ENDIF
ENDIF

;아무것도 읽지 않고 여기에 왔을 때는 플래그 해제
TFLAG:40 = 0
TFLAG:41 = 0
TFLAG:49 = 0
TFLAG:42 = 0

PRINTL 기승위한다
STR:0 = 기승위한다

;-------------------------------------------------
;실행 판정 처리
;-------------------------------------------------
;모든 명령에 공통의 요소를 고려
;(순종가 높다고 명령에 따르기 쉬운 등)
CALL COM_ORDER

;실행 판정 조작
CALL KOJO_DENIAL_PLUS

;합계를 표시(20이상으로 실행)
PRINT  = 
PRINTV A

V = 20
SIF A < V
	PRINT  < 
SIF A == V
	PRINT  = 
SIF A > V
	PRINT  > 
PRINT 실행치
PRINTV V

;실행 불가입상/실행 판정 조작
SIF A < V
	CALL KOJO_DENIAL

WAIT

;실행 할 수 없다
SIF A < V
	RETURN 0

CALL KOJO_MESSAGE_COM


;스타킹 찌릿찌릿 플래그
IF TEQUIP:특수 2 == 5 || TEQUIP:특수 2 == 6
	TFLAG:55 = 1
ENDIF

;조교자가처녀였던 경우의 상실 플래그(처녀막재생 대응)
TFLAG:19 |= 2

;노예가동정였던 경우의 상실 플래그
SIF TALENT:동정
	TFLAG:160 |= 1
;주인에 의한 섹스로동정상실
SIF TALENT:동정 && (TALENT:연모 || TALENT:친애) && ASSIPLAY == 0
	TFLAG:162 = 1


;-------------------------------------------------
;사정게이지 체크
;-------------------------------------------------
B = 1000

CALL SHOOT_GAUGE_CHECK_MT

;재장전 처리중은사정게이지는 20분의 1에
SIF CFLAG:PLAYER:9 > 0
	B /= 20

SIF PENIS(PLAYER)
	BASE:PLAYER:사정 += B

;-------------------------------------------------
;소스 추가 처리
;-------------------------------------------------
;소비체력, 기력
LOSEBASE:체력 += 50
LOSEBASE:기력 += 100

SOURCE:쾌C += 250

SOURCE:애정 += 300

;스킨십선호라면애정의 소스 추가
SIF TALENT:스킨십선호
	SOURCE:애정 += 50

SOURCE:성행동 += 500
SOURCE:굴복 += 1000
SOURCE:일탈 += 500
SOURCE:반발 += 500

;위 쪽으로 계산한 더러운 데이터
SOURCE:불결 += Y*10 + 60

;-------------------------------------------------
;특수 소스 처리
;-------------------------------------------------
;억압, 저항소유에는, 억울소스 추가
SIF TALENT:억압 || TALENT:저항
	SOURCE:울굴 += 700
	
;정조관념소유로연모, 친애 가져 이외로는 항상반발의 소스를 추가한다(섹스계만)
SIF TALENT:정조관념 && (TALENT:연모 == 0 && TALENT:친애 == 0)
	SOURCE:반발 += 1000

;중독에 의한중독충족의 소스는 먼저 처리해 둔다
;섹스중독를 본다
IF ABL:섹스중독 == 1
	TIMES SOURCE:중독충족, 0.00
ELSEIF ABL:섹스중독 == 2
	TIMES SOURCE:중독충족, 0.30
ELSEIF ABL:섹스중독 == 3
	TIMES SOURCE:중독충족, 0.60
ELSEIF ABL:섹스중독 == 4
	TIMES SOURCE:중독충족, 0.90
ELSEIF ABL:섹스중독 == 5
	TIMES SOURCE:중독충족, 1.20
ELSEIF ABL:섹스중독 == 6
	TIMES SOURCE:중독충족, 1.50
ELSEIF ABL:섹스중독 == 7
	TIMES SOURCE:중독충족, 1.80
ELSEIF ABL:섹스중독 == 8
	TIMES SOURCE:중독충족, 2.30
ELSEIF ABL:섹스중독 == 9
	TIMES SOURCE:중독충족, 3.00
ELSEIF ABL:섹스중독 >= 10
	TIMES SOURCE:중독충족, 5.00
ENDIF

;말자지소유로, 조교자가소인체형였던 경우, 일탈소스 추가
IF TALENT:PLAYER:소인체형 && TALENT:말자지
	SOURCE:일탈 += 500
ENDIF


;-------------------------------------------------
;사정체크
;-------------------------------------------------
CALL SAMEN_CHECK(PLAYER, 1)

;사정시의 처리
IF E >= 1
	;사정처 타입의 설정
	TFLAG:703 = 9
	;사정처 선택의 가능·불가
	TFLAG:704 = 1
	;범용적인 처리는 이 함수로 실시한다
	;(사정게이지의 재계산이나사정경험의 상승과 공통 부분의 표시)
	CALL SAMEN_SHOOT
ENDIF

;-------------------------------------------------
;더러움의 처리
;-------------------------------------------------
;노예의 C⇔조교자의 V와질내의 더러움이 이동
;페니스밴드를 사용하는 경우, 더러움은 이동하지 않는다
SIF PENIS(TARGET)
	CALL COM_STAIN


;-------------------------------------------------
;경험의 처리
;-------------------------------------------------
;조교자의 V경험
EXP:PLAYER:V경험 += 1

;조교자가소인체형로 노예가말자지였던 경우
IF TALENT:PLAYER:소인체형 && TALENT:말자지
	IF EXP:PLAYER:V확장경험 == 0
		EXP:이상경험 += 1
		PRINTL V확장을 강요받아이상경험＋1
		EXP:PLAYER:이상경험 += 1
		PRINTFORML V확장 첫경험으로 %CALLNAME:PLAYER%의이상경험＋1
	ENDIF
	EXP:PLAYER:V확장경험 += 1
	PRINTFORML %CALLNAME:PLAYER%의 V확장경험＋1
ENDIF

;성교경험
EXP:PLAYER:성교경험 += 1
EXP:성교경험 += 1
PRINTS EXPNAME:4
PRINTL +1

;삽입경험
EXP:삽입경험 += 1
PRINTS EXPNAME:17
PRINTL +1

;게이경험라든지 가산
CALL COM_EXP(4)

;의존도 벡터
TFLAG:33 = 1

;의존도 보정치(주인 또한연모, 음란, 복종로 1. 친애, 창부, 예속로 2)
IF (TALENT:연모 || TALENT:음란 || TALENT:복종) && ASSIPLAY == 0
	TFLAG:30 += 1
ELSEIF (TALENT:친애 || TALENT:창부 || TALENT:예속) && ASSIPLAY == 0
	TFLAG:30 += 2
ENDIF

;봉사 쾌락 플래그
TFLAG:100 = 1

RETURN 1



