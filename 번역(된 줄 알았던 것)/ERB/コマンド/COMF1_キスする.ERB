;-------------------------------------------------
;키스한다
;기본계 커맨드
;-------------------------------------------------
@COM1

PRINTL 키스한다
STR:0 = 키스한다

CALL KOJO_MESSAGE_COM

;실행자가 노예인 플래그
TFLAG:28 = 1

;-------------------------------------------------
;사정게이지 체크
;-------------------------------------------------
B = 50

CALL SHOOT_GAUGE_CHECK_MT

;재장전 처리중은사정게이지는 20분의 1에
SIF CFLAG:PLAYER:9 > 0
	B /= 20

SIF PENIS(PLAYER)
	BASE:PLAYER:사정 += B


;-------------------------------------------------
;소스 추가 처리
;-------------------------------------------------
;소비체력, 기력
LOSEBASE:체력 += 0
LOSEBASE:기력 += 30

SOURCE:애정 += 100

;스킨십선호라면애정의 소스 추가
SIF TALENT:스킨십선호
	SOURCE:애정 += 50
;연모계 함락이 끝난 상태라면애정의 소스 추가
SIF TALENT:연모 || TALENT:친애
	TIMES SOURCE:애정, 1.50

SOURCE:성행동 += 10
SOURCE:굴복 += 10
SOURCE:일탈 += 10
SOURCE:반발 += 10

;위 쪽으로 계산한 더러운 데이터
SOURCE:불결 += Y*20 + 10


;-------------------------------------------------
;특수 소스 처리
;-------------------------------------------------
;억압, 저항소유에는, 억울소스 추가
SIF TALENT:억압 || TALENT:저항
	SOURCE:울굴 += 10


;기절로부터 회복시켰을 때는체력기력보너스
IF TALENT:기절
	BASE:체력 += 100
	BASE:기력 += 100
ENDIF

;-------------------------------------------------
;더러움의 처리
;-------------------------------------------------
CALL COM_STAIN


;-------------------------------------------------
;경험의 처리
;-------------------------------------------------
;게이경험라든지 가산
CALL COM_EXP(3)

;애정경험
IF (TALENT:연모 == 1 || TALENT:친애 == 1) && ASSIPLAY == 0
	EXP:애정경험 += 1
	PRINTL EXP:애정경험+1
ENDIF

;노예가고양이혀소유의 경우는, 조교자에게가학쾌락경험를 추가
IF TALENT:TARGET:고양이혀
	EXP:가학쾌락경험 += 1
	PRINTL 가학쾌락경험+1
ENDIF

;봉사 쾌락 플래그
TFLAG:100 = 1

;의존도 벡터
TFLAG:33 = 2

;의존도 보정치(연모로 1, 친애로 2)
IF TALENT:연모 && ASSIPLAY == 0
	TFLAG:30 += 1
ELSEIF TALENT:친애 && ASSIPLAY == 0
	TFLAG:30 += 2
ENDIF


RETURN 1



