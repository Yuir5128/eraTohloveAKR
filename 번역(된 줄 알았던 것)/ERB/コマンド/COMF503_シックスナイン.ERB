;-------------------------------------------------
;식스나인
;파생계 커맨드
;-------------------------------------------------
@COM503

PRINTL 식스나인
STR:0 =식스나인

SELECTCOM = 503
CALL KOJO_MESSAGE_COM

;실행자가 노예인 플래그
TFLAG:28 = 1

;스타킹 찌릿찌릿 플래그
IF TEQUIP:특수 2 == 5 || TEQUIP:특수 2 == 6
	TFLAG:55 = 1
ENDIF

;-------------------------------------------------
;사정게이지 체크
;-------------------------------------------------
B = 1000

CALL SHOOT_GAUGE_CHECK_MT

;재장전 처리중은사정게이지는 20분의 1에
SIF CFLAG:PLAYER:9 > 0
	B /= 20

SIF PENIS(PLAYER)
	BASE:PLAYER:사정 += B

;-------------------------------------------------
;소스 추가 처리
;-------------------------------------------------
;소비체력, 기력
LOSEBASE:체력 += 30
LOSEBASE:기력 += 100

SOURCE:쾌C += 150

;혀를 사용하는 조교에서는, 조교자의혀놀림에 의해, 쾌감의 소스 증가
IF TALENT:PLAYER:혀놀림
	TIMES SOURCE:쾌C , 1.50
ELSEIF TALENT:PLAYER:고양이혀
	TIMES SOURCE:쾌C , 0.75
ENDIF

SOURCE:애정 += 300

;스킨십선호라면애정의 소스 추가
SIF TALENT:스킨십선호
	SOURCE:애정 += 50

SOURCE:성행동 += 450
SOURCE:성취감 += 200
SOURCE:액체 += 80
SOURCE:노출 += 100
SOURCE:굴복 += 800
SOURCE:일탈 += 600
SOURCE:반발 += 600

;위 쪽으로 계산한 더러운 데이터
SOURCE:불결 += Y*80 + 50

;-------------------------------------------------
;특수 소스 처리
;-------------------------------------------------
;억압, 저항소유에는, 억울소스 추가
SIF TALENT:억압 || TALENT:저항
	SOURCE:울굴 += 800


;-------------------------------------------------
;사정체크
;-------------------------------------------------
CALL SAMEN_CHECK(PLAYER, 1)

;사정시의 처리
IF E >= 1
	;사정처 타입의 설정
	TFLAG:703 = 4
	;사정처 선택의 가능·불가
	TFLAG:704 = 1
	;범용적인 처리는 이 함수로 실시한다
	;(사정게이지의 재계산이나사정경험의 상승과 공통 부분의 표시)
	CALL SAMEN_SHOOT
ENDIF

;-------------------------------------------------
;더러움의 처리
;-------------------------------------------------
CALL COM_STAIN

SIF E >= 1
	TFLAG:8 = 1

;-------------------------------------------------
;경험의 처리
;-------------------------------------------------
;펠라경험
IF PENIS(PLAYER)
	PRINTFORML %EXPNAME:22%＋1
	EXP:펠라경험 += 1
ENDIF

;게이경험라든지 가산
CALL COM_EXP(7)

;애정경험
IF (TALENT:연모 == 1 || TALENT:친애 == 1) && ASSIPLAY == 0
	PRINTS EXPNAME:23
	PRINTL +1
	EXP:애정경험 += 1
ENDIF

;노예가고양이혀소유의 경우는, 노예에게가학쾌락경험를 추가
IF TALENT:고양이혀
	EXP:가학쾌락경험 += 1
	PRINTFORML %EXPNAME:32%＋1
ENDIF

;의존도 벡터
TFLAG:33 = 1

;의존도 보정치(주인 또한연모, 음란, 복종로 1. 친애, 창부, 예속로 2)
IF (TALENT:연모 || TALENT:음란 || TALENT:복종) && ASSIPLAY == 0
	TFLAG:30 += 1
ELSEIF (TALENT:친애 || TALENT:창부 || TALENT:예속) && ASSIPLAY == 0
	TFLAG:30 += 2
ENDIF

;봉사 쾌락 플래그
TFLAG:100 = 1

RETURN 1


