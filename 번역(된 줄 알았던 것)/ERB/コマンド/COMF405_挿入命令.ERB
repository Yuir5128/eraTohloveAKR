;-------------------------------------------------
;삽입명령
;조수명령계 커맨드
;-------------------------------------------------
@COM405

;-------------------------------------------------
;특수 파생 처리
;-------------------------------------------------
;전회와 이번 조교자가 같음
IF (ASSIPLAY && TFLAG:50) || (ASSIPLAY == 0 && TFLAG:50 == 0)
	;양쪽 모두 삽입하고 있으면 「이륜 꽂아」
	IF PREVCOM == 102 || PREVCOM == 103 || PREVCOM == 104 || PREVCOM == 105 || PREVCOM == 106
		CALL COM_ABLE517
		IF RESULT == 1
			TFLAG:40 = 1
			TFLAG:41 = 1
			TFLAG:49 = 0
			JUMP COM517
		ENDIF
	;주인이 조수를 범하고 있으면 「연결(노예←조수←주인)」
	ELSEIF PREVCOM == 440 || PREVCOM == 441
		CALL COM_ABLE519
		IF RESULT == 1
			IF PREVCOM == 440
				TFLAG:40 = 11
			ELSE
				TFLAG:40 = 12
			ENDIF
			TFLAG:41 = 1
			TFLAG:49 = 0
			JUMP COM519
		ENDIF
	;노예가 주인을 범하고 있으면 「연결(주인←노예←조수)」
	ELSEIF PREVCOM == 260 || PREVCOM == 261 || PREVCOM == 263 || PREVCOM == 264
		CALL COM_ABLE519
		IF RESULT == 1
			IF PREVCOM == 260 || PREVCOM == 261
				TFLAG:49 = 6
			ELSE
				TFLAG:49 = 7
			ENDIF 
			TFLAG:41 = 1
			TFLAG:40 = 0
			JUMP COM519
		ENDIF
	;주인이 삽입하고 있으면 「샌드위치(두 구멍 삽입)」
	ELSEIF PREVCOM == 113 || PREVCOM == 115
		CALL COM_ABLE515
		IF RESULT == 1
			TFLAG:40 = 2
			TFLAG:41 = 1
			TFLAG:49 = 0
			JUMP COM515
		ENDIF
	;주인이입봉사하고 있으면 「샌드위치」
	ELSEIF PREVCOM == 31 || PREVCOM == 40 || PREVCOM == 280
		CALL COM_ABLE515
		IF RESULT == 1
			IF PREVCOM == 31
				TFLAG:40 = 3
			ELSEIF PREVCOM == 40
				TFLAG:40 = 4
			ELSEIF PREVCOM == 280
				TFLAG:40 = 5
			ENDIF
			TFLAG:41 = 1
			TFLAG:49 = 0
			JUMP COM515
		ENDIF
	;노예가입봉사 당하고 있으면 「샌드위치」
	ELSEIF PREVCOM == 71 || PREVCOM == 72 || PREVCOM == 222
		CALL COM_ABLE515
		IF RESULT == 1
			TFLAG:40 = 0
			TFLAG:41 = 1
			IF PREVCOM == 71
				TFLAG:49 = 8
			ELSEIF PREVCOM == 72
				TFLAG:49 = 10
			ELSEIF PREVCOM == 222
				TFLAG:49 = 9
			ENDIF
			JUMP COM515
		ENDIF
	;전회 커맨드가 「샌드위치」 「연결」 「이륜 꽂아」
	ELSEIF PREVCOM == 515 || PREVCOM == 519 || PREVCOM == 517
		;양쪽 모두 삽입하고 있으면 「이륜 꽂아」
		IF TFLAG:40 == 1
			CALL COM_ABLE517
			IF RESULT == 1
				TFLAG:41 = 1
				TFLAG:49 = 0
				JUMP COM517
			ENDIF
		;주인이 삽입하고 있으면 「샌드위치(두 구멍 삽입)」
		ELSEIF TFLAG:40 == 2
			CALL COM_ABLE515
			IF RESULT == 1
				TFLAG:41 = 1
				TFLAG:49 = 0
				JUMP COM515
			ENDIF
		;주인이 조수를 범하고 있으면 「연결(노예←조수←주인)」
		ELSEIF TFLAG:40 == 11 || TFLAG:40 == 12
			CALL COM_ABLE519
			IF RESULT == 1
				TFLAG:41 = 1
				TFLAG:49 = 0
				JUMP COM519
			ENDIF
		;주인이입봉사하고 있으면 「샌드위치」
		ELSEIF TFLAG:40 == 3 || TFLAG:40 == 4 || TFLAG:40 == 5
			CALL COM_ABLE515
			IF RESULT == 1
				TFLAG:41 = 1
				TFLAG:49 = 0
				JUMP COM515
			ENDIF
		;노예가 주인을 범하고 있으면 「연결(주인←노예←조수)」
		ELSEIF TFLAG:49 == 6 || TFLAG:49 == 7
			CALL COM_ABLE519
			IF RESULT == 1
				TFLAG:40 = 0
				TFLAG:41 = 1
				JUMP COM519
			ENDIF
		;노예가입봉사 당하고 있으면 「샌드위치」
		ELSEIF TFLAG:49 == 8 || TFLAG:49 == 9 || TFLAG:49 == 10
			CALL COM_ABLE515
			IF RESULT == 1
				TFLAG:40 = 0
				TFLAG:41 = 1
				JUMP COM515
			ENDIF
		ENDIF
	ENDIF
ENDIF

;아무것도 읽지 않고 여기에 왔을 때는 플래그 해제
TFLAG:40 = 0
TFLAG:41 = 0
TFLAG:49 = 0
TFLAG:42 = 0

PRINTL 삽입명령
STR:0 = 삽입명령

;-------------------------------------------------
;실행 판정 처리
;-------------------------------------------------
;모든 명령에 공통의 요소를 고려
;(순종가 높다고 명령에 따르기 쉬운 등)
CALL COM_ORDER

;실행 판정 조작
CALL KOJO_DENIAL_PLUS

;합계를 표시(25이상으로 실행)
PRINT  = 
PRINTV A

V = 25
SIF A < V
	PRINT  < 
SIF A == V
	PRINT  = 
SIF A > V
	PRINT  > 
PRINT 실행치
PRINTV V

;실행 불가입상/실행 판정 조작
SIF A < V
	CALL KOJO_DENIAL

WAIT

;실행 할 수 없다
SIF A < V
	RETURN 0

CALL KOJO_MESSAGE_COM

;스타킹 찌릿찌릿 플래그
IF TEQUIP:특수 2 == 5 || TEQUIP:특수 2 == 6
	TFLAG:55 = 1
ENDIF

;처녀상실 플래그
SIF TALENT:처녀 || TALENT:재생처녀
	TFLAG:19 |= 1

;조교자가동정였던 경우의 상실 플래그
SIF TALENT:ASSI:동정
	TFLAG:160 |= 3


;-------------------------------------------------
;사정게이지 체크(조수)
;-------------------------------------------------
B = 1000

CALL SHOOT_GAUGE_CHECK_AT

;재장전 처리중은사정게이지는 20분의 1에
SIF CFLAG:ASSI:9 > 0
	B /= 20

SIF PENIS(ASSI)
	BASE:ASSI:사정 += B

;-------------------------------------------------
;소스 추가 처리
;-------------------------------------------------
;소비체력, 기력
LOSEBASE:체력 += 50
LOSEBASE:기력 += 100

SOURCE:쾌V += 200
SIF ABL:ASSI:기교
	SOURCE:쾌V += 10*ABL:ASSI:기교

SOURCE:통증 += 50
SOURCE:노출 += 20
SOURCE:일탈 += 40
SOURCE:반발 += 40


;-------------------------------------------------
;특수 소스 처리
;-------------------------------------------------
;억압, 저항소유에는, 억울소스 추가
SIF TALENT:억압 || TALENT:저항
	SOURCE:울굴 += 40

;처녀였던 경우는, 고통의 소스와반발의 소스를 추가한다
IF TALENT:처녀 == 1
	SOURCE:통증 += 2000
	SOURCE:반발 += 3000
ENDIF

;재생처녀였던 경우는, 고통의 소스와반발의 소스를 추가한다
IF TALENT:재생처녀 == 1
	SOURCE:통증 += 500
	SOURCE:반발 += 1000
ENDIF

;소인체형로 조수가거근나말자지의 경우, 고통와공포의 소스를 추가
IF TALENT:소인체형
	IF TALENT:ASSI:거근
		SOURCE:통증 += 200
		SOURCE:두려움 += 500
	ELSEIF TALENT:ASSI:말자지
		SOURCE:통증 += 500
		SOURCE:두려움 += 1000
	ENDIF
ENDIF

;조수가말자지의 경우
IF TALENT:ASSI:말자지
	;V확장경험일정 이상으로중독충족의 소스를 추가
	IF EXP:V확장경험 >= 20
		SOURCE:중독충족 += 500
	;V확장경험일정 이하로고통와공포의 소스를 추가
	ELSE
		SOURCE:통증 += 500
		SOURCE:두려움 += 1000
	ENDIF
ENDIF

;조수의기교에 의해쾌V 소스 추가, 하손 지나면통증의 소스 추가
IF ABL:ASSI:2 == 0
	SOURCE:통증 += 100
ELSEIF ABL:ASSI:2 == 1
	SOURCE:통증 += 50
ENDIF
;조수가소인체형로, 조교 대상이소인체형가 아니고도착적도 아니면일탈의 소스 추가
SIF TALENT:소인체형 == 0 && TALENT:ASSI:소인체형 && TALENT:도착적
	SOURCE:일탈 += 50	
;V에의고통는 먼저 처리해 둔다
;EXP:V경험를 본다(처녀의반감는 별도 처리필 보고인 것으로 생략)
IF EXP:V경험 < EXPLV:1
	TIMES SOURCE:통증, 2.50
ELSEIF EXP:V경험 < EXPLV:2
	TIMES SOURCE:통증, 0.80
	TIMES SOURCE:반발, 1.20
ELSEIF EXP:V경험 < EXPLV:3
	TIMES SOURCE:통증, 0.40
ELSEIF EXP:V경험 < EXPLV:4
	TIMES SOURCE:통증, 0.10
ELSEIF EXP:V경험 < EXPLV:5
	TIMES SOURCE:통증, 0.00
ELSEIF EXP:V경험 >= EXPLV:5
	TIMES SOURCE:통증, 0.00
ENDIF

;PALAM:윤활를 본다
IF PALAM:윤활 < PALAMLV:1
	TIMES SOURCE:통증, 1.20
	TIMES SOURCE:반발, 1.50
ELSEIF PALAM:윤활 < PALAMLV:2
	TIMES SOURCE:통증, 0.70
	TIMES SOURCE:반발, 1.20
ELSEIF PALAM:윤활 < PALAMLV:3
	TIMES SOURCE:통증, 0.40
ELSEIF PALAM:윤활 < PALAMLV:4
	TIMES SOURCE:통증, 0.10
ELSEIF PALAM:윤활 >= PALAMLV:4
	TIMES SOURCE:통증, 0.10
ENDIF


;-------------------------------------------------
;더러움의 처리
;-------------------------------------------------
;노예의 V⇔조수의페니스의 더러움이 이동
;페니스밴드를 사용하는 경우, 더러움은 이동하지 않는다
IF PENIS(ASSI)
	STAIN:바기나 |= STAIN:ASSI:페니스
	STAIN:ASSI:페니스 |= STAIN:바기나
	STAIN:질내 |= STAIN:ASSI:페니스
	STAIN:ASSI:페니스 |= STAIN:질내
ENDIF

;-------------------------------------------------
;경험의 처리
;-------------------------------------------------
;V경험
EXP:V경험 += 1
PRINTL V경험＋1

;조수가말자지로 노예가소인체형였던 경우, V확장경험
IF TALENT:소인체형 && TALENT:ASSI:말자지
	IF EXP:V확장경험 == 0
		EXP:이상경험 += 1
		PRINTL V확장 첫경험으로이상경험＋1
	ENDIF
	EXP:V확장경험 += 1
	PRINTL V확장경험＋1
ENDIF

;노예와 조수에성교경험
EXP:성교경험 += 1
EXP:ASSI:성교경험 += 1

;삽입경험
EXP:ASSI:삽입경험 += 1

;조수에쾌C·습득·순종·부정의 구슬
JUEL:ASSI:쾌C += 100
JUEL:ASSI:순종 += 100
JUEL:ASSI:습득 += 100
JUEL:ASSI:부정 += 100
PRINTFORML %CALLNAME:ASSI%에 %PALAMNAME:0%의 구슬＋100
PRINTFORML %CALLNAME:ASSI%에 %PALAMNAME:5%의 구슬＋100
PRINTFORMW %CALLNAME:ASSI%에 %PALAMNAME:8%의 구슬＋100
PRINTFORMW %CALLNAME:ASSI%에 %PALAMNAME:100%의 구슬＋100

;게이경험라든지 가산
CALL COM_EXP(5)

;의존도 벡터
TFLAG:33 = 1


RETURN 1



