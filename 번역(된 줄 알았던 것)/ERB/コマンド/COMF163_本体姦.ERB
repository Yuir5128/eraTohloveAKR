;-------------------------------------------------
;본체간
;SM-고문계 커맨드
;-------------------------------------------------
@COM163

PRINTL 본체간
STR:0 = 본체간

;에로한 이미지
IF RAND:3 == 0
	TFLAG:56 = 1
;무서운 이미지
ELSEIF RAND:2 == 0
	TFLAG:56 = 2
;행복한 이미지
ELSE
	TFLAG:56 = 3
ENDIF

CALL KOJO_MESSAGE_COM


;-------------------------------------------------
;소스 추가 처리
;-------------------------------------------------
;소비체력, 기력
LOSEBASE:체력 += 1000
LOSEBASE:기력 += 1000

IF EXP:뇌간경험 < EXPLV:1
	TIMES LOSEBASE:체력, 1.10
	TIMES LOSEBASE:기력, 2.00
ELSEIF EXP:뇌간경험 < EXPLV:2
	TIMES LOSEBASE:체력, 0.90
	TIMES LOSEBASE:기력, 1.80
ELSEIF EXP:뇌간경험 < EXPLV:3
	TIMES LOSEBASE:체력, 0.80
	TIMES LOSEBASE:기력, 1.50
ELSEIF EXP:뇌간경험 < EXPLV:4
	TIMES LOSEBASE:체력, 0.70
	TIMES LOSEBASE:기력, 1.20
ELSEIF EXP:뇌간경험 < EXPLV:5
	TIMES LOSEBASE:체력, 0.60
	TIMES LOSEBASE:기력, 1.00
ELSE
	TIMES LOSEBASE:체력, 0.50
	TIMES LOSEBASE:기력, 0.50
ENDIF

;에로한 이미지
IF TFLAG:56 == 1
	IF ABL:B감각 == 0
		SOURCE:쾌B += 500
	ELSEIF ABL:B감각 == 1
		SOURCE:쾌B += 800
	ELSEIF ABL:B감각 >= 2
		LOCAL = ABL:B감각 * 500
		SOURCE:쾌B += LOCAL
	ENDIF
	
	IF ABL:C감각 == 0
		SOURCE:쾌C += 500
	ELSEIF ABL:C감각 == 1
		SOURCE:쾌C += 800
	ELSEIF ABL:C감각 >= 2
		LOCAL = ABL:C감각 * 500
		SOURCE:쾌C += LOCAL
	ENDIF
	
	IF VAGINA(TARGET) && ABL:V감각 == 0
		SOURCE:쾌V += 500
	ELSEIF VAGINA(TARGET) && ABL:V감각 == 1
		SOURCE:쾌V += 800
	ELSEIF VAGINA(TARGET) && ABL:V감각 >= 2
		LOCAL = ABL:V감각 * 500
		SOURCE:쾌V += LOCAL
	ENDIF
	
	IF ABL:A감각 == 0
		SOURCE:쾌A += 500
	ELSEIF ABL:A감각 == 1
		SOURCE:쾌A += 800
	ELSEIF ABL:A감각 >= 2
		LOCAL = ABL:A감각 * 500
		SOURCE:쾌A += LOCAL
	ENDIF
	
	SOURCE:성행동 += 750
	SOURCE:성취감 += 1000
	SOURCE:중독충족 += 1000
	SOURCE:액체 += 2500
	SOURCE:욕정 += 3000
;무서운 이미지
ELSEIF TFLAG:56 == 2
	SOURCE:통증 += 3000
	SOURCE:두려움 += 2000
	SOURCE:욕정 += 2000
	SOURCE:노출 += 1500
	SOURCE:굴복 += 3000
;행복한 이미지
ELSEIF TFLAG:56 == 3
	SOURCE:애정 += 2000
	SOURCE:성행동 += 500
	SOURCE:성취감 += 800
	SOURCE:중독충족 += 500
	SOURCE:액체 += 1200
	SOURCE:욕정 += 2000
	SOURCE:순종 += 3000
ENDIF

SOURCE:굴복 += 2000
SOURCE:일탈 += 1200
SOURCE:반발 += 1500

;-------------------------------------------------
;특수 소스 처리
;-------------------------------------------------
;억압, 저항소유에는, 억울소스 추가
SIF TALENT:억압 || TALENT:저항
	SOURCE:울굴 += 1200

;마조를 본다
IF TALENT:마조
	TIMES SOURCE:욕정, 1.50
	TIMES SOURCE:반발, 0.80
ENDIF

;스트레스 가득 증가 10-45(행복한 이미지의 경우는 15-50) 가산
CFLAG:10 += 10

SIF TFLAG:56 == 3
	CFLAG:10 += 5

CFLAG:10 += RAND:36


;-------------------------------------------------
;경험의 처리
;-------------------------------------------------
IF EXP:뇌간경험 == 0
	EXP:이상경험 += 1
	PRINTL 뇌간첫경험으로이상경험＋1
ENDIF

EXP:뇌간경험 += 1

;의존도 벡터
TFLAG:33 = 5

RETURN 1

