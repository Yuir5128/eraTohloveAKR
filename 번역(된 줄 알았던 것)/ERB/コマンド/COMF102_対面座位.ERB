;-------------------------------------------------
;대면좌위
;섹스계 커맨드
;-------------------------------------------------
@COM102

;-------------------------------------------------
;특수 파생 처리
;-------------------------------------------------
;전회와 이번 조교자가 같음
IF (ASSIPLAY && TFLAG:50) || (ASSIPLAY == 0 && TFLAG:50 == 0)
	;V삽입중인가, 전회 커맨드가삽입명령 때 「이륜 꽂아」
	IF PREVCOM == 405
		CALL COM_ABLE517
		IF RESULT == 1
			TFLAG:40 = 1
			TFLAG:41 = 1
			TFLAG:49 = 0
			JUMP COM517
		ENDIF
	;A삽입중인가, 전회 커맨드가 A삽입명령 때 「샌드위치(두 구멍 삽입)」
	ELSEIF PREVCOM == 406
		CALL COM_ABLE515
		IF RESULT == 1
			TFLAG:40 = 1
			TFLAG:41 = 2
			TFLAG:49 = 0
			JUMP COM515
		ENDIF
	;전회의 조교가정상위나대면좌위 때, 삽입 G스포 고문
	ELSEIF PREVCOM == 100 || PREVCOM == 102
		CALL COM_ABLE507
		SIF RESULT == 1
			JUMP COM507
	;전회 커맨드가 이륜삽 밖에 샌드위치
	ELSEIF PREVCOM == 517 || PREVCOM == 515
		;조수가 V삽입하고 있으면 이륜 꽂아
		IF TFLAG:41 == 1
			CALL COM_ABLE517
			IF RESULT == 1
				TFLAG:40 = 1
				TFLAG:41 = 1
				TFLAG:49 = 0
				JUMP COM517
			ENDIF
		;조수가 A삽입하고 있으면 샌드위치
		ELSEIF TFLAG:41 == 2
			CALL COM_ABLE515
			IF RESULT == 1
				TFLAG:40 = 1
				TFLAG:41 = 2
				TFLAG:49 = 0
				JUMP COM515
			ENDIF
		ENDIF
	ENDIF
ENDIF

;아무것도 읽지 않고 여기에 왔을 때는 플래그 해제
TFLAG:40 = 0
TFLAG:41 = 0
TFLAG:49 = 0
TFLAG:42 = 0

PRINTL 대면좌위섹스
STR:0 = 대면좌위섹스

;-------------------------------------------------
;실행 판정 처리
;-------------------------------------------------
;구속되어 있지 않아서, 체격차이가 1 미만 때(주인과 노예의 체형이 같은건 안돼) 마셔 실행 판정을 실시한다
CALL BUILD
IF !TEQUIP:밧줄 && S < 1
	;모든 명령에 공통의 요소를 고려
	;(순종가 높다고 명령에 따르기 쉬운 등)
	CALL COM_ORDER

	;실행 판정 조작
	CALL KOJO_DENIAL_PLUS

	;합계를 표시(40이상으로 실행)
	PRINT  = 
	PRINTV A

	V = 40
	SIF A < V
		PRINT  < 
	SIF A == V
		PRINT  = 
	SIF A > V
		PRINT  > 
	PRINT 실행치
	PRINTV V

	;실행 불가입상/실행 판정 조작
	SIF A < V
		CALL KOJO_DENIAL

	WAIT

	;실행 할 수 없다
	SIF A < V
		RETURN 0
ENDIF

CALL KOJO_MESSAGE_COM

;스타킹 찌릿찌릿 플래그
IF TEQUIP:특수 2 == 5 || TEQUIP:특수 2 == 6
	TFLAG:55 = 1
ENDIF

;처녀상실 플래그
SIF TALENT:처녀 || TALENT:재생처녀
	TFLAG:19 |= 1
;주인에 의한처녀상실
SIF TALENT:처녀 && (TALENT:연모 || TALENT:친애) && ASSIPLAY == 0
	TFLAG:20 = 1

;조교자가동정였던 경우의 상실 플래그
SIF TALENT:PLAYER:동정
	TFLAG:160 |= 2


;-------------------------------------------------
;사정게이지 체크
;-------------------------------------------------
B = 1200

CALL SHOOT_GAUGE_CHECK_MT

;재장전 처리중은사정게이지는 20분의 1에
SIF CFLAG:PLAYER:9 > 0
	B /= 20

SIF PENIS(PLAYER)
	BASE:PLAYER:사정 += B

;-------------------------------------------------
;소스 추가 처리
;-------------------------------------------------
;소비체력, 기력
LOSEBASE:체력 += 50
LOSEBASE:기력 += 100

SOURCE:쾌V += 350

SOURCE:애정 += 200
;스킨십선호라면애정의 소스 추가
SIF TALENT:스킨십선호
	SOURCE:애정 += 50

SOURCE:통증 += 500

SOURCE:중독충족 += 500
SOURCE:순종 += 300
SOURCE:일탈 += 500
SOURCE:반발 += 1000

;위 쪽으로 계산한 더러운 데이터
SOURCE:불결 += Y*10 + 60

;-------------------------------------------------
;특수 소스 처리
;-------------------------------------------------
;억압, 저항소유에는, 억울소스 추가
SIF TALENT:억압 || TALENT:저항
	SOURCE:울굴 += 800

;조교자가 몸집이 컸던 경우는굴복소스 추가
SIF TALENT:PLAYER:큰체구
	SOURCE:굴복 += 800

;조교자가 몸집이 크고, 조교상대가 몸집이 작았던 경우는순종소스 추가
SIF TALENT:PLAYER:큰체구 && TALENT:작은체구
	SOURCE:순종 += 300

;정조관념소유로연모, 친애 가져 이외로는 항상반발의 소스를 추가한다(섹스계만)
SIF TALENT:정조관념 && (TALENT:연모 == 0 && TALENT:친애 == 0)
	SOURCE:반발 += 1000

;처녀였던 경우는, 고통의 소스와반발의 소스를 추가한다
IF TALENT:처녀 == 1
	SOURCE:통증 += 2000
	SOURCE:반발 += 3000
ENDIF

;재생처녀였던 경우는, 고통의 소스와반발의 소스를 추가한다
IF TALENT:재생처녀 == 1
	SOURCE:통증 += 500
	SOURCE:반발 += 1000
ENDIF

;소인체형로 조교자가거근나말자지의 경우, 고통와공포의 소스를 추가
IF TALENT:소인체형
	IF TALENT:PLAYER:거근
		SOURCE:통증 += 200
		SOURCE:두려움 += 500
	ELSEIF TALENT:PLAYER:말자지
		SOURCE:통증 += 500
		SOURCE:두려움 += 1000
	ENDIF
ENDIF

;조교자가말자지의 경우
IF TALENT:PLAYER:말자지
	;V확장경험일정 이상으로중독충족의 소스를 추가
	IF EXP:V확장경험 >= 20
		SOURCE:중독충족 += 500
	;V확장경험일정 이하로고통와공포의 소스를 추가
	ELSE
		SOURCE:통증 += 500
		SOURCE:두려움 += 1000
	ENDIF
ENDIF

;V에의고통는 먼저 처리해 둔다
;EXP:V경험를 본다(처녀의반감는 별도 처리필 보고인 것으로 생략)
IF EXP:V경험 < EXPLV:1
	TIMES SOURCE:통증, 2.50
ELSEIF EXP:V경험 < EXPLV:2
	TIMES SOURCE:통증, 0.80
	TIMES SOURCE:반발, 1.20
ELSEIF EXP:V경험 < EXPLV:3
	TIMES SOURCE:통증, 0.40
ELSEIF EXP:V경험 < EXPLV:4
	TIMES SOURCE:통증, 0.10
ELSEIF EXP:V경험 < EXPLV:5
	TIMES SOURCE:통증, 0.00
ELSEIF EXP:V경험 >= EXPLV:5
	TIMES SOURCE:통증, 0.00
ENDIF

;PALAM:윤활를 본다
IF PALAM:윤활 < PALAMLV:1
	TIMES SOURCE:통증, 1.20
	TIMES SOURCE:반발, 1.50
ELSEIF PALAM:윤활 < PALAMLV:2
	TIMES SOURCE:통증, 0.70
	TIMES SOURCE:반발, 1.20
ELSEIF PALAM:윤활 < PALAMLV:3
	TIMES SOURCE:통증, 0.40
ELSEIF PALAM:윤활 < PALAMLV:4
	TIMES SOURCE:통증, 0.10
ELSEIF PALAM:윤활 >= PALAMLV:4
	TIMES SOURCE:통증, 0.10
ENDIF

;중독에 의한중독충족의 소스는 먼저 처리해 둔다
;섹스중독를 본다
IF ABL:섹스중독 == 1
	TIMES SOURCE:중독충족, 0.00
ELSEIF ABL:섹스중독 == 2
	TIMES SOURCE:중독충족, 0.30
ELSEIF ABL:섹스중독 == 3
	TIMES SOURCE:중독충족, 0.60
ELSEIF ABL:섹스중독 == 4
	TIMES SOURCE:중독충족, 0.90
ELSEIF ABL:섹스중독 == 5
	TIMES SOURCE:중독충족, 1.20
ELSEIF ABL:섹스중독 == 6
	TIMES SOURCE:중독충족, 1.50
ELSEIF ABL:섹스중독 == 7
	TIMES SOURCE:중독충족, 1.80
ELSEIF ABL:섹스중독 == 8
	TIMES SOURCE:중독충족, 2.30
ELSEIF ABL:섹스중독 == 9
	TIMES SOURCE:중독충족, 3.00
ELSEIF ABL:섹스중독 >= 10
	TIMES SOURCE:중독충족, 5.00
ENDIF

;-------------------------------------------------
;사정체크
;-------------------------------------------------
CALL SAMEN_CHECK(PLAYER, 1)

;사정시의 처리
IF E >= 1
	;사정처 타입의 설정
	TFLAG:703 = 1
	;사정처 선택의 가능·불가
	TFLAG:704 = 1
	;범용적인 처리는 이 함수로 실시한다
	;(사정게이지의 재계산이나사정경험의 상승과 공통 부분의 표시)
	CALL SAMEN_SHOOT
ENDIF

;-------------------------------------------------
;더러움의 처리
;-------------------------------------------------
;페니스밴드를 사용하는 경우, 더러움은 이동하지 않는다
IF !PENIS(PLAYER)
ELSE
	CALL COM_STAIN
ENDIF

;-------------------------------------------------
;경험의 처리
;-------------------------------------------------
;V경험
EXP:V경험 += 1
PRINTL V경험＋1

;조교자가말자지로 노예가소인체형였던 경우, V확장경험
IF TALENT:소인체형 && TALENT:PLAYER:말자지
	IF EXP:V확장경험 == 0
		EXP:이상경험 += 1
		PRINTL V확장 첫경험으로이상경험＋1
	ENDIF
	EXP:V확장경험 += 1
	PRINTL V확장경험＋1
ENDIF

;성교경험
EXP:PLAYER:성교경험 += 1
EXP:성교경험 += 1
PRINTS EXPNAME:4
PRINTL +1

;삽입경험
EXP:PLAYER:삽입경험 += 1

;게이경험라든지 가산
CALL COM_EXP(4)

;애정경험
IF TALENT:연모 == 1 && ASSIPLAY == 0
	PRINTS EXPNAME:23
	PRINTL +2
	EXP:애정경험 += 2
ELSEIF TALENT:친애 == 1 && ASSIPLAY == 0
	PRINTS EXPNAME:23
	PRINTL +4
	EXP:애정경험 += 4
ENDIF

;의존도 벡터
TFLAG:33 = 1

;의존도 보정치(연모로 2, 친애로 4)
IF TALENT:연모 && ASSIPLAY == 0
	TFLAG:30 += 2
ELSEIF TALENT:친애 && ASSIPLAY == 0
	TFLAG:30 += 4
ENDIF

RETURN 1



