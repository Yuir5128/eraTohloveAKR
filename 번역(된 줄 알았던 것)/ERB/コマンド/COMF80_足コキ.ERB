;-------------------------------------------------
;풋잡
;봉사계 커맨드
;-------------------------------------------------
@COM80

IF TEQUIP:신발
	PRINTL 신발코키
	STR:0 = 신발코키
ELSE
	PRINTL 풋잡
	STR:0 = 풋잡
ENDIF

;-------------------------------------------------
;실행 판정 처리
;-------------------------------------------------
;모든 명령에 공통의 요소를 고려
;(순종가 높다고 명령에 따르기 쉬운 등)
CALL COM_ORDER

;실행 판정 조작
CALL KOJO_DENIAL_PLUS

;합계를 표시(22이상으로 실행)
PRINT  = 
PRINTV A

V = 22
SIF A < V
	PRINT  < 
SIF A == V
	PRINT  = 
SIF A > V
	PRINT  > 
PRINT 실행치
PRINTV V

;실행 불가대사/실행 판정 조작
SIF A < V
	CALL KOJO_DENIAL

WAIT

;실행 할 수 없다
SIF A < V
	RETURN 0

CALL KOJO_MESSAGE_COM


;실행자가 노예인 플래그
TFLAG:28 = 1


;-------------------------------------------------
;사정게이지 체크
;-------------------------------------------------
B = 500

CALL SHOOT_GAUGE_CHECK_MT

;재장전 처리중은사정게이지는 20분의 1에
SIF CFLAG:PLAYER:9 > 0
	B /= 20

SIF PENIS(PLAYER)
	BASE:PLAYER:사정 += B

;-------------------------------------------------
;소스 추가 처리
;-------------------------------------------------
;소비체력, 기력
LOSEBASE:체력 += 20
LOSEBASE:기력 += 80

SOURCE:성행동 += 700
SOURCE:성취감 += 500
SOURCE:굴복 += 800
SOURCE:일탈 += 400
SOURCE:반발 += 400

;위 쪽으로 계산한 더러운 데이터
SOURCE:불결 += Y*10 + 50

;-------------------------------------------------
;특수 소스 처리
;-------------------------------------------------
;억압, 저항소유에는, 억울소스 추가
SIF TALENT:억압 || TALENT:저항
	SOURCE:울굴 += 600

;-------------------------------------------------
;사정체크
;-------------------------------------------------
CALL SAMEN_CHECK(PLAYER, 1)

;사정시의 처리
IF E >= 1
	;사정처 타입의 설정
	TFLAG:703 = 5
	;사정처 선택의 가능·불가
	;노예의새드가 너무 높으면 선택 불가
	IF ABL:새드끼 >= 5 || TALENT:새드
		TFLAG:704 = 0
	ELSE
		TFLAG:704 = 1
	ENDIF
	;범용적인 처리는 이 함수로 실시한다
	;(사정게이지의 재계산이나사정경험의 상승과 공통 부분의 표시)
	CALL SAMEN_SHOOT
ENDIF
;-------------------------------------------------
;더러움의 처리
;-------------------------------------------------
CALL COM_STAIN


;-------------------------------------------------
;경험의 처리
;-------------------------------------------------
;게이경험라든지 가산
CALL COM_EXP(4)

;의존도 벡터
TFLAG:33 = 1

;의존도 보정치(주인 또한연모, 음란, 복종로 1. 친애, 창부, 예속로 2)
IF (TALENT:연모 || TALENT:음란 || TALENT:복종) && ASSIPLAY == 0
	TFLAG:30 += 1
ELSEIF (TALENT:친애 || TALENT:창부 || TALENT:예속) && ASSIPLAY == 0
	TFLAG:30 += 2
ENDIF

A = 0
;새드소유는가학쾌락경험도 넣는다(지문으로부터 판단)
IF TALENT:새드 || ABL:새드끼 >= 3
	E = 1
	L = 0
	;신발 신고 있다면 한층 더 들어갈 수 있다
	SIF TEQUIP:신발 && ABL:새드끼 >= 4
		E += 1
	;그것이 핀 힐이라면 한층 더 들어갈 수 있다
	SIF TEQUIP:신발 == 4 && ABL:새드끼 >= 5
		E += 1
	;새드끼에 응해가학쾌락경험
	IF ABL:새드끼 >= 3
		E += 2
		L += 1
	ENDIF
	;조교자의마조끼로가학쾌락경험
	IF ABL:PLAYER:9 >= 3
		E += 1
		L += 2
	ENDIF
	PRINT 가학쾌락경험+
	PRINTVL E
	EXP:가학쾌락경험 += E
	IF L > 0
		PRINTFORM %CALLNAME:PLAYER%의
		PRINT 고통쾌락경험+
		PRINTVL L
		EXP:PLAYER:고통쾌락경험 += L
		TFLAG:27 = L
	ENDIF
ENDIF


TFLAG:100 = 1

RETURN 1



