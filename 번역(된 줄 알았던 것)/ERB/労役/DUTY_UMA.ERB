;====================================================================
;DUTY_UMA.ERB 내번-마 당번
;1날 1조 밖에 낼 수 없다
;내번에 낸 캐릭터는 그 날 1일 구속된다
;====================================================================
@DUTY_UMA
;각각 말당번 담당의 남사의 등록 번호를 보유 초기치-1
#DIM DYNAMIC UMA_A = -1
#DIM DYNAMIC UMA_B = -1


DRAWLINE
PRINTL 【 내 번-마 당번 】
PRINTL 
PRINTL 노예에게 2명 1조로 페어를 짜게 해 동안차례 「말당번」을 실시하게 한다.
PRINTL 기력의 최대치가 상승한다
PRINTL 
PRINTL ■ 당번 내용
PRINTL 　:[마 당번]
PRINTL 
PRINTL ■ 선택되고 있는 추가 옵션
PRINTL 　:＜이용 가능한 옵션은 없습니다＞
DRAWLINE
PRINTL 

;--------------------------------------------------------
;내번 시키는 노예를 선택
;--------------------------------------------------------
PRINTL
PRINTL 마 당번을 시키는 남사를 2명, 선택해 주세요
PRINTL
$INPUT_LOOP_START
;양쪽 모두 선택이 끝난 상태
IF UMA_A > 0 && UMA_B > 0
	PRINTFORML [{UMA_A}]%CALLNAME:UMA_A%를 제외한다
	PRINTFORML [{UMA_B}]%CALLNAME:UMA_B%를 제외한다
	PRINTL
ELSE
	REPEAT CHARANUM
		;말당번 할 수 있는 플래그가 서 있지 않은 남사는 표시하지 않는다
		SIF (CFLAG:COUNT:151 & 256) == 0
			CONTINUE
		PRINTFORM [{COUNT}]%CALLNAME:COUNT%
		IF UMA_A == COUNT || UMA_B == COUNT
			PRINTL 를 제외한다
		ELSE
			PRINTL 를 선택
		ENDIF
	REND
ENDIF

;양쪽 모두 선택이 끝난 상태
SIF UMA_A > 0 && UMA_B > 0
	PRINTL [900] - 현재의 2명이 확정
PRINTL [999] - 돌아온다
$INPUT_LOOP
INPUT
IF RESULT == 900

ELSEIF RESULT == 999
	RETURN 0
ELSEIF (CFLAG:RESULT:151 & 256) == 0
	GOTO INPUT_LOOP
ELSE
	;선택 끝난 캐릭터를 클릭 했을 경우
	IF RESULT == UMA_A
		UMA_A = -1
		;앞당기기 처리(UMA_B에 0이 들어가도 대입한다)
		UMA_A = UMA_B
	;선택 끝난 캐릭터를 클릭 했을 경우
	ELSEIF RESULT == UMA_B
		UMA_B = -1
	ELSE
		;비어있는 (분)편에게 대입한다
		IF UMA_A == -1
			UMA_A = RESULT
		ELSEIF UMA_B == -1
			UMA_B = RESULT
		ENDIF
		
		;등록 번호가 작은 (분)편을 A로 한다
		IF UMA_A > UMA_B
			LOCAL = UMA_A
			UMA_A = UMA_B
			UMA_B = LOCAL
		ENDIF
	ENDIF
	GOTO INPUT_LOOP_START
ENDIF

;--------------------------------------------------------
;말당번을 실행한다
;--------------------------------------------------------
;PRINTFORML %CALLNAME:UMA_A%과 %CALLNAME:UMA_B%가, 말당번을 개시했습니다

;노역 플래그를 말당번으로 설정
CFLAG:UMA_A:12 = 5
CFLAG:UMA_B:12 = 5

;지문, 대사 호출을 위해서(때문에) 일시적으로 현재의 조교 대상을 퇴피
LOCAL = TARGET
TFLAG:13 = 84

UCHIBAN_A = UMA_A
UCHIBAN_B = UMA_B

;현재가 기수일눈이라면 UCHIBAN_A의 (분)편을 부른다
IF DAY % 2 == 1
	TARGET = UMA_A
	;이벤트대사(내번개시시);지문과 A의대사
	CALL KOJO_MESSAGE_EVENT
;현재가 짝수 일째라면 UCHIBAN_B의 (분)편을 부른다
ELSE
	TARGET = UMA_B
	;이벤트대사(내번개시시);B의대사
	CALL KOJO_MESSAGE_EVENT
ENDIF

TARGET = LOCAL
PRINTL 

;말당번 담당남사가 조교 대상이나 조수의 경우는, 조교 대상이나 조수로부터 제외한다
SIF UCHIBAN_A == TARGET || UCHIBAN_B == TARGET
	TARGET = -1
SIF UCHIBAN_A == ASSI || UCHIBAN_B == ASSI
	ASSI = -1

;내번개시가 끝난 플래그(말당번=4)를 세운다
FLAG:161 |= 4


;====================================================================
;종료시의 처리
;====================================================================
@END_DUTY_UMA
#DIM DYNAMIC UMA_A = -1
#DIM DYNAMIC UMA_B = -1

REPEAT CHARANUM
	IF CFLAG:COUNT:12 == 5
		IF UMA_A == -1
			UMA_A = COUNT
		ELSE
			UMA_B = COUNT
		ENDIF
	ENDIF
REND

DRAWLINE

;지문, 대사 호출을 위해서(때문에) 일시적으로 현재의 조교 대상을 퇴피
LOCAL = TARGET
TFLAG:13 = 85

UCHIBAN_A = UMA_A
UCHIBAN_B = UMA_B


;필연적으로, 내번개시시에 불리지 않았던 (분)편의 지문과대사이 불립니다

;현재가 기수일눈이라면 UCHIBAN_A의 (분)편을 부른다
IF DAY % 2 == 1
	TARGET = UMA_A
	;이벤트대사(내번종료시);지문과 A의대사
	CALL KOJO_MESSAGE_EVENT
;현재가 짝수 일째라면 UCHIBAN_B의 (분)편을 부른다
ELSE
	TARGET = UMA_B
	;이벤트대사(내번종료시);지문과 B의대사
	CALL KOJO_MESSAGE_EVENT
ENDIF

TARGET = LOCAL

PRINTL 


;--------------------------------------------------------
;습득의 구슬
;--------------------------------------------------------
JUEL:UMA_A:습득 += 200
JUEL:UMA_A:굴복 += 50
JUEL:UMA_A:부정 += 100
JUEL:UMA_B:습득 += 200
JUEL:UMA_B:굴복 += 50
JUEL:UMA_B:부정 += 100
PRINTFORML %CALLNAME:UMA_A%과 %CALLNAME:UMA_B%에습득의 구슬＋{200}
PRINTFORML %CALLNAME:UMA_A%과 %CALLNAME:UMA_B%에굴복의 구슬＋{50}
PRINTFORML %CALLNAME:UMA_A%과 %CALLNAME:UMA_B%에부정의 구슬＋{100}


;--------------------------------------------------------
;기력의 최대치 업
;확정입니다 (웃음)
;--------------------------------------------------------
;오르는 값은 20-30의 랜덤
LOCAL = RAND:10 + 20
MAXBASE:UMA_A:기력 += LOCAL
PRINTFORML %CALLNAME:UMA_A%의기력의 최대치＋{LOCAL}

LOCAL = RAND:10 + 20
MAXBASE:UMA_B:기력 += LOCAL
PRINTFORML %CALLNAME:UMA_B%의기력의 최대치＋{LOCAL}


;--------------------------------------------------------
;체력의 감소
;--------------------------------------------------------
;체력의 감소
BASE:UMA_A:체력 -= 500
BASE:UMA_B:체력 -= 500

;플래그의 리셋트
CFLAG:UMA_A:12 = 0
CFLAG:UMA_B:12 = 0

;내번개시가 끝난 플래그(말당번=4)를 떨어뜨린다
SIF FLAG:161 & 4
	FLAG:161 -= 4

DRAWLINE





