;-------------------------------------------------
;개체차 관련의 함수
;-------------------------------------------------
;-------------------------------------------------
;사양 메모
;※CSV의 소질 우선의 비트가 온 때
;【소인체형/작은체구/큰체구/술꾼/알콜쓰레기/아이사이즈/단소/포경/요리치/요리고수】가 CSV로 설정되어 있는 경우는 그 쪽이 우선되고
;상반되는 소질이 붙을 것은 없다
;
;인수:ARG… 캐릭터 등록 번호
;
;FLAG:3 = ▲개체차 설정 플래그
;1:CSV의 소질 우선 |2:성전환 |4:반음반양 후타나리 |8:오메가 |16:쇼타화 |32:대형화 |64:젖기 쉬움 |128:술의 약함
;|256:둔감 민감 |512:페니스의 크기 |1024:페니스의 특징 |2048:가슴의 크기 |4096:그 외
;
;CFLAG:4   = ▲개체차 기록 플래그
;1형목개체차 플래그 온 오프(0,개체차 없음 1,CSV의 소질 우선 2,개체차 우선)
;2		성별(0,남자 1,(여자) 2,반음반양 3,후타나리 4,컨트보이 5,쉬메일)
;3		성별 오메가버스(0,없음 1,오메가)
;4		체격(0,없음 1,소인체형 2,작은체구 3,보통체형 4,큰체구)
;5		젖기 쉬움(0,없음 1,젖기쉬움 3,보통 3,젖기어려움)
;6		술의 약함(0,없음 1,술꾼 3,보통 3,알콜쓰레기)
;7		C둔감민감(0,없음 1,C둔감,2,C민감)
;8		V둔감민감(0,없음 1,V둔감,2,V민감)
;9		A둔감민감(0,없음 1,A둔감,2,A민감)
;10		B둔감민감(0,없음 1,B둔감,2,B민감 3,함몰유두)
;11		페니스의 크기(0,없음 1,아이사이즈 2,단소 3,보통 4,거근 5,말자지)
;12		페니스의 특징(0,없음 1,조루 2,지루 3,절륜 4,포경)
;13		가슴의 크기(0,없음 1,절벽 2,빈유 3,(평유) 4,거유 5,폭유)
;14		그 외(0,없음 1,음모없음 2,요리치 3,독약내성 4,모유체질)
;
;※어디까지나 개체차의 처리를 할 때에 붙은 것만을 기록해,원래 CSV로 설정되어 있는 것은 기록하지 않는다
; 다만 중복 해 기록되는 경우도 있다(예:CSV로포경의 캐릭터가 개체차에 의해포경를 얻었을 경우)
;-------------------------------------------------
@DIFF_MAIN(ARG)
;만약을 위해 초기화
CFLAG:ARG:4 = 0

;성별 관련
IF (FLAG:3 & 2) || (FLAG:3 & 4)
	CALL DIFF_SEX(ARG)
	CFLAG:ARG:4 += (POWER(10,1) * RESULT)
ENDIF

IF (FLAG:3 & 8) && MALE(ARG)
	CALL DIFF_SEX_OMEGA(ARG)
	CFLAG:ARG:4 += (POWER(10,2) * RESULT)
ENDIF

;체격 관련
IF (FLAG:3 & 16) || (FLAG:3 & 32)
	CALL DIFF_BODY(ARG)
	CFLAG:ARG:4 += (POWER(10,3) * RESULT)
ENDIF

;젖기 쉬움
IF (FLAG:3 & 64)
	CALL DIFF_WET(ARG)
	CFLAG:ARG:4 += (POWER(10,4) * RESULT)
ENDIF

;술의 약함
IF (FLAG:3 & 128)
	CALL DIFF_DRINK(ARG)
	CFLAG:ARG:4 += (POWER(10,5) * RESULT)
ENDIF

;민감 둔감
;부위 마다 3 분의 1의 확률로 부여
IF (FLAG:3 & 256)
	;C
	IF RAND:3 == 0
		CALL DIFF_WEAK_C(ARG)
		CFLAG:ARG:4 += (POWER(10,6) * RESULT)
	ENDIF
	;V
	IF VAGINA(ARG) && RAND:3 == 0
		CALL DIFF_WEAK_V(ARG)
		CFLAG:ARG:4 += (POWER(10,7) * RESULT)
	ENDIF
	
	;A
	IF RAND:3 == 0
		CALL DIFF_WEAK_A(ARG)
		CFLAG:ARG:4 += (POWER(10,8) * RESULT)
	ENDIF
	
	;B
	IF RAND:3 == 0
		CALL DIFF_WEAK_B(ARG)
		CFLAG:ARG:4 += (POWER(10,9) * RESULT)
	ENDIF

ENDIF

;페니스의 크기
IF (FLAG:3 & 512) && PENIS(ARG)
	CALL DIFF_PENIS_SIZE(ARG)
	CFLAG:ARG:4 += (POWER(10,10) * RESULT)
ENDIF

;페니스의 특징
IF (FLAG:3 & 1024) && PENIS(ARG)
	CALL DIFF_PENIS_CHARA(ARG)
	CFLAG:ARG:4 += (POWER(10,11) * RESULT)
ENDIF

;가슴의 크기
IF (FLAG:3 & 2048) && MALE(ARG) == 0
	CALL DIFF_BREAST_SIZE(ARG)
	CFLAG:ARG:4 += (POWER(10,12) * RESULT)
ENDIF


;그 외
IF (FLAG:3 & 4096)
	CALL DIFF_ANOTHER(ARG)
	CFLAG:ARG:4 += (POWER(10,13) * RESULT)
ENDIF

;어떠한 개체차를 얻었을 경우
IF CFLAG:ARG:4 > 0
	
	;개체차 플래그 온 오프
	;CSV의 소질 우선
	IF (FLAG:3 & 1)
		CFLAG:ARG:4 += (POWER(10,0) * 1)
	;개체차 우선
	ELSE
		CFLAG:ARG:4 += (POWER(10,0) * 2)
	ENDIF

	PRINTL 
	PRINTL 
	PRINTL ……!
	PRINTFORML 맞아들인 %CALLNAME:ARG%는,아무래도 통상과는 조금 다른 개체인 것 같다
	PRINTFORM 얻은 소질：
				CALL DIFF_TALENT_NAME(ARG)
	PRINTL
	PRINTL 
	
ENDIF


;-------------------------------------------------
;이하,개체차를 얻는 처리
;-------------------------------------------------
;성별(남자 (여자) 반음반양 후타나리 컨트보이 쉬메일)
@DIFF_SEX(ARG)
TALENT:ARG:남자 = 0
TALENT:ARG:반음반양 = 0
TALENT:ARG:후타나리 = 0
TALENT:ARG:컨트보이 = 0
TALENT:ARG:쉬메일 = 0

TALENT:ARG:처녀 = 0
TALENT:ARG:동정 = 0

;반음반양와후타나리와컨트보이와쉬메일
IF (FLAG:3 & 4) && RAND:10 == 0
	IF RAND:4 == 0
		TALENT:ARG:반음반양 = 1
		TALENT:ARG:처녀 = 1
		TALENT:ARG:동정 = 1
		RETURN 2
	ELSEIF RAND:3 == 0
		TALENT:ARG:후타나리 = 1
		TALENT:ARG:처녀 = 1
		TALENT:ARG:동정 = 1
		RETURN 3
	ELSEIF RAND:2 == 0
		TALENT:ARG:컨트보이 = 1
		TALENT:ARG:처녀 = 1
		RETURN 4
	ELSE
		TALENT:ARG:쉬메일 = 1
		TALENT:ARG:동정 = 1
		RETURN 5
	ENDIF
;여자
ELSEIF (FLAG:3 & 2) && RAND:5 == 0
	TALENT:ARG:처녀 = 1
	RETURN 1
;남자
ELSE
	TALENT:ARG:남자 = 1
	TALENT:ARG:동정 = 1
	RETURN 0
ENDIF

;모순되는 소질을 삭제
SIF VAGINA(ARG) == 0
	CALL CLEAR_TALENT_VAGINA(ARG)
SIF PENIS(ARG) == 0
	CALL CLEAR_TALENT_PENIS(ARG,3)
SIF MALE(ARG) == 1
	CALL CLEAR_TALENT_BREAST(ARG)


;오메가성 ※남자나반음반양나컨트보이 때만
@DIFF_SEX_OMEGA(ARG)
IF RAND:10 == 0
	TALENT:ARG:오메가 = 1
	;발정기의 카운트 0-6으로 랜덤으로 초기치를 부여
	CFLAG:ARG:700 = RAND:7
	RETURN 1
ELSE
	RETURN 0
ENDIF

;체격(소인체형 작은체구 큰체구)
@DIFF_BODY(ARG)
IF RAND:10 == 0
	;큰체구/작은체구/소인체형를 가지고 있다
	IF CSVTALENT(NO:ARG, 113 ,0) || CSVTALENT(NO:ARG, 100 ,0) || CSVTALENT(NO:ARG, 142 ,0)
		;CSV의 소질을 우선적으로 비트온
		SIF (FLAG:3 & 1)
			RETURN 0
		
		TALENT:ARG:소인체형 = 0
		TALENT:ARG:작은체구 = 0
		TALENT:ARG:큰체구 = 0
		
		;보통체형이 된다
		SIF RAND:4 == 0
			RETURN 3
	ENDIF
	
	IF (FLAG:3 & 32) && RAND:3 == 0
		TALENT:ARG:큰체구 = 1
		RETURN 4
	ELSEIF RAND:2 == 0
		TALENT:ARG:작은체구 = 1
		RETURN 2
	ELSE
		TALENT:ARG:소인체형 = 1
		RETURN 1
	ENDIF
ELSE
	;그대로
	RETURN 0
ENDIF

;젖기 쉬움(젖기쉬움 젖기어려움)
@DIFF_WET(ARG)
IF RAND:10 == 0
	;젖기쉬움/젖기어려움를 가지고 있다
	;※현상 초기치로 가지고 있는 캐릭터는 없다
	IF CSVTALENT(NO:ARG, 42 ,0) || CSVTALENT(NO:ARG, 43 ,0)
		;CSVの素質優先ビットオン
		SIF (FLAG:3 & 1)
			RETURN 0
		
		TALENT:ARG:젖기어려움 = 0
		TALENT:ARG:젖기쉬움 = 0
		
		;보통으로 된다
		SIF RAND:3 == 0
			RETURN 2
	ENDIF
	
	IF RAND:2 == 0
		TALENT:ARG:젖기쉬움 = 1
		RETURN 1
	ELSE
		TALENT:ARG:젖기어려움 = 1
		RETURN 3
	ENDIF
ELSE
	;붙지 않는다
	RETURN 0
ENDIF

;술의 약함(술꾼 알콜쓰레기)
@DIFF_DRINK(ARG)
IF RAND:10 == 0
	;술꾼/알콜쓰레기를 가지고 있다
	IF CSVTALENT(NO:ARG, 48 ,0) || CSVTALENT(NO:ARG, 49 ,0)
		;CSV의 소질 우선 비트 온
		SIF (FLAG:3 & 1)
			RETURN 0
		
		TALENT:ARG:술꾼 = 0
		TALENT:ARG:알콜쓰레기 = 0
		
		;보통으로 된다
		SIF RAND:3 == 0
			RETURN 2
	ENDIF
	
	IF RAND:2 == 0
		TALENT:ARG:술꾼 = 1
		RETURN 1
	ELSE
		TALENT:ARG:알콜쓰레기 = 1
		RETURN 3
	ENDIF
ELSE
	;붙지 않는다
	RETURN 0
ENDIF

;C둔감민감
@DIFF_WEAK_C(ARG)
IF RAND:2 == 0
	SIF TALENT:ARG:C민감
		TALENT:ARG:C민감 = 0
	
	TALENT:ARG:C둔감 = 1
	RETURN 1
ELSE
	SIF TALENT:ARG:C둔감
		TALENT:ARG:C둔감 = 0
	
	TALENT:ARG:C민감 = 1
	RETURN 2
ENDIF

;V둔감민감
@DIFF_WEAK_V(ARG)
IF RAND:2 == 0
	SIF TALENT:ARG:V민감
		TALENT:ARG:V민감 = 0
	
	TALENT:ARG:V둔감 = 1
	RETURN 1
ELSE
	SIF TALENT:ARG:V둔감
		TALENT:ARG:V둔감 = 0
	
	TALENT:ARG:V민감 = 1
	RETURN 2
ENDIF

;A둔감민감
@DIFF_WEAK_A(ARG)
IF RAND:2 == 0
	SIF TALENT:ARG:A민감
		TALENT:ARG:A민감 = 0
	
	TALENT:ARG:A둔감 = 1
	RETURN 1
ELSE
	SIF TALENT:ARG:A둔감
		TALENT:ARG:A둔감 = 0
	
	TALENT:ARG:A민감 = 1
	RETURN 2
ENDIF

;B둔감민감
@DIFF_WEAK_B(ARG)
IF RAND:3 == 0
	SIF TALENT:ARG:B민감
		TALENT:ARG:B민감 = 0
	
	TALENT:ARG:B둔감 = 1
	RETURN 1
ELSEIF RAND:2 == 0
	SIF TALENT:ARG:B둔감
		TALENT:ARG:B둔감 = 0
	
	TALENT:ARG:B민감 = 1
	RETURN 2
ELSE
	TALENT:ARG:함몰유두 = 1
	RETURN 3
ENDIF


;페니스의 크기(아이사이즈 단소 거근 말자지)
@DIFF_PENIS_SIZE(ARG)
IF RAND:3 == 0
	;;아이사이즈/단소/거근/말자지를 가지고 있다
	IF CSVTALENT(NO:ARG, 114 ,0) || CSVTALENT(NO:ARG, 115 ,0) || CSVTALENT(NO:ARG, 116 ,0) || CSVTALENT(NO:ARG, 117 ,0)
		;CSVの素質優先ビットオン
		SIF (FLAG:3 & 1)
			RETURN 0
		
		;페니스의 크기 관련의 소질 삭제
		CALL CLEAR_TALENT_PENIS(ARG,1)
		
		;보통으로 된다
		SIF RAND:5 == 0
			RETURN 3
	ENDIF

	IF RAND:4 == 0
		TALENT:ARG:아이사이즈 = 1
		RETURN 1
	ELSEIF RAND:3 == 0
		TALENT:ARG:단소 = 1
		RETURN 2
	ELSEIF RAND:2 == 0
		TALENT:ARG:거근 = 1
		RETURN 4
	ELSE
		TALENT:ARG:말자지 = 1
		RETURN 5
	ENDIF
ELSE
	;그대로
	RETURN 0
ENDIF

;페니스의 특징(조루 지루 절륜 포경)
@DIFF_PENIS_CHARA(ARG)
IF RAND:5 == 0
	IF RAND:4 == 0
		SIF TALENT:ARG:지루
			TALENT:ARG:지루 = 0
		
		TALENT:ARG:조루 = 1
		RETURN 1
	ELSEIF RAND:3 == 0
		SIF TALENT:ARG:조루
			TALENT:ARG:조루 = 0
		
		TALENT:ARG:지루 = 1
		RETURN 2
	ELSEIF RAND:2 == 0
		TALENT:ARG:절륜 = 1
		RETURN 3
	ELSE
		;CSVの素質優先ビットオン　포경を持っている
		SIF (FLAG:3 & 1) && CSVTALENT(NO:ARG, 119 ,0)
			RETURN 0
		
		TALENT:ARG:포경 = 1
		RETURN 4
	ENDIF
ELSE
	RETURN 0
ENDIF

;가슴의 크기(절벽 빈유 거유 폭유)
@DIFF_BREAST_SIZE(ARG)
IF RAND:3 == 0
	;가슴의 크기 관련의 소질 삭제
	CALL CLEAR_TALENT_BREAST(ARG)
	
	IF RAND:4 == 0
		TALENT:ARG:절벽 = 1
		RETURN 1
	ELSEIF RAND:3 == 0
		TALENT:ARG:빈유 = 1
		RETURN 2
	ELSEIF RAND:2 == 0
		TALENT:ARG:거유 = 1
		RETURN 4
	ELSE
		TALENT:ARG:폭유 = 1
		RETURN 5
	ENDIF
ELSE
	;평유
	RETURN 3
ENDIF

;그 외(음모없음 요리치 독약내성 모유체질)
@DIFF_ANOTHER(ARG)
IF RAND:10 == 0
	IF RAND:4 == 0
		TALENT:ARG:음모없음 = 1
		RETURN 1
	ELSEIF RAND:3 == 0
		;CSVの素質優先ビットオン　요리고수を持っている
		SIF (FLAG:3 & 1) && CSVTALENT(NO:ARG, 59 ,0)
			RETURN 0
		
		SIF TALENT:ARG:요리고수
			TALENT:ARG:요리고수 = 0
		
		TALENT:ARG:요리치 = 1
		RETURN 2
	ELSEIF RAND:2 == 0
		TALENT:ARG:독약내성 = 1
		RETURN 3
	ELSE
		;남자모유취득 콘피그오후
		SIF (FLAG:2 & 256) == 0
			RETURN 0
			
		TALENT:ARG:모유체질 = 1
		RETURN 4
	ENDIF
ELSE
	;붙지 않는다
	RETURN 0
ENDIF

;-------------------------------------------------
;함수명:@DIFF_GET_TALENT(ARG:0,ARG:1)
;개요:;구입시에 취득한 소질을 요구하는 함수
;인수:ARG:0… 캐릭터 등록 번호
;　　　：ARG:1…CFLAG:4의 몇 자리수째인가(1-14)
;반환값:CFLAG:4에 대해,아래와 같이 소질에 할당되어지고 있는 숫자
;비고:문중 함수
;

;CFLAG:4   = ▲개체차 기록 플래그
;1형목개체차 플래그 온 오프(0,개체차 없음 1,CSV의 소질 우선 2,개체차 우선)
;2		성별(0,남자 1,(여자) 2,반음반양 3,후타나리 4,컨트보이 5,쉬메일)
;3		성별 오메가버스(0,없음 1,오메가)
;4		체격(0,없음 1,소인체형 2,작은체구 3,보통체형 4,큰체구)
;5		젖기 쉬움(0,없음 1,젖기쉬움 3,보통 3,젖기어려움)
;6		술의 약함(0,없음 1,술꾼 3,보통 3,알콜쓰레기)
;7		C둔감민감(0,없음 1,C둔감,2,C민감)
;8		V둔감민감(0,없음 1,V둔감,2,V민감)
;9		A둔감민감(0,없음 1,A둔감,2,A민감)
;10		B둔감민감(0,없음 1,B둔감,2,B민감 3,함몰유두)
;11		페니스의 크기(0,없음 1,아이사이즈 2,단소 3,보통 4,거근 5,말자지)
;12		페니스의 특징(0,없음 1,조루 2,지루 3,절륜 4,포경)
;13		가슴의 크기(0,없음 1,절벽 2,빈유 3,(평유) 4,거유 5,폭유)
;14		그 외(0,없음 1,음모없음 2,요리치 3,독약내성 4,모유체질)
;
;-------------------------------------------------
@DIFF_GET_TALENT(ARG:0, ARG:1)
#FUNCTION

;분명하게 이상한 것은 연주한다
SIF ARG:1 <= 0 || ARG:1 > 14
	RETURNF 100

LOCAL:1 = ARG:1

LOCAL = INTTU(CFLAG:(ARG:0):4, ARG:1)

;분명하게 이상한 것은 연주한다
SIF LOCAL > 5
	RETURNF 50

;원래 CSV로 가지고 있는 것은 +10한 것을 돌려준다(개체차로 얻은 소질과 원래 CSV로 가지고 있는 소질이 감쌌다고 하는 상황)
IF LOCAL > 0
	;체형
	IF ARG:1 == 4
		;소인체형　작은체구　큰체구
		SIF (CSVTALENT(NO:(ARG:0), 142 ,0) && LOCAL == 1) || (CSVTALENT(NO:(ARG:0), 100 ,0) && LOCAL == 2) || (CSVTALENT(NO:(ARG:0), 113 ,0) && LOCAL == 4)
			LOCAL += 10
	;술의 약함
	ELSEIF ARG:1 == 6
		;술꾼　알콜쓰레기
		SIF (CSVTALENT(NO:(ARG:0), 48 ,0) && LOCAL == 1) || (CSVTALENT(NO:(ARG:0), 49 ,0) && LOCAL == 3)
			LOCAL += 10
	;페니스의 크기
	ELSEIF ARG:1 == 11
		;아이사이즈　단소
		SIF (CSVTALENT(NO:(ARG:0), 114 ,0) && LOCAL == 1 )|| (CSVTALENT(NO:(ARG:0), 115 ,0) && LOCAL == 2)
			LOCAL += 10
	;페니스의 특징
	ELSEIF ARG:1 == 12
		;포경
		SIF CSVTALENT(NO:(ARG:0), 119 ,0) && LOCAL == 4
			LOCAL += 10
	;음모없음 독약내성
	ELSEIF ARG:1 == 14
		SIF (CSVTALENT(NO:(ARG:0), 118 ,0) && LOCAL ==1) || (CSVTALENT(NO:(ARG:0), 56 ,0) && LOCAL ==3)
			LOCAL += 10
	;일단 보류
	;성별(원래로부터남자의 경우도+10되어 돌아간다)
	;ELSEIF ARG:1 == 2
	;	SIF (CSVTALENT(NO:(ARG:0), 140 ,0) && LOCAL == 0) || (CSVTALENT(NO:(ARG:0), 137 ,0) && LOCAL == 2) || (CSVTALENT(NO:(ARG:0), 139 ,0) && LOCAL == 3)
	;		LOCAL += 10

	ENDIF
ENDIF

RETURNF LOCAL

;-------------------------------------------------
;함수명 @DIFF_GET_BUY(ARG)
;개요:;구입시에 취득한 소질을 다시 붙이는 함수
;인수:ARG… 캐릭터 등록 번호
;비고:되감기약용의 처리
;-------------------------------------------------
@DIFF_GET_BUY(ARG)

;개체차 나시라면 조속히 연주한다
SIF DIFF_GET_TALENT(ARG,1) == 0
	RETURN 0

LOCAL = DIFF_GET_TALENT(ARG, 2)
;性別
IF LOCAL > 0 && LOCAL < 10 
	TALENT:ARG:남자 = 0
	TALENT:ARG:반음반양 = 0
	TALENT:ARG:후타나리 = 0
	TALENT:ARG:컨트보이 = 0
	TALENT:ARG:쉬메일 = 0
	
	TALENT:ARG:처녀 = 0
	TALENT:ARG:동정 = 0
	
	SELECTCASE LOCAL
		;남자
		CASE 0
			TALENT:ARG:남자 = 1
			TALENT:ARG:동정 = 1
		;여자
		CASE 1
			TALENT:처녀 = 1
		;반음반양
		CASE 2
			TALENT:ARG:반음반양 = 1
			TALENT:ARG:처녀 = 1
			TALENT:ARG:동정 = 1
		;후타나리
		CASE 3
			TALENT:ARG:후타나리 = 1
			TALENT:ARG:처녀 = 1
			TALENT:ARG:동정 = 1
		;컨트보이
		CASE 4
			TALENT:ARG:컨트보이 = 1
			TALENT:ARG:처녀 = 1
		;쉬메일
		CASE 5
			TALENT:ARG:쉬메일 = 1
			TALENT:ARG:동정 = 1
	ENDSELECT

	;모순되는 소질을 삭제
	SIF PENIS(ARG) == 0
		CALL CLEAR_TALENT_PENIS(ARG,3)
	SIF MALE(ARG) == 1
		CALL CLEAR_TALENT_BREAST(ARG)
ENDIF

;오메가
SIF DIFF_GET_TALENT(ARG,3) > 0
	TALENT:ARG:오메가 = 1


LOCAL = DIFF_GET_TALENT(ARG,4)
;체격
IF LOCAL > 0 && LOCAL < 10
	TALENT:ARG:소인체형 = 0
	TALENT:ARG:작은체구 = 0
	TALENT:ARG:큰체구 = 0
	
	SELECTCASE LOCAL
		CASE 1
			TALENT:ARG:소인체형 = 1
		CASE 2
			TALENT:ARG:작은체구 = 1
		CASE 3
			;보통체형
		CASE 4
			TALENT:ARG:큰체구 = 1
	ENDSELECT
	
ENDIF

LOCAL = DIFF_GET_TALENT(ARG,5)
;젖기 쉬움
IF LOCAL > 0 && LOCAL < 10
	TALENT:ARG:젖기쉬움 = 0
	TALENT:ARG:젖기어려움 = 0
	
	SELECTCASE LOCAL
		CASE 1
			TALENT:ARG:젖기쉬움 = 1
		CASE 2
			;보통 젖기 쉬움
		CASE 3
			TALENT:ARG:젖기어려움 = 1
	ENDSELECT
ENDIF

LOCAL = DIFF_GET_TALENT(ARG,6)
;술의 약함
IF LOCAL > 0 && LOCAL < 10
	TALENT:ARG:술꾼 = 0
	TALENT:ARG:알콜쓰레기 = 0
	
	SELECTCASE LOCAL
		CASE 1
			TALENT:ARG:술꾼 = 1
		CASE 2
			;보통 술의 약함
		CASE 3
			TALENT:ARG:알콜쓰레기 = 1
	ENDSELECT
ENDIF

LOCAL = DIFF_GET_TALENT(ARG,7)
;둔감 민감 C
IF LOCAL > 0 && LOCAL < 10
	TALENT:ARG:C둔감 = 0
	TALENT:ARG:C민감 = 0
	
	SELECTCASE LOCAL
		CASE 1
			SIF TALENT:ARG:C민감
				TALENT:ARG:C민감 = 0
			
			TALENT:ARG:C둔감 = 1
		CASE 2
			SIF TALENT:ARG:C둔감
				TALENT:ARG:C둔감 = 0
				
			TALENT:ARG:C민감 = 1
	ENDSELECT
ENDIF

LOCAL = DIFF_GET_TALENT(ARG,8)
;둔감 민감 V
IF (LOCAL > 0 && LOCAL < 10) && VAGINA(ARG)
	TALENT:ARG:V둔감 = 0
	TALENT:ARG:V민감 = 0
	
	SELECTCASE LOCAL
		CASE 1
			SIF TALENT:ARG:V민감
				TALENT:ARG:V민감 = 0
			
			TALENT:ARG:V둔감 = 1
		CASE 2
			SIF TALENT:ARG:V둔감
				TALENT:ARG:V둔감 = 0
			
			TALENT:ARG:V민감 = 1
	ENDSELECT
ENDIF

LOCAL = DIFF_GET_TALENT(ARG,9)
;둔감 민감 A
IF LOCAL > 0 && LOCAL < 10
	TALENT:ARG:A둔감 = 0
	TALENT:ARG:A민감 = 0
	
	SELECTCASE LOCAL
		CASE 1
			SIF TALENT:ARG:A민감
				TALENT:ARG:A민감 = 0
			
			TALENT:ARG:A둔감 = 1
		CASE 2
			SIF TALENT:ARG:A둔감
				TALENT:ARG:A둔감 = 0
			
			TALENT:ARG:A민감 = 1
	ENDSELECT
ENDIF

LOCAL = DIFF_GET_TALENT(ARG,10)
;둔감 민감 B
IF LOCAL > 0 && LOCAL < 10
	TALENT:ARG:B둔감 = 0
	TALENT:ARG:B민감 = 0
	TALENT:ARG:함몰유두 = 0
	
	SELECTCASE LOCAL
		CASE 1
			SIF TALENT:ARG:B민감
				TALENT:ARG:B민감 = 0
			
			TALENT:ARG:B둔감 = 1
		CASE 2
			SIF TALENT:ARG:B둔감
				TALENT:ARG:B둔감 = 0
			
			TALENT:ARG:B민감 = 1
		CASE 3
			TALENT:ARG:함몰유두 = 1
	ENDSELECT
ENDIF

LOCAL = DIFF_GET_TALENT(ARG,11)
;페니스의 크기
IF (LOCAL > 0 && LOCAL < 10) && PENIS(ARG)
	;페니스의 크기 관련의 소질 삭제
	CALL CLEAR_TALENT_PENIS(ARG,1)
	
	SELECTCASE LOCAL
		CASE 1
			TALENT:ARG:아이사이즈 = 1
		CASE 2
			TALENT:ARG:단소 = 1
		CASE 3
			;보통 크기
		CASE 4
			TALENT:ARG:거근 = 1
		CASE 5
			TALENT:ARG:말자지 = 1
	ENDSELECT
ENDIF

LOCAL = DIFF_GET_TALENT(ARG,12)
;페니스의 특징
IF (LOCAL > 0 && LOCAL < 10) && PENIS(ARG)
	SELECTCASE LOCAL
		CASE 1
			SIF TALENT:ARG:지루
				TALENT:ARG:지루 = 0
			
			TALENT:ARG:조루 = 1
		CASE 2
			SIF TALENT:ARG:조루
				TALENT:ARG:조루 = 0
			
			TALENT:ARG:지루 = 1
		CASE 3
			TALENT:ARG:절륜 = 1
		CASE 4
			TALENT:ARG:포경 = 1
	ENDSELECT
ENDIF

LOCAL = DIFF_GET_TALENT(ARG,13)
;가슴의 크기
IF (LOCAL > 0 && LOCAL < 10) && MALE(ARG) == 0
	;가슴의 크기 관련의 소질 삭제
	CALL CLEAR_TALENT_BREAST(ARG)
	
	SELECTCASE LOCAL
		CASE 1
			TALENT:ARG:절벽 = 1
		CASE 2
			TALENT:ARG:빈유 = 1
		CASE 3
			;평유
		CASE 4
			TALENT:ARG:거유 = 1
		CASE 5
			TALENT:ARG:폭유 = 1
	ENDSELECT
ENDIF

LOCAL = DIFF_GET_TALENT(ARG,14)
;그 외
IF LOCAL > 0 && LOCAL < 10
	TALENT:ARG:음모없음 = 0
	TALENT:ARG:요리치 = 0
	TALENT:ARG:독약내성 = 0
	TALENT:ARG:모유체질 = 0
	
	SELECTCASE LOCAL
		CASE 1
			TALENT:ARG:음모없음 = 1
		CASE 2
			SIF TALENT:ARG:요리고수
				TALENT:ARG:요리고수 = 0
			
			TALENT:ARG:요리치 = 1
		CASE 3
			TALENT:ARG:독약내성 = 1
		CASE 4
			;남자모유취득 콘피그온 때만　
			SIF (FLAG:2 & 256)
				TALENT:ARG:모유체질 = 1
	ENDSELECT
ENDIF


;-------------------------------------------------
;함수명:@DIFF_TALENT_NAME(ARG)
;개요:개체차로 취득한 소질을 표시하는 녀석[]으로 단락지어 표시
;인수:ARG… 캐릭터 등록 번호
;반환값:없음
;비고:
;-------------------------------------------------
@DIFF_TALENT_NAME(ARG)
#DIM DYNAMIC GET_TALENT = 0
#DIMS DYNAMIC GET_TALENT_NAME = ""


FOR COUNT,2,15
	GET_TALENT = DIFF_GET_TALENT(ARG,COUNT)
	
	SIF !GET_TALENT
		CONTINUE
	
	;성별
	IF COUNT == 2 && GET_TALENT
		SELECTCASE GET_TALENT
			CASE 1
				LOCALS = 여자
			CASE 2 
				LOCALS = 반음반양
			CASE 3
				LOCALS = 후타나리
			CASE 4
				LOCALS = 컨트보이
			CASE 5
				LOCALS = 쉬메일
		ENDSELECT
	;오메가
	ELSEIF COUNT == 3 && GET_TALENT
		LOCALS = 오메가
	;체격
	ELSEIF COUNT == 4 && GET_TALENT
		SELECTCASE GET_TALENT
			CASE 1,11
				LOCALS = 소인체형
			CASE 2,12
				LOCALS = 작은체구
			CASE 3
				LOCALS = 보통체형
			CASE 4,14
				LOCALS = 큰체구
		ENDSELECT
	;젖기 쉬움
	ELSEIF COUNT == 5 && GET_TALENT
		SELECTCASE GET_TALENT
			CASE 1
				LOCALS = 젖기쉬움
			CASE 2
				LOCALS = 윤활보통
			CASE 3 
				LOCALS = 젖기어려움
		ENDSELECT
	;술의 약함
	ELSEIF COUNT == 6 && GET_TALENT
		SELECTCASE GET_TALENT
			CASE 1,11
				LOCALS = 술꾼
			CASE 2
				LOCALS = 주량보통
			CASE 3,13 
				LOCALS = 알콜쓰레기
		ENDSELECT
	;C둔감민감
	ELSEIF COUNT == 7 && GET_TALENT
		SELECTCASE GET_TALENT
			CASE 1
				LOCALS = C둔감
			CASE 2
				LOCALS = C민감
		ENDSELECT
	;V둔감민감
	ELSEIF COUNT == 8 && GET_TALENT
		SELECTCASE GET_TALENT
			CASE 1
				LOCALS = V둔감
			CASE 2
				LOCALS = V민감
		ENDSELECT
	;A둔감민감
	ELSEIF COUNT == 9 && GET_TALENT
		SELECTCASE GET_TALENT
			CASE 1
				LOCALS = A둔감
			CASE 2
				LOCALS = A민감
		ENDSELECT
	;B둔감민감
	ELSEIF COUNT == 10 && GET_TALENT
		SELECTCASE GET_TALENT
			CASE 1
				LOCALS = B둔감
			CASE 2
				LOCALS = B민감
			CASE 3
				LOCALS = 함몰유두
		ENDSELECT
	;페니스의 크기
	ELSEIF COUNT == 11 && GET_TALENT
		SELECTCASE GET_TALENT
			CASE 1,11
				LOCALS = 아이사이즈
			CASE 2,12
				LOCALS = 단소
			CASE 3
				LOCALS =보통 정도의페니스의 크기
			CASE 4,14
				LOCALS = 거근
			CASE 5,15
				LOCALS = 말자지
		ENDSELECT
	;페니스의 특징
	ELSEIF COUNT == 12 && GET_TALENT
		SELECTCASE GET_TALENT
			CASE 1
				LOCALS = 조루
			CASE 2 
				LOCALS = 지루
			CASE 3 
				LOCALS = 절륜
			CASE 4,14
				LOCALS = 포경
		ENDSELECT
	;가슴의 크기
	ELSEIF COUNT == 13 && GET_TALENT
		SELECTCASE GET_TALENT
			CASE 1
				LOCALS = 절벽
			CASE 2 
				LOCALS = 빈유
			CASE 3 
				LOCALS = 평유
			CASE 4 
				LOCALS = 거유
			CASE 5
				LOCALS = 폭유
		ENDSELECT
	;그 외
	ELSEIF COUNT == 14 && GET_TALENT
		SELECTCASE GET_TALENT
			CASE 1,10
				LOCALS = 음모없음
			CASE 2 
				LOCALS = 요리치
			CASE 3,10
				LOCALS = 독약내성
			CASE 4 
				LOCALS = 모유체질
		ENDSELECT
	ENDIF
	SIF STRLENS(LOCALS)
		GET_TALENT_NAME = %GET_TALENT_NAME%[%LOCALS%]
	LOCALS =
NEXT

PRINTFORM %GET_TALENT_NAME%

;-------------------------------------------------
;함수명:@CLEAR_TALENT_VAGINA(ARG)
;개요:바기나관련의 소질의 삭제
;인수:ARG… 캐릭터 등록 번호
;반환값:없음
;비고:
;-------------------------------------------------
@CLEAR_TALENT_VAGINA(ARG)
TALENT:ARG: 처녀 = 0

TALENT:ARG:V민감 = 0
TALENT:ARG:V둔감 = 0

;-------------------------------------------------
;함수명:@CLEAR_TALENT_PENIS(ARG)
;개요:페니스관련 소질의 삭제
;인수:
;ARG:0… 캐릭터 등록 번호
;ARG:1…1=크기 2=특징 3=특징/크기(비트 연산)
;반환값:없음
;비고:
;-------------------------------------------------
@CLEAR_TALENT_PENIS(ARG:0, ARG:1)
SIF ARG:1 < 1 && ARG:1 > 3
	RETURN

IF (ARG:1 & 1)
	TALENT:(ARG:0):아이사이즈 = 0
	TALENT:(ARG:0):단소 = 0
	TALENT:(ARG:0):거근 = 0
	TALENT:(ARG:0):말자지 = 0
ENDIF

IF (ARG:1 & 2)
	TALENT:(ARG:0):지루 = 0
	TALENT:(ARG:0):조루 = 0
	TALENT:(ARG:0):절륜 = 0
	TALENT:(ARG:0):포경 = 0
ENDIF

;-------------------------------------------------
;함수명:@CLEAR_TALENT_BREAST(ARG)
;개요:가슴의 크기 관련 소질의 삭제
;인수:ARG… 캐릭터 등록 번호
;반환값:없음
;비고:
;-------------------------------------------------
@CLEAR_TALENT_BREAST(ARG)
TALENT:ARG:절벽 = 0
TALENT:ARG:빈유 = 0
TALENT:ARG:거유 = 0
TALENT:ARG:폭유 = 0



