;--------------------------------------------------
;オプション設定処理
;--------------------------------------------------
@OPTION_CHARA
;現在の調教対象を保存しておく
LOCAL = TARGET
PRINTL 
PRINTL 
PRINTL 
PRINTL キャラごとのオプション設定です
PRINTL 設定項目を選択してください
PRINTL [0] - あだ名設定
PRINTL [1] - 衣装の独自設定
PRINTL [2] - 汎用喘ぎ設定
PRINTL [999] -戻る
$INPUT_LOOP
INPUT
IF RESULT == 0
	CALL CHARA_CALLNAME_SETTING
	;保持しておいた現在の調教対象を格納
	TARGET = LOCAL
ELSEIF RESULT == 1
	CALL CHARA_CLOTHES_SETTING
	;保持しておいた現在の調教対象を格納
	TARGET = LOCAL
ELSEIF RESULT == 2
	CALL CHARA_PANT_VOICE_SETTING
	;保持しておいた現在の調教対象を格納
	TARGET = LOCAL
ELSEIF RESULT == 999
	RETURN 0
ELSE
	CLEARLINE 1
	GOTO INPUT_LOOP
ENDIF
RESTART

;--------------------------------------------------
;あだ名設定　一覧画面
;--------------------------------------------------
@CHARA_CALLNAME_SETTING
PRINTL どのキャラのあだ名を設定しますか？
FOR COUNT, 0, CHARANUM, 1
	PRINTFORML [{COUNT}] %CSVNAME(NO:COUNT,0)%　（あだ名：%CALLNAME:COUNT%）
NEXT
	PRINTL [999] 戻る
$INPUT_LOOP
INPUT
IF RESULT <= CHARANUM && RESULT != 999
	CALL CALLNAME_SETTING(RESULT)
ELSEIF RESULT == 999
	RETURN 0
ELSE
	CLEARLINE 1
	GOTO INPUT_LOOP
ENDIF

;--------------------------------------------------
;あだ名設定　設定画面
;--------------------------------------------------
@CALLNAME_SETTING(ARG)
PRINTFORML %CSVNAME(NO:ARG,0)%のあだ名を設定します
PRINTFORML 現在のあだ名：%CALLNAME:ARG%
PRINTL
PRINTL [0] デフォルトに戻す
PRINTL [1] 設定する
PRINTL [999] 戻る
$INPUT_LOOP
INPUT
IF RESULT == 0 || RESULT == 1
	IF RESULT == 0
		LOCALS = %CSVCALLNAME(NO:ARG,0)%
	ELSEIF RESULT == 1
		PRINTL あだ名を入力してください
		INPUTS
		LOCALS = %RESULTS%
	ENDIF
	
	PRINTFORML 「%LOCALS%」でよろしいですか？
	PRINTL [0] はい
	PRINTL [1] いいえ
	$INPUT_LOOP_DEFAULT
	INPUT
	IF RESULT == 0
		CALLNAME:ARG = %LOCALS%
		PRINTFORML %CSVNAME(NO:ARG,0)%のあだ名を「%LOCALS%」に設定しました
	ELSEIF RESULT == 1
		RESTART
	ELSE
		CLEARLINE 1
		GOTO INPUT_LOOP_DEFAULT
	ENDIF
ELSEIF RESULT == 999
	RETURN 0
ELSE
	CLEARLINE 1
	GOTO INPUT_LOOP
ENDIF


;--------------------------------------------------
;衣装の独自設定
;気が向いたら直します
;--------------------------------------------------
@CHARA_CLOTHES_SETTING
PRINTL どのキャラの衣装を独自に設定しますか？
PRINTL ※設定を反映するには着衣初期設定を【独自設定】にしてください
PRINTL 仕様上、全裸は設定できません
FOR COUNT, 1, CHARANUM, 1
	;共犯者は除外
	SIF CFLAG:COUNT:1 != 3
		PRINTFORML [{COUNT}] %CALLNAME:COUNT%
NEXT
	PRINTL [999] 戻る
$INPUT_LOOP
INPUT
IF RESULT <= CHARANUM && RESULT != 999
	TARGET = RESULT
	
	;呼び出す前に独自衣装を着せておく
	REPEAT 11
		SELECTCASE COUNT
			CASE 0 TO 4
				LOCAL = COUNT
				;N桁目を求めたいときは　10のN乗の余りを10のN-1乗で割ればよい
				;例）8,7桁目を求めたいときは　COUNT=LOCAL= 3となり、10の8乗(=(3+1)*2)の余りを10の6乗(=3*2)で割る
				LOCAL:1 = CFLAG:133 % POWER(10, (LOCAL+1)*2) / POWER(10, LOCAL*2)
				
				TEQUIP:COUNT = LOCAL:1
			CASE 5 TO 9
				LOCAL = COUNT - 5
				;処理は上と同様
				LOCAL:1 = CFLAG:134 % POWER(10, (LOCAL+1)*2) / POWER(10, LOCAL*2)
				
				TEQUIP:COUNT = LOCAL:1
			;악세사리ー
			CASEELSE
				TEQUIP:10 = CFLAG:135
		ENDSELECT
	REND
	
	;調教中でないと分かるように、通常入らない値をPREVCOMに代入
	PREVCOM = -2
	
	CALL CLOTHES_CHANGE
		;[900]戻るが押下されるとZにRESULTが格納される
		IF Z == 900
			REPEAT 8
				D:COUNT = TEQUIP:COUNT
			REND
			D:9 = TEQUIP:9
			
			;CFLAG:133にTEQUIP:0～TEQUIP:4（特殊、신발下、속옷（아래）、속옷（위）、하의）
			;CFLAG:134にTEQUIP:5～TEQUIP:9（상의、전신상의、머리장식、眼鏡、신발）
			;CFLAG:135にTEQUIP:10（악세사리ー）を記録
			;今후のことを考えて、TEQUOP0～9は1つにつき2桁分枠を取っておく
			
			;３つに分けたのは値の上限の関係
			CFLAG:133 = D:0 + (D:1 * 100) + (D:2 * 10000) + (D:3 * 1000000) + (D:4 * 100000000)
			CFLAG:134 = D:5 + (D:6 * 100) + (D:7 * 10000) + (D:8 * 1000000) + (D:9 * 100000000)
			CFLAG:135 = D:10

			SIF CFLAG:133 > 0 || CFLAG:134 > 0 || CFLAG:135 > 0
				PRINTW 設定しました
		ENDIF
		IF FLAG:1 != 99
			PRINTL 着衣初期設定を【独自設定】にしますか？
			PRINTL [0]はい
			PRINTL [1]いいえ
			INPUT
			$INPUT_LOOP_2
			IF RESULT == 0
				PRINTW 初期の着衣を独自設定に設定しました
				FLAG:1 = 99
			ELSEIF RESULT == 1
			ELSE
				GOTO INPUT_LOOP_2
			ENDIF
		ENDIF
		
		;初期値に戻しておく
		PREVCOM = -1
		
ELSEIF RESULT == 999
	RETURN 0
ELSE
	CLEARLINE 1
	GOTO INPUT_LOOP
ENDIF

;--------------------------------------------------
;汎用喘ぎ設定
;--------------------------------------------------
@CHARA_PANT_VOICE_SETTING
PRINTL どのキャラの汎用喘ぎを設定しますか？
FOR COUNT, 1, CHARANUM, 1
	;共犯者は除外
	SIF CFLAG:COUNT:1 != 3
		PRINTFORML [{COUNT}] %CALLNAME:COUNT%
NEXT
	PRINTL [999] 戻る
$INPUT_LOOP
INPUT
IF RESULT <= CHARANUM && RESULT != 999
	TARGET = RESULT
	;以下の関数は「PANT_SET.ERB」にあります
	CALL PANT_VOICE_SETTING
ELSEIF RESULT == 999
	CALL OPTION_CHARA
ELSE
	CLEARLINE 1
	GOTO INPUT_LOOP
ENDIF


