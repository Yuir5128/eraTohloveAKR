;フリーミッションの名前とクリア条件、0にはミッション番号を入れる(頭に0をつけなくていい)
;使用時は 0 をミッション番号（１桁なら頭0なし）に置換してください

;フリーミッションのサンプルがてらに作ってみました
@FREE_MISSION_NAME_0
PRINTFORM [0] 始まりの刀　
PRINTFORML (初期刀内のいずれか1振り以上を30日以内に陥落させる)

;0にはミッションの番号を入れる(頭に0をつけなくていい)
@FREE_MISSION_START_0

;調教対象にするキャラを追加
ADDCHARA 85
ADDCHARA 89
ADDCHARA 93
ADDCHARA 95
ADDCHARA 101
TARGET = 1

DRAWLINE
PRINTFORML 
PRINTFORMW プレイヤーは%CALLNAME:MASTER%です
PRINTFORML 
PRINTFORML 【 調教対象者 】
FOR LOCAL, 1, CHARANUM
	PRINTFORML - 　%CALLNAME:LOCAL%
NEXT
PRINTFORMW
PRINTFORML 【 所有アイテム 】
PRINTFORML -　なし
PRINTFORML 
PRINTFORML ～　MISSION　～
PRINTFORML 目標：카슈 키요미츠、카센 카네사다、무츠노카미 요시유키、야만바기리 쿠니히로、하치스카 코테츠のいずれか1振り以上を30日以内に陥落させる
PRINTFORMW ※陥落素質は問わない
DRAWLINE


;※補발
;キャラの素質や能力を変化させたい場合も、ここで記載しておくとよい
;ABL:MASTER:기교 = 3
;TALENT:MASTER:경험풍부 = 1

;初期資金を変更する場合
;MONEY = 50000

;アイテムを持たせる場合（調教アイテムは1、消費アイテムは個数）
;ITEM:로터 = 1
;ITEM:로션 = 10


@FREE_MISSION_SALEITEM_CHECK_0
;アイテム購入可否設定
REPEAT 200
	;基本的なアイテム(ITEM:0～ITEM:29)と複数購入可のアイテム(ITEM:40～ITEM:49)のみ購入可にする
	SIF COUNT < 30 || (COUNT > 39 && COUNT < 50)
		CONTINUE

	ITEMSALES:COUNT = 0
	
REND

;※補발
;アイテムの購入可否は「ITEMSALES:アイテム番号」もしくは「ITEMSALES:アイテム名」で判断している
;アイテム番号とアイテム名はITEM.csvを参照


@FREE_MISSION_CONFIRM_0
PRINTFORML 目標：카슈 키요미츠、카센 카네사다、무츠노카미 요시유키、야만바기리 쿠니히로、하치스카 코테츠のいずれか1振り以上を30日以内に陥落させる


@FREE_MISSION_CHECK_0
#DIM DYNAMIC CLEAR = 0

;クリア条件
REPEAT CHARANUM
	;思慕畏怖対象は判定してないので、男士→男士の연모や복종でもクリアできます
	SIF KANRAKU2(COUNT)
		CLEAR += 1
	SIF CLEAR
		BREAK
REND
	
IF CLEAR >= 1
	PRINTFORMW -　目標達成
	PRINTFORMW -　お疲れ様です
	PRINTFORMW -　エンディング等はありません
	PRINTFORMW -　
	PRINTFORMW -　　　　　　　　　　　　　　　　　　　SEE YOU NEXT DREAM...
	
	CALL GAME_END(1)
	
	;ゲーム継続を押下した場合、キャラクタ購入フラグを復活させる
	IF FLAG:5 == 9
		REPEAT FLAG:44
	
			;CSVがないキャラはスキップ
			SIF !EXISTCSV(COUNT, 0)
				CONTINUE
			
			;購入済みキャラはスキップ
			SIF COUNT == 85 || COUNT == 89 || COUNT == 93 || COUNT == 95 || COUNT == 101
				CONTINUE
			
			;ITEMPRICE：購入金額=0はスキップ
			IF ITEMPRICE:(199+COUNT) == 0
				CONTINUE
			ELSEIF ITEMPRICE:(199 + COUNT)
				ITEMSALES:(199 + COUNT) = 1
			ENDIF
		REND
	ENDIF
ELSEIF DAY >= 31
	PRINTFORMW 
	PRINTFORMW 　――目標失敗
	PRINTFORMW 　　　時間切れです
	PRINTFORMW 
	
	CALL GAME_END
ENDIF

;※補발
;日付経過などでキャラを追加したり、素質を変化させるといったことも可能
;IF DAY == 2
;	ADDCHARA 198
;	TALENT:MASTER:거근 = 1
;ENDIF




