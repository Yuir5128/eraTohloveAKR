;--------------------------------------------------
;アイテム購入
;--------------------------------------------------
@EVENTBUY
;この店の商品ではない、販売条件を満たしていない
IF !SHOW_ITEM(TFLAG:1, BOUGHT) || !ITEMSALES:BOUGHT
	CLEARLINE 1
	RETURN 0
ENDIF

;--------------------------------------------------
;購入処理
;--------------------------------------------------
;アイテム説明表示
MONEY += ITEMPRICE:BOUGHT
ITEM:BOUGHT--
CALL ITEM_LIST
SELECTCASE BOUGHT
	;【더러움무시】
	CASE 50
		PRINTFORML %ITEMNAME:BOUGHT%
		TRYCALL DICTIONARY_TALENT_64
	;【조제지식】
	CASE 51
		PRINTFORML %ITEMNAME:BOUGHT%
		TRYCALL DICTIONARY_TALENT_55
	;【금단의지식】
	CASE 52
		PRINTFORML %ITEMNAME:BOUGHT%
		TRYCALL DICTIONARY_TALENT_144
	;【천리안】
	CASE 53
		PRINTFORML %ITEMNAME:BOUGHT%
		TRYCALL DICTIONARY_TALENT_156
	;【절륜】
	CASE 54
		PRINTFORML %ITEMNAME:BOUGHT%
		TRYCALL DICTIONARY_TALENT_47
	;【수】
	CASE 55
		PRINTFORML %ITEMNAME:BOUGHT%
		TRYCALL DICTIONARY_TALENT_157
	;【공】
	CASE 56
		PRINTFORML %ITEMNAME:BOUGHT%
		TRYCALL DICTIONARY_TALENT_158
	;【오메가】
	CASE 59
		PRINTFORML %ITEMNAME:BOUGHT%
		TRYCALL DICTIONARY_TALENT_170
	CASEELSE
		PRINTFORML 【%ITEMNAME:BOUGHT%】
		TRYCALLFORM DICTIONARY_ITEM_{BOUGHT}
		PRINTL 
ENDSELECT
SETCOLOR 140,140,140
DRAWLINE
RESETCOLOR
SELECTCASE BOUGHT
	;基本アイテム
	;0～26アダルトグッズ、30정조대、31～33동물귀、34헤드셋、38에이프런、39ウェディングセット
	;57촉수사역술、58진화의비법、90손術道具、91우리、92～93포장마차（増築）、94플랜카드
	CASE 0 TO 26,30 TO 34,38,39,57,58,90 TO 94,96 TO 94
		PRINTFORML 購入しますか？
		SETCOLOR 140,140,140
		DRAWLINE
		RESETCOLOR
		PRINTBUTTON "[999 キャンセル]", 999
		PRINT 　
		PRINTBUTTON "[0 購入する]", 0
		PRINTL 
		$INPUT_LOOP_2
		INPUT
		SIF RESULT == 999
			RETURN 0
		IF RESULT != 0
			CLEARLINE 1
			GOTO INPUT_LOOP_2
		ELSEIF MONEY < ITEMPRICE:BOUGHT
			CLEARLINE 1
			PRINTW お金がたりません
			GOTO INPUT_LOOP_2
		ENDIF
		IF BOUGHT == 57 || BOUGHT == 58
			PRINTFORMW ＜%ITEMNAME:BOUGHT%を身につけました＞
		ELSE
			PRINTFORMW ＜%ITEMNAME:BOUGHT%を購入しました＞
		ENDIF
		ITEMSALES:BOUGHT = 0
		ITEM:BOUGHT++
		MONEY -= ITEMPRICE:BOUGHT
		TFLAG:13 = 28
		CALL KOJO_MESSAGE_EVENT
	;消費アイテム
	;36,37스타킹,타이츠、40～49薬品系、86～88酒類
	CASE 36,37,40 TO 49,86 TO 88
		IF (100 - ITEM:BOUGHT) <= (MONEY / ITEMPRICE:BOUGHT)
			LOCAL:3 = 100 - ITEM:BOUGHT
		ELSE
			LOCAL:3 = MONEY / ITEMPRICE:BOUGHT
			LOCAL:4 = 1
		ENDIF
		SETCOLOR 140,140,140
		DRAWLINE
		RESETCOLOR
		PRINTFORML %ITEMNAME:BOUGHT%をいくつ購入しますか？（1-{LOCAL:3}個まで）
		PRINTFORML 数字を入力するか以下のボタンをクリックしてください
		PRINTL 
		PRINTBUTTON "[999 キャンセル]", 999
		PRINT 　
		PRINTBUTTON "[1個]", 1
		IF LOCAL:3 >= 5
			PRINT 　
			PRINTBUTTON "[5個]", 5
		ENDIF
		IF LOCAL:3 >= 10
			PRINT 　
			PRINTBUTTON "[10個]", 10
		ENDIF
		PRINT 　
		IF LOCAL:4
			PRINTBUTTON "[買えるだけ]", LOCAL:3
		ELSE
			PRINTBUTTON "[持てるだけ]", LOCAL:3
		ENDIF
		PRINTL 
		$INPUT_LOOP_3
		INPUT
		SIF RESULT == 999
			RETURN 0
		IF RESULT < 0
			CLEARLINE 1
			GOTO INPUT_LOOP_3
		ELSEIF RESULT > LOCAL:3
			CLEARLINE 1
			IF RESULT > LOCAL:3 && !LOCAL:4
				PRINTW そんなに持てません
			ELSEIF RESULT > LOCAL:3 && LOCAL:4
				PRINTW お金が발りません
			ENDIF
			GOTO INPUT_LOOP_3
		ENDIF
		PRINTFORMW ＜%ITEMNAME:BOUGHT%を{RESULT}個購入しました＞
		ITEM:BOUGHT += RESULT
		MONEY -= ITEMPRICE:BOUGHT * RESULT
		TFLAG:13 = 28
		CALL KOJO_MESSAGE_EVENT
	;使用アイテム
	;50더러움무시、51조제지식、52금지된지식、53천리안、54절륜、55수、56공、59오메가
	;60～85体質変化系の薬、96부적、97부적・극
	CASE 50 TO 56,59 TO 85,96,97
		IF BOUGHT < 60
			PRINTFORML 誰が身につけますか？
		ELSEIF BOUGHT > 95
			PRINTFORML 誰に持たせますか？
		ELSE
			PRINTFORML 誰に使用しますか？
		ENDIF
		SETCOLOR 140,140,140
		DRAWLINE
		RESETCOLOR
		FOR COUNT, 0, CHARANUM
			;アイテム使用可能
			SIF !ITEM_USE_CHECK(COUNT, BOUGHT)
				CONTINUE
			SETCOLOR 0x8888FF
			SIF (BOUGHT:96 || BOUGHT:97) && COUNT == MASTER
				CONTINUE
			IF COUNT == MASTER
				PRINTFORM %UNICODE(0x27A0)%
			ELSEIF COUNT == ASSI
				PRINT 助
			ELSEIF CFLAG:COUNT:1 == 3
				PRINT 共
			ELSEIF COUNT == TARGET
				PRINT ☆
			ELSE
				PRINT 　
			ENDIF
			RESETCOLOR
			PRINTFORM [{COUNT,2}] %CALLNAME:COUNT,16,LEFT%
			PRINTFORM %UNICODE(0x208D)%
			CALL PRINT_COLORBAR,BASE:COUNT:체력,MAXBASE:COUNT:체력,8,UNICODE(0x2583),UNICODE(0x2583),BARCOLORSET("赤"),RESULT:1, 327685
			PRINTFORM %UNICODE(0x208E)%
			PRINTFORM %UNICODE(0x208D)%
			CALL PRINT_COLORBAR,BASE:COUNT:기력,MAXBASE:COUNT:기력,8,UNICODE(0x2583),UNICODE(0x2583),BARCOLORSET("青"),RESULT:1, 327685
			PRINTFORM %UNICODE(0x208E)%
			IF TALENT:MASTER:천리안
				PRINT  ストレス
				IF CFLAG:COUNT:10 >= 95
					SETCOLOR 0xC0C0C0
				ELSEIF CFLAG:COUNT:10 >= 80
					SETCOLOR 0x7070C0
				ELSEIF CFLAG:COUNT:10 >= 50
					SETCOLOR 0x70C070
				ELSEIF CFLAG:COUNT:10 >= 30
					SETCOLOR 0xC07070
				ELSE
					SETCOLOR 0xC070C0
				ENDIF
				PRINTFORM {CFLAG:COUNT:10,3} 
				RESETCOLOR
			ENDIF
			;変更系は現在の素質表示
			SELECTCASE BOUGHT
				CASE 55,56,59
					IF TALENT:COUNT:수
						PRINT  수
					ELSEIF TALENT:COUNT:공
						PRINT  공
					ELSEIF TALENT:COUNT:오메가
						PRINT  오메가
					ENDIF
				CASE 73,74,75
					IF TALENT:COUNT:아이사이즈
						PRINT  아이사이즈
					ELSEIF TALENT:COUNT:단소
						PRINT  단소
					ELSEIF TALENT:COUNT:거근
						PRINT  거근
					ELSEIF TALENT:COUNT:말자지
						PRINT  말자지
					ELSE
						PRINT  人並み
					ENDIF
				CASE 65,66
					IF TALENT:COUNT:빈유
						PRINT  빈유
					ELSEIF TALENT:COUNT:절벽
						PRINT  절벽
					ELSEIF TALENT:COUNT:거유
						PRINT  거유
					ELSEIF TALENT:COUNT:폭유
						PRINT  폭유
					ELSE
						PRINT  人並み
					ENDIF
				CASE 71,72
					IF TALENT:COUNT:소인체형
						PRINT  소인체형
					ELSEIF TALENT:COUNT:작은체구
						PRINT  작은체구
					ELSEIF TALENT:COUNT:큰체구
						PRINT  큰체구
					ELSE
						PRINT  人並み
					ENDIF
				CASE 61,62,69,70
					IF TALENT:COUNT:반음반양 || TALENT:COUNT:후타나리
						SETCOLOR 0x999999
						PRINT  双
					ENDIF
					IF TALENT:COUNT:컨트보이
						SETCOLOR 0x8491c3
						PRINTFORM 上♂下♀
					ELSEIF TALENT:COUNT:쉬메일
						SETCOLOR 0xcc7eb1
						PRINTFORM 上♀下♂
					ELSEIF MALE(COUNT)
						SETCOLOR 0x8491c3
						PRINTFORM  ♂
					ELSE
						SETCOLOR 0xcc7eb1
						PRINTFORM  ♀
					ENDIF
					RESETCOLOR
				CASE 96,97
					SIF CFLAG:COUNT:693 == 0
						SETCOLOR 0x808080
						PRINTFORM  부적({CFLAG:COUNT:693})
						RESETCOLOR
					PRINTFORM  /
					SIF CFLAG:COUNT:694 == 0
						SETCOLOR 0x808080
						PRINTFORM  부적・극({CFLAG:COUNT:694})
						RESETCOLOR
					ENDSELECT
			PRINTL 
		NEXT
		SETCOLOR 140,140,140
		DRAWLINE
		RESETCOLOR
		PRINT [999] 戻る　　　　　　　　
		$INPUT_LOOP_4
		INPUT
		IF RESULT == 999
			TFLAG:0 = -2
			RETURN 0
		ELSEIF MONEY < ITEMPRICE:BOUGHT
			CLEARLINE 1
			PRINTW お金がたりません
			GOTO INPUT_LOOP_2
		ELSEIF !ITEM_USE_CHECK(RESULT, BOUGHT)
			CLEARLINE 1
			GOTO INPUT_LOOP_2
		ENDIF
		MONEY -= ITEMPRICE:BOUGHT
		LOCAL:5 = RESULT
		LOCAL:6 = TARGET
		TARGET = LOCAL:5
		;アイテム使用
		CALL USE_ITEM
		;입上呼び出し処理
		;産卵イベントはイベント입上を別枠で呼び出す(アイテム：노란환약分のテンプレは無い)
		IF BOUGHT == 84
			TFLAG:13 = 10
		ELSE
			TFLAG:13 = 28
		ENDIF
		CALL KOJO_MESSAGE_EVENT
		TARGET = LOCAL:6
ENDSELECT
TFLAG:0 = -2


;--------------------------------------------------
;アイテム購入の処理
;--------------------------------------------------
@SHOP_ITEM_LIST(ARG)
DRAWLINE
CALL ITEM_LIST
LOCALS = 
LOCAL:1 = 0
FOR LOCAL, 0, 199
		CALL SALEITEM_CHECK(LOCAL)
NEXT

;ミッションモード
SIF FLAG:5 == 8
	CALL FREE_MISSION_SALEITEM_CHECK

FOR LOCAL, 0, 199
	;この店で買える商品か、販売フラグがあるか
	SIF !SHOW_ITEM(ARG, LOCAL) || !ITEMSALES:LOCAL
		CONTINUE
	LOCALS = [{LOCAL,2}] %ITEMNAME:LOCAL%(%MONEY_FORM(ITEMPRICE:LOCAL)%고슈금)
	IF STRLENS(LOCALS)
		IF STRLENS(LOCALS) > 26
			LOCAL:1++
			SIF (LOCAL:1 % 3) == 0
				PRINTL 
			PRINTFORM %LOCALS,52,LEFT%
			PRINT  
			SIF (LOCAL:1 % 3) == 0
				LOCAL:1++
		ELSE
			PRINTFORM %LOCALS,26,LEFT%
		ENDIF
		LOCAL:1++
	ENDIF
	IF (LOCAL:1 % 3) == 0
		PRINTL 
	ELSE
		PRINT  
	ENDIF
NEXT
SIF (LOCAL:1 % 3) != 0
	PRINTL 
SETCOLOR 140,140,140
DRAWLINE
RESETCOLOR
TFLAG:0 = -1


;--------------------------------------------------
@USE_ITEM
;--------------------------------------------------
VARSET LOCAL
TFLAG:407 = 0
SELECTCASE BOUGHT
	;【더러움무시】
	CASE 50
		TALENT:더러움무시 = 1
	;【조제지식】
	CASE 51
		TALENT:조제지식 = 1
	;【금단의지식】
	CASE 52
		TALENT:금지된지식 = 1
	;【천리안】
	CASE 53
		TALENT:천리안 = 1
	;【절륜】
	CASE 54
		TALENT:절륜 = 1
	;수
	CASE 55
		;공なら素質削除
		IF TALENT:공
			TALENT:공 = 0
		;無ければ[수]追加
		ELSE
			TALENT:수 = 1
		ENDIF
	;공
	CASE 56
		;수なら素質削除
		IF TALENT:수
			TALENT:수 = 0
		;無ければ[공]追加
		ELSE
			TALENT:공 = 1
		ENDIF
	;오메가
	CASE 59
		TALENT:오메가 = 1
		;발정기のカウント　0-6でランダムで初期値を付与
		CFLAG:700 = RAND:7
	;河童の妙薬
	CASE 60
		;체력기력500回復、기절回復、ストレス5追加
		;主人か助손が[치료]か[조제지식]持ちなら追加で체력기력300回復
		CFLAG:10 += 5
		BASE:체력 += 500
		BASE:기력 += 500
		IF TALENT:MASTER:치료 || TALENT:MASTER:조제지식 || (ASSI >= 0 && (TALENT:ASSI:치료 || TALENT:ASSI:조제지식))
			BASE:체력 += 300
			BASE:기력 += 300
		ENDIF
		SIF BASE:체력 > MAXBASE:체력
			BASE:체력 = MAXBASE:체력
		SIF BASE:기력 > MAXBASE:기력
			BASE:기력 = MAXBASE:기력
		TALENT:기절 = 0
	;약의재료(후타)
	CASE 61
		;ストレス30追加
		CFLAG:10 += 30
		;男のときは반음반양(男性体型に바기나あり)になる
		IF TALENT:남자
			;바기나生えた経験フラグが無ければランダムでＶ関係素質＆[처녀]追加
			SIF !CFLAG:24
				CALL VAGINA_SET(TARGET)
			;바기나生えた経験フラグ、페니스あったことある経験フラグ
			CFLAG:24 = 1
			CFLAG:25 = 1
			;[남자]消去[반음반양]追加
			TALENT:남자 = 0
			TALENT:반음반양 = 1
		;女のときは후타나리(女性体型に페니스あり)になる
		ELSE
			;페니스生えた経験なければ、ランダムで페니스サイズ設定＆ランダムで包む＆[동정]
			IF !CFLAG:25
				CALL PENIS_SET(TARGET)
				;입上追加表示フラグ
				TFLAG:407 = 1
			ENDIF
			;페니스生えた経験フラグ、바기나・가슴有ったことある経験フラグ
			CFLAG:23 = 1
			CFLAG:24 = 1
			CFLAG:25 = 1
			;[후타나리]追加
			TALENT:후타나리 = 1
		ENDIF
	;약의재료(소멸)
	CASE 62
		;ストレス10追加
		CFLAG:10 += 10
		;반음반양のときは男に
		IF TALENT:반음반양
			;바기나生えた経験フラグ、페니스あったことある経験フラグ
			CFLAG:24 = 1
			CFLAG:25 = 1
			;[반음반양]消去[남자]追加
			TALENT:남자 = 1
			TALENT:반음반양 = 0
		;후타나리のときは女に
		ELSE
			;페니스生えた経験フラグ、바기나・가슴有ったことある経験フラグ
			CFLAG:23 = 1
			CFLAG:24 = 1
			CFLAG:25 = 1
			;[후타나리]消去
			TALENT:후타나리 = 0
		ENDIF
	;푸른환약
	CASE 63
		;ストレス5追加
		CFLAG:10 += 5
		;[모유체질]追加
		TALENT:모유체질 = 1
	;붉은환약
	CASE 64
		;ストレス10追加
		CFLAG:10 += 10
		;[재생처녀]追加
		TALENT:재생처녀 = 1
	;풍유환
	CASE 65
		;ストレス10追加
		CFLAG:10 += 10
		;절벽→빈유
		IF TALENT:절벽
			TALENT:절벽 = 0
			TALENT:빈유 = 1
		;빈유→普乳
		ELSEIF TALENT:빈유
			TALENT:빈유 = 0
		;普乳→거유
		ELSEIF !TALENT:거유
			TALENT:거유 = 1
		;거유→폭유
		ELSE
			TALENT:거유 = 0
			TALENT:폭유 = 1
		ENDIF
	;축유환
	CASE 66
		;ストレス10追加
		CFLAG:10 += 10
		;폭유→거유
		IF TALENT:폭유
			TALENT:거유 = 1
			TALENT:폭유 = 0
		;거유→普乳
		ELSEIF TALENT:거유
			TALENT:거유 = 0
		;普乳→빈유
		ELSEIF !TALENT:빈유
			TALENT:빈유 = 1
		;빈유→절벽、모유も出なくなる
		ELSE
			TALENT:절벽 = 1
			TALENT:빈유 = 0
			;입上追加表示フラグ
			SIF TALENT:모유체질
				TFLAG:407 = 1
			TALENT:모유체질 = 0
		ENDIF
	;프롤락틴억제제
	CASE 67
		;ストレス5追加
		CFLAG:10 += 5
		;[모유체질]削除
		TALENT:모유체질 = 0
	;3환계항우울제
	CASE 68
		;ストレス5追加
		CFLAG:10 += 5
		;[실금버릇]削除
		TALENT:실금버릇 = 0
	;노란환약
	CASE 84
		;LOCAL = 最終的に加算されるストレス値
		;LOCAL:1 = 減算されるストレス値（산란경험）　LCOAL:2 = 加算されるストレス値（父親）
		;LOCAL:3 = 加算される이상경험の数
		;PREGNANT = 임신してるキャラの登録番号　FATHER = 父親の登録番号
		PREGNANT = TARGET
		;[임신]削除
		TALENT:PREGNANT:임신 = 0
		;임신イベントで減った체력の最大値が回復（少なめ）
		MAXBASE:PREGNANT:체력 += 300
		;膣で孕んでいるなら女
		IF CFLAG:PREGNANT:101 / 100 == 1
			;가슴の縮小処理を実行
			CALL REVERSE_B_SIZE
		ENDIF
		;임신関係のフラグ処理
		CALL CLEAR_FLAG
		
		;ストレス値の計算
		;산란경험をみる
		IF EXP:PREGNANT:산란경험 < EXPLV:1
			LOCAL:1 = 0
		ELSEIF EXP:PREGNANT:산란경험 < EXPLV:2
			LOCAL:1 = 2
		ELSEIF EXP:PREGNANT:산란경험 < EXPLV:3
			LOCAL:1 = 4
		ELSEIF EXP:PREGNANT:산란경험 < EXPLV:4
			LOCAL:1 = 6
		ELSEIF EXP:PREGNANT:산란경험 < EXPLV:5
			LOCAL:1 = 8
		ELSE
			LOCAL:1 = 10
		ENDIF
		;임신相손と陥落素質をみる
		;母親が未陥落
		IF !KANRAKU2(PREGNANT)
			LOCAL:2 = 100
		;父親が顧客
		ELSEIF CFLAG:PREGNANT:101 % 100 == 5
			;母親が복종陥落
			IF KANRAKU2(PREGNANT) == 3
				LOCAL:2 = 30
			;母親が연모陥落
			ELSEIF KANRAKU2(PREGNANT) == 1
				LOCAL:2 = 80
			;母親が음란陥落
			ELSE
				LOCAL:2 = 50
			ENDIF
		;母親が음란陥落
		ELSEIF KANRAKU2(PREGNANT) == 2
			LOCAL:2 = 50
		;父親が売却された元思慕・畏怖対象
		ELSEIF CFLAG:PREGNANT:38 > 0 && CFLAG:PREGNANT:111 == CFLAG:PREGNANT:38
			LOCAL:2 = 200
		;母親が복종陥落
		ELSEIF KANRAKU2(PREGNANT) == 3
			LOCAL:2 = 30
		;母親が연모陥落
		ELSE
			;思慕対象が父親
			FATHER = GETCHARA(CFLAG:PREGNANT:111)
			IF FATHER >= 0 && CFLAG:PREGNANT:41 == NO:(FATHER)
				LOCAL:2 = 120
			;それ以外
			ELSE
				LOCAL:2 = 80
			ENDIF
		ENDIF
		LOCAL = LOCAL:2 - (LOCAL:1 + ABL:PREGNANT:순종 * 4 + ABL:PREGNANT:V감각 * 2) + TALENT:PREGNANT:모성 * 5 - TALENT:PREGNANT:꿋꿋함 * 2 - TALENT:PREGNANT:감정부족 * 5
		SIF LOCAL < 0
			LOCAL = 0
		
		;最終的なストレス値の加算
		CFLAG:PREGNANT:10 += LOCAL
		
		;経験の追加
		;膣で孕んでる
		IF CFLAG:PREGNANT:101 / 100 == 1
			EXP:PREGNANT:V확장경험 += 5
			PRINTL V확장경험＋5
		;애널で孕んでる
		ELSEIF CFLAG:PREGNANT:101 / 100 == 7
			EXP:PREGNANT:A확장경험 += 5
			PRINTL A확장경험＋5
		ENDIF
		
		EXP:PREGNANT:산란경험 += 1
		PRINTL 산란경험＋1
		
		;出産、V확장경험・A확장경험がなければ이상경험
		SIF !EXP:PREGNANT:출산경험
			LOCAL:3 += 2
		SIF EXP:PREGNANT:V확장경험 == 5 && (CFLAG:PREGNANT:101 / 100 == 1)
			LOCAL:3 += 1
		SIF EXP:PREGNANT:A확장경험 == 5 && (CFLAG:PREGNANT:101 / 100 == 7)
			LOCAL:3 += 1
		IF LOCAL:3
			EXP:PREGNANT:이상경험 += LOCAL:3
			PRINTFORML 이상경험＋{LOCAL:3}
		ENDIF
		;체력は10になる
		BASE:PREGNANT:체력 = 10
		;붕괴処理
		SIF CFLAG:PREGNANT:10 >= 100 && !TALENT:PREGNANT:붕괴
			CALL COLLAPSE_MIND
		;休憩フラグをたて、ターンエンド
		PRINTW 
		FLAG:0 = 1
		BEGIN TURNEND
	;절반성전환약
	CASE 69
		;ストレス30追加
		CFLAG:10 += 30
		;컨트보이から男へ
		IF TALENT:컨트보이
			;페니스生えた経験なければ、ランダムで페니스サイズ設定＆ランダムで包む＆[동정]
			IF !CFLAG:25
				CALL PENIS_SET(TARGET)
				;입上追加表示フラグ
				TFLAG:407 = 1
			ENDIF
			;페니스生えた経験フラグ、바기나有ったことある経験フラグ
			CFLAG:24 = 1
			CFLAG:25 = 1
			;[컨트보이]消去
			TALENT:컨트보이 = 0
			;[남자]追加
			TALENT:남자 = 1
		;쉬메일から女へ
		ELSEIF TALENT:쉬메일
			;바기나生えた経験フラグが無ければランダムでＶ関係素質＆[처녀]追加
			SIF !CFLAG:24
				CALL VAGINA_SET(TARGET)
			;바기나生えた経験フラグ、페니스あったことある経験フラグ
			CFLAG:24 = 1
			CFLAG:25 = 1
			;[쉬메일]消去
			TALENT:쉬메일 = 0
		;후타나리から쉬메일
		ELSEIF TALENT:후타나리
			;바기나有ったことある経験フラグ
			CFLAG:24 = 1
			;[후타나리]消去
			TALENT:후타나리 = 0
			;[쉬메일]追加
			TALENT:쉬메일 = 1
		;반음반양から컨트보이
		ELSEIF TALENT:반음반양
			;페니스あったことある経験フラグ
			CFLAG:25 = 1
			;[반음반양]消去
			TALENT:반음반양 = 0
			;[컨트보이]追加
			TALENT:컨트보이 = 1
		;男から컨트보이に
		ELSEIF TALENT:남자
			;바기나生えた経験フラグが無ければランダムでＶ関係素質＆[처녀]追加
			SIF !CFLAG:24
				CALL VAGINA_SET(TARGET)
			;바기나生えた経験フラグ、페니스あったことある経験フラグ
			CFLAG:24 = 1
			CFLAG:25 = 1
			;[남자]消去
			TALENT:남자 = 0
			;[컨트보이]追加
			TALENT:컨트보이 = 1
		;女から쉬메일に
		ELSE
			;페니스生えた経験なければ、ランダムで페니스サイズ設定＆ランダムで包む＆[동정]
			IF !CFLAG:25
				CALL PENIS_SET(TARGET)
				;입上追加表示フラグ
				TFLAG:407 = 1
			ENDIF
			;페니스生えた経験フラグ、바기나有ったことある経験フラグ
			CFLAG:24 = 1
			CFLAG:25 = 1
			;[쉬메일]追加
			TALENT:쉬메일 = 1
		ENDIF
	;성전환약
	CASE 70
		;ストレス30追加
		CFLAG:10 += 30
		;후타나리から반음반양
		IF TALENT:후타나리
			;페니스・바기나・가슴生えた経験フラグ
			CFLAG:23 = 1
			CFLAG:24 = 1
			CFLAG:25 = 1
			;[후타나리]消去
			TALENT:후타나리 = 0
			;[반음반양]追加
			TALENT:반음반양 = 1
		;반음반양から후타나리
		ELSEIF TALENT:반음반양
			;가슴生えた経験なければ、ランダムで가슴サイズ設定
			IF !CFLAG:23
				CALL BREAST_SET(TARGET)
				;입上追加表示フラグ
				TFLAG:407 = 1
			ENDIF
			;페니스・바기나・가슴生えた経験フラグ
			CFLAG:23 = 1
			CFLAG:24 = 1
			CFLAG:25 = 1
			;[반음반양]消去
			TALENT:반음반양 = 0
			;[후타나리]追加
			TALENT:후타나리 = 1
		;컨트보이から쉬메일に
		ELSEIF TALENT:컨트보이
			;가슴生えた経験なければ、ランダムで가슴サイズ設定
			IF !CFLAG:23
				CALL BREAST_SET(TARGET)
				;입上追加表示フラグ
				TFLAG:407 = 1
			ENDIF
			;페니스生えた経験なければ、ランダムで페니스サイズ設定＆ランダムで包む＆[동정]
			IF !CFLAG:25
				CALL PENIS_SET(TARGET)
				;입上追加表示フラグ
				TFLAG:407 = 1
			ENDIF
			;페니스・가슴生えた経験フラグ、바기나有ったことある経験フラグ
			CFLAG:23 = 1
			CFLAG:24 = 1
			CFLAG:25 = 1
			;[컨트보이]消去
			TALENT:컨트보이  = 0
			;[쉬메일]追加
			TALENT:쉬메일  = 1
		;쉬메일から컨트보이に
		ELSEIF TALENT:쉬메일
			;바기나生えた経験フラグが無ければランダムでＶ関係素質＆[처녀]追加
			SIF !CFLAG:24
				CALL VAGINA_SET(TARGET)
			;바기나生えた経験フラグ、페니스・가슴有ったことある経験フラグ
			CFLAG:23 = 1
			CFLAG:24 = 1
			CFLAG:25 = 1
			;[쉬메일]消去
			TALENT:쉬메일  = 0
			;[컨트보이]追加
			TALENT:컨트보이  = 1
		;男から女に
		ELSEIF TALENT:남자
			;가슴生えた経験なければ、ランダムで가슴サイズ設定
			IF !CFLAG:23
				CALL BREAST_SET(TARGET)
				;입上追加表示フラグ
				TFLAG:407 = 1
			ENDIF
			;바기나生えた経験フラグが無ければランダムでＶ関係素質＆[처녀]追加
			SIF !CFLAG:24
				CALL VAGINA_SET(TARGET)
			;바기나、가슴生えた経験フラグ、페니스あったことある経験フラグ
			CFLAG:23 = 1
			CFLAG:24 = 1
			CFLAG:25 = 1
			;[남자]消去
			TALENT:남자 = 0
		;女から男に
		ELSE
			;페니스生えた経験なければ、ランダムで페니스サイズ設定＆ランダムで包む＆[동정]
			IF !CFLAG:25
				CALL PENIS_SET(TARGET)
				;입上追加表示フラグ
				TFLAG:407 = 1
			ENDIF
			;페니스生えた経験フラグ、바기나・가슴有ったことある経験フラグ
			CFLAG:23 = 1
			CFLAG:24 = 1
			CFLAG:25 = 1
			;[남자]追加
			TALENT:남자 = 1
		ENDIF
	;천사의기적
	CASE 71
		;ストレス20追加
		CFLAG:10 += 20
		;작은체구→소인체형
		IF TALENT:작은체구
			;[음모없음][소인체형]追加[작은체구]削除
			TALENT:음모없음 = 1
			TALENT:소인체형 = 1
			TALENT:작은체구 = 0
			;페니스あり
			IF PENIS(TARGET)
				;[아이사이즈][포경]追加、他の페니스サイズ素質を削除
				TALENT:아이사이즈 = 1
				TALENT:포경 = 1
				TALENT:단소 = 0
				TALENT:거근 = 0
				TALENT:말자지 = 0
			ENDIF
		;普通体型→작은체구
		ELSEIF !TALENT:큰체구
			;[작은체구]追加
			TALENT:작은체구 = 1
		;큰체구→普通体型
		ELSE
			;[큰체구]削除
			TALENT:큰체구 = 0
		ENDIF
	;악마의저주
	CASE 72
		;ストレス20追加
		CFLAG:10 += 20
		;소인체형→작은체구
		IF TALENT:소인체형
			;[음모없음][소인체형]削除[작은체구]追加
			TALENT:음모없음 = 0
			TALENT:소인체형 = 0
			TALENT:작은체구 = 1
			;페니스あり
			IF PENIS(TARGET)
				;[아이사이즈]削除[단소]追加
				TALENT:아이사이즈 = 0
				TALENT:단소 = 1
			ENDIF
		;작은체구→普通体型
		ELSEIF TALENT:작은체구
			;[작은체구]削除
			TALENT:작은체구 = 0
		;普通体型→큰체구
		ELSEIF !TALENT:큰체구
			;[큰체구]追加
			TALENT:큰체구 = 1
		ENDIF
	;포근환
	CASE 75
		;ストレス5追加
		CFLAG:10 += 5
		;[포경]追加
		TALENT:포경 = 1
	;기억소거제
	CASE 80
		;記憶消去経験
		CFLAG:51++
		;素質の一部初期化
		FOR LOCAL, 0, 170
			SELECTCASE LOCAL
				;[연모][음란][복종][친애][창부][예속][붕괴][실의][상사상애]
				CASE 3 TO 9,162,165
					TALENT:LOCAL = 0
				;身体特徴素質はそのまま
				CASE 10 TO 37,44,63,82 TO 94,141,150,151,155
					;아이には効果なし
					SIF NO:TARGET >= 890 && NO:TARGET <= 899
						CONTINUE
					CSVTALENT NO:TARGET, LOCAL, TFLAG:407
					TALENT:LOCAL = RESULT
					;個体差処理　요리치のみ
					SIF DIFF_GET_TALENT(TARGET, 12) == 2
						TALENT:LOCAL:요리치 = 1
			ENDSELECT
		NEXT
		;刻印の初期化
		FOR LOCAL, 0, 5
			MARK:LOCAL = 0
		NEXT
		;能力の一部初期化
		FOR LOCAL, 0, 23
			SIF LOCAL >= 3 && LOCAL <= 6
				CONTINUE
			ABL:LOCAL = 0
		NEXT
		;상사상애がある場合、상사상애相손の[상사상애]を消し、陥落素質記録フラグを[친애]に戻す
		IF CFLAG:39 == 165
			LOCAL = RESULT
			TALENT:(NO2(CFLAG:LOCAL:41)):상사상애 = 0
			CFLAG:(NO2(CFLAG:LOCAL:41)):39 = 6
		ENDIF
		;キャラフラグの一部初期化
		;입上用CFLAGのうち5000～の方は初期化しないので、不都合がある場合입上内で初期化して下さい
		FOR LOCAL, 0, 1300
			SELECTCASE LOCAL
				CASE 1,2,10,38 TO 41,140 TO 147,201 TO 600,1099 TO 2098,2099 TO 3098
					CFLAG:LOCAL = 0
			ENDSELECT
		NEXT
		;FIRSTTIME関数の初期化
		CSTR:20 = 
		;ストレス50追加
		CFLAG:10 += 50
	;배란유발제
	CASE 82
		CFLAG:109 = 1
	;고성능피임구
	CASE 83
		CFLAG:108 = 1
	;긴급피임약
	CASE 85
		PREGNANT = TARGET
		;임신関係のフラグ処理
		CALL CLEAR_FLAG
	;풍근환
	CASE 73
		;ストレス10追加
		CFLAG:10 += 10
		;아이사이즈→단소
		IF TALENT:아이사이즈
			;[아이사이즈]削除[단소]追加
			TALENT:아이사이즈 = 0
			TALENT:단소 = 1
		;단소→人並み
		ELSEIF TALENT:단소
			;[단소]削除
			TALENT:단소 = 0
		;人並み→거근
		ELSEIF !TALENT:거근 && !TALENT:말자지
			;[거근]追加
			TALENT:거근 = 1
		;거근→말자지
		ELSE
			;[거근]削除[말자지]追加
			TALENT:거근 = 0
			TALENT:말자지 = 1
		ENDIF
	;축근환
	CASE 74
		;ストレス15追加
		CFLAG:10 += 15
		;말자지→거근
		IF TALENT:말자지
			;[말자지]削除[거근]追加
			TALENT:거근 = 1
			TALENT:말자지 = 0
		;거근→人並み
		ELSEIF TALENT:거근
			;[거근]削除
			TALENT:거근 = 0
		;人並み→단소
		ELSEIF !TALENT:단소 && !TALENT:아이사이즈
			;[단소]追加
			TALENT:단소 = 1
		;단소→아이사이즈
		ELSE
			;[단소]削除[아이사이즈]追加
			TALENT:아이사이즈 = 1
			TALENT:단소 = 0
		ENDIF
	;되감기약
	CASE 81
		;CSVの設定から呼び出すので、입上で生まれつき女体化、未経験などの設定を付けるときは
		;イベント입上：巻き戻し薬に素質変更を仕込むこと
		
		;体格は戻さず、CSVが소인체형でなく現소인체형のとき、経験・能力・처녀系素質も白紙にする
		
		;元は소인체형でない
		SIF !CSVTALENT(NO:TARGET, 142, CFLAG:0)
			LOCAL:1 = 1
		;個体差で소인체형を得ていない
		SIF DIFF_GET_TALENT(TARGET , 4) != 1
			LOCAL:1 = 1
		;巻き戻し経験
		CFLAG:52++
		;ストレス30追加
		CFLAG:10 += 30
		;체력の初期化
		MAXBASE:체력 = CSVBASE(NO:TARGET, 0, CFLAG:0)
		SIF BASE:체력 > MAXBASE:체력
			BASE:체력 = MAXBASE:체력
		;経験の初期化
		FOR LOCAL, 0, 70
			EXP:LOCAL = CSVEXP(NO:TARGET, LOCAL, CFLAG:0)
			SIF LOCAL:1
				EXP:LOCAL = 0
		NEXT
		;能力の一部初期化
		FOR LOCAL, 0, 23
			SIF LOCAL < 2 || (LOCAL > 6 && LOCAL < 12) || LOCAL > 14
				CONTINUE
			ABL:LOCAL = CSVABL(NO:TARGET, LOCAL, CFLAG:0)
			SIF LOCAL:1
				ABL:LOCAL = 0
		NEXT
		;素質の一部初期化
		FOR LOCAL, 0, 205
			SELECTCASE LOCAL
				;낙인欠損は消す
				CASE 159, 200 TO 205
					TALENT:LOCAL = 0
				;変化してるかもしれない素質を元に戻す
				;요리치,독약내성,실금버릇,악취민감
				;身体特徴素質,性別,동물귀,모유체질,절륜,낙인欠損
				CASE 1, 44 TO 47, 56, 57, 62, 100 TO 140, 142, 143, 149, 163, 172, 173
					;아이には効果なし
					SIF NO:TARGET >= 890 && NO:TARGET <= 899
						CONTINUE
					TALENT:LOCAL = CSVTALENT(NO:TARGET, LOCAL, CFLAG:0)
				;처녀系素質
				CASE 0, 160, 161
					TALENT:LOCAL = CSVTALENT(NO:TARGET, LOCAL, CFLAG:0)
					SIF LOCAL:1
						TALENT:LOCAL = 1
			ENDSELECT
		NEXT
		;個体差処理
		CALL DIFF_GET_BUY(TARGET)
		;고통/쾌락각인の初期化
		MARK:고통각인 = 0
		MARK:쾌락각인 = 0
		;経験系フラグを初期化
		CFLAG:23 = 0
		CFLAG:24 = 0
		CFLAG:25 = 0
		CFLAG:43 = 0
		CFLAG:44 = 0
		CFLAG:45 = 0
		CFLAG:46 = 0
		CFLAG:47 = 0
		CFLAG:49 = 0
		CFLAG:50 = 0
		;임신パッチまわりのフラグをリセット
		SIF !CFLAG:110
			CALL CLEAR_FLAG
	CASE 96
		CFLAG:693 += 1
	CASE 97
		CFLAG:694 += 1
ENDSELECT
;アイテムによる붕괴の処理
SIF CFLAG:10 >= 100 && !TALENT:붕괴
	CALL COLLAPSE_MIND


;--------------------------------------------------
;それぞれの店で買える商品
;--------------------------------------------------
@SHOW_ITEM(ARG, ARG:1)
#FUNCTION
LOCAL = 0
;アダルトショップ
;로터　진동마사지기　클리캡　전동오나홀　바이브　애널바이브　애널비즈
;바디클립　확장벌룬　관장도구세트　니틀캡　착유기　페니스밴드
;채찍　바늘　아이마스크　밧줄　볼개그　비디오카메라　카메라　플레이매트　대거울
;목걸이＋리드　전극플레이키트　리본　카테터　삼각목마
;비디오테이프　필름　저온초　酒
;（薬局兼用）로션　미약　이뇨제　콘돔　강정신약　구토제
;（ブティック兼用）정조대
IF ARG == 1
	SELECTCASE ARG:1
		CASE 0 TO 26,30 TO 34,36 TO 39,40 TO 75,80 TO 88,90 TO 94,96,97,
			LOCAL = 1
	ENDSELECT
ENDIF
RETURNF LOCAL


;--------------------------------------------------
;販売条件
;--------------------------------------------------
@SALEITEM_CHECK(ARG)
VARSET LOCAL
ITEMSALES:ARG = 1
;存在しないアイテムは売れない
SIF !STRLENS(ITEMNAME:ARG)
	ITEMSALES:ARG = 0
;アイテム番号以上
SIF ARG >= 200
	ITEMSALES:ARG = 0
;基本アイテム
IF (ARG >= 0 && ARG != 36 && ARG != 37 && ARG <= 39) || ARG == 57 || ARG == 58 || (ARG >= 90 && ARG <= 94)
	;すでに所持しているなら販売しない
	SIF ITEM:ARG
		ITEMSALES:ARG = 0
ENDIF
;消費アイテム
IF ARG == 36 || ARG == 37 || (ARG >= 40 && ARG <= 49) || (ARG >= 86 && ARG <= 88)
	;すでに99個持っているなら販売しない
	SIF ITEM:ARG >= 99
		ITEMSALES:ARG = 0
ENDIF
;主人か助손が금지된지식を持っている
SIF TALENT:MASTER:금지된지식 || (ASSI >= 0 && TALENT:ASSI:금지된지식)
	LOCAL:1 = 1
;主人か助손が조제지식を持っている
SIF TALENT:MASTER:조제지식 || (ASSI >= 0 && TALENT:ASSI:조제지식)
	LOCAL:2 = 1
FOR LOCAL, 0, CHARANUM
	;主人か共犯者、助손経験者のいずれかが금지된지식を持っている
	IF TALENT:LOCAL:금지된지식
		SIF CFLAG:LOCAL:1 == 3 || ISASSI:LOCAL == 1
			LOCAL:3++
	ENDIF
	;主人か共犯者、助손経験者のいずれかが조제지식を持っている
	IF TALENT:LOCAL:조제지식
		SIF CFLAG:LOCAL:1 == 3 || ISASSI:LOCAL == 1
			LOCAL:4++
	ENDIF
	;상사상애カップルがいる
	SIF TALENT:LOCAL:상사상애
		LOCAL:5++
	;더러움무시を持っている
	SIF TALENT:LOCAL:더러움무시
		LOCAL:6++
	;조제지식を持っている
	SIF TALENT:LOCAL:조제지식
		LOCAL:7++
	;금지된지식を持っている
	SIF TALENT:LOCAL:금지된지식
		LOCAL:8++
	;천리안を持っている
	SIF TALENT:LOCAL:천리안
		LOCAL:9++
	;절륜を持っている
	SIF TALENT:LOCAL:절륜
		LOCAL:10++
	;수を持っている
	SIF TALENT:LOCAL:수
		LOCAL:11++
	;공を持っている
	SIF TALENT:LOCAL:공
		LOCAL:12++
	;陥落済み奴隷がいる
	SIF TALENT:LOCAL:복종 || TALENT:LOCAL:예속 || TALENT:LOCAL:연모 || TALENT:LOCAL:친애 || TALENT:LOCAL:음란 || TALENT:LOCAL:창부
		LOCAL:13++
	
	;アイテム使用個人条件
	SIF ARG >= 60 && ARG <= 85
		LOCAL:(ARG - 50) += ITEM_USE_CHECK(LOCAL, ARG)
NEXT

;主人か共犯者、助손経験者のだれも금지된지식を持っていない
;촉수사역술　진화의비법　약의재료(후타)　절반성전환약　수술도구세트
SELECTCASE ARG
	CASE 57,58,61,69,90
		SIF !LOCAL:3
			ITEMSALES:ARG = 0
ENDSELECT

;【조제지식】主人か共犯者、助손経験者のだれも조제지식を持っていない
SIF ARG == 52 && !LOCAL:4
	ITEMSALES:ARG = 0

;主人か助손の조제지식が必須
;약의재료(후타) 약의재료(소멸) 푸른환약 붉은환약 풍유환 축유환
;프롤락틴억제제 3환계항우울제 절반성전환약 성전환약 풍근환 축근환 포근환
;노란환약 기억소거제 되감기약　긴급피임약
SELECTCASE ARG
	CASE 61 TO 70,73 TO 75,80,81,84,85
		SIF !LOCAL:2
			ITEMSALES:ARG = 0
ENDSELECT

;主人か助손の금지된지식が必須
;천리안　【오메가】　천사의기적　악마의저주　기억소거제　되감기약　노란환약
SELECTCASE ARG
	CASE 53,59,71,72,80,81,84
		SIF !LOCAL:1
			ITEMSALES:ARG = 0
ENDSELECT
;その他条件
SELECTCASE ARG
	;우리、포장마차、플랜카드
	CASE 91,92,94
		;陥落済み奴隷がいない
		SIF !LOCAL:13
			ITEMSALES:ARG = 0
	;포장마차・증축
	CASE 93
		;포장마차を持ってない
		SIF !ITEM:92
			ITEMSALES:ARG = 0
	;진화의비법
	CASE 58
		;촉수사역술を持っていない
		SIF !ITEM:57
			ITEMSALES:ARG = 0
	;웨딩세트
	CASE 39
		;상사상애のカップルがいない
		SIF !LOCAL:5
			ITEMSALES:ARG = 0
	;수술도구세트
	CASE 90
		;猟奇コマンド拡張オフ
		SIF (FLAG:2 & 32) == 0
			ITEMSALES:ARG = 0
	;비디오테이프
	CASE 43
		;비디오카메라が無い
		SIF !ITEM:18
			ITEMSALES:ARG = 0
	;필름
	CASE 44
		;카메라が無い
		SIF !ITEM:19
			ITEMSALES:ARG = 0
	;【더러움무시】【조제지식】【금단의지식】【천리안】【절륜】【수】【공】
	;全員所持している
	CASE 50 TO 56
		SIF LOCAL:(ARG - 35) == CHARANUM
			ITEMSALES:ARG = 0
ENDSELECT


;--------------------------------------------------
;所持アイテムの表示
;--------------------------------------------------
@ITEM_LIST
TFLAG:0 = -1
SETCOLOR 140,140,140
DRAWLINE
RESETCOLOR
PRINTFORML 所持甲州金：{MONEY}고슈금
PRINT 所持アイテム：
LOCAL:1 = 0
FOR LOCAL, 0, 199
	IF ITEM:LOCAL
		SIF (LOCAL >= 50 && LOCAL <= 56) || (LOCAL >= 59 && LOCAL <= 85)
			CONTINUE
		LOCAL:2 = STRLENS(ITEMNAME:LOCAL)
		LOCAL:1 += LOCAL:2 + 3
		IF LOCAL:1 > 60
			PRINTL 
			PRINT 　　　　　　　
			LOCAL:1 = LOCAL:2 + 3
		ENDIF
		PRINTFORM [%ITEMNAME:LOCAL%
		IF ITEM:LOCAL > 1
			PRINTFORM ×{ITEM:LOCAL}
			LOCAL:1 += 5
		ENDIF
		PRINTFORM ] 
	ENDIF
NEXT
SIF !LOCAL:1
	PRINT なし
PRINTL 
SETCOLOR 140,140,140
DRAWLINE
RESETCOLOR


;--------------------------------------------------
;アイテム使用個人条件
;--------------------------------------------------
@ITEM_USE_CHECK(ARG, ARG:1)
#FUNCTION
;労役中のキャラは除外
SIF CFLAG:ARG:12 > 0
	RETURNF 0
;成長前の아이は除外
SIF CFLAG:ARG:26
	RETURNF 0
;育児部屋に移動しているキャラは除外
SIF (TALENT:ARG:임신 && CFLAG:ARG:110 - 3 <= DAY) || TALENT:ARG:육아중
	RETURNF 0
SELECTCASE ARG:1
	;【더러움무시】【조제지식】【금단의지식】【천리안】【절륜】は助손可能キャラのみ
	CASE 50 TO 54
		SIF CFLAG:ARG:1 >= 2
			RETURNF 1
	;수
	CASE 55
		;수でない
		SIF !TALENT:ARG:수
			RETURNF 1
	;공
	CASE 56
		;공でない
		SIF !TALENT:ARG:공
			RETURNF 1
	;오메가
	CASE 59
		;오메가でない
		SIF !TALENT:ARG:오메가
			RETURNF 1
	;河童の妙薬
	CASE 60
		;체력기력のどちらかが減少している
		SIF BASE:ARG:체력 < MAXBASE:ARG:체력 || BASE:ARG:기력 < MAXBASE:ARG:기력
			RETURNF 1
	;약의재료(후타)
	CASE 61
		;후타나리/반음반양/컨트보이/쉬메일でなく、主人か助손が금지된지식を持っているか対象に이상한뿌리か구현が必要
		SIF !TALENT:ARG:후타나리 && !TALENT:ARG:반음반양 && !TALENT:ARG:컨트보이 && !TALENT:ARG:쉬메일 && ((TALENT:MASTER:금지된지식 || (ASSI >= 0 && TALENT:ASSI:금지된지식)) || (TALENT:ARG:이상한뿌리 || TALENT:ARG:구현))
			RETURNF 1
	;약의재료(소멸)
	CASE 62
		;후타나리/반음반양をもっている
		SIF TALENT:ARG:후타나리 || TALENT:ARG:반음반양
			RETURNF 1
	;푸른환약
	CASE 63
		;모유체질でない
		SIF !TALENT:ARG:모유체질 && ((FLAG:2 & 256) || ((BREAST(ARG) || TALENT:ARG:절벽) && (FLAG:2 & 256) == 0 ))
			RETURNF 1
	;붉은환약
	CASE 64
		;처녀/재생처녀でなく、바기나がある
		SIF !TALENT:ARG:처녀 && !TALENT:ARG:재생처녀 && VAGINA(ARG)
			RETURNF 1
	;풍유환
	CASE 65
		;男でなく、폭유でない
		SIF !MALE(ARG) && !TALENT:ARG:폭유
			RETURNF 1
	;축유환
	CASE 66
		;男でなく、절벽でない
		SIF !MALE(ARG) && !TALENT:ARG:절벽
			RETURNF 1
	;프롤락틴억제제
	CASE 67
		;모유체질である
		SIF TALENT:ARG:모유체질
			RETURNF 1
	;3환계항우울제
	CASE 68
		;실금버릇である
		SIF TALENT:ARG:실금버릇
			RETURNF 1
	;절반성전환약
	CASE 69
		;主人か助손が금지된지식を持っている　かつ　임신していない（発覚前含む）
		SIF (TALENT:MASTER:금지된지식 || (ASSI >= 0 && TALENT:ASSI:금지된지식)) && CFLAG:ARG:110 <= 0
			RETURNF 1
	;性転換薬
	CASE 70
		;임신していない（発覚前含む）
		SIF CFLAG:ARG:110 <= 0
			RETURNF 1
	;천사의기적
	CASE 71
		;소인체형でない
		SIF !TALENT:ARG:소인체형
			RETURNF 1
	;악마의저주
	CASE 72
		;큰체구でない
		SIF !TALENT:ARG:큰체구
			RETURNF 1
	;풍근환
	CASE 73
		;페니스があり말자지でない
		SIF PENIS(ARG) && !TALENT:ARG:말자지
			RETURNF 1
	;축근환
	CASE 74
		;페니스があり아이사이즈でない
		SIF PENIS(ARG) && !TALENT:ARG:아이사이즈
			RETURNF 1
	;포근환
	CASE 75
		;페니스があり包まれていない
		SIF PENIS(ARG) && !TALENT:ARG:포경
			RETURNF 1
	;기억소거제
	CASE 80
		;共犯者フラグが立っていなくて임신中、육아중でない
		SIF CFLAG:ARG:1 <= 2 && !TALENT:ARG:임신 && !TALENT:ARG:육아중
			RETURNF 1
	;되감기약
	CASE 81
		;共犯者フラグが立っていなくて임신中、육아중でなく、共犯者フラグが無い
		SIF CFLAG:ARG:1 <= 2 && !TALENT:ARG:임신 && !TALENT:ARG:육아중 && CFLAG:ARG:1 < 3
			RETURNF 1
	;배란유발제
	CASE 82
		;男でないか오메가持ち　かつ배란유발제効果発動中でなく、共犯者フラグが無い
		SIF (!MALE(ARG) || TALENT:ARG:오메가) && !CFLAG:ARG:109 && CFLAG:ARG:1 < 3
			RETURNF 1
	;고성능피임구
	CASE 83
		;男でないか오메가持ち　かつ고성능피임구効果発動中でなく、共犯者フラグが無い
		SIF (!MALE(ARG) || TALENT:ARG:오메가) && !CFLAG:ARG:108 && CFLAG:ARG:1 < 3
			RETURNF 1
	;노란환약
	CASE 84
		;임신中で臨月であり、共犯者フラグがない
		SIF TALENT:ARG:임신 && (CFLAG:ARG:110 - 3) > DAY && CFLAG:ARG:1 < 3
			RETURNF 1
	;긴급피임약
	CASE 85
		;남자でなく中田氏フラグが立ってる　もしくは　오메가持ち남자で腸中田氏フラグが立ってる
		SIF (!MALE(ARG) && (CFLAG:ARG:102 || CFLAG:ARG:103 || CFLAG:ARG:104 || CFLAG:ARG:105)) || (MALE(ARG) && TALENT:ARG:오메가 && (CFLAG:ARG:702 || CFLAG:ARG:703 || CFLAG:ARG:704 || CFLAG:ARG:705))
			RETURNF 1
	CASEELSE
		RETURNF 1
ENDSELECT
RETURNF 0


;--------------------------------------------------
;【붕괴】を得た場合の処理
;--------------------------------------------------
@COLLAPSE_MIND
TFLAG:13 = 52
;입上分岐フラグ
TFLAG:53 = 4
CALL KOJO_MESSAGE_EVENT
;부적があれば復活
IF CFLAG:693 || CFLAG:694
	TFLAG:13 = 72
	CALL KOJO_MESSAGE_EVENT
	IF CFLAG:694
		CFLAG:10 = 0
		CFLAG:694 -= 1
	ELSE
		CFLAG:10 = 90
		CFLAG:693 -= 1
	ENDIF
ELSE
	TALENT:붕괴 = 1
	TALENT:연모 = 0
	TALENT:음란 = 0
	TALENT:복종 = 0
	TALENT:친애 = 0
	TALENT:창부 = 0
	TALENT:예속 = 0
	TALENT:상사상애 = 0
	;상사상애がある場合、상사상애相손の[상사상애]を消し、陥落素質記録フラグを[친애]に戻す
	IF CFLAG:39 == 165
		TALENT:(NO2(CFLAG:LOCAL:41)):상사상애 = 0
		CFLAG:(NO2(CFLAG:LOCAL:41)):39 = 6
	ENDIF
	CFLAG:40 = 0
	CFLAG:41 = -1
	CFLAG:10 = 0
ENDIF


;--------------------------------------------------
;性転換・후타나리用페니스を生やす処理
;--------------------------------------------------
@PENIS_SET(ARG)
;페니스用素質が既にある場合
SIF TALENT:ARG:아이사이즈 || TALENT:ARG:단소 || TALENT:ARG:거근 || TALENT:ARG:말자지
	RETURN 0
;体型に応じてランダムな大きさの페니스が生える
LOCAL = RAND:100
IF TALENT:ARG:소인체형
	IF LOCAL >= 20
		LOCAL:1 = 0
	ELSEIF LOCAL >= 5
		LOCAL:1 = 1
	ELSE
		LOCAL:1 = 2
	ENDIF
ELSEIF TALENT:ARG:작은체구
	IF LOCAL >= 95
		LOCAL:1 = 0
	ELSEIF LOCAL >= 45
		LOCAL:1 = 1
	ELSEIF LOCAL:1 >= 10
		LOCAL:1 = 2
	ELSEIF LOCAL >= 3
		LOCAL:1 = 3
	ELSE
		LOCAL:1 = 4
	ENDIF
ELSEIF !TALENT:ARG:큰체구
	IF LOCAL >= 97
		LOCAL:1 = 0
	ELSEIF LOCAL >= 80
		LOCAL:1 = 1
	ELSEIF LOCAL:1 >= 20
		LOCAL:1 = 2
	ELSEIF LOCAL >= 5
		LOCAL:1 = 3
	ELSE
		LOCAL:1 = 4
	ENDIF
ELSE
	IF LOCAL >= 99
		LOCAL:1 = 0
	ELSEIF LOCAL >= 90
		LOCAL:1 = 1
	ELSEIF LOCAL:1 >= 30
		LOCAL:1 = 2
	ELSEIF LOCAL >= 10
		LOCAL:1 = 3
	ELSE
		LOCAL:1 = 4
	ENDIF
ENDIF
IF LOCAL:1 == 0
	TALENT:ARG:아이사이즈 = 1
ELSEIF LOCAL:1 == 1
	TALENT:ARG:단소 = 1
ELSEIF LOCAL:1 == 3
	TALENT:ARG:거근 = 1
ELSEIF LOCAL:1 == 4
	TALENT:ARG:말자지 = 1
ENDIF
;体型に応じてランダムで包まれる
SIF (TALENT:아이사이즈 && RAND:4 > 0) || (TALENT:단소 && RAND:2) || (!TALENT:아이사이즈 && !TALENT:단소 && !RAND:5)
	TALENT:ARG:포경 = 1
;ランダムで페니스関連の素質がつく
IF !RAND:5
	TALENT:ARG:조루 = 1
	TALENT:ARG:지루 = 0
ELSEIF !RAND:4
	TALENT:ARG:지루 = 1
	TALENT:ARG:조루 = 0
ENDIF
;[동정]追加
TALENT:ARG:동정 = 1


;--------------------------------------------------
;性転換用가슴を生やす処理
;--------------------------------------------------
@BREAST_SET(ARG)
;가슴用素質が既にある場合
SIF TALENT:ARG:절벽 || TALENT:ARG:빈유 || TALENT:ARG:거유 || TALENT:ARG:폭유
	RETURN 0
;体型に応じてランダムな大きさの가슴が生える
LOCAL = RAND:100
IF TALENT:ARG:소인체형
	IF LOCAL >= 20
		LOCAL:1 = 0
	ELSEIF LOCAL >= 5
		LOCAL:1 = 1
	ELSE
		LOCAL:1 = 2
	ENDIF
ELSEIF TALENT:ARG:작은체구
	IF LOCAL >= 95
		LOCAL:1 = 0
	ELSEIF LOCAL >= 45
		LOCAL:1 = 1
	ELSEIF LOCAL:1 >= 10
		LOCAL:1 = 2
	ELSEIF LOCAL >= 3
		LOCAL:1 = 3
	ELSE
		LOCAL:1 = 4
	ENDIF
ELSEIF !TALENT:ARG:큰체구
	IF LOCAL >= 97
		LOCAL:1 = 0
	ELSEIF LOCAL >= 80
		LOCAL:1 = 1
	ELSEIF LOCAL:1 >= 20
		LOCAL:1 = 2
	ELSEIF LOCAL >= 5
		LOCAL:1 = 3
	ELSE
		LOCAL:1 = 4
	ENDIF
ELSE
	IF LOCAL >= 99
		LOCAL:1 = 0
	ELSEIF LOCAL >= 90
		LOCAL:1 = 1
	ELSEIF LOCAL:1 >= 30
		LOCAL:1 = 2
	ELSEIF LOCAL >= 10
		LOCAL:1 = 3
	ELSE
		LOCAL:1 = 4
	ENDIF
ENDIF
IF LOCAL:1 == 0
	TALENT:ARG:절벽 = 1
ELSEIF LOCAL:1 == 1
	TALENT:ARG:빈유 = 1
ELSEIF LOCAL:1 == 3
	TALENT:ARG:거유 = 1
ELSEIF LOCAL:1 == 4
	TALENT:ARG:폭유 = 1
ENDIF


;--------------------------------------------------
;性転換用Ｖ関連素質処理
;--------------------------------------------------
@VAGINA_SET(ARG)
;ランダムでＶ関連の素質がつく
IF !RAND:5
	TALENT:ARG:V민감 = 1
	TALENT:ARG:V둔감 = 0
ELSEIF !RAND:4
	TALENT:ARG:V둔감 = 1
	TALENT:ARG:V민감 = 0
ENDIF
;[처녀]追加
TALENT:ARG:처녀 = 1

;--------------------------------------------------
;消耗品の自動補充
;--------------------------------------------------
@ITEM_AUTO_SUPPLY
;補充個数
#DIM DYNAMIC SUPPLY = 0
;消費金額
#DIM DYNAMIC USED_MONEY = 0

;所持基準数を持っているフラグ番号
;FLAG:50=로션、FLAG:51=비디오테이프、FLAG:52=필름、FLAG:53=콘돔
#DIM DYNAMIC FLG = 49

;COUNTの数はアイテム番号と一致
;40=로션、43=비디오테이프、44=필름、45=콘돔
FOR COUNT,40,46
	;自動補充の対象でないアイテムはスキップ
	SIF COUNT == 41 || COUNT == 42
		CONTINUE
	
	FLG++
	
	;現在の所持個数が、所持基準数を上回るならスキップ
	SIF ITEM:COUNT >= FLAG:FLG
		CONTINUE
	
	;비디오테이프は비디오카메라を所持していないと補充されない
	SIF COUNT == 43 && ITEM:비디오카메라 == 0
		CONTINUE
	
	;필름は카메라を所持していないと補充されない
	SIF COUNT == 44 && ITEM:카메라 == 0
		CONTINUE
	
	;以下、アイテム補充処理
	
	;補充個数を算出
	SUPPLY = FLAG:FLG - ITEM:COUNT
	;消費金額を算出
	USED_MONEY = SUPPLY * ITEMPRICE:COUNT
	
	;消費金額 > 所持金
	IF USED_MONEY > MONEY
		;所持金で買える最大数を算出
		SUPPLY = MONEY / ITEMPRICE:COUNT
		
		IF SUPPLY
			;消費金額を算出
			USED_MONEY = SUPPLY * ITEMPRICE:COUNT
		ELSE
			PRINTFORML 所持金が발りないため、%ITEMNAME:COUNT%が補充できませんでした。(現在所持数：{ITEM:COUNT}個)
			CONTINUE
		ENDIF
	ENDIF
	
	MONEY = MONEY - USED_MONEY
	ITEM:COUNT += SUPPLY
	PRINTFORML {SUPPLY}個の%ITEMNAME:COUNT%が補充され、{USED_MONEY}円消費しました。(現在所持数：{ITEM:COUNT}個)
NEXT




