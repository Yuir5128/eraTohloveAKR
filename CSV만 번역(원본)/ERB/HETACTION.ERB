;=============================================================================
;「へたくしょん」v1.00　「erahetaA」用汎用関数ファイル
;無断再配布ＯＫですが、erahetaA以外での使い道は皆無かと思われます（特に誕生日サーチ機能）
;=============================================================================

;--------------------------------------------------
;このファイルの中に入っている機能一覧

;erahetaAの仕様に関する関数群
;COMF999						駆け落ちイベント用「調教を強制終了させる」機能
;								駆け落ちイベントに限らず開幕で調教を強制終了させたい場合も利用可能
;								使用変数 … TFLAG:400 ～ 401

;MASTERBIRTHDAY					「主人の誕生日をサーチする」機能
;								使用変数 … B:1

;COMPARE_BSIZE					「가슴のサイズを比較する」機能
;								使用変数 … A、A:1、A:2、B、B:1、B:2

;COMPARE_CSIZE					「下のサイズを比較する」機能
;								使用変数 … A、A:1、A:2、B、B:1、B:2

;面識機能に関する関数群
;SAVE_MEMBER					「ここ一週間以内の調教対象と助손を記録する」機能
;								使用変数 … PREVCOM:801 ～ 828

;SAVE_MEMBER					「ここ一週間以内の調教対象と助손を記録する」機能
;								使用変数 … PREVCOM:801 ～ 830

;SAVE_MEMBER_MASTER				「ここ一週間以内の主人を記録する」機能　※eraToloveA02私家改造版にて追記
;								使用変数 … PREVCOM:831 ～ 845

;SAVE_ACQUAINTANCE_DAILY_LIFE
;SAVE_ACQUAINTANCE_ASSI			「助손や日常パッチでの面識管理」機能
;								使用変数 … CFLAG:799 ～ 898
;								→eraToloveA02私家改造版 CFLAG:2099 ～ 3098

;GET_MEMBER_TARGET				「ここ一週間以内の調教対象をサーチする」機能
;								使用変数 … R、D:1、D:2

;GET_MEMBER_ASSI				「ここ一週間以内の助손をサーチする」機能
;								使用変数 … R、D:3、D:4

;GET_MEMBER_MASTER				「ここ一週間以内の主人をサーチする」機能　※eraToloveA02私家改造版にて追記
;								使用変数 … R、D:5、D:6

;GET_LOSTPURE					「입上主の동정、처녀、후처녀を奪った相손をサーチする」機能
;								使用変数 … R、L

;GET_EATPURE					「입上主が奪ってきた동정、처녀、후처녀の人数をサーチする」機能
;								使用変数 … R、E:1、E:2、E:3

;GET_JIBUN						「처녀・동정・후처녀喪失用にょたちびサーチ」
;								使用変数　…　R、L、A

;勝손にウフフ機能に関する関数群
;TARGETPLAY_NEXT				「調教対象から奉仕・역조교コマンドを実行してくれる」機能
;								使用変数 … NEXTCOM:0 ～ 7
;--------------------------------------------------

;=============================================================================
;erahetaAの仕様に関する関数群
;=============================================================================
;-------------------------------------------------
;駆け落ちイベント用「調教を強制終了させる」機能
;NEXTCOM = 999でこの処理が呼び出せる
;TFLAG:400 == 1してから呼び出せばキャラを消去出来る
;TFLAG:400 == 0なら消去せず日付だけを送る
;TFLAG:401にキャラ番号を入れることで駆け落ち相손の購入可能フラグもいじれます（要へたくしょん拡張）
;999は調教終了なので、今후も使う事がないはず。という事で占有
;-------------------------------------------------
@COM999
;消去される場合用の処理
IF TFLAG:400 == 1
	;奴隷が脱走して손元からいなくなる。
	T = TARGET
	A = ASSI
	DELCHARA T
	IF A != -1
		IF A > T
			ASSI = A-1
		ELSE
			ASSI = A
		ENDIF
	ENDIF
	TARGET = -1
	;-----------------------
	;テンプレ用：あんみつ専用処理
	;IF TFLAG:401 == 1
	;	FLAG:1023 = 0
	;連れられて逃げる方（清光）をX、清光を連れて行く方（安定）をYとして、
	;Xに清光のキャラ番号、Yに安定のキャラ番号-1を入れることで、駆け落ち相손が買えなくなる
	;ELSEIF TFLAG:401 == 85
	;	FLAG:1086 == 0
	;ENDIF
	;-----------------------
	;-----------------------
	;テンプレ
	;IF TFLAG:401 == 1
	;	FLAG:1023 = 0
	;ELSEIF TFLAG:401 == X
	;	FLAG:10Y == 0
	;ENDIF
	;-----------------------
ENDIF
;消去の有無にかかわらず行われる、調教を強制終了して日付を送る処理
REPEAT CHARANUM
	;기력の回復
	IF TALENT:기절
		TALENT:기절 -= 1
	ELSE
		BASE:COUNT:기력 = MAXBASE:COUNT:기력
	ENDIF

	;労役中のキャラは체력回復処理無し
	SIF CFLAG:COUNT:12 > 0
		CONTINUE

	;체력回復停止処理
	IF CFLAG:COUNT:6 > 0
		CFLAG:COUNT:6 -= 1
		CONTINUE
	ENDIF

	;체력の回復（午후の調教후は夜の休みが入るので回復大きい）
	IF TIME == 0
		A = MAXBASE:COUNT:체력 / 15
	ELSE
		A = MAXBASE:COUNT:체력 / 5
	ENDIF

	;회복빠름、회복느림、발정기、불사신
	IF TALENT:COUNT:회복빠름
		TIMES A , 1.50
	ELSEIF TALENT:COUNT:회복느림 || TALENT:COUNT:발정기
		TIMES A , 0.75
	ELSEIF TALENT:COUNT:불사신
		TIMES A , 1.80
	ENDIF

	;일광욕、월광욕、흡혈귀
	IF TIME == 0
		SIF TALENT:COUNT:일광욕
			A += 100
	ELSE
		SIF TALENT:COUNT:흡혈귀
			TIMES A , 1.50
		SIF TALENT:COUNT:월광욕
			A += 100
	ENDIF

	;치료
	IF ASSI >= 0
		SIF TALENT:ASSI:치료
		A += 150
	ENDIF

	;休憩フラグ（休憩フラグが経っている、もしくは調教対象でない）
	IF FLAG:0 || TARGET != COUNT
		A += 100
		CFLAG:COUNT:10 -= 1
	ENDIF

	;ショップで休憩を選択時
	IF FLAG:0 == 2
		TIMES A , 2.00
		CFLAG:COUNT:10 -= 1
	ENDIF

	BASE:COUNT:체력 += A
	SIF BASE:COUNT:체력 > MAXBASE:COUNT:체력
		BASE:COUNT:체력 = MAXBASE:COUNT:체력

	;ストレス値が0未満の場合は0にする
	SIF CFLAG:COUNT:10 < 0
		CFLAG:COUNT:10 = 0

REND

CALL EVENTCHECK_ABL
FLAG:0 = 0

;午후なら次の日、午前なら午후にする
IF TIME == 1
	FLAG:81 += 1
	FLAG:82 += 1

	;日付変更時のイベント呼び出し
	CALL EVENT_NEXTDAY

	DAY = DAY+1
	PRINTW 一日が終わった……
	TIME = 0
	;バッドエンドの判定
	SIF DAY > 50 && FLAG:5 == 0
		CALL BADENDING_1
	SIF MONEY < 0 && FLAG:5 == 0
		CALL BADENDING_2

	;朝になった時のイベント呼び出し
	CALL EVENT_NEWDAY
	CALL EVENT_NEWDAY_SELF
ELSE
	;全体の임신判定の処理
	CALL IN_VAGINA_ANAL_ALL

	PRINTL 
	PRINTW 夜になりました
	TIME = 1
ENDIF

BEGIN SHOP


;-------------------------------------------------
;主人の誕生日サーチ機能
;CALL MASTERBIRTHDAYでこの処理が呼び出せる
;当日が誕生日であればB:1に1が、一週間以内に誕生日が来るならB:1に2が代入されて返ってくる
;-------------------------------------------------
@MASTERBIRTHDAY
;入れ子を空にする
B:1 = 0



;-------------------------------------------------
;가슴のサイズを比較する機能
;AとBに比較したい相손の登録番号を入れてCALL
;B:1に大きかった方の登録番号、B:2にその差を返します。引き分けの時はB:1が-1になります
;登録番号は@NOなりTARGETとASSIで判定するなりして使ってください
;-------------------------------------------------
@COMPARE_BSIZE
A:1 = 0
A:2 = 0
B:1 = 0
B:2 = 0
IF TALENT:A:절벽
	A:1 = 0
ELSEIF TALENT:A:빈유
	A:1 = 1
ELSEIF TALENT:A:거유
	A:1 = 3
ELSEIF TALENT:A:폭유
	A:1 = 4
ELSE
	A:1 = 2
ENDIF

IF TALENT:B:절벽
	A:2 = 0
ELSEIF TALENT:B:빈유
	A:2 = 1
ELSEIF TALENT:B:거유
	A:2 = 3
ELSEIF TALENT:B:폭유
	A:2 = 4
ELSE
	A:2 = 2
ENDIF

IF A:1 > A:2
	B:1 = A
	B:2 = A:1
	B:2 -= A:2
ELSEIF A:1 < A:2
	B:1 = B
	B:2 = A:2
	B:2 -= A:2
ELSE
	B:1 = -1
	B:2 = 0
ENDIF

;-------------------------------------------------
;下のサイズを比較する機能
;AとBに比較したい相손の登録番号を入れてCALL
;B:1に大きかった方の登録番号、B:2にその差を返します。引き分けの時はB:1が-1になります
;登録番号は@NOなりTARGETとASSIで判定するなりして使ってください
;-------------------------------------------------
@COMPARE_CSIZE
A:1 = 0
A:2 = 0
B:1 = 0
B:2 = 0
IF TALENT:A:아이사이즈
	A:1 = 0
ELSEIF TALENT:A:단소
	A:1 = 1
ELSEIF TALENT:A:거근
	A:1 = 3
ELSEIF TALENT:A:말자지
	A:1 = 4
ELSE
	A:1 = 2
ENDIF

IF TALENT:B:아이사이즈
	A:2 = 0
ELSEIF TALENT:B:단소
	A:2 = 1
ELSEIF TALENT:B:거근
	A:2 = 3
ELSEIF TALENT:B:말자지
	A:2 = 4
ELSE
	A:2 = 2
ENDIF

IF A:1 > A:2
	B:1 = A
	B:2 = A:1
	B:2 -= A:2
ELSEIF A:1 < A:2
	B:1 = B
	B:2 = A:2
	B:2 -= A:1
ELSE
	B:1 = -1
	B:2 = 0
ENDIF

;=============================================================================
;面識機能に関する関数群
;=============================================================================
;-------------------------------------------------
;記録機能用の@EVENT関数
;-------------------------------------------------
@EVENTEND
	CALL SAVE_ACQUAINTANCE_ASSI
	PREVCOM:829 = NO:TARGET
	IF ASSI >= 0
		PREVCOM:830 = NO:ASSI
	ELSE
		PREVCOM:830 = -1
	ENDIF
	PREVCOM:845 = NO:MASTER
@EVENTTURNEND
	CALL SAVE_MEMBER
	CALL SAVE_MEMBER_MASTER

;-------------------------------------------------
;ここ一週間の助손と調教対象を記録する機能
;PREVCOM:800以降を盛大に使用します
;801 ～ 814 = ここ一週間の奴隷のキャラ番号
;815 ～ 828 = ここ一週間の助손
;829 保存用
;830 保存用
;-------------------------------------------------
@SAVE_MEMBER
;奴隷のキャラ番号を登録
I = 814
REPEAT 14
	IF I != 801
		PREVCOM:I = PREVCOM:(I-1)
	ELSE
		IF TARGET > 0
			IF PREVCOM:829 == NO:TARGET
				PREVCOM:801 = NO:TARGET
			ELSE
				PREVCOM:801 = -1
			ENDIF
		ELSE
			PREVCOM:801 = -1
		ENDIF
	ENDIF
	I -= 1
REND
;助손のキャラ番号を登録
I = 828
REPEAT 14
	IF I != 815
		PREVCOM:I = PREVCOM:(I-1)
	ELSE
		IF ASSI >= 0
			IF PREVCOM:830 == NO:ASSI
				PREVCOM:815 = NO:ASSI
			ELSE
				PREVCOM:815 = -1
			ENDIF
		ELSE
			PREVCOM:815 = -1
		ENDIF
	ENDIF
	I -= 1
REND
PREVCOM:829 = -1
PREVCOM:830 = -1

;-------------------------------------------------
;ここ一週間の主人を記録する機能　※eraToloveA02私家改造版にて追記
;念のため関数分けました
;PREVCOM:800以降を盛大に使用します
;831 ～ 844 = ここ一週間の主人のキャラ番号
;845 保存用
;-------------------------------------------------
@SAVE_MEMBER_MASTER
;助손のキャラ番号を登録
I = 844
REPEAT 14
	IF I != 831
		PREVCOM:I = PREVCOM:(I-1)
	ELSE
		IF MASTER >= 0
			IF PREVCOM:845 == NO:MASTER
				PREVCOM:831 = NO:MASTER
			ELSE
				PREVCOM:831 = -1
			ENDIF
		ELSE
			PREVCOM:831 = -1
		ENDIF
	ENDIF
	I -= 1
REND
PREVCOM:845 = -1

;-------------------------------------------------
;日常での面識を更新する機能
;へたくしょん導入で勝손に保存されるようになります
;更新には입上側の制御が必要なので注意
;-------------------------------------------------
@SAVE_ACQUAINTANCE_DAILY_LIFE
;Bが登場しない場合は無視
IF B > 0 && B < CHARANUM
	G = (2099 + NO:A)
	H = (2099 + NO:B)
	CFLAG:A:H |= 1
	CFLAG:B:G |= 1
	;Cが居るならCの分も判定
	IF C > 0 && C < CHARANUM && C != B
		I = (2099 + NO:C)
		CFLAG:A:I |= 1
		CFLAG:B:I |= 1
		CFLAG:C:G |= 1
		CFLAG:C:H |= 1
	ENDIF
ENDIF

;-------------------------------------------------
;助손調教での面識を更新する機能
;へたくしょん導入で勝손に保存されるようになります
;これも更新には입上側の制御が必要なので注意
;-------------------------------------------------
@SAVE_ACQUAINTANCE_ASSI
H = (2099 + NO:TARGET)
I = (2099 + NO:MASTER)
CFLAG:TARGET:I |= 2
CFLAG:MASTER:H |= 2
;ASSIがいない場合は無視
IF ASSI >= 0
	G = (2099 + NO:ASSI)
	CFLAG:ASSI:H |= 2
	CFLAG:ASSI:I |= 2
	CFLAG:TARGET:G |= 2
	CFLAG:MASTER:G |= 2
ENDIF
IF ASSIPLAY
	CFLAG:ASSI:H |= 8
	CFLAG:TARGET:G |= 4
ELSE
	CFLAG:MASTER:H |= 8
	CFLAG:TARGET:I |= 4
ENDIF

;-------------------------------------------------
;ここ一週間の調教対象をサーチする機能
;Rに調教対象のキャラ番号を入れてCALL
;D:1に調教対象と会った日を返す。D:2に実際に会ったのが何回前かを返す
;-------------------------------------------------
@GET_MEMBER_TARGET
;調教対象サーチ
I = 814
D:1 = -1
D:2 = -1
REPEAT 14
	SIF PREVCOM:I == R
		D:1 = (I - 800)
	I -= 1
REND
IF D:1 >= 0
	D:2 = D:1
	;割って余りが出ない時は、そのまま÷2で日付が出る
	IF (D:1 % 2) == 0
		D:1 /= 2
	;割って余りが出る時は、今が昼か夜かで結果が変わる
	ELSE
		;昼なら前日なので、1を발す
		IF TIME == 0
			D:1 += 1
		;夜ならその日のうちに会ってるので1を引く
		ELSE
			D:1 -= 1
		ENDIF
		D:1 /= 2
	ENDIF
ENDIF

;-------------------------------------------------
;ここ一週間の助손をサーチする機能
;Rに助손のキャラ番号を入れてCALL
;D:3に助손と会った日を、D:4に実際に会ったのが何回前か返す
;当日の助손？ 助손입上で判定すればいいと思うよ！
;-------------------------------------------------
@GET_MEMBER_ASSI
;助손サーチ
I = 828
D:3 = -1
D:4 = -1
REPEAT 14
	SIF PREVCOM:I == R
		D:3 = (I - 814)
	I -= 1
REND
IF D:3 >= 0
	D:4 = D:3
	;割って余りが出ない時は、そのまま÷2で日付が出る
	IF (D:3 % 2) == 0
		D:3 /= 2
	;割って余りが出る時は、今が昼か夜かで結果が変わる
	ELSE
		;昼なら前日なので、1を발す
		IF TIME == 0
			D:3 += 1
		;夜ならその日のうちに会ってるので1を引く
		ELSE
			D:3 -= 1
		ENDIF
		D:3 /= 2
	ENDIF
ENDIF

;-------------------------------------------------
;ここ一週間の主人をサーチする機能
;Rに主人のキャラ番号を入れてCALL
;D:5に助손と会った日を、D:6に実際に会ったのが何回前か返す
;-------------------------------------------------
@GET_MEMBER_MASTER
;助손サーチ
I = 844
D:5 = -1
D:6 = -1
REPEAT 14
	SIF PREVCOM:I == R
		D:5 = (I - 830)
	I -= 1
REND
IF D:5 >= 0
	D:6 = D:5
	;割って余りが出ない時は、そのまま÷2で日付が出る
	IF (D:5 % 2) == 0
		D:5 /= 2
	;割って余りが出る時は、今が昼か夜かで結果が変わる
	ELSE
		;昼なら前日なので、1を발す
		IF TIME == 0
			D:5 += 1
		;夜ならその日のうちに会ってるので1を引く
		ELSE
			D:5 -= 1
		ENDIF
		D:5 /= 2
	ENDIF
ENDIF

;-------------------------------------------------
;입上主の동정、처녀、후처녀を奪った相손をサーチする機能
;Rに동정なら1、처녀なら4、후처녀なら16を入れてCALL
;存在すればLに奪った相손のキャラ番号が格納されます。ついでに名前もSTR:300に格納してあります
;-------------------------------------------------
@GET_LOSTPURE
R *= 32
L = -1
REPEAT FLAG:44
	G = (2099 + COUNT)
	SIF (CFLAG:G & R)
		L = COUNT
REND
SIF L > 0
	CALLF LOSTNAME, L
	
;-------------------------------------------------
;입上主が今までに奪ってきた동정、처녀、후처녀の人数をサーチする機能
;E:1に동정、E:2に처녀、E:3に후처녀を奪ってきた人数を格納します
;-------------------------------------------------
@GET_EATPURE
E:1 = 0
E:2 = 0
E:3 = 0
REPEAT FLAG:44
	G = (2099 + COUNT)
	SIF (CFLAG:G & 16)
		E:1 += 1
	SIF (CFLAG:G & 32)
		E:2 += 1
	SIF (CFLAG:G & 64)
		E:3 += 1
REND

;-------------------------------------------------
;처녀・동정・후처녀喪失用이상경험サーチ
;自分（同じ国）だと気づいたら異常だと思うかも
;なのでちび→大きい方は判定してません
;Rに喪失する対象、Lに喪失相손を入れてCALLすると、
;Aに이상경험取得なら１、しなければ０が返ります
;-------------------------------------------------
@GET_JIBUN
A = 0

;同一刀剣なら이상경험が付く（同一刀剣の特と극同士など）
SIF SAME_TOKEN(R, L, 1) == 1
	A = 1

;サンプル
;清光が安定に처녀喪失させられた場合に異常が付く
;IF R == 85
;	SIF L == 87
;		A = 1
;ENDIF


;=============================================================================
;勝손にウフフ機能に関する関数群
;=============================================================================
;-------------------------------------------------
;調教対象が勝손に次のコマンドを選ぶ機能
;実行したいところでCALL TARGETPLAY_NEXT
;아무것도안한다、느긋하게한다、스킨십あたりで呼び出すといいんじゃないかと
;「調教対象からフラグ」としてNEXTCOM:5が1になるので、입上はそれで分岐させられます
;-------------------------------------------------
@TARGETPLAY_NEXT

A = 0
B = 0
;まずだいたいの方向性を決める
;罵倒と풋잡は方向性に差異があるので역조교系と一緒に先に処理
IF PREVCOM == 152 || PREVCOM == 80 || (PREVCOM >= 260 && PREVCOM < 286)
	B += 20
;前のコマンドが애무・奉仕・스킨십系であれば奉仕系に偏る
ELSEIF (PREVCOM >=0 && PREVCOM < 1) || (PREVCOM >= 70 && PREVCOM < 87) || (PREVCOM >= 200 && PREVCOM < 207)
	A += 20
;それ以外ならどっこいどっこい
ENDIF

;ABL:마조끼による影響
A += ABL:마조끼 * 5

;ABL:調教者の새드끼による影響
A += ABL:PLAYER:새드끼 * 5

;ABL:새드끼による影響
B += ABL:새드끼 * 5

;ABL:調教者の마조끼による影響
B += ABL:PLAYER:마조끼 * 5

;연모と친애は奉仕側に偏りやすい
SIF TALENT:연모 || TALENT:친애
	A += 50

;복종と예속はほぼ奉仕側に行く
SIF TALENT:복종 || TALENT:예속
	A += 200

;結構なランダム要素が入る
A += RAND:25
B += RAND:25


;겁쟁이　奉仕側に片寄りやすい
IF TALENT:겁쟁이
	TIMES A , 1.20
	TIMES B , 0.80
ENDIF

;반항적　역조교側に片寄りやすい
IF TALENT:반항적
	TIMES A , 0.80
	TIMES B , 1.20
ENDIF

;솔직함　どちらの影響も強くなる
IF TALENT:솔직함
	TIMES A , 1.20
	TIMES B , 1.20
ENDIF

;얌전함　奉仕側に片寄りやすい
IF TALENT:얌전함
	TIMES A , 1.20
	TIMES B , 0.60
ENDIF

;프라이드높음　역조교側に片寄りやすい
IF TALENT:프라이드높음
	TIMES A , 0.70
	TIMES B , 1.30
ENDIF

;건방짐　역조교側に片寄りやすい
IF TALENT:건방짐
	TIMES A , 0.90
	TIMES B , 1.30
ENDIF

;프라이드낮음　奉仕側に片寄りやすい
IF TALENT:프라이드낮음
	TIMES A , 1.30
	TIMES B , 0.70
ENDIF

;자제심　奉仕側に片寄りやすい
IF TALENT:자제심
	TIMES A , 1.20
	TIMES B , 0.80
ENDIF

;무관심　どちらの影響も弱くなる
IF TALENT:무관심
	TIMES A , 0.80
	TIMES B , 0.80
ENDIF

;감정부족　どちらの影響も弱くなる
IF TALENT:감정부족
	TIMES A , 0.80
	TIMES B , 0.80
ENDIF

;호기심　역조교側に片寄りやすい
IF TALENT:호기심
	TIMES A , 0.70
	TIMES B , 1.20
ENDIF

;보수적　奉仕側に片寄りやすい
IF TALENT:보수적
	TIMES A , 1.30
	TIMES B , 0.70
ENDIF

;눈에띄고싶음　역조교側に非常に片寄りやすい
IF TALENT:눈에띄고싶음
	TIMES A , 0.60
	TIMES B , 1.40
ENDIF

;부끄럼쟁이　奉仕側に片寄りやすい
IF TALENT:부끄럼쟁이
	TIMES A , 1.20
	TIMES B , 0.80
ENDIF

;새드　역조교側に非常に片寄りやすい
IF TALENT:새드
	TIMES A , 0.50
	TIMES B , 2.00
ENDIF

;마조　奉仕側に非常に片寄りやすい
IF TALENT:마조
	TIMES A , 2.00
	TIMES B , 0.50
ENDIF

;소악마　역조교側に片寄りやすい
IF TALENT:소악마
	TIMES A , 0.80
	TIMES B , 1.20
ENDIF

;요호　역조교側に片寄りやすい
IF TALENT:요호
	TIMES A , 0.80
	TIMES B , 1.20
ENDIF

;매력　どちらの影響も強くなる
IF TALENT:매력
	TIMES A , 1.10
	TIMES B , 1.10
ENDIF

;매혹　どちらの影響も強くなる
IF TALENT:매혹
	TIMES A , 1.10
	TIMES B , 1.10
ENDIF

;수수께끼의매력　どちらの影響も強くなる
IF TALENT:수수께끼의매력
	TIMES A , 1.20
	TIMES B , 1.20
ENDIF

;고무적　特に奉仕側に片寄りやすい
IF TALENT:고무적
	TIMES A , 1.50
	TIMES B , 0.80
ENDIF

;ここから調教者の素質
;새드마조以外はほぼ奉仕側に偏りやすくなる
;새드
IF TALENT:PLAYER:새드
	TIMES A , 1.50
	TIMES B , 0.80
ENDIF

;마조
IF TALENT:PLAYER:마조
	TIMES A , 0.80
	TIMES B , 1.50
ENDIF

;소악마
IF TALENT:PLAYER:소악마
	TIMES A , 1.20
ENDIF

;요호
IF TALENT:PLAYER:요호
	TIMES A , 1.20
ENDIF

;매력
IF TALENT:PLAYER:매력
	TIMES A , 1.10
ENDIF

;매혹
IF TALENT:PLAYER:매혹
	TIMES A , 1.10
ENDIF

;수수께끼의매력
IF TALENT:PLAYER:수수께끼의매력
	TIMES A , 1.20
	TIMES B , 1.10
ENDIF

;고무적
IF TALENT:PLAYER:고무적
	TIMES A , 1.20
	TIMES B , 0.90
ENDIF


;Aが高いなら奉仕系の処理
IF A >= B
	;前のコマンドが스킨십ならキスかまったりしたがる
	IF PREVCOM == 202
		IF RAND:3 == 0
			NEXTCOM:1 = 1
		ELSEIF RAND:2 == 0
			NEXTCOM:1 = 203
		ELSE
			CALL NEXT_COM_ORDER_H
		ENDIF
	;まったりしてたらキスか스킨십したがる
	ELSEIF PREVCOM == 203
		IF RAND:3 == 0
			NEXTCOM:1 = 1
		ELSEIF RAND:2 == 0
			NEXTCOM:1 = 202
		ELSE
			CALL NEXT_COM_ORDER_H
		ENDIF
	;それ以外なら適当
	ELSE
		CALL NEXT_COM_ORDER_H
	ENDIF
;そうでないなら역조교処理
ELSE
	CALL NEXT_COM_ORDER_G
ENDIF

;奉仕コマンドに絞りたい時はこっち
@TARGETPLAY_NEXT_H
A = 1
B = 0
;前のコマンドが스킨십ならキスかまったりしたがる
IF PREVCOM == 202
	IF RAND:3 == 0
		NEXTCOM:1 = 1
	ELSEIF RAND:2 == 0
		NEXTCOM:1 = 203
	ELSE
		CALL NEXT_COM_ORDER_H
	ENDIF
;まったりしてたらキスか스킨십したがる
ELSEIF PREVCOM == 203
	IF RAND:3 == 0
		NEXTCOM:1 = 1
	ELSEIF RAND:2 == 0
		NEXTCOM:1 = 202
	ELSE
		CALL NEXT_COM_ORDER_H
	ENDIF
;それ以外なら適当
ELSE
	CALL NEXT_COM_ORDER_H
ENDIF


;역조교コマンドに絞りたい時はこっち
@TARGETPLAY_NEXT_G
A = 0
B = 1
CALL NEXT_COM_ORDER_G




;----------------------------------------------------------------
;次回コマンド強制決定関数関係
;----------------------------------------------------------------
@NEXT_COM_ORDER_H
NEXTCOM:6 = 1
;奉仕系コマンドをランダムで選択
;세로파이즈리は거유か폭유のときのみ
IF RAND:10 == 0 && (TALENT:폭유 || TALENT:거유)
	NEXTCOM:1 = 74
;파이즈리펠라は男と절벽不可
ELSEIF RAND:9 == 0 && TALENT:남자 == 0 && TALENT:절벽 == 0
	NEXTCOM:1 = 75
;파이즈리は男不可
ELSEIF RAND:8 == 0 && TALENT:남자 == 0
	NEXTCOM:1 = 73
;발扱きは새드なしの時のみ（새드いと踏むから）
ELSEIF RAND:6 == 0 && TALENT:새드 == 0
	NEXTCOM:1 = 80
;느긋하게한다は연모か친애の時のみ
ELSEIF RAND:5 == 0 && (TALENT:연모 || TALENT:친애)
	NEXTCOM:1 = 203
;스킨십は연모か친애の時のみ
ELSEIF RAND:4 == 0 && (TALENT:연모 || TALENT:친애)
	NEXTCOM:1 = 202
;키스한다
ELSEIF RAND:3 == 0
	NEXTCOM:1 = 1
;수음
ELSEIF RAND:2 == 0
	NEXTCOM:1 = 70
;펠라치오
ELSE
	NEXTCOM:1 = 71
ENDIF

@NEXT_COM_ORDER_G
NEXTCOM:6 = 1
;역조교系コマンドをランダムで選択
;발扱きは새드いの時のみ
IF RAND:7 == 0 && TALENT:새드
	NEXTCOM:1 = 80
;채찍打ちは새드끼3以上で채찍持ってるときのみ
ELSEIF RAND:6 == 0 && (ITEM:13 || NOITEM) && ABL:새드끼 >= 3
	NEXTCOM:1 = 284
;커닐링구스は女の子のみ
ELSEIF RAND:5 == 0 && TALENT:남자 == 0 && TALENT:후타나리 == 0 && TALENT:반음반양 == 0 && TALENT:쉬메일 == 0
	NEXTCOM:1 = 40
;フェラはフタか男の子のみ
ELSEIF RAND:5 == 0 && (TALENT:남자 || TALENT:후타나리 || TALENT:반음반양 || TALENT:쉬메일)
	NEXTCOM:1 = 31
;애무시킨다
ELSEIF RAND:4 == 0
	NEXTCOM:1 = 220
;罵倒させる
ELSEIF RAND:3 == 0
	NEXTCOM:1 = 281
;스팽킹させる
ELSEIF RAND:2 == 0
	NEXTCOM:1 = 283
;발을핥는다
ELSE
	NEXTCOM:1 = 282
ENDIF

@NEXT_COM_ABLE
;基本的にはCOMABLEと同じ
;NEXTCOM:7 が10になったら無限ループ入ったと判定して残念ながら아무것도안한다に移動
IF NEXTCOM:7 == 10
	NEXTCOM:1 = 21
	RETURN 1
;커닐링구스実行判定
ELSEIF NEXTCOM:1 == 40
	CALL COM_ABLE40
;펠라한다実行判定
ELSEIF NEXTCOM:1 == 31
	CALL COM_ABLE31
;키스한다実行判定
ELSEIF NEXTCOM:1 == 1
	CALL COM_ABLE1
;스킨십
ELSEIF NEXTCOM:1 == 202
	CALL COM_ABLE202
;느긋하게한다
ELSEIF NEXTCOM:1 == 203
	CALL COM_ABLE203
;수음実行判定
ELSEIF NEXTCOM:1 == 70
	CALL COM_ABLE70
;펠라치오実行判定
ELSEIF NEXTCOM:1 == 71
	CALL COM_ABLE71

;파이즈리実行判定
ELSEIF NEXTCOM:1 == 73
	CALL COM_ABLE73

;발扱き実行判定
ELSEIF NEXTCOM:1 == 80
	CALL COM_ABLE80
;세로파이즈리
ELSEIF NEXTCOM:1 == 74
	CALL COM_ABLE74
;파이즈리펠라
ELSEIF NEXTCOM:1 == 75
	CALL COM_ABLE75
;가슴을주무르게한다実行判定
ELSEIF NEXTCOM:1 == 221
	CALL COM_ABLE221
;애무시킨다実行判定
ELSEIF NEXTCOM:1 == 220
	CALL COM_ABLE220
;罵倒させる実行判定
ELSEIF NEXTCOM:1 == 281
	CALL COM_ABLE281
;스팽킹させる実行判定
ELSEIF NEXTCOM:1 == 283
	CALL COM_ABLE283
;채찍打たせる実行判定
ELSEIF NEXTCOM:1 == 284
	CALL COM_ABLE284
;발을핥는다実行判定
ELSEIF NEXTCOM:1 == 282
	CALL COM_ABLE282
ENDIF

;----------------------------------------------------------------
;NEXTCOMのバグ防止用
;----------------------------------------------------------------
@EVENTCOM
;消去待ちの場合は消去
IF NEXTCOM:6 < 0
	NEXTCOM = -1
	NEXTCOM:1 = 0
	NEXTCOM:5 = 0
	NEXTCOM:6 = 0
;実行待ちの場合はまず実行判定
ELSEIF NEXTCOM:6 > 0
	CALL NEXT_COM_ABLE
	;RETURN 1で帰って来たら進行
	;NEXTCOM:6を消去待ちに更新
	IF RESULT == 1
		NEXTCOM = NEXTCOM:1
		NEXTCOM:6 = -1
		NEXTCOM:5 = 1
	;RETURN 0で帰って来たら、NEXTCOM:1を破壊してもっかい次のコマンドを取得
	ELSE
		NEXTCOM:1 = -1
		NEXTCOM:7 += 1
		;Aが高いなら奉仕系の処理
		IF A >= B
			;前のコマンドが스킨십ならキスかまったりしたがる
			IF PREVCOM == 202
				IF RAND:3 == 0
					NEXTCOM:1 = 1
				ELSEIF RAND:2 == 0
					NEXTCOM:1 = 203
				ELSE
					CALL NEXT_COM_ORDER_H
				ENDIF
			;まったりしてたらキスか스킨십したがる
			ELSEIF PREVCOM == 203
				IF RAND:3 == 0
					NEXTCOM:1 = 1
				ELSEIF RAND:2 == 0
					NEXTCOM:1 = 202
				ELSE
					CALL NEXT_COM_ORDER_H
				ENDIF
			;それ以外なら適当
			ELSE
				CALL NEXT_COM_ORDER_H
			ENDIF
		;そうでないなら역조교処理
		ELSE
			CALL NEXT_COM_ORDER_G
		ENDIF
	ENDIF
ENDIF

@EVENTCOMEND
;消去待ちの場合は消去
IF NEXTCOM:6 < 0
	NEXTCOM = -1
	NEXTCOM:1 = 0
	NEXTCOM:5 = 0
	NEXTCOM:6 = 0
;実行待ちの場合はまず実行判定
ELSEIF NEXTCOM:6 > 0
	CALL NEXT_COM_ABLE
	;RETURN 1で帰って来たら進行
	;NEXTCOM:6を消去待ちに更新
	IF RESULT == 1
		NEXTCOM = NEXTCOM:1
		NEXTCOM:6 = -1
		NEXTCOM:5 = 1
	;RETURN 0で帰って来たら、NEXTCOM:1を破壊してもっかい次のコマンドを取得
	ELSE
		NEXTCOM:1 = -1
		NEXTCOM:7 += 1
		;Aが高いなら奉仕系の処理
		IF A >= B
			;前のコマンドが스킨십ならキスかまったりしたがる
			IF PREVCOM == 202
				IF RAND:3 == 0
					NEXTCOM:1 = 1
				ELSEIF RAND:2 == 0
					NEXTCOM:1 = 203
				ELSE
					CALL NEXT_COM_ORDER_H
				ENDIF
			;まったりしてたらキスか스킨십したがる
			ELSEIF PREVCOM == 203
				IF RAND:3 == 0
					NEXTCOM:1 = 1
				ELSEIF RAND:2 == 0
					NEXTCOM:1 = 202
				ELSE
					CALL NEXT_COM_ORDER_H
				ENDIF
			;それ以外なら適当
			ELSE
				CALL NEXT_COM_ORDER_H
			ENDIF
		;そうでないなら역조교処理
		ELSE
			CALL NEXT_COM_ORDER_G
		ENDIF
	ENDIF
ENDIF

