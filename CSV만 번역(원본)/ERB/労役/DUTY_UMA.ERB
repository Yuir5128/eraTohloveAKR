;====================================================================
;DUTY_UMA.ERB　内番-馬当番
;１日１組しか出せない
;内番に出したキャラはその日１日拘束される
;====================================================================
@DUTY_UMA
;それぞれ馬当番担当の男士の登録番号を保持　初期値-1
#DIM DYNAMIC UMA_A = -1
#DIM DYNAMIC UMA_B = -1


DRAWLINE
PRINTL 【 内番-馬当番 】
PRINTL 
PRINTL 奴隷に２人１組でペアを組ませ、内番「馬当番」を行わせる。
PRINTL 기력の最大値が上昇する
PRINTL 
PRINTL ■ 当番内容
PRINTL 　：[馬当番]
PRINTL 
PRINTL ■ 選択されている追加オプション
PRINTL 　：＜利用可能なオプションはありません＞
DRAWLINE
PRINTL 

;--------------------------------------------------------
;内番させる奴隷を選択
;--------------------------------------------------------
PRINTL
PRINTL 馬当番をさせる男士を２人、選択してください
PRINTL
$INPUT_LOOP_START
;両方とも選択済み
IF UMA_A > 0 && UMA_B > 0
	PRINTFORML [{UMA_A}]%CALLNAME:UMA_A%を外す
	PRINTFORML [{UMA_B}]%CALLNAME:UMA_B%を外す
	PRINTL
ELSE
	REPEAT CHARANUM
		;馬当番できるフラグが立っていない男士は表示しない
		SIF (CFLAG:COUNT:151 & 256) == 0
			CONTINUE
		PRINTFORM [{COUNT}]%CALLNAME:COUNT%
		IF UMA_A == COUNT || UMA_B == COUNT
			PRINTL を外す
		ELSE
			PRINTL を選択
		ENDIF
	REND
ENDIF

;両方とも選択済み
SIF UMA_A > 0 && UMA_B > 0
	PRINTL [900] - 現在の２人で確定
PRINTL [999] - 戻る
$INPUT_LOOP
INPUT
IF RESULT == 900

ELSEIF RESULT == 999
	RETURN 0
ELSEIF (CFLAG:RESULT:151 & 256) == 0
	GOTO INPUT_LOOP
ELSE
	;選択済みのキャラをクリックした場合
	IF RESULT == UMA_A
		UMA_A = -1
		;繰り上げ処理(UMA_Bに0が入ってても代入する)
		UMA_A = UMA_B
	;選択済みのキャラをクリックした場合
	ELSEIF RESULT == UMA_B
		UMA_B = -1
	ELSE
		;空いてる方に代入する
		IF UMA_A == -1
			UMA_A = RESULT
		ELSEIF UMA_B == -1
			UMA_B = RESULT
		ENDIF
		
		;登録番号が小さい方をAにする
		IF UMA_A > UMA_B
			LOCAL = UMA_A
			UMA_A = UMA_B
			UMA_B = LOCAL
		ENDIF
	ENDIF
	GOTO INPUT_LOOP_START
ENDIF

;--------------------------------------------------------
;馬当番を実行する
;--------------------------------------------------------
;PRINTFORML %CALLNAME:UMA_A%と%CALLNAME:UMA_B%が、馬当番を開始しました

;労役フラグを馬当番に設定
CFLAG:UMA_A:12 = 5
CFLAG:UMA_B:12 = 5

;地の文、입上呼び出しのために一時的に現在の調教対象を退避
LOCAL = TARGET
TFLAG:13 = 84

UCHIBAN_A = UMA_A
UCHIBAN_B = UMA_B

;現在が奇数日目ならUCHIBAN_Aの方を呼ぶ
IF DAY % 2 == 1
	TARGET = UMA_A
	;イベント입上（内番開始時）;地の文とAの입上
	CALL KOJO_MESSAGE_EVENT
;現在が偶数日目ならUCHIBAN_Bの方を呼ぶ
ELSE
	TARGET = UMA_B
	;イベント입上（内番開始時）;Bの입上
	CALL KOJO_MESSAGE_EVENT
ENDIF

TARGET = LOCAL
PRINTL 

;馬当番担当男士が調教対象や助손の場合は、調教対象や助손から外す
SIF UCHIBAN_A == TARGET || UCHIBAN_B == TARGET
	TARGET = -1
SIF UCHIBAN_A == ASSI || UCHIBAN_B == ASSI
	ASSI = -1

;内番開始済みフラグ（馬当番=4）を立てる
FLAG:161 |= 4


;====================================================================
;終了時の処理
;====================================================================
@END_DUTY_UMA
#DIM DYNAMIC UMA_A = -1
#DIM DYNAMIC UMA_B = -1

REPEAT CHARANUM
	IF CFLAG:COUNT:12 == 5
		IF UMA_A == -1
			UMA_A = COUNT
		ELSE
			UMA_B = COUNT
		ENDIF
	ENDIF
REND

DRAWLINE

;地の文、입上呼び出しのために一時的に現在の調教対象を退避
LOCAL = TARGET
TFLAG:13 = 85

UCHIBAN_A = UMA_A
UCHIBAN_B = UMA_B


;必然的に、内番開始時に呼ばれなかった方の地の文と입上が呼ばれます

;現在が奇数日目ならUCHIBAN_Aの方を呼ぶ
IF DAY % 2 == 1
	TARGET = UMA_A
	;イベント입上（内番終了時）;地の文とAの입上
	CALL KOJO_MESSAGE_EVENT
;現在が偶数日目ならUCHIBAN_Bの方を呼ぶ
ELSE
	TARGET = UMA_B
	;イベント입上（内番終了時）;地の文とBの입上
	CALL KOJO_MESSAGE_EVENT
ENDIF

TARGET = LOCAL

PRINTL 


;--------------------------------------------------------
;습득の珠
;--------------------------------------------------------
JUEL:UMA_A:습득 += 200
JUEL:UMA_A:굴복 += 50
JUEL:UMA_A:부정 += 100
JUEL:UMA_B:습득 += 200
JUEL:UMA_B:굴복 += 50
JUEL:UMA_B:부정 += 100
PRINTFORML %CALLNAME:UMA_A%と%CALLNAME:UMA_B%に습득の珠＋{200}
PRINTFORML %CALLNAME:UMA_A%と%CALLNAME:UMA_B%に굴복の珠＋{50}
PRINTFORML %CALLNAME:UMA_A%と%CALLNAME:UMA_B%に부정の珠＋{100}


;--------------------------------------------------------
;기력の最大値アップ
;確定です（笑）
;--------------------------------------------------------
;上がる値は20-30のランダム
LOCAL = RAND:10 + 20
MAXBASE:UMA_A:기력 += LOCAL
PRINTFORML %CALLNAME:UMA_A%の기력の最大値＋{LOCAL}

LOCAL = RAND:10 + 20
MAXBASE:UMA_B:기력 += LOCAL
PRINTFORML %CALLNAME:UMA_B%の기력の最大値＋{LOCAL}


;--------------------------------------------------------
;체력の減少
;--------------------------------------------------------
;체력の減少
BASE:UMA_A:체력 -= 500
BASE:UMA_B:체력 -= 500

;フラグのリセット
CFLAG:UMA_A:12 = 0
CFLAG:UMA_B:12 = 0

;内番開始済みフラグ（馬当番=4）を落とす
SIF FLAG:161 & 4
	FLAG:161 -= 4

DRAWLINE





