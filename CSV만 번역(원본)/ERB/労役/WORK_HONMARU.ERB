;====================================================================
;労役指示(本丸)
;====================================================================
;本丸要素の濃い労役を分けました

;該当する労役
;料理を作る　料理を作ってもらう
;손合わせ　畑当番　馬当番
;====================================================================

;-------------------------------------------------
@CHARA_WORKING_HONMARU
;-------------------------------------------------
;項目表示用のカウント変数
#DIM DYNAMIC TEAWASE = 0
#DIM DYNAMIC HATAKE = 0
#DIM DYNAMIC UMA = 0
#DIM DYNAMIC KIWAME = 0

D = 1
CALL DAILY_LIFE_MASTER_CHANGE

REPEAT CHARANUM
	;あなたは不可
	SIF NO:COUNT == 0
		CONTINUE
	;現在の主人は不可
	SIF NO:COUNT == NO:MASTER
		CONTINUE
	;既に労役中は不可
	SIF CFLAG:COUNT:12 > 0
		CONTINUE
	;기절中は不可
	SIF TALENT:COUNT:기절
		CONTINUE
	;육아중は不可
	SIF TALENT:COUNT:육아중
		CONTINUE
	;발정기は不可
	SIF TALENT:COUNT:발정기
		CONTINUE
	
	;손合わせ
	;임신中は不可
	IF TALENT:COUNT:임신
	;체력1000未満で不可
	ELSEIF BASE:COUNT:체력 < 1000
	ELSE
		CFLAG:COUNT:151 |= 64
		TEAWASE += 1
	ENDIF
	
	;畑当番　馬当番
	;달마は不可
	IF TALENT:COUNT:달마
	;체력1000未満で不可
	ELSEIF BASE:COUNT:체력 < 1000
	ELSE
		CFLAG:COUNT:151 |= 128
		HATAKE += 1
		CFLAG:COUNT:151 |= 256
		UMA += 1
	ENDIF
	
	;修行
	;극キャラのCSVがなかったら不可
	;該当男士の극キャラが既にいたら不可（売却してても不可）
	;히게키리は先に処理
	IF NO:COUNT == 107
		SIF !EXISTCSV(111 ,0)
			CONTINUE
		SIF ITEMSALES:(199 + 111) == 2
			CONTINUE
	;히자마루は先に処理
	ELSEIF NO:COUNT == 112
		SIF !EXISTCSV(115 ,0)
			CONTINUE
		SIF ITEMSALES:(199 + 115) == 2
			CONTINUE
	ELSE
		SIF !EXISTCSV(NO:COUNT + 1 ,0)
			CONTINUE
		SIF ITEMSALES:(199 + NO:COUNT + 1) == 2
			CONTINUE
	ENDIF
	;助손可能、共犯者でなかったら不可
	SIF CFLAG:COUNT:1 != 2 && CFLAG:COUNT:1 != 3
		CONTINUE
	;すでに극だと不可
	SIF TALENT:COUNT:극
		CONTINUE
	;달마、임신は不可
	SIF TALENT:COUNT:달마 || TALENT:COUNT:임신
		CONTINUE
	;붕괴、실의、광기は不可
	SIF TALENT:COUNT:붕괴 || TALENT:COUNT:실의 || TALENT:COUNT:광기
		CONTINUE
	;후天的な유아／유아퇴행は不可
	IF TALENT:COUNT:유아／유아퇴행
		SIF !CSVTALENT(NO:COUNT, 150, 0)
			CONTINUE
	ENDIF
	;체력1000未満で不可
	IF BASE:COUNT:체력 < 1000
		CONTINUE
	ENDIF
	;ここまでで弾かれなかったら修行可能としてフラグオン
	CFLAG:COUNT:151 |= 512
	KIWAME += 1
REND


;以下画面表示部分
SIF TARGET != -1 && ((KANRAKU(TARGET) || (ABL:순종 >= 3 && ABL:봉사정신 >= 3) || TALENT:요리고수) && MARK:반발각인 != 3 && TALENT:달마 == 0 && TALENT:발정기 == 0)
	PRINTFORML [10] - %CALLNAME%に料理を作ってもらう

SIF ABL:MASTER:기교 >= 3 || ABL:MASTER:요리기능 || TALENT:MASTER:요리고수 || TALENT:MASTER:요리치
	PRINTL [11] - 自分で料理を作る

;内番は昼なら可能
SIF TIME == 0 && (FLAG:161 & 1) == 0 && TEAWASE >= 2
	PRINTL [20] 内番 -손合わせ
SIF TIME == 0 && (FLAG:161 & 2) == 0 && HATAKE >= 2
	PRINTL [30] 内番 -畑当番
SIF TIME == 0 && (FLAG:161 & 4) == 0 && UMA >= 2
	PRINTL [40] 内番 -馬当番

;修行も昼なら可能
SIF TIME == 0 && (FLAG:161 & 8) == 0 && KIWAME > 0
	PRINTL [99] 修行

PRINTL [100]- 戻る
$INPUT_LOOP
INPUT
IF RESULT == 10
	CALL LUNCH_JUDGE
ELSEIF RESULT == 11
	CALL LUNCH_SELF
ELSEIF RESULT == 20 && TEAWASE >= 2
	CALL DUTY_PRACTICE
ELSEIF RESULT == 30 && HATAKE >= 2
	CALL DUTY_HATAKE
ELSEIF RESULT == 40 && UMA >= 2
	CALL DUTY_UMA
ELSEIF RESULT == 99 && KIWAME > 0
	CALL KIWAME
ELSEIF RESULT == 100
	;労役可能フラグのリセット
	REPEAT CHARANUM
		CFLAG:COUNT:151 = 0
	REND
	RETURN 0
ELSE
	GOTO INPUT_LOOP
ENDIF

;労役可能フラグのリセット
REPEAT CHARANUM
	CFLAG:COUNT:151 = 0
REND

