;調教対象選択(eratohoR+ 1.31)
@CHANGE_TARGET
IF TARGET >= 0
	PRINT 現在
	PRINTFORML %NAME:TARGET%を調教中
ENDIF
PRINTL 今回は誰を調教しますか？
R = 1
CALL LIFE_LIST
PRINTFORML  [1000]戻る

$INPUT_LOOP
INPUT
IF RESULT == 1000
	RETURN 0
ELSEIF RESULT < 0 || RESULT >= CHARANUM || MASTER == RESULT
	GOTO INPUT_LOOP
ELSE
	SIF CFLAG:RESULT:1 == 3
		GOTO INPUT_LOOP
	SIF CFLAG:RESULT:12 > 0
		GOTO INPUT_LOOP
	C = RESULT
	CALL CHECK_CHILD_CARE
	SIF RESULT == 1
		GOTO INPUT_LOOP
ENDIF

SIF C != 0
	TARGET = C
SIF TARGET == ASSI
	ASSI = -1

@SELECT_ASSI
PRINT 現在
IF ASSI >= 0
	PRINTFORML   [{ASSI}]%NAME:ASSI%が助손
ELSE
	PRINTFORML 助손はいません
ENDIF
PRINTL 誰を助손にしますか？(現在の助손は☆)
DRAWLINE
CALL LIFE_LIST(1)
PRINTFORML   [999]助손なし
PRINTFORML  [1000]戻る

$INPUT_LOOP
INPUT
IF RESULT == 1000
	RETURN 0
ELSEIF RESULT == 999
	ASSI = -1
	RETURN 0
ELSEIF RESULT == MASTER
	ASSI = -1
	RETURN 0
ELSEIF RESULT < 0 || RESULT >= CHARANUM
	GOTO INPUT_LOOP
ELSEIF CFLAG:RESULT:12 > 0
	GOTO INPUT_LOOP
ELSEIF CFLAG:RESULT:1 < 2
	GOTO INPUT_LOOP
ELSEIF CFLAG:RESULT:26
	GOTO INPUT_LOOP
ELSE
	C = RESULT
	CALL CHECK_CHILD_CARE
	SIF RESULT == 1
		GOTO INPUT_LOOP
ENDIF

ASSI = C
CALL ASSI_CHANGE
ISASSI:ASSI = 1
SIF ASSI == TARGET
	TARGET = -1


@LIFE_LIST(ARG)
DRAWLINE
REPEAT CHARANUM
	IF CFLAG:COUNT:1 == 3 && CFLAG:COUNT:12 == 0
		IF COUNT == MASTER
			SETCOLOR 0x8888FF
			PRINTFORM %UNICODE(0x27A0)%
		ELSEIF COUNT == ASSI
			SETCOLOR 0x8888FF
			PRINT 助
		ELSE
			PRINT 　
		ENDIF
		RESETCOLOR
		PRINTFORM [{COUNT,2}]%CALLNAME:COUNT, 40, LEFT%
		;調教者変更のときのインターフェイス
		X = COUNT
		CALL LIFE_LIST_MASTER_CHANGE
		COUNT = X
		PRINTL 
	ENDIF
REND
DRAWLINE
REPEAT CHARANUM
	;共犯者フラグ有りのキャラは排除
	SIF CFLAG:COUNT:1 == 3
		CONTINUE
	;労役中のキャラは除外
	SIF CFLAG:COUNT:12 > 0
		CONTINUE
	;成長前の아이は除外
	SIF CFLAG:COUNT:26
		CONTINUE
	;育児部屋に移動しているキャラは除外
	C = COUNT
	CALL CHECK_CHILD_CARE
	SIF R == 1 && RESULT == 1
		CONTINUE
	SIF ARG && CFLAG:COUNT:1 < 2
		CONTINUE
	IF ARG
		IF CFLAG:COUNT:1 < 2
			PRINT ×
		ENDIF
	ELSEIF COUNT == TARGET
		SETCOLOR 0x8888FF
		PRINT ☆
	ELSEIF COUNT == ASSI
		SETCOLOR 0x8888FF
		PRINT 助
	ELSE
		PRINT 　
	ENDIF
	RESETCOLOR
	IF COUNT<10
		PRINTFORM [ {COUNT}]
	ELSE
		PRINTFORM [{COUNT}]
	ENDIF
	SIF TALENT:COUNT:기절
		SETCOLOR 120,120,120
	PRINTFORM %CALLNAME:COUNT,20,LEFT%
	RESETCOLOR
	SIF TALENT:COUNT:기절
		PRINT 기절
	IF ARG
		PRINTFORM 기교Lv{ABL:COUNT:기교} 
		SIF ABL:COUNT:기교 < 10
			PRINT  
	ELSE
		PRINTFORM %UNICODE(0x208D)%
		CALL PRINT_COLORBAR,BASE:COUNT:체력,MAXBASE:COUNT:체력,8,UNICODE(0x2583),UNICODE(0x2583),BARCOLORSET("赤"),RESULT:1, 327685
		PRINTFORM %UNICODE(0x208E)%
		PRINTFORM %UNICODE(0x208D)%
		CALL PRINT_COLORBAR,BASE:COUNT:기력,MAXBASE:COUNT:기력,8,UNICODE(0x2583),UNICODE(0x2583),BARCOLORSET("青"),RESULT:1, 327685
		PRINTFORM %UNICODE(0x208E)%
	ENDIF
	;調教者変更のときのインターフェイス
	X = COUNT
	CALL LIFE_LIST_MASTER_CHANGE
	COUNT = X
	PRINTL 
REND
PRINTL 
IF E >= 1
	SETCOLOR 0x8888FF
	PRINTFORM 　　　　%UNICODE(0x27A0)%
	PRINTD ：調教者　
	PRINT ☆
	PRINTD ：調教中　
	PRINT 助
	PRINTDL ：助손　を表しています
	RESETCOLOR
ENDIF

;ステータス表示
@SHOW_CHARADATA
R = 0
CALL LIFE_LIST
PRINTFORML  [1000]戻る

$INPUT_LOOP
INPUT
IF RESULT == 1000
	RETURN 0
ELSEIF RESULT < 0 || RESULT >= CHARANUM
	GOTO INPUT_LOOP
ENDIF

CALL PRINT_STATUS(RESULT)
RESTART

;-------------------------------------------------
;キャラ購入
;-------------------------------------------------
@CHARA_BUY(ARG,ARG:1)
VARSET LOCAL
CALL CHARA_PRICE
IF MASTER == -1
	PRINTL 　★★プレイヤーを変更できます★★
	PRINT 　★★キャラを選択してください★★
ELSEIF ARG
	PRINTL 　★★共犯者を追加できます★★
	PRINT 　★★キャラを選択してください★★
ELSEIF ARG:1
	PRINTL 　★★最初に奴隷を１人もらえます★★
	PRINTL 　★★キャラを選択してください★★
ELSE
	PRINTFORML {DAY}日目 \@ TIME ? 夜 # 昼 \@ 残り資金(%MONEY_FORM(MONEY)%고슈금)
	PRINT 　★★購入するキャラを選択してください★★
ENDIF
SETCOLOR 0x8888FF
PRINTL 　입上あり
SETCOLOR 140,140,140
DRAWLINE
RESETCOLOR
LOCAL:1 = 0
FOR LOCAL, 1, FLAG:44
	IF EXISTCSV(LOCAL, 0)
		IF ITEMSALES:(199 + LOCAL) == 1 && CHARA_BUY_PRE(LOCAL)
			LOCALS = [{LOCAL}] 
			TRYCCALLFORM SELECTER_K{LOCAL}()
				SETCOLOR 0x8888FF
			CATCH
			ENDCATCH
			LOCALS = %LOCALS%%NATION_OR_H(LOCAL)%
			SIF MASTER > -1 && !ARG && !ARG:1
				LOCALS = %LOCALS%(%MONEY_FORM(ITEMPRICE:(199+LOCAL))%고슈금)
			IF STRLENS(LOCALS) > 27
				LOCAL:1++
				SIF (LOCAL:1 % 3) == 0
					PRINTL 
				PRINTFORM %LOCALS, 54,LEFT%
				PRINT  
				SIF (LOCAL:1 % 3) == 0
					LOCAL:1++
			ELSE
				PRINTFORM %LOCALS,27,LEFT%
			ENDIF
			RESETCOLOR
			LOCAL:1++
			IF LOCAL:1 % 3 == 0
				PRINTL 
			ELSE
				PRINT  
			ENDIF
		ENDIF
	ENDIF
NEXT
SIF LOCAL:1 % 3 != 0
	PRINTL 
SIF (MASTER == -1 || ARG || ARG:1 || MONEY > 1) && LOCAL:1 > 0
	PRINTFORML [998]だれでもいい\@ MASTER == -1 || ARG || ARG:1 ? 　　　 # (時価特価)\@　　　
SETCOLOR 140,140,140
DRAWLINE
RESETCOLOR
IF MASTER == -1
	PRINTL [999]"あなた"にする(素質選択可能)
ELSEIF ARG
	PRINTL [999]共犯者は必要ない
ELSEIF ARG:1
	PRINTL [999]奴隷は必要ない
ELSE
	PRINTL [999]戻る
ENDIF
$INPUT_LOOP
INPUT
LOCAL = RESULT
;"あなた"/戻る/必要ない
IF RESULT == 999
	IF MASTER == -1
		MASTER = 0
		CALL ANATA_TALENT_SET(MASTER)
	ENDIF
	CALL CHARA_BUY_AFTER
	RETURN 0
;だれでも 前提条件ありのキャラも呼ばれる
ELSEIF RESULT == 998 && (MASTER == -1 || ARG || ARG:1 || MONEY > 1) && LOCAL:1 > 0
	$RANDOMSELECT_LOOP
	LOCAL = 1 + RAND:(FLAG:44)
	SIF LOCAL > 799 || ITEMSALES:(199+LOCAL) != 1
		GOTO RANDOMSELECT_LOOP
	LOCAL:3 = 1
;キャラが居ないか前提条件を満たしていないなら再入力
ELSEIF ITEMSALES:(199+LOCAL) != 1 || !CHARA_BUY_PRE(LOCAL)
	CLEARLINE 1
	GOTO INPUT_LOOP
ENDIF
DRAWLINE
;キャラ解説を呼ぶ
IF RESULT == 998 && (MASTER > -1 && !ARG && !ARG:1)
ELSEIF MASTER == -1 || ARG
	CALL CHARA_MANUAL(LOCAL,2)
ELSE
	CALL CHARA_MANUAL(LOCAL,1)
ENDIF
SIF MASTER > -1 && !ARG && !ARG:1
	PRINTFORML 価格\@ LOCAL:3 ? {MONEY/2} # {ITEMPRICE:(199+LOCAL)} \@고슈금 残り資金{MONEY}고슈금
DRAWLINE
PRINT [0] 戻る　　　　　　　　
IF MASTER == -1
	PRINTL [9] プレイヤーに選択する
ELSEIF ARG
	PRINTL [9] 共犯者として迎え入れる
ELSEIF ARG:1
	PRINTL [9] この奴隷が欲しい　　　
ELSE
	PRINTFORML [9] \@ LOCAL:3 ? # %NATION_OR_H(LOCAL)%を \@購入する
ENDIF
$INPUT_LOOP_2
INPUT
IF RESULT == 0
	RESTART
ELSEIF RESULT != 9
	CLEARLINE 1
	GOTO INPUT_LOOP_2
ENDIF
IF !LOCAL:3 && MASTER > -1 && !ARG && !ARG:1 && MONEY < ITEMPRICE:(199+LOCAL)
	PRINTW 金額が발りません
	PRINTL 
	RESTART
ENDIF
ITEM:(199 + LOCAL) = 1
ADDCHARA LOCAL
CALL CHARANAME_PRINT_NATION
IF MASTER == -1
	DELCHARA 0
	MASTER = 0
	CFLAG:MASTER:1 = 3
	PRINTFORMW プレイヤーを「%NAME:MASTER%」に設定します
	LOCAL:1 = 0
	LOCAL:2 = MASTER
ELSEIF ARG
	ASSI = CHARANUM - 1
	CFLAG:ASSI:1 = 3
	PRINTFORMW 「%NAME:ASSI%」が共犯になりました
	LOCAL:1 = ARG - 1
	LOCAL:2 = ASSI
	

ELSE
	IF !ARG:1
		IF LOCAL:3
			MONEY -= MONEY / 2
		ELSE
			MONEY -= ITEMPRICE:(199+LOCAL)
		ENDIF
	ENDIF
	SIF MONEY < 0
		MONEY = 0
	TARGET = CHARANUM - 1
	PRINTFORMW 「%NAME:TARGET%」を奴隷として引き取ります
	LOCAL:1 = 0
	LOCAL:2 = TARGET
ENDIF
CALL VIRGIN_MODE(LOCAL:2)
ITEMSALES:(199+LOCAL) = 0
CALL CHARA_BUY_AFTER(LOCAL:1,LOCAL:2)
;個体差処理
CALL DIFF_MAIN(LOCAL:2)

@CHARA_BUY_AFTER(ARG:0, ARG:1)
;즉각함락を付ける処理
SIF FLAG:(1000 + NO:(ARG:1)) & 32
	TALENT:(ARG:1):즉각함락 = 1
;ITEMSALESは立たせっぱなしにしておく　（様子見中なので、消さずにコメントアウト）
;REPEAT FLAG:44
;	A = COUNT + 199
;	ITEMSALES:A = 0
;	;ここTFLAGの処理いらなくない…？
;	;ITEMSALES:COUNT = TFLAG:COUNT
;	;TFLAG:COUNT = 0
;REND
SIF ARG
	CALL CHARA_BUY(ARG)

@CHARA_PRICE
REPEAT FLAG:44
	
	;CSVがないキャラはスキップ
	SIF !EXISTCSV(COUNT, 0)
		CONTINUE
	
	;購入済みキャラはスキップ
	;ただし最初の１回はITEMSALESを代入する必要があるので、キャラが「あなた」だけしかいない場合はスキップしない
	;ITEMPRICE：購入金額
	IF ITEMPRICE:(199+COUNT) == 0 || (ITEMSALES:(199 + COUNT) == 0 && CHARANUM > 1)
		CONTINUE
	ELSEIF ITEMPRICE:(199 + COUNT)
		ITEMSALES:(199 + COUNT) = 1
	ENDIF
REND
;-------------------------------------------------
;PCを選択式にしてみる
;-------------------------------------------------
@START_CHARA_SELECT
K = 0
PRINTFORML ★★プレイヤーを設定します★★
MASTER = 0
CALL ANATA_TALENT_SET(MASTER)

IF FLAG:5 == 9
	CALL KYOHAN_CHARA_SELECT
	CALL CHARA_BUY(0,1)
ENDIF

;-------------------------------------------------
;共犯者を追加できるようにしてみる
;-------------------------------------------------
@KYOHAN_CHARA_SELECT
PRINTFORML ★★共犯者を追加できます★★
PRINTFORML 
IF FLAG:5 == 9
	PRINTL [0] ２人選ぶ
	PRINTL [1] １人選ぶ
	PRINTL [2] 選ばない
ELSE
	PRINTL [1] 選ぶ
	PRINTL [2] 選ばない
ENDIF
$INPUT_LOOP
INPUT
IF RESULT == 0
	PRINTFORML ―共犯者追加―
	FLAG:42 = 2
	CALL CHARA_BUY(2)
ELSEIF RESULT == 1
	PRINTFORML ―共犯者追加―
	FLAG:42 = 1
	CALL CHARA_BUY(1)
ELSEIF RESULT == 2
	FLAG:42 = 0
ELSE 
	GOTO INPUT_LOOP
ENDIF

;-------------------------------------------------
;強くてニューゲームしてみる
;-------------------------------------------------
;強くてニューゲームの処理はここで実行
;ボーナスポイントで色々購入する
@TSUYOKUTE_GAME
PRINTFORML ボーナスポイントを得てニューゲームを開始します。よろしいですか？
PRINTL [0] はい
PRINTL [1] いいえ

$INPUT_LOOP_TSUYOKUTE
INPUT
IF RESULT == 0
	FLAG:5 = 0
	FLAG:40 = 0
	
	;日付、時間、資金、調教対象や助손のリセット
	DAY = 1
	TIME = 0
	MONEY = 30000
	TARGET = -1
	ASSI = -1
	
	;奴隷消去処理
	DELALLCHARA
	
	;フラグリセット
	REPEAT 300
		ITEM:COUNT = 0
	REND
	REPEAT 999
		TFLAG:COUNT = 0
	REND
	FLAG:23 = 0
	FLAG:30 = 0
	FLAG:31 = 0
	FLAG:33 = 0
	FLAG:32 = 0
	FLAG:42 = 0
	CALL CHARA_PRICE
	FLAG:500 = 0
	;新主人の選択
	ADDCHARA 0
	MASTER = -1
	
	;難易度設定
	CALL START_MODE(1)
	
	;ボーナス取得
	Z = FLAG:41
	CALL TSUYOKUTE_BONUS
	
ELSEIF RESULT == 1
	RETURN 1
ELSE
	GOTO INPUT_LOOP_TSUYOKUTE
ENDIF

;-------------------------------------------------
;強くてニューゲームのボーナス取得
;-------------------------------------------------
@TSUYOKUTE_BONUS
IF Z > 0
	PRINTFORML 残りボーナスポイント {Z}
	PRINTL 
	SIF ABL:MASTER:기교 < 10
		PRINTL [0]   - 기교Lv＋3
	PRINTL [1]   - 資金＋30000
	PRINTL [2]   - 共犯者を呼ぶ
	SIF TALENT:MASTER:금지된지식 == 0
		PRINTL [3]   - 【금단의지식】を身につける
	SIF TALENT:MASTER:조제지식 == 0
		PRINTL [4]   - 【조제지식】を身につける
	SIF TALENT:MASTER:새드 == 0
		PRINTL [5]   - 【새드】を身につける
	SIF TALENT:MASTER:마조 == 0
		PRINTL [6]   - 【마조】を身につける
	SIF TALENT:MASTER:수수께끼의매력 == 0 && B > 1
		PRINTL [7]   - 【수수께끼의매력】を身につける（ＢＰ２）
	PRINTL [100] - ボーナス取得をやめる
	
	$INPUT_LOOP
	INPUT
	IF RESULT == 0 && ABL:MASTER:기교 < 10
		IF ABL:MASTER:기교 > 7
			ABL:MASTER:기교 = 10
		ELSE
			ABL:MASTER:기교 += 3
		ENDIF
		PRINTFORML %CALLNAME:MASTER%の기교が{ABL:MASTER:기교}:LVになった
		Z -= 1
	ELSEIF RESULT == 1
		MONEY += 30000
		PRINTFORML 所持甲州金が{MONEY}고슈금になった
		Z -= 1
	ELSEIF RESULT == 2
		PRINTL ―共犯者追加―
		FLAG:42 += 1
		Z -= 1
		CALL CHARA_BUY(1)
	ELSEIF RESULT == 3 && TALENT:MASTER:금지된지식 == 0
		PRINTFORML %CALLNAME:MASTER%は【금단의지식】を身につけた
		TALENT:MASTER:금지된지식 = 1
		Z -= 1
	ELSEIF RESULT == 4 && TALENT:MASTER:조제지식 == 0
		PRINTFORML %CALLNAME:MASTER%は【조제지식】を身につけた
		TALENT:MASTER:조제지식 = 1
		Z -= 1
	ELSEIF RESULT == 5 && TALENT:MASTER:새드 == 0
		PRINTFORM %CALLNAME:MASTER%は
		IF TALENT:MASTER:마조
			TALENT:MASTER:마조 = 0
			PRINT 【마조】を失い
		ENDIF
		PRINTL 【새드】を身に付けた
		TALENT:MASTER:새드 = 1
		Z -= 1
	ELSEIF RESULT == 6 && TALENT:MASTER:마조 == 0
		PRINTFORM %CALLNAME:MASTER%は
		IF TALENT:MASTER:새드
			TALENT:MASTER:새드 = 0
			PRINT 【새드】を失い
		ENDIF
		PRINTL 【마조】を身に付けた
		TALENT:MASTER:마조 = 1
		Z -= 1
	ELSEIF RESULT == 7 && TALENT:MASTER:수수께끼의매력 == 0 && B > 1
		PRINTFORML %CALLNAME:MASTER%は【수수께끼의매력】を身につけた
		TALENT:MASTER:수수께끼의매력 = 1
		Z -= 2
	ELSEIF RESULT == 100
		PRINTL ボーナス取得をやめ、ゲームを開始します。よろしいですか？
		PRINTL [1] はい
		PRINTL [2] いいえ
		$INPUT_LOOP_2
		INPUT
		IF RESULT == 1
			Z = 0
		ELSEIF RESULT == 2
		ELSE
			GOTO INPUT_LOOP_2
		ENDIF
	ELSE
		GOTO INPUT_LOOP
	ENDIF
	
	CALL TSUYOKUTE_BONUS
ENDIF

