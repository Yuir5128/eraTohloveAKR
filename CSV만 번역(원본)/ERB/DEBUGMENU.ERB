@DEBUG_MENU_SHOP
DRAWLINE
IF TARGET >= 0
	PRINTFORM %CALLNAME:TARGET% 調教中　
	CALL LIFE_BAR
ENDIF
SIF ASSI >= 0
	PRINTFORM （助손:%CALLNAME:ASSI%）
DRAWLINE
;
PRINTL 何をしますか？
PRINTL 

PRINTS "依存度変化処理　　　　　　"
SIF TARGET >= 0
	PRINTBUTTON "[ 0] 調教対象", 0
PRINTS "　"
PRINTBUTTON "[10] 主人", 10
PRINTL 
PRINTS "ストレス変化処理　　　　　"
SIF TARGET >= 0
	PRINTBUTTON "[ 1] 調教対象", 1
PRINTS "　"
PRINTBUTTON "[11] 主人", 11
PRINTL 
PRINTS "陥落系素質着脱処理　　　　"
SIF TARGET >= 0
	PRINTBUTTON "[ 2] 調教対象", 2
PRINTS "　"
PRINTBUTTON "[12] 主人", 12
PRINTL 
PRINTS "その他の素質着脱処理　　　"
SIF TARGET >= 0
	PRINTBUTTON "[ 3] 調教対象", 3
PRINTS "　"
PRINTBUTTON "[13] 主人", 13
PRINTL 
PRINTL [ 4] - 思慕対象者チェック
PRINT [ 5] - 임신確率
IF FLAG:151
	PRINTL 　　　　　　百発百中
ELSE
	PRINTL 　　　　　　　　通常
ENDIF
PRINTS "珠数変化処理　　　　　　　"
SIF TARGET >= 0
	PRINTBUTTON "[ 6] 調教対象", 6
PRINTS "　"
PRINTBUTTON "[16] 主人", 16
PRINTL 
PRINTS "経験変化処理　　　　　　　"
SIF TARGET >= 0
	PRINTBUTTON "[ 7] 調教対象", 7
PRINTS "　"
PRINTBUTTON "[17] 主人", 17
PRINTL 
PRINTS "能力変化処理　　　　　　　"
SIF TARGET >= 0
	PRINTBUTTON "[ 8] 調教対象", 8
PRINTS "　"
PRINTBUTTON "[18] 主人", 18
PRINTL 
PRINTS "調教・被조교경험変化処理　"
SIF TARGET >= 0
	PRINTBUTTON "[ 9] 調教対象", 9
PRINTS "　"
PRINTBUTTON "[19] 主人", 19
PRINTL 
PRINTL [31] - FLAGを表示・変更する
PRINTS "CFLAGを表示・変更する　　 "
SIF TARGET >= 0
	PRINTBUTTON "[32] 調教対象", 32
PRINTS "　"
PRINTBUTTON "[42] 主人", 42
PRINTL 
PRINTS "CSTRを表示・変更する　　　"
SIF TARGET >= 0
	PRINTBUTTON "[33] 調教対象", 33
PRINTS "　"
PRINTBUTTON "[43] 主人", 43
PRINTL 
PRINTL [34] - 所持金を変更する
PRINTS "체력変化処理　　　　　　　"
SIF TARGET >= 0
	PRINTBUTTON "[36] 調教対象", 36
PRINTS "　"
PRINTBUTTON "[46] 主人", 46
PRINTL 
PRINTS "刻印変化処理　　　　　　　"
SIF TARGET >= 0
	PRINTBUTTON "[37] 調教対象", 37
PRINTS "　"
PRINTBUTTON "[47] 主人", 47
PRINTL 
PRINTL [38] - 日数を経過させる
PRINTL [100] - 戻る

$INPUT_LOOP
INPUT
IF RESULT == 0 && TARGET >= 0
	CALL EDIT_FAVORABILITY(TARGET)
ELSEIF RESULT == 10
	CALL EDIT_FAVORABILITY(MASTER)
ELSEIF RESULT == 1 && TARGET >= 0
	CALL EDIT_STRESS(TARGET)
ELSEIF RESULT == 11
	CALL EDIT_STRESS(MASTER)
ELSEIF RESULT == 2 && TARGET >= 0
	CALL EDIT_TALENT_KANRAKU(TARGET)
ELSEIF RESULT == 12
	CALL EDIT_TALENT_KANRAKU(MASTER)
ELSEIF RESULT == 3
	CALL EDIT_TALENT(TARGET)
ELSEIF RESULT == 13
	CALL EDIT_TALENT(MASTER)
ELSEIF RESULT == 4 && TARGET >= 0
	CALL DEBUGCHECK_TARGET
ELSEIF RESULT == 5
	IF FLAG:151
		FLAG:151 = 0
	ELSE
		FLAG:151 = 1
	ENDIF
ELSEIF RESULT == 6 && TARGET >= 0
	CALL EDIT_JUEL(TARGET)
ELSEIF RESULT == 16
	CALL EDIT_JUEL(MASTER)
ELSEIF RESULT == 7 && TARGET >= 0
	CALL EDIT_EXP(TARGET)
ELSEIF RESULT == 17
	CALL EDIT_EXP(MASTER)
ELSEIF RESULT == 8 && TARGET >= 0
	CALL EDIT_ABL(TARGET)
ELSEIF RESULT == 18
	CALL EDIT_ABL(MASTER)
ELSEIF RESULT == 9 && TARGET >= 0
	CALL EDIT_HITYOUKYOU(TARGET)
ELSEIF RESULT == 19
	CALL EDIT_HITYOUKYOU(MASTER)
ELSEIF RESULT == 31
	CALL EDIT_FLAG
ELSEIF RESULT == 32 && TARGET >= 0
	CALL EDIT_CFLAG(TARGET)
ELSEIF RESULT == 42
	CALL EDIT_CFLAG(MASTER)
ELSEIF RESULT == 33 && TARGET >= 0
	CALL EDIT_CSTR(TARGET)
ELSEIF RESULT == 43
	CALL EDIT_CSTR(MASTER)
ELSEIF RESULT == 34
	CALL EDIT_MONEY(0)
ELSEIF RESULT == 36
	CALL EDIT_BASE(TARGET)
ELSEIF RESULT == 46
	CALL EDIT_BASE(MASTER)
ELSEIF RESULT == 37
	CALL EDIT_MARK(TARGET)
ELSEIF RESULT == 47
	CALL EDIT_MARK(MASTER)
ELSEIF RESULT == 38
	CALL EDIT_DAY
ELSEIF RESULT == 100
	RETURN 0
ELSE
	REUSELASTLINE 入力しなおしてください
	GOTO INPUT_LOOP
ENDIF
IF TARGET >= 0
	CLEARLINE 8
ELSE
	CLEARLINE 4
ENDIF
RESTART

;--------------------------------------------------
;依存度変化処理
;--------------------------------------------------
;主人または現在調教中のキャラの依存度を好きな値に変化させます
;연모などのイベントは調教終了時に発生するので即効果が発揮するわけではない
;あまりにも大きすぎる値にするとバグることがあります
@EDIT_FAVORABILITY(ARG)
DRAWLINE
PRINTFORML %CALLNAME:ARG%の依存度:{CFLAG:ARG:2}
PRINTL まず、どの値にするか入力してください(0-999999/無効な値でキャンセル)
PRINTL [   0] 0
PRINTL [ 999] 999
PRINTL [2999] 2999
PRINTL [9999] 9999
PRINTL [10000000] キャンセル

INPUT
SIF RESULT < 0 || RESULT > 999999
	RETURN 0
CFLAG:ARG:2 = RESULT
SIF RESULT == 0
	RETURN 0
PRINTL 次に、値をプラスにするかマイナスにするか選んでください。
PRINTL [ 0] プラス
PRINTL [ 1] マイナス
$INPUT_LOOP
INPUT
IF RESULT == 0
	PRINTFORMW %CALLNAME:ARG%の依存度を{CFLAG:ARG:2}に変更します。
ELSEIF RESULT == 1
	CFLAG:2 *= -1
	PRINTFORMW %CALLNAME:ARG%の依存度を{CFLAG:ARG:2}に変更します。
ELSE
	GOTO INPUT_LOOP
ENDIF
RETURN 0

;--------------------------------------------------
;ストレス変化処理
;--------------------------------------------------
;現在調教中のキャラのストレスを好きな値に変化させます
;あまりにも大きすぎる値にするとバグることがあります
@EDIT_STRESS(ARG)
DRAWLINE
PRINTFORML %CALLNAME:ARG%のストレス:{CFLAG:ARG:10}
PRINTL どの値にするか入力してください(0-999999/無効な値でキャンセル)
PRINTL [ 0] 0
PRINTL [99] 99
PRINTL [1000000] キャンセル

INPUT
SIF RESULT < 0 || RESULT > 999999
	RETURN 0
CFLAG:ARG:10 = RESULT
PRINTFORML %CALLNAME:ARG%のストレスを{CFLAG:ARG:65}に変更しました
RETURN 0

;--------------------------------------------------
;陥落系素質着脱処理
;--------------------------------------------------
;陥落系素質を付加/削除させます
@EDIT_TALENT_KANRAKU(ARG)
DRAWLINE
PRINTFORML %CALLNAME:ARG%に付加/削除する素質を選んでください
PRINT [  0] 素質[연모]を
IF TALENT:ARG:연모
	PRINTL 削除する
ELSE
	PRINTL 追加する
ENDIF
PRINT [  1] 素質[친애]を
IF TALENT:ARG:친애
	PRINTL 削除する
ELSE
	PRINTL 追加する
ENDIF
PRINT [  2] 素質[음란]を
IF TALENT:ARG:음란
	PRINTL 削除する
ELSE
	PRINTL 追加する
ENDIF
PRINT [  3] 素質[창부]を
IF TALENT:ARG:창부
	PRINTL 削除する
ELSE
	PRINTL 追加する
ENDIF
PRINT [  4] 素質[복종]を
IF TALENT:ARG:복종
	PRINTL 削除する
ELSE
	PRINTL 追加する
ENDIF
PRINT [  5] 素質[예속]を
IF TALENT:ARG:예속
	PRINTL 削除する
ELSE
	PRINTL 追加する
ENDIF
PRINT [  6] 素質[붕괴]を
IF TALENT:ARG:붕괴
	PRINTL 削除する
ELSE
	PRINTL 追加する
ENDIF
PRINT [  7] 素質[새드]を
IF TALENT:ARG:새드
	PRINTL 削除する
ELSE
	PRINTL 追加する
ENDIF
SIF TALENT:ARG:새드 == 1
	PRINTL [ 17] 素質[새드]を[ドＳ]にする
PRINT [  8] 素質[마조]を
IF TALENT:ARG:마조
	PRINTL 削除する
ELSE
	PRINTL 追加する
ENDIF
SIF TALENT:ARG:마조 == 1
	PRINTL [ 18] 素質[마조]を[ドＭ]にする
PRINT [  9] 素質[실의]を
IF TALENT:ARG:실의
	PRINTL 削除する
ELSE
	PRINTL 追加する
ENDIF
PRINT [ 10] 素質[상사상애]を
IF TALENT:ARG:상사상애
	PRINTL 削除する
ELSE
	PRINTL 追加する
ENDIF
PRINT [100] 終了

$INPUT_LOOP
INPUT
IF RESULT == 0
	PRINT 素質[연모]を
	IF TALENT:ARG:연모
		PRINTW 削除しました
		MARK:ARG:굴복각인 = 0
		TALENT:ARG:연모 = 0
		CFLAG:ARG:39 = 0
		CFLAG:ARG:41 = -1
		CFLAG:ARG:40 = 0
	ELSE
		PRINTW 追加しました
		MARK:ARG:굴복각인 = 3
		ABL:ARG:순종 = MAX(ABL:ARG:순종, 3)
		ABL:ARG:봉사정신 = MAX(ABL:ARG:봉사정신, 3)
		EXP:ARG:봉사쾌락경험 = MAX(EXP:ARG:봉사쾌락경험, 200)
		EXP:ARG:이상경험 = MIN(EXP:ARG:이상경험, 1)
		CFLAG:ARG:2 = MAX(CFLAG:ARG:2, 1000)
		TALENT:ARG:연모 = 1
		TALENT:ARG:음란 = 0
		TALENT:ARG:복종 = 0
		TALENT:ARG:친애 = 0
		TALENT:ARG:창부 = 0
		TALENT:ARG:예속 = 0
		TALENT:ARG:붕괴 = 0
		TALENT:ARG:실의 = 0
		PRINTL 思慕対象を選択して下さい。
		R = 0
		CALL LIFE_LIST
		$INPUT_LOOP1
		INPUT
		IF RESULT < 0 || RESULT >= CHARANUM || RESULT == ARG
			GOTO INPUT_LOOP1
		ELSE
			CFLAG:ARG:41 = NO:RESULT
			CFLAG:ARG:40 = 1
			CFLAG:ARG:39 = 3
			PRINTFORMW 思慕対象を%CALLNAME:RESULT%に指定しました。
		ENDIF
	ENDIF
ELSEIF RESULT == 1
	PRINT 素質[친애]を
	IF TALENT:ARG:친애
		PRINTW 削除しました
		MARK:ARG:굴복각인 = 0
		TALENT:ARG:친애 = 0
		CFLAG:ARG:39 = 0
		CFLAG:ARG:41 = -1
		CFLAG:ARG:40 = 0
	ELSE
		PRINTW 追加しました
		MARK:ARG:굴복각인 = 3
		ABL:ARG:순종 = MAX(ABL:ARG:순종, 5)
		ABL:ARG:봉사정신 = MAX(ABL:ARG:봉사정신, 5)
		EXP:ARG:봉사쾌락경험 = MAX(EXP:ARG:봉사쾌락경험, 400)
		EXP:ARG:애정경험 = MAX(EXP:ARG:애정경험, 200)
		EXP:ARG:이상경험 = MIN(EXP:ARG:이상경험, 5)
		CFLAG:ARG:2 = MAX(CFLAG:ARG:2, 3000)
		TALENT:ARG:연모 = 0
		TALENT:ARG:음란 = 0
		TALENT:ARG:복종 = 0
		TALENT:ARG:친애 = 1
		TALENT:ARG:창부 = 0
		TALENT:ARG:예속 = 0
		TALENT:ARG:붕괴 = 0
		TALENT:ARG:실의 = 0
		PRINTL 思慕対象を選択して下さい。
		R = 0
		CALL LIFE_LIST
		$INPUT_LOOP2
		INPUT
		IF RESULT < 0 || RESULT >= CHARANUM || RESULT == ARG
			GOTO INPUT_LOOP2
		ELSE
			CFLAG:ARG:41 = NO:RESULT
			CFLAG:ARG:40 = 1
			CFLAG:ARG:39 = 6
			PRINTFORMW 思慕対象を%CALLNAME:RESULT%に指定しました。
		ENDIF
	ENDIF
ELSEIF RESULT == 10
	PRINT 素質[상사상애]を
	IF TALENT:ARG:상사상애
		PRINTW 削除しました
		PRINTW 素質[친애]を削除しました
		MARK:ARG:굴복각인 = 0
		TALENT:GETCHARA(CFLAG:ARG:41):상사상애 = 0
		CFLAG:GETCHARA(CFLAG:ARG:41):39 = 6
		TALENT:ARG:친애 = 0
		TALENT:ARG:상사상애 = 0
		CFLAG:ARG:39 = 0
		CFLAG:ARG:41 = -1
		CFLAG:ARG:40 = 0
	ELSE
		PRINTW 追加しました
		MARK:ARG:굴복각인 = 3
		TALENT:ARG:연모 = 0
		TALENT:ARG:음란 = 0
		TALENT:ARG:복종 = 0
		TALENT:ARG:친애 = 1
		TALENT:ARG:창부 = 0
		TALENT:ARG:예속 = 0
		TALENT:ARG:붕괴 = 0
		TALENT:ARG:실의 = 0
		TALENT:ARG:상사상애 = 1
		PRINTL 思慕対象を選択して下さい。
		R = 0
		CALL LIFE_LIST
		$INPUT_LOOP5
		INPUT
		IF RESULT < 0 || RESULT >= CHARANUM || RESULT == ARG
			GOTO INPUT_LOOP5
		ELSE
			CFLAG:ARG:41 = NO:RESULT
			CFLAG:ARG:40 = 1
			CFLAG:ARG:39 = 165
			TALENT:RESULT:친애 = 1
			TALENT:RESULT:상사상애 = 1
			CFLAG:RESULT:41 = NO:ARG
			CFLAG:RESULT:40 = 1
			CFLAG:RESULT:39 = 165
			PRINTFORMW 思慕対象を%CALLNAME:RESULT%に指定しました。
		ENDIF
	ENDIF
ELSEIF RESULT == 2
	CFLAG:ARG:41 = -1
	PRINT 素質[음란]を
	IF TALENT:ARG:음란
		PRINTW 削除しました
		MARK:ARG:굴복각인 = 0
		TALENT:ARG:음란 = 0
		CFLAG:ARG:39 = 0
	ELSE
		PRINTW 追加しました
		MARK:ARG:굴복각인 = 3
		MARK:ARG:쾌락각인 = 3
		ABL:ARG:욕망 = MAX(ABL:ARG:욕망, 3)
		ABL:ARG:C감각 += MIN(10 - ABL:C감각 - ABL:V감각 - ABL:A감각 - ABL:B감각, 0)
		IF ABL:ARG:C감각 > 10
			ABL:ARG:B감각 += ABL:ARG:C감각 - 10
			ABL:ARG:C감각 = 10
		ENDIF
		IF ABL:ARG:B감각 > 10
			ABL:ARG:A감각 += ABL:ARG:B감각 - 10
			ABL:ARG:B감각 = 10
		ENDIF
		EXP:ARG:절정경험 = MAX(EXP:ARG:절정경험, 50)
		EXP:ARG:이상경험 = MAX(EXP:ARG:이상경험, 3)
		TALENT:ARG:연모 = 0
		TALENT:ARG:음란 = 1
		TALENT:ARG:복종 = 0
		TALENT:ARG:친애 = 0
		TALENT:ARG:창부 = 0
		TALENT:ARG:예속 = 0
		TALENT:ARG:붕괴 = 0
		TALENT:ARG:실의 = 0
		CFLAG:ARG:39 = 4
	ENDIF
ELSEIF RESULT == 3
	CFLAG:ARG:41 = -1
	PRINT 素質[창부]を
	IF TALENT:ARG:창부
		PRINTW 削除しました
		MARK:ARG:굴복각인 = 0
		TALENT:ARG:창부 = 0
		CFLAG:ARG:39 = 0
	ELSE
		PRINTW 追加しました
		MARK:ARG:굴복각인 = 3
		MARK:ARG:쾌락각인 = 3
		ABL:ARG:욕망 = MAX(ABL:ARG:욕망, 5)
		ABL:ARG:C감각 += MIN(20 - ABL:C감각 - ABL:V감각 - ABL:A감각 - ABL:B감각, 0)
		IF ABL:ARG:C감각 > 10
			ABL:ARG:B감각 += ABL:ARG:C감각 - 10
			ABL:ARG:C감각 = 10
		ENDIF
		IF ABL:ARG:B감각 > 10
			ABL:ARG:A감각 += ABL:ARG:B감각 - 10
			ABL:ARG:B감각 = 10
		ENDIF
		IF ABL:ARG:A감각 > 10
			ABL:ARG:V감각 += ABL:ARG:A감각 - 10
			ABL:ARG:A감각 = 10
		ENDIF
		EXP:ARG:절정경험 = MAX(EXP:ARG:절정경험, 200)
		EXP:ARG:이상경험 = MAX(EXP:ARG:이상경험, 6)
		TALENT:ARG:연모 = 0
		TALENT:ARG:음란 = 0
		TALENT:ARG:복종 = 0
		TALENT:ARG:친애 = 0
		TALENT:ARG:창부 = 1
		TALENT:ARG:예속 = 0
		TALENT:ARG:붕괴 = 0
		TALENT:ARG:실의 = 0
		CFLAG:ARG:39 = 7
	ENDIF
ELSEIF RESULT == 4
	PRINT 素質[복종]を
	IF TALENT:ARG:복종
		PRINTW 削除しました
		MARK:ARG:굴복각인 = 0
		TALENT:ARG:복종 = 0
		CFLAG:ARG:39 = 0
		CFLAG:ARG:41 = -1
		CFLAG:ARG:40 = 0
	ELSE
		PRINTW 追加しました
		MARK:ARG:굴복각인 = 3
		ABL:ARG:순종 = MAX(ABL:ARG:순종, 3)
		ABL:ARG:마조끼 = MAX(ABL:ARG:마조끼, 3)
		ABL:ARG:C감각 -= MIN(10 - ABL:C감각 - ABL:V감각 - ABL:A감각 - ABL:B감각, 0)
		IF ABL:ARG:C감각 < 0
			ABL:ARG:B감각 += ABL:ARG:C감각
			ABL:ARG:C감각 = 0
		ENDIF
		IF ABL:ARG:B감각 < 0
			ABL:ARG:A감각 += ABL:ARG:B감각
			ABL:ARG:B감각 = 0
		ENDIF
		EXP:ARG:고통쾌락경험 += MAX(200 - EXP:ARG:고통쾌락경험 - EXP:ARG:속박경험, 0)
		EXP:ARG:이상경험 = MAX(EXP:ARG:이상경험, 2)
		CFLAG:ARG:2 = MIN(CFLAG:ARG:2, -1000)
		TALENT:ARG:연모 = 0
		TALENT:ARG:음란 = 0
		TALENT:ARG:복종 = 1
		TALENT:ARG:친애 = 0
		TALENT:ARG:창부 = 0
		TALENT:ARG:예속 = 0
		TALENT:ARG:붕괴 = 0
		TALENT:ARG:실의 = 0
		PRINTL 思慕対象を選択して下さい。
		R = 0
		CALL LIFE_LIST
		$INPUT_LOOP3
		INPUT
		IF RESULT < 0 || RESULT >= CHARANUM || RESULT == ARG
			GOTO INPUT_LOOP3
		ELSE
			CFLAG:ARG:41 = NO:RESULT
			CFLAG:ARG:40 = 1
			CFLAG:ARG:39 = 5
			PRINTFORMW 思慕対象を%CALLNAME:RESULT%に指定しました。
		ENDIF
	ENDIF
ELSEIF RESULT == 5
	PRINT 素質[예속]を
	IF TALENT:ARG:예속
		PRINTW 削除しました
		MARK:ARG:굴복각인 = 0
		TALENT:ARG:예속 = 0
		CFLAG:ARG:39 = 0
		CFLAG:ARG:41 = -1
		CFLAG:ARG:40 = 0
	ELSE
		PRINTW 追加しました
		MARK:ARG:굴복각인 = 3
		ABL:ARG:순종 = MAX(ABL:ARG:순종, 5)
		ABL:ARG:마조끼 = MAX(ABL:ARG:마조끼, 5)
		ABL:ARG:C감각 -= MIN(15 - ABL:C감각 - ABL:V감각 - ABL:A감각 - ABL:B감각, 0)
		IF ABL:ARG:C감각 < 0
			ABL:ARG:B감각 += ABL:ARG:C감각
			ABL:ARG:C감각 = 0
		ENDIF
		IF ABL:ARG:B감각 < 0
			ABL:ARG:A감각 += ABL:ARG:B감각
			ABL:ARG:B감각 = 0
		ENDIF
		IF ABL:ARG:A감각 < 0
			ABL:ARG:V감각 += ABL:ARG:A감각
			ABL:ARG:A감각 = 0
		ENDIF
		EXP:ARG:고통쾌락경험 += MAX(500 - EXP:ARG:고통쾌락경험 - EXP:ARG:속박경험, 0)
		EXP:ARG:이상경험 = MAX(EXP:ARG:이상경험, 5)
		CFLAG:ARG:2 = MIN(CFLAG:ARG:2, -3000)
		TALENT:ARG:연모 = 0
		TALENT:ARG:음란 = 0
		TALENT:ARG:복종 = 0
		TALENT:ARG:친애 = 0
		TALENT:ARG:창부 = 0
		TALENT:ARG:예속 = 1
		TALENT:ARG:붕괴 = 0
		TALENT:ARG:실의 = 0
		PRINTL 思慕対象を選択して下さい。
		R = 0
		CALL LIFE_LIST
		$INPUT_LOOP4
		INPUT
		IF RESULT < 0 || RESULT >= CHARANUM || RESULT == ARG
			GOTO INPUT_LOOP4
		ELSE
			CFLAG:ARG:41 = NO:RESULT
			CFLAG:ARG:40 = 1
			CFLAG:ARG:39 = 8
			PRINTFORMW 思慕対象を%CALLNAME:RESULT%に指定しました。
		ENDIF
	ENDIF
ELSEIF RESULT == 6
	PRINT 素質[붕괴]を
	IF TALENT:ARG:붕괴
		PRINTW 削除しました
		TALENT:ARG:붕괴 = 0
	ELSE
		PRINTW 追加しました
		TALENT:ARG:연모 = 0
		TALENT:ARG:음란 = 0
		TALENT:ARG:복종 = 0
		TALENT:ARG:친애 = 0
		TALENT:ARG:창부 = 0
		TALENT:ARG:예속 = 0
		TALENT:ARG:붕괴 = 1
		TALENT:ARG:실의 = 0
		CFLAG:ARG:41 = -1
		CFLAG:ARG:40 = 0
	ENDIF
ELSEIF RESULT == 7
	PRINT 素質[새드]を
	IF TALENT:ARG:새드
		PRINTW 削除しました
		TALENT:ARG:새드 = 0
	ELSE
		PRINTW 追加しました
		TALENT:ARG:새드 = 1
		TALENT:ARG:마조 = 0
	ENDIF
ELSEIF RESULT == 17
	PRINTW 素質[새드]を[ドＳ]にしました
	TALENT:ARG:새드 = 2
	TALENT:ARG:마조 = 0
ELSEIF RESULT == 8
	PRINT 素質[마조]を
	IF TALENT:ARG:마조
		PRINTW 削除しました
		TALENT:ARG:마조 = 0
	ELSE
		PRINTW 追加しました
		TALENT:ARG:새드 = 0
		TALENT:ARG:마조 = 1
	ENDIF
ELSEIF RESULT == 18
	PRINTW 素質[마조]を[ドＭ]にしました
	TALENT:ARG:마조 = 2
ELSEIF RESULT == 9
	PRINT 素質[실의]を
	IF TALENT:ARG:실의
		PRINTW 削除しました
		TALENT:ARG:실의 = 0
	ELSE
		PRINTW 追加しました
		TALENT:ARG:연모 = 0
		TALENT:ARG:음란 = 0
		TALENT:ARG:복종 = 0
		TALENT:ARG:친애 = 0
		TALENT:ARG:창부 = 0
		TALENT:ARG:예속 = 0
		TALENT:ARG:붕괴 = 0
		TALENT:ARG:실의 = 23
		CFLAG:ARG:41 = -1
		CFLAG:ARG:40 = 0
	ENDIF
ELSEIF RESULT == 100
	RETURN 0
ELSE
	GOTO INPUT_LOOP
ENDIF
RETURN 0
;--------------------------------------------------
;その他の素質着脱処理
;--------------------------------------------------
;その他の素質を付加/削除させます
@EDIT_TALENT(ARG)
#LOCALSIZE 2
VARSET LOCAL
DRAWLINE
PRINTFORML □ %CALLNAME:ARG%の現在の素質 □
FOR LOCAL, 0, 300
	SIF STRLENS(TALENTNAME:LOCAL) <= 0
		CONTINUE
	SIF TALENT:ARG:LOCAL
		PRINTFORM [%TALENTNAME:LOCAL%] 
NEXT
PRINTW 
DRAWLINE
FOR LOCAL, 0, 300
	SIF STRLENS(TALENTNAME:LOCAL) <= 0
		CONTINUE
	SIF (LOCAL >= 3 && LOCAL <= 9) || LOCAL == 83 || LOCAL == 84 || LOCAL == 162 || LOCAL == 165
		CONTINUE
	;改行or表示位置あけ
	IF LOCAL:1 % 4 == 3
		PRINTFORML [{LOCAL, 3}] %TALENTNAME:LOCAL%
	ELSE
		PRINTFORM [{LOCAL, 3}] %TALENTNAME:LOCAL, 14, LEFT%
	ENDIF
	LOCAL:1 = LOCAL:1 + 1
NEXT
PRINTL 
PRINTFORML %CALLNAME:ARG%に付加/削除する素質を選んでください(無効な値でキャンセル)
PRINTL [1000] キャンセル

INPUT
SIF RESULT >= 1000
	RETURN 0
SELECTCASE RESULT
	CASE 3 TO 9, 83, 84, 162
		RETURN 0
	CASE 118
		LOCAL = RESULT
		PRINTL どの値にするか入力してください(0-15/無効な値でキャンセル)
		PRINTL [0] なし
		PRINTL [1] 天然
		PRINTL [2] 제모
		PRINTL [100] キャンセル
		INPUT
		SIF RESULT < 0 || RESULT > 2
			RETURN 0
		TALENT:ARG:LOCAL = RESULT
		PRINTFORMW [%TALENTNAME:LOCAL%]を\@ TALENT:ARG:LOCAL ? 付加 # 削除\@しました
	CASE 159
		LOCAL = RESULT
		PRINTL どの値にするか入力してください(0-15/無効な値でキャンセル)
		PRINTL [0] なし
		PRINTL [1] 傷痕あり
		PRINTL [2] 刺青あり
		PRINTL [4] 焼き印あり
		PRINTL [8] ピアスあり
		PRINTL [15] 全て
		PRINTL [100] キャンセル
		INPUT
		SIF RESULT < 0 || RESULT > 15
			RETURN 0
		TALENT:ARG:LOCAL |= RESULT
		PRINTFORMW [%TALENTNAME:LOCAL%]を\@ TALENT:ARG:LOCAL ? 付加 # 削除\@しました
	CASEELSE
		IF STRLENS(TALENTNAME:RESULT) <= 0
			RETURN 0
		ELSEIF TALENT:ARG:RESULT
			TALENT:ARG:RESULT = 0
			PRINTFORMW [%TALENTNAME:RESULT%]を削除しました
		ELSE
			TALENT:ARG:RESULT = 1
			PRINTFORMW [%TALENTNAME:RESULT%]を付加しました
		ENDIF
	ENDSELECT
RETURN 0

;--------------------------------------------------
;好感度チェック
;--------------------------------------------------
@DEBUGCHECK_TARGET
#LOCALSIZE 7
VARSET LOCAL
;まずは好感度一位のキャラを調査しつつ、助손조교경험を算出してみる
FOR LOCAL:1, 0, 900
	SIF GETCHARA(LOCAL:1) == -1
		CONTINUE
	LOCAL = LOCAL:1 + 1099
	LOCAL:2 = CFLAG:LOCAL
	LOCAL:3 += CFLAG:LOCAL
	IF LOCAL:4 < LOCAL:2
		LOCAL:4 = LOCAL:2
		LOCAL:5 = LOCAL
	ENDIF
NEXT
LOCAL:3 -= CFLAG:(LOCAL:5)
SIF LOCAL:5
	LOCAL:5 -= 1099
IF CFLAG:2 < 0
	CFLAG:2 *= -1
	LOCAL:6 = 1
ENDIF
PRINTFORM %CALLNAME:TARGET%は
IF CFLAG:2 < 250 || (LOCAL:3 > 1000 && !TALENT:음란 && !TALENT:창부) || TALENT:붕괴 || !LOCAL:4
	PRINTL 誰にも関心がないようです
ELSE
	PRINTFORM %CALLNAME:GETCHARA(LOCAL:5)%
	IF TALENT:연모 || TALENT:친애
		PRINTL を恋い慕っています
	ELSEIF TALENT:음란 || TALENT:창부
		PRINTL がお気に入りのようです
	ELSEIF TALENT:복종 || TALENT:예속
		PRINTL に복종を誓っています
	ELSEIF CFLAG:2 < 500
		IF LOCAL:6
			PRINTL を怖がっているようです
		ELSE
			PRINTL が気になっているようです
		ENDIF
	ELSEIF CFLAG:2 < 750
		IF LOCAL:6
			PRINTL に圧倒されているようです
		ELSE
			PRINTL が好きなようです
		ENDIF
	ELSE
		IF LOCAL:6
			PRINTL に戦慄しているようです
		ELSE
			PRINTL を愛してしまったようです
		ENDIF
	ENDIF
ENDIF
PRINTW 
SIF LOCAL:6
	CFLAG:2 *= -1
RETURN 0

;--------------------------------------------------
;珠数変化処理
;--------------------------------------------------
;主人または現在調教中のキャラの珠数を好きな値に変化させます
;あまりにも大きすぎる値にするとバグることがあります
@EDIT_JUEL(ARG)
#LOCALSIZE 3
DRAWLINE
PRINTFORML □ %CALLNAME:ARG%の珠数 □
LOCAL:2 = 0
FOR LOCAL:1, 0 ,13
	IF LOCAL:1 == 4
		CONTINUE
	ELSEIF LOCAL:1 == 12
		LOCAL = 100
	ELSE
		LOCAL = LOCAL:1
	ENDIF
	PRINTFORM [{LOCAL, 3}] %PALAMNAME:LOCAL%の珠：{JUEL:ARG:LOCAL, 8}
	;改行or表示位置あけ
	IF LOCAL:2 % 3 == 2
		PRINTL 
	ELSE
		PRINT 　
	ENDIF
	LOCAL:2 = LOCAL:2 + 1
NEXT
PRINTL 
PRINTLC [98] 全て0にする
PRINTLC [99] 全て9999999にする
PRINTLC [1000] キャンセル
DRAWLINE
PRINTL まず、どの珠にするか入力してください(無効な値でキャンセル)
INPUT
IF RESULT < 0 || (RESULT > 14 && RESULT != 100 && RESULT != 98 && RESULT != 99)
	RETURN 0
ELSEIF RESULT == 98 || RESULT == 99
	FOR LOCAL:1, 0 ,13
		IF LOCAL:1 == 4
			CONTINUE
		ELSEIF LOCAL:1 == 12
			LOCAL = 100
		ELSE
			LOCAL = LOCAL:1
		ENDIF
		IF RESULT == 98
			JUEL:ARG:LOCAL = 0
		ELSE
			JUEL:ARG:LOCAL = 9999999
		ENDIF
		
	NEXT
	RETURN 0
ENDIF
LOCAL = RESULT
PRINTL 次に、どの値にするか入力してください(0-9999999/無効な値でキャンセル)
PRINTL [0] 0
PRINTL [9999999] 9999999
PRINTL [10000000] キャンセル
INPUT
SIF RESULT < 0 || RESULT > 9999999
	RETURN 0
JUEL:ARG:LOCAL = RESULT
RETURN 0

;--------------------------------------------------
;経験変化処理
;--------------------------------------------------
;主人または現在調教中のキャラの経験を好きな値に変化させます
;あまりにも大きすぎる値にするとバグることがあります
@EDIT_EXP(ARG)
#LOCALSIZE 2
DRAWLINE
PRINTFORML □ %CALLNAME:ARG%の経験 □
LOCAL:1 = 0
FOR LOCAL, 0, 71
	SIF !STRLENS(EXPNAME:LOCAL)
		CONTINUE
	PRINTFORM [{LOCAL, 2}] %EXPNAME:LOCAL, 12%:{EXP:ARG:LOCAL, 5}
	IF ++LOCAL:1 == 3
		PRINTL 
		LOCAL:1 = 0
	ELSE
		PRINT 　
	ENDIF
NEXT
PRINTL 
PRINTLC [98] 全て0にする
PRINTLC [99] 全て9999にする
PRINTL [100] キャンセル
DRAWLINE
PRINTL まず、どの経験にするか入力してください
INPUT
IF RESULT != 98 && RESULT != 99 && (RESULT < 0 || RESULT > 70 || !STRLENS(EXPNAME:RESULT))
	RETURN 0
ELSEIF RESULT == 98 || RESULT == 99
	FOR LOCAL, 0, 71
		SIF !STRLENS(EXPNAME:LOCAL)
			CONTINUE
		IF RESULT == 98
			EXP:ARG:LOCAL = 0
		ELSE
			EXP:ARG:LOCAL = 9999
		ENDIF
	NEXT
	RETURN 0
ENDIF
LOCAL = RESULT
PRINTL 次に、どの値にするか入力してください(0-9999/無効な値でキャンセル)
PRINTL [   0] 0
PRINTL [ 100] 100
PRINTL [9999] 9999
PRINTL [10000] 100増やす
PRINTL [10001] 100減らす
PRINTL [10002] キャンセル
INPUT
IF RESULT < 0 || RESULT > 10001
	RETURN 0
ELSEIF RESULT == 10000
	EXP:ARG:LOCAL += 100
	RETURN 0
ELSEIF RESULT == 10001
	EXP:ARG:LOCAL -= 100
	RETURN 0
ENDIF
SIF RESULT < 0 || RESULT > 10001
	RETURN 0
EXP:ARG:LOCAL = RESULT
RETURN 0

;--------------------------------------------------
;能力変化処理
;--------------------------------------------------
;主人または現在調教中のキャラの能力を好きな値に変化させます
;あまりにも大きすぎる値にするとバグることがあります
@EDIT_ABL(ARG)
#LOCALSIZE 2
DRAWLINE
PRINTFORML □ %CALLNAME:ARG%の能力 □
LOCAL:1 = 0
FOR LOCAL, 0, 23
	SIF !STRLENS(ABLNAME:LOCAL)
		CONTINUE
	PRINTFORM [{LOCAL, 2}] %ABLNAME:LOCAL, 12%:{ABL:ARG:LOCAL, 2}
	IF ++LOCAL:1 == 3
		PRINTL 
		LOCAL:1 = 0
	ELSE
		PRINT 　
	ENDIF
NEXT
PRINTL 
PRINTLC [98] 全て0にする
PRINTLC [99] 全て10にする
PRINTL [100] キャンセル
DRAWLINE
PRINTL まず、どの能力にするか入力してください(無効な値でキャンセル)
INPUT
IF RESULT != 98 && RESULT != 99 && (RESULT < 0 || RESULT > 23 || !STRLENS(ABLNAME:RESULT))
	RETURN 0
ELSEIF RESULT == 98 || RESULT == 99
	FOR LOCAL, 0, 23
		SIF !STRLENS(ABLNAME:LOCAL)
			CONTINUE
		IF RESULT == 98
			ABL:ARG:LOCAL = 0
		ELSE
			ABL:ARG:LOCAL = 10
		ENDIF
	NEXT
	RETURN 0
ENDIF
LOCAL = RESULT
PRINTL 次に、どの値にするか入力してください(0-10/無効な値でキャンセル)
PRINTL [ 0] 0
PRINTL [ 5] 5
PRINTL [10] 10
SIF ABL:ARG:LOCAL != 10
	PRINTL [98] 1増やす
SIF ABL:ARG:LOCAL != 0
	PRINTL [99] 1減らす
PRINTL [100] キャンセル
INPUT
IF RESULT != 98 && RESULT != 99 && (RESULT < 0 || RESULT > 10)
	RETURN 0
ELSEIF RESULT == 98
	SIF ABL:ARG:LOCAL != 10
		ABL:ARG:LOCAL += 1
	RETURN 0
ELSEIF RESULT == 99
	SIF ABL:ARG:LOCAL != 0
		ABL:ARG:LOCAL -= 1
	RETURN 0
ENDIF
ABL:ARG:LOCAL = RESULT
RETURN 0

;--------------------------------------------------
;조교경험・被조교경험変化処理
;--------------------------------------------------
;主人または現在調教中のキャラの能力を好きな値に変化させます
;あまりにも大きすぎる値にするとバグることがあります
@EDIT_HITYOUKYOU(ARG)
#LOCALSIZE 5
VARSET LOCAL
DRAWLINE
PRINTL 表示・変更する値を選んでください。
PRINTFORML [ 0] %CALLNAME:ARG%の조교경험
PRINTFORML [ 1] %CALLNAME:ARG%の被조교경험
PRINTFORML [ 2] %CALLNAME:ARG%の助손조교경험を表示する
$INPUT_LOOP1
INPUT
IF RESULT == 0
	PRINTFORML □ %CALLNAME:ARG%の조교경험 □
	FOR LOCAL:3, 0, CHARANUM
		LOCAL:2 = NO:ARG + 1099
		IF CFLAG:(LOCAL:3):(LOCAL:2)
			PRINTFORM [{LOCAL:3, 3}] %NATION_OR_H(NO:(LOCAL:3)), 10%：{CFLAG:(LOCAL:3):(LOCAL:2), 5}
			IF (LOCAL:4 % 3) == 2
				PRINTL 
			ELSE
				PRINT 　
			ENDIF
			LOCAL:4++
		ENDIF
	NEXT
	PRINTL 
	PRINTL [1000] キャンセル
	PRINTL 
	PRINTL 相손のキャラ登録番号を入力してください(無効な値でキャンセル)
	INPUT
	IF RESULT < 0 || RESULT >= CHARANUM || RESULT == ARG
		RETURN 0
	ELSE
		LOCAL = RESULT
		LOCAL:1 = LOCAL:2
	ENDIF
ELSEIF RESULT == 1
	PRINTFORML □ %CALLNAME:ARG%の被조교경험 □
	FOR LOCAL:3, 0, 900
		LOCAL:2 = LOCAL:3 + 1099
		IF CFLAG:ARG:(LOCAL:2)
			PRINTFORM [{LOCAL:3, 3}] %NATION_OR_H(LOCAL:3), 16%：{CFLAG:ARG:(LOCAL:2), 6}
			IF (LOCAL:4 % 3) == 2
				PRINTL 
			ELSE
				PRINT 　
			ENDIF
			LOCAL:4++
		ENDIF
	NEXT
	PRINTL 
	PRINTL [1000] キャンセル
	PRINTL 
	PRINTL 相손のキャラ固有番号を入力してください(無効な値でキャンセル)
	INPUT
	IF EXISTCSV(RESULT , 0) && (RESULT != NO:ARG)
		LOCAL = ARG
		LOCAL:1 = RESULT + 1099
	ELSE
		RETURN 0
	ENDIF
ELSEIF RESULT == 2
	;助손조교경험を算出
	FOR LOCAL:1, 0, 900
		LOCAL = LOCAL:1 + 1099
		LOCAL:2 = CFLAG:LOCAL
		LOCAL:3 += CFLAG:LOCAL
	NEXT
	LOCAL:3 -= CFLAG:(NO:MASTER + 1099)
	PRINTFORMW %CALLNAME:ARG%の助손조교경험は{LOCAL:3}です
	RETURN 0
ELSE
	GOTO INPUT_LOOP1
ENDIF
PRINTL 
PRINTL どの値にするか入力してください(0-999999/無効な値でキャンセル)
PRINTL [    0] 0
PRINTL [  100] 100
PRINTL [ 1000] 1000
PRINTL [10000] 10000
PRINTL [1000000] キャンセル
PRINTL 
$INPUT_LOOP
INPUT
SIF RESULT < 0 || RESULT > 999999
	RETURN 0
CFLAG:LOCAL:(LOCAL:1) = RESULT
RETURN 0



;--------------------------------------------------
;FLAG表示・変更処理
;--------------------------------------------------
;現在のFLAGを好きな値に変化させます
;うっかり弄るとエラー落ちすることがあります
@EDIT_FLAG
#LOCALSIZE 3
VARSET LOCAL
DRAWLINE
PRINTL （注意！）
PRINTL あくまでデバッグ用です。へたに弄るとエラーやプレイ不能を引き起こす可能性があるため、
PRINTL ここを操作した場合、セーブはしないでください。
PRINTFORML □ 現在のFLAG:0～5000 □
FOR LOCAL:1, 0, 5001
	IF FLAG:(LOCAL:1)
		PRINTFORM [{LOCAL:1, 4}] FLAG:{LOCAL:1, 4} = {FLAG:(LOCAL:1), 6}
		IF (LOCAL:2 % 3) == 2
			PRINTL 
		ELSE
			PRINT 　
		ENDIF
		LOCAL:2++
	ENDIF
NEXT
PRINTL 
PRINTL [10000] キャンセル
PRINTL 変更するFLAG番号を入力してください(0-5000/無効な値でキャンセル)

INPUT
SIF RESULT < 0 || RESULT > 5000
	RETURN 0
LOCAL = RESULT
PRINTL 
PRINTL どの値にするか入力してください(0-999999/無効な値でキャンセル)
PRINTL [0] 0
PRINTL [1] 1
PRINTL [1000000] キャンセル

INPUT
SIF RESULT < 0 || RESULT > 999999
	RETURN 0
FLAG:LOCAL = RESULT
PRINTFORMW FLAG:{LOCAL}を{FLAG:LOCAL}に変更しました
RETURN 0

;--------------------------------------------------
;CFLAG表示・変更処理
;--------------------------------------------------
;主人または現在調教中のキャラのCFLAGを好きな値に変化させます
;うっかり弄るとエラー落ちすることがあります
@EDIT_CFLAG(ARG)
#LOCALSIZE 3
VARSET LOCAL
DRAWLINE
PRINTL （注意！）
PRINTL あくまでデバッグ用です。へたに弄るとエラーやプレイ不能を引き起こす可能性があるため、
PRINTL ここを操作した場合、セーブはしないでください。
PRINTFORML □ %CALLNAME:ARG%のCFLAG:0～6000 □
FOR LOCAL:1, 0, 6001
	IF CFLAG:ARG:(LOCAL:1)
		PRINTFORM [{LOCAL:1, 4}] CFLAG:{LOCAL:1, 4} = {CFLAG:ARG:(LOCAL:1), 4}
		IF (LOCAL:2 % 3) == 2
			PRINTL 
		ELSE
			PRINT 　
		ENDIF
		LOCAL:2++
	ENDIF
NEXT
PRINTL 
PRINTL [10000] キャンセル
PRINTL 変更するCFLAG番号を入力してください(0-6000/無効な値でキャンセル)

INPUT
SIF RESULT < 0 || RESULT > 6000
	RETURN 0
LOCAL = RESULT
PRINTL 
PRINTL どの値にするか入力してください(－9999999999-9999999999/無効な値でキャンセル)
PRINTL [0] 0
PRINTL [1] 1
PRINTL [2] 2
PRINTL [3] 3
PRINTL [10000000000] キャンセル

INPUT
SIF RESULT < -9999999999 || RESULT > 9999999999
	RETURN 0
CFLAG:ARG:LOCAL = RESULT
PRINTFORMW %CALLNAME:ARG%のCFLAG:{LOCAL}を{CFLAG:ARG:LOCAL}に変更しました
RETURN 0


;--------------------------------------------------
;CSTR表示・変更処理
;--------------------------------------------------
;主人または現在調教中のキャラのCSTRを表示・変更します
@EDIT_CSTR(ARG)
#LOCALSIZE 3
VARSET LOCAL
DRAWLINE
PRINTFORML □ %CALLNAME:ARG%のCSTR:0～60 □
FOR LOCAL:1, 0, 61
	IF CSTR:ARG:(LOCAL:1) != ""
		PRINTFORM [{LOCAL:1, 2}] CSTR:{LOCAL:1, 2} = %CSTR:ARG:(LOCAL:1)%
		IF (LOCAL:2 % 3) == 2
			PRINTL 
		ELSE
			PRINT 　
		ENDIF
		LOCAL:2++
	ENDIF
NEXT
PRINTL 
PRINTL [100] キャンセル
PRINTL 変更するCSTR番号を入力してください(0-60/無効な値でキャンセル)

INPUT
SIF RESULT < 0 || RESULT > 60
	RETURN 0
LOCAL = RESULT
PRINTL 
PRINTL 文字列を入力してください
PRINTBUTTON "[] 空白を代入する", ""
PRINTL 
PRINTBUTTON "[キャンセル] ", "キャンセル"

INPUTS
SIF RESULTS == "キャンセル"
	RETURN 0
CSTR:ARG:LOCAL = %RESULTS%
PRINTFORMW %CALLNAME:ARG%のCSTR:{LOCAL}を%CSTR:ARG:LOCAL%に変更しました
RETURN 0

;--------------------------------------------------
;所持金変更処理
;--------------------------------------------------
;現在の所持金を好きな値に変化させます
;あまりにも大きすぎる値にするとバグることがあります
@EDIT_MONEY(ARG)
DRAWLINE
PRINTFORML 現在の所持金${MONEY}
PRINTL どの値にするか入力してください(0-99999999/無効な値でキャンセル)
PRINTL [ 0] 0
PRINTL [1000] 1000
PRINTL [10000] 10000
PRINTL [100000] 10万
PRINTL [1000000] 100万
PRINTL [10000000] 1000万
PRINTL [100000000] キャンセル

INPUT
SIF RESULT < 0 || RESULT > 99999999
	RETURN 0
MONEY:ARG = RESULT
PRINTFORMW 所持金を${MONEY}に変更しました
RETURN 0

;--------------------------------------------------
;체력変化処理
;--------------------------------------------------
;主人または現在調教中のキャラの체력を好きな値に変化させます
;あまりにも大きすぎる値にするとバグることがあります
@EDIT_BASE(ARG)
DRAWLINE
PRINTFORML %CALLNAME:ARG%の現在の체력:{BASE:ARG:체력, 4}/{MAXBASE:ARG:체력}
PRINTFORML どの値にするか入力してください(1-{MAXBASE:ARG:체력}/無効な値でキャンセル)
PRINTL [   1]   1
PRINTL [ 300] 300
PRINTL [ 500] 500
PRINTL [ 800] 800
PRINTL [1000]1000
PRINTFORML [{MAXBASE:ARG:체력}]{MAXBASE:ARG:체력}
PRINTL [100000] キャンセル
PRINTL [100001] 最大체력を変更する

INPUT
IF RESULT == 100001
	PRINTFORML どの値にするか入力してください(1-99999/無効な値でキャンセル)
	PRINTL [1000]1000
	PRINTL [1500]1500
	PRINTL [2000]2000
	PRINTL [2500]2500
	PRINTL [3000]3000
	PRINTL [100000] 500増やす
	PRINTL [100001] 1000増やす
	PRINTL [100002] キャンセル
	INPUT
	IF RESULT < 501 || RESULT > 100001
		RETURN 0
	ELSEIF RESULT == 100000
		MAXBASE:ARG:체력 += 500
	ELSEIF RESULT == 100001
		MAXBASE:ARG:체력 += 1000
	ELSEIF RESULT < 501 || RESULT > 100001
		MAXBASE:ARG:체력 = RESULT
	ENDIF
	PRINTFORMW %CALLNAME:ARG%の最大체력を{MAXBASE:ARG:체력}に変更しました
	RETURN 0
ELSEIF RESULT < 501 || RESULT > MAXBASE:ARG:체력
	RETURN 0
ENDIF
BASE:ARG:체력 = RESULT
PRINTFORMW %CALLNAME:ARG%の現在の체력を{BASE:ARG:체력}に変更しました
RETURN 0

;--------------------------------------------------
;刻印変化処理
;--------------------------------------------------
;主人または現在調教中のキャラの刻印を好きな値に変化させます
@EDIT_MARK(ARG)
#LOCALSIZE 2
DRAWLINE
PRINTFORML □ %CALLNAME:ARG%の刻印 □
LOCAL:1 = 0
FOR LOCAL, 0, 5
	PRINTFORM [{LOCAL}] %MARKNAME:LOCAL, 12%:{MARK:ARG:LOCAL}
	IF ++LOCAL:1 == 3
		PRINTL 
		LOCAL:1 = 0
	ELSE
		PRINT 　
	ENDIF
NEXT
PRINTL 
PRINTLC [98] 全て0にする
PRINTLC [99] 全て3にする
PRINTL [100] キャンセル
DRAWLINE
PRINTL まず、どの刻印にするか入力してください(無効な値でキャンセル)
INPUT
IF RESULT < 0 || (RESULT > 4 && RESULT != 98 && RESULT != 99)
	RETURN 0
ELSEIF RESULT == 98 || RESULT == 99
	FOR LOCAL, 0, 5
		IF RESULT == 98
			MARK:ARG:LOCAL = 0
		ELSE
			MARK:ARG:LOCAL = 3
		ENDIF
	NEXT
	RETURN 0
ENDIF
LOCAL = RESULT
PRINTL 次に、どの値にするか入力してください(0-10/無効な値でキャンセル)
PRINTL [0] 0
PRINTL [1] 1
PRINTL [2] 2
PRINTL [3] 3
PRINTL [100] キャンセル
INPUT
SIF RESULT < 0 || RESULT > 3
	RETURN 0
MARK:ARG:LOCAL = RESULT
RETURN 0

;--------------------------------------------------
;日数経過処理
;--------------------------------------------------
;任意の日数후にジャンプします
;間にイベントが挟まると不具合が起こるので慎重に操作してください
;あまりにも大きすぎる値にするとバグることがあります
@EDIT_DAY
DRAWLINE
PRINTL 任意の日数후にジャンプします。ジャンプした間に起こるイベントはスキップされるので、
PRINTL イベントを起こしたい場合は大人しくマクロを使ったほうがいいです。
PRINTL なお、カレンダー的な意味での日付を操作したい場合はFLAGを操作する必要があります。
PRINTL 
PRINTL （注意！）
PRINTL あくまでデバッグ用です。へたに弄るとエラーやプレイ不能を引き起こす可能性があるため、
PRINTL ここを操作した場合、セーブはしないでください。
PRINTL 
PRINTFORML 現在の日数:{DAY}日目
PRINTL 何日経過させるか入力してください(－{DAY}-{9999 - DAY}/無効な値でキャンセル)
PRINTL [1] 1日후
PRINTL [2] 2日후（３日レンタル待ちなど）
PRINTL [4] 4日후（５日レンタル待ちなど）
PRINTL [9] 9日후（１０日レンタル待ちなど）
PRINTL [11] 11日후（임신確定待ちなど）
PRINTL [19] 19日후（育児待ちなど）
PRINTL [50] 50日후（臨月待ちなど）
PRINTL [10000] キャンセル

INPUT
SIF DAY + RESULT < 0 || DAY + RESULT > 9999
	RETURN 0
DAY += RESULT
PRINTFORMW 現在の日数を{DAY}日目に変更しました
RETURN 0
