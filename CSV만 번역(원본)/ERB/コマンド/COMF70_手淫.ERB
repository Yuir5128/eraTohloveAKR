;-------------------------------------------------
;수음
;奉仕系コマンド
;-------------------------------------------------
@COM70

;-------------------------------------------------
;特殊派生処理
;-------------------------------------------------
;前回が모유마시기で奴隷が모유체질の場合、授乳손コキに飛ぶ
IF PREVCOM == 18 && TALENT:모유체질
	CALL COM_ABLE528
	SIF RESULT == 1
		JUMP COM528
ENDIF

PRINTL 수음
STR:0 = 수음

;-------------------------------------------------
;実行判定処理
;-------------------------------------------------
;すべての命令に共通の要素を考慮
;(순종が高いと命令に従いやすいなど)
CALL COM_ORDER

;実行判定操作
CALL KOJO_DENIAL_PLUS

;合計を表示(14以上で実行)
PRINT  = 
PRINTV A

V = 14
SIF A < V
	PRINT  < 
SIF A == V
	PRINT  = 
SIF A > V
	PRINT  > 
PRINT 実行値
PRINTV V

;実行不可입上/実行判定操作
SIF A < V
	CALL KOJO_DENIAL

WAIT

;実行できない
SIF A < V
	RETURN 0

CALL KOJO_MESSAGE_COM


;実行者が奴隷であるフラグ
TFLAG:28 = 1



;-------------------------------------------------
;사정ゲージチェック
;-------------------------------------------------
B = 600

CALL SHOOT_GAUGE_CHECK_MT

;再装填処理中は사정ゲージは20分の1に
SIF CFLAG:PLAYER:9 > 0
	B /= 20

SIF PENIS(PLAYER)
	BASE:PLAYER:사정 += B


;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費체력、기력
LOSEBASE:체력 += 5
LOSEBASE:기력 += 30

SOURCE:애정 += 50
;스킨십선호だと애정のソース追加
SIF TALENT:스킨십선호
	SOURCE:애정 += 50

SOURCE:성행동 += 250
SOURCE:성취감 += 50
SOURCE:굴복 += 350
SOURCE:일탈 += 100
SOURCE:반발 += 100

;上のほうで計算した汚れデータ
SOURCE:불결 += Y*10 + 60


;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;억압、저항持ちには、억울ソース追加
SIF TALENT:억압 || TALENT:저항
	SOURCE:울굴 += 200


;-------------------------------------------------
;사정チェック
;-------------------------------------------------
CALL SAMEN_CHECK(PLAYER,1)

;사정時の処理
IF E >= 1
	;사정先タイプの設定
	TFLAG:703 = 3
	;사정先選択の可・不可
	TFLAG:704 = 1
	;汎用的な処理はこの関数で行う
	;（사정ゲージの再計算や사정경험の上昇と共通部分の表示）
	CALL SAMEN_SHOOT
ENDIF

;-------------------------------------------------
;汚れの処理
;-------------------------------------------------
CALL COM_STAIN


;-------------------------------------------------
;経験の処理
;-------------------------------------------------
;게이경험とか加算
CALL COM_EXP(4)

;依存度ベクトル
TFLAG:33 = 1

;依存度補正値(主人かつ연모、음란、복종で1。친애、창부、예속で2）
IF (TALENT:연모 || TALENT:음란 || TALENT:복종) && ASSIPLAY == 0
	TFLAG:30 += 1
ELSEIF (TALENT:친애 || TALENT:창부 || TALENT:예속) && ASSIPLAY == 0
	TFLAG:30 += 2
ENDIF

;奉仕快楽フラグ
TFLAG:100 = 1


RETURN 1


