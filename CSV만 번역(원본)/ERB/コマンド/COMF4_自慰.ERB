;-------------------------------------------------
;자위
;자위系コマンド
;-------------------------------------------------
@COM4

;바이브、비디오촬영の時に内容変化
;비디오촬영中なら공개자위
;야외플레이中なら野外オナニー
;샤워使用中なら샤워オナニー
;바이브装備で바이브オナニー
;オナホ装備でオナホオナニー
LOCALS = 

SIF TEQUIP:비디오카메라
	LOCALS = 公開
SIF TEQUIP:야외플레이
	LOCALS = 野外

IF TEQUIP:바이브
	LOCALS = %LOCALS%바이브
	TFLAG:56 = 1
ELSEIF TEQUIP:로터삽입
	LOCALS = %LOCALS%로터
	TFLAG:56 = 1
ELSEIF TEQUIP:전동오나홀
	LOCALS = %LOCALS%オナホ
	TFLAG:56 = 0
ELSEIF TEQUIP:샤워
	LOCALS =  %LOCALS%샤워
	TFLAG:56 = 0
ELSE
	;페니스があって、元が남자なら페니스を優先して使う
	IF PENIS(TARGET) && MALE2(TARGET)
		TFLAG:56 = 0
	;바기나が使える場合は指を入れる
	ELSEIF (!TALENT:처녀 && VAGINA(TARGET)) || EXP:V경험 >= 20
		TFLAG:56 = 1
	ELSE
		TFLAG:56 = 0
	ENDIF
ENDIF

IF TEQUIP:비디오카메라 || TEQUIP:야외플레이 || TEQUIP:바이브 || TEQUIP:로터삽입 || TEQUIP:전동오나홀 || TEQUIP:샤워
	LOCALS = %LOCALS%オナニー
ELSE
	LOCALS = %LOCALS%자위
ENDIF

STR:0 = %LOCALS%
PRINTFORML %STR:0%

;-------------------------------------------------
;実行できるかの判定
;-------------------------------------------------
;すべての命令に共通の要素を考慮
;(순종が高いと命令に従いやすいなど)
CALL COM_ORDER

;実行判定操作
CALL KOJO_DENIAL_PLUS

;合計を表示(33以上で実行)
PRINT  = 
PRINTV A

;難易度上昇
;公開で+10、바이브で+5、샤워で＋3
V = 33
SIF TEQUIP:비디오카메라
	V += 10
SIF TEQUIP:야외플레이
	V += 10

SIF TEQUIP:바이브
	V += 5
SIF TEQUIP:로터삽입
	V += 3
SIF TEQUIP:전동오나홀
	V += 3
SIF TEQUIP:샤워
	V += 3

SIF A < V
	PRINT  < 
SIF A == V
	PRINT  = 
SIF A > V
	PRINT  > 
PRINT 実行値
PRINTV V

;実行不可입上/実行判定操作
SIF A < V
	CALL KOJO_DENIAL

WAIT

;実行できない
SIF A < V
	RETURN 0

CALL KOJO_MESSAGE_COM


;実行者が奴隷であるフラグ
TFLAG:28 = 1

;스타킹びりびりフラグ
IF TEQUIP:특수2 == 5 || TEQUIP:특수2 == 6
	TFLAG:55 = 1
ENDIF


;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費체력、기력
LOSEBASE:체력 += 10
LOSEBASE:기력 += 50

;基本の快感
SOURCE:쾌C += 80
SIF TFLAG:56 == 1
	SOURCE:쾌V += 80

IF TEQUIP:바이브 || TEQUIP:로터삽입
	TIMES SOURCE:쾌C , 0.70

	SIF TEQUIP:바이브
		TIMES SOURCE:쾌V , 1.50
	SIF TEQUIP:로터삽입
		TIMES SOURCE:쾌V , 1.20

	SIF TEQUIP:바이브
		SOURCE:통증 += 50
	SIF TEQUIP:로터삽입
		SOURCE:통증 += 20

	;Vへの고통は先に処理しておく
	;EXP:V경험をみる（처녀の반감は別途処理済みなので省略）
	IF EXP:V경험 < EXPLV:1
		TIMES SOURCE:통증 , 2.50
	ELSEIF EXP:V경험 < EXPLV:2
		TIMES SOURCE:통증 , 0.80
		TIMES SOURCE:반발 , 1.20
	ELSEIF EXP:V경험 < EXPLV:3
		TIMES SOURCE:통증 , 0.40
	ELSEIF EXP:V경험 < EXPLV:4
		TIMES SOURCE:통증 , 0.10
	ELSEIF EXP:V경험 < EXPLV:5
		TIMES SOURCE:통증 , 0.00
	ELSEIF EXP:V경험 >= EXPLV:5
		TIMES SOURCE:통증 , 0.00
	ENDIF
	
	;PALAM:윤활をみる
	IF PALAM:윤활 < PALAMLV:1
		TIMES SOURCE:통증 , 1.20
		TIMES SOURCE:반발 , 1.50
	ELSEIF PALAM:윤활 < PALAMLV:2
		TIMES SOURCE:통증 , 0.70
		TIMES SOURCE:반발 , 1.20
	ELSEIF PALAM:윤활 < PALAMLV:3
		TIMES SOURCE:통증 , 0.40
	ELSEIF PALAM:윤활 < PALAMLV:4
		TIMES SOURCE:통증 , 0.10
	ELSEIF PALAM:윤활 >= PALAMLV:4
		TIMES SOURCE:통증 , 0.10
	ENDIF

	SOURCE:굴복 += 100
	SOURCE:반발 += 100
ELSEIF TEQUIP:전동오나홀
	TIMES SOURCE:쾌C , 1.50
ELSEIF TEQUIP:샤워
	TIMES SOURCE:쾌C , 1.20
ENDIF

SOURCE:성행동 += 100
SOURCE:두려움 += 200
SOURCE:중독충족 += 200
SOURCE:노출 += 400
SOURCE:굴복 += 500
SOURCE:일탈 += 300
SOURCE:반발 += 250

;ビデオor野外
IF TEQUIP:비디오카메라 || TEQUIP:야외플레이
	SOURCE:액체 += 50
	SOURCE:욕정 += 100
	SOURCE:반발 += 200
ENDIF

;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;中毒による중독충족のソースは先に処理しておく
;자위중독を見る
IF ABL:자위중독 == 1
	TIMES SOURCE:쾌C , 1.00
	TIMES SOURCE:쾌V , 1.00
	TIMES SOURCE:중독충족 , 0.00
ELSEIF ABL:자위중독 == 2
	TIMES SOURCE:쾌C , 1.10
	TIMES SOURCE:쾌V , 1.10
	TIMES SOURCE:중독충족 , 0.30
ELSEIF ABL:자위중독 == 3
	TIMES SOURCE:쾌C , 1.20
	TIMES SOURCE:쾌V , 1.20
	TIMES SOURCE:중독충족 , 0.60
ELSEIF ABL:자위중독 == 4
	TIMES SOURCE:쾌C , 1.30
	TIMES SOURCE:쾌V , 1.30
	TIMES SOURCE:중독충족 , 0.90
ELSEIF ABL:자위중독 == 5
	TIMES SOURCE:쾌C , 1.50
	TIMES SOURCE:쾌V , 1.50
	TIMES SOURCE:중독충족 , 1.20
ELSEIF ABL:자위중독 == 6
	TIMES SOURCE:쾌C , 1.70
	TIMES SOURCE:쾌V , 1.70
	TIMES SOURCE:중독충족 , 1.50
ELSEIF ABL:자위중독 == 7
	TIMES SOURCE:쾌C , 2.00
	TIMES SOURCE:쾌V , 2.00
	TIMES SOURCE:중독충족 , 1.80
ELSEIF ABL:자위중독 == 8
	TIMES SOURCE:쾌C , 2.30
	TIMES SOURCE:쾌V , 2.30
	TIMES SOURCE:중독충족 , 2.30
ELSEIF ABL:자위중독 == 9
	TIMES SOURCE:쾌C , 2.50
	TIMES SOURCE:쾌V , 2.50
	TIMES SOURCE:중독충족 , 3.00
ELSEIF ABL:자위중독 >= 10
	TIMES SOURCE:쾌C , 3.50
	TIMES SOURCE:쾌V , 3.50
	TIMES SOURCE:중독충족 , 5.00
ENDIF

;억압、저항持ちには、억울ソース追加
SIF TALENT:억압 || TALENT:저항
	SOURCE:울굴 += 400

;-------------------------------------------------
;汚れの処理
;-------------------------------------------------
;奴隷の손（장갑）⇔奴隷のＣorＶor질내
;바이브、로터、オナホールを使っているときは汚れはどこにも移動しない
IF TEQUIP:바이브 || TEQUIP:로터삽입 || TEQUIP:전동오나홀
;샤워オナニーの場合汚れをリセット、윤활更に半分。
ELSEIF TEQUIP:샤워
	STAIN:손 = 0
	STAIN:페니스 = 2
	STAIN:바기나 = 1
	STAIN:질내 = 1
	STAIN:애널 = 8
	STAIN:발 = 0
	STAIN:장갑 = 256
	STAIN:전신상의 = 256
	STAIN:하의 = 256
	STAIN:속옷（아래） = 256
	STAIN:발장식 = 256
	STAIN:신발 = 256
	PALAM:윤활 /= 2
ELSE
	CALL COM_STAIN
ENDIF

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
IF TFLAG:56 == 1
	EXP:V경험 += 1
	PRINTL V경험＋１
ENDIF

;비디오촬영時は経験にプラス
IF TEQUIP:비디오카메라
	EXP:자위경험 += 2
	PRINTL 자위경험＋２
	EXP:조교자위경험 += 2
	PRINTL 조교자위경험＋２
	
	IF !EXP:공개자위 && !TALENT:음란 && !TALENT:창부
		EXP:이상경험 += 1
		PRINTL 공개자위初経験で이상경험＋１
	ENDIF
	EXP:공개자위 += 1
	PRINTL 공개자위経験＋１
ELSE
	EXP:자위경험 += 1
	PRINTL 자위경험＋１
	EXP:조교자위경험 += 1
	PRINTL 조교자위경험＋１
ENDIF

;撮影時の調教内容チェック
IF TEQUIP:바이브 || TEQUIP:로터삽입
	TFLAG:90 = 991
ELSEIF TEQUIP:샤워 && !PENIS(TARGET)
	TFLAG:90 = 994
ELSE
	TFLAG:90 = 0
ENDIF

;게이경험とか加算
CALL COM_EXP(3)

;依存度ベクトル
TFLAG:33 = 3

;依存度補正値(복종で1、예속で2）
IF TALENT:복종 && ASSIPLAY == 0
	TFLAG:30 += 1
ELSEIF TALENT:예속 && ASSIPLAY == 0
	TFLAG:30 += 2
ENDIF


TFLAG:56 = 0

RETURN 1


