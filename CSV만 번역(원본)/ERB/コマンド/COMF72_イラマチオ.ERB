;-------------------------------------------------
;이라마치오
;強制奉仕系コマンド
;-------------------------------------------------
@COM72

;-------------------------------------------------
;特殊派生処理
;-------------------------------------------------
;前回と今回の調教者が同じ
IF (ASSIPLAY && TFLAG:50) || (ASSIPLAY == 0 && TFLAG:50 == 0)
	;助손挿入中、前回コマンドが助손挿入のとき＝「サンドイッチ」
	IF PREVCOM == 405 || PREVCOM == 406
		CALL COM_ABLE515
		IF RESULT == 1
			TFLAG:40 = 0
			IF PREVCOM == 405
				TFLAG:41 = 1
			ELSE
				TFLAG:41 = 2
			ENDIF
			TFLAG:49 = 10
			JUMP COM515
		ENDIF
	;前回コマンドが촉수에범하게시킨다のとき、「サンドイッチさせる」
	ELSEIF PREVCOM == 270 || PREVCOM == 271
		CALL COM_ABLE520
		IF RESULT == 1
			TFLAG:40 = 0
			IF PREVCOM == 270
				TFLAG:41 = 6
			ELSE
				TFLAG:41 = 7
			ENDIF
			TFLAG:49 = 8
			JUMP COM520
		ENDIF
	;前回コマンドがサンドイッチのとき
	ELSEIF PREVCOM == 515
		;助손が奴隷に挿入してれば再び「サンドイッチ」に
		IF TFLAG:41 == 1 || TFLAG:41 == 2
			CALL COM_ABLE515
			IF RESULT == 1
				TFLAG:40 = 0
				TFLAG:49 = 10
				JUMP COM515
			ENDIF
		;助손が主人に挿入してると「サンドイッチさせる」に
		ELSEIF TFLAG:41 == 6 || TFLAG:41 == 7
			CALL COM_ABLE520
			IF RESULT == 1
				TFLAG:40 = 0
				TFLAG:49 = 10
				JUMP COM520
			ENDIF
		ENDIF
	ENDIF
ENDIF

;何も読まずにこっちに来たときはフラグ解除
TFLAG:40 = 0
TFLAG:41 = 0
TFLAG:49 = 0
TFLAG:42 = 0

PRINTL 이라마치오
STR:0 = 이라마치오
CALL KOJO_MESSAGE_COM

;実行者が奴隷であるフラグ
TFLAG:28 = 1


;-------------------------------------------------
;사정ゲージチェック
;-------------------------------------------------
B = 1300

CALL SHOOT_GAUGE_CHECK_MT

;再装填処理中は사정ゲージは20分の1に
SIF CFLAG:PLAYER:9 > 0
	B /= 20

SIF PENIS(PLAYER)
	BASE:PLAYER:사정 += B

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費체력、기력
LOSEBASE:체력 += 50
LOSEBASE:기력 += 100

SOURCE:성행동 += 500
SOURCE:통증 += 200
SOURCE:굴복 += 1500
SOURCE:일탈 += 700
SOURCE:반발 += 700

;上のほうで計算した汚れデータ
SOURCE:불결 += Y*40 + 100


;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;억압、저항持ちには、억울ソース追加
SIF TALENT:억압 || TALENT:저항
	SOURCE:울굴 += 700


;-------------------------------------------------
;사정チェック
;-------------------------------------------------
CALL SAMEN_CHECK(PLAYER,1)

;사정時の処理
IF E >= 1
	;사정先タイプの設定
	TFLAG:703 = 4
	;사정先選択の可・不可
	TFLAG:704 = 1
	;汎用的な処理はこの関数で行う
	;（사정ゲージの再計算や사정경험の上昇と共通部分の表示）
	CALL SAMEN_SHOOT
ENDIF

SIF E >= 1
	TFLAG:8 = 1

;-------------------------------------------------
;汚れの処理
;-------------------------------------------------
CALL COM_STAIN

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
;펠라경험
PRINTL 펠라경험＋１
EXP:펠라경험 += 1

;게이경험とか加算
CALL COM_EXP(7)

;애정경험(마조끼5以上限定）
IF (TALENT:연모 == 1 || TALENT:친애 == 1) && ASSIPLAY == 0 && ABL:마조끼 >= 5
	EXP:애정경험 += 1
	PRINTL 애정경험+１
ENDIF

;依存度ベクトル
TFLAG:33 = 3

;依存度補正値(主人かつ연모、음란、복종で1。친애、창부、예속で2）
IF (TALENT:연모 || TALENT:음란 || TALENT:복종) && ASSIPLAY == 0
	TFLAG:30 += 1
ELSEIF (TALENT:친애 || TALENT:창부 || TALENT:예속) && ASSIPLAY == 0
	TFLAG:30 += 2
ENDIF

;奉仕快楽フラグ
TFLAG:100 = 1


RETURN 1


