;-------------------------------------------------
;조수의A를범한다
;主人×助손系コマンド
;-------------------------------------------------
@COM441

;-------------------------------------------------
;特殊派生処理
;-------------------------------------------------
;前回と今回の調教者が同じ
IF (ASSIPLAY && TFLAG:50) || (ASSIPLAY == 0 && TFLAG:50 == 0)
	;両方Ａ挿入してると「助손を二輪挿し애널」
	IF PREVCOM == 431
		CALL COM_ABLE524
		IF RESULT == 1
			TFLAG:40 = 12
			TFLAG:41 = 0
			TFLAG:49 = 12
			JUMP COM524
		ENDIF
	;奴隷が助손を犯しているとき「助손をサンドイッチ（二穴挿入）」
	ELSEIF PREVCOM == 430
		CALL COM_ABLE521
		IF RESULT == 1
			TFLAG:40 = 11
			TFLAG:41 = 0
			TFLAG:49 = 12
			JUMP COM521
		ENDIF
	;입辱中、구욕명령は「助손をサンドイッチ」
	ELSEIF PREVCOM == 407
		CALL COM_ABLE521
		IF RESULT == 1
			TFLAG:40 = 12
			TFLAG:41 = 0
			TFLAG:49 = 13
			JUMP COM521
		ENDIF
	;助손が奴隷を犯す場合「連結（奴隷←助손←主人）」
	ELSEIF PREVCOM == 405 || PREVCOM == 406
		CALL COM_ABLE519
		IF RESULT == 1
			IF  PREVCOM == 405
				TFLAG:41 = 1
			ELSE
				TFLAG:41 = 2
			ENDIF
			TFLAG:40 = 12
			TFLAG:49 = 0
			JUMP COM519
		ENDIF
	;奴隷が主人を犯す場合「連結（助손←主人←奴隷）」
	ELSEIF PREVCOM == 261 || PREVCOM == 264
		CALL COM_ABLE519
		IF RESULT == 1
			IF PREVCOM == 261
				TFLAG:49 = 6
			ELSE
				TFLAG:49 = 7
			ENDIF
			TFLAG:40 = 12
			TFLAG:41 = 0
			JUMP COM519
		ENDIF
	;前回コマンドが「助손を二輪挿し애널」「助손をサンドイッチ」「連結」
	ELSEIF PREVCOM == 525 || PREVCOM == 521 || PREVCOM == 519
		;両方挿入してると「助손を二輪挿し」
		IF TFLAG:49 == 12
			CALL COM_ABLE525
			IF RESULT == 1
				TFLAG:40 = 12
				TFLAG:41 = 0
				JUMP COM525
			ENDIF
		;奴隷が助손を犯しているとき「助손をサンドイッチ（二穴挿入）」
		ELSEIF TFLAG:49 == 11
			CALL COM_ABLE521
			IF RESULT == 1
				TFLAG:40 = 12
				TFLAG:41 = 0
				JUMP COM521
			ENDIF
		;입辱中、구욕명령は「助손をサンドイッチ」
		ELSEIF TFLAG:49 == 13
			CALL COM_ABLE521
			IF RESULT == 1
				TFLAG:40 = 12
				TFLAG:41 = 0
				JUMP COM521
			ENDIF
		;奴隷が主人を犯す場合「連結（助손←主人←奴隷）」
		ELSEIF TFLAG:49 == 6 || TFLAG:49 == 7
			CALL COM_ABLE519
			IF RESULT == 1
				TFLAG:40 = 12
				TFLAG:41 = 0
				JUMP COM519
			ENDIF
		;助손が奴隷を犯す場合「連結（奴隷←助손←主人）」
		ELSEIF TFLAG:41 == 1 || TFLAG:41 == 2
			CALL COM_ABLE519
			IF RESULT == 1
				TFLAG:40 = 12
				TFLAG:49 = 0
				JUMP COM519
			ENDIF
		ENDIF
	ENDIF
ENDIF

;何も読まずにこっちに来たときはフラグ解除
TFLAG:40 = 0
TFLAG:41 = 0
TFLAG:49 = 0
TFLAG:42 = 0

PRINTL 조수의A를범한다
STR:0 = 조수의A를범한다

CALL KOJO_MESSAGE_COM

;처녀喪失フラグ
SIF TALENT:ASSI:백버진
	TFLAG:19 |= 3

;調教者が동정だった場合の喪失フラグ
SIF TALENT:PLAYER:동정
	TFLAG:160 |= 2

;挿入個所判定フラグ
TFLAG:705 = 1


;-------------------------------------------------
;사정ゲージチェック(主人)
;-------------------------------------------------
B = 1350

CALL SHOOT_GAUGE_CHECK_MA1

;再装填処理中は사정ゲージは20分の1に
SIF CFLAG:MASTER:9 > 0
	B /= 20

SIF PENIS(PLAYER)
	BASE:MASTER:사정 += B

;-------------------------------------------------
;사정ゲージチェック（助손）
;-------------------------------------------------
B = 1000

CALL SHOOT_GAUGE_CHECK_MA2

;再装填処理中は사정ゲージは20分の1に
SIF CFLAG:ASSI:9 > 0
	B /= 20

SIF PENIS(ASSI)
	BASE:ASSI:사정 += B

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費체력、기력
LOSEBASE:체력 += 0
LOSEBASE:기력 += 100

SOURCE:액체 += 100
SOURCE:굴복 += 1500
SOURCE:일탈 += 700
SOURCE:반발 += 700

;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;억압、저항持ちには、억울ソース追加
SIF TALENT:억압 || TALENT:저항
	SOURCE:울굴 += 700

;助손が처녀喪失をした場合、굴복と일탈を増加
IF TALENT:ASSI:백버진 == 1
	TIMES SOURCE:일탈 , 20.00
	TIMES SOURCE:굴복 , 3.00
ENDIF

;助손と奴隷の相性によって、全体のソースが変化
T = NO:TARGET
A = NO:ASSI
SOURCE:액체 *= RELATION:A
SOURCE:굴복 *= RELATION:A
SOURCE:울굴 *= RELATION:A
SOURCE:일탈 *= RELATION:A
SOURCE:반발 *= RELATION:A
SOURCE:액체 /= 100
SOURCE:굴복 /= 100
SOURCE:울굴 /= 100
SOURCE:일탈 /= 100
SOURCE:반발 /= 100


;-------------------------------------------------
;사정チェック（主人）
;-------------------------------------------------
CALL SAMEN_CHECK(MASTER,1)
IF E >= 1
	;사정先タイプの設定
	TFLAG:703 = 2
	;사정先選択の可・不可
	TFLAG:704 = 0
	;汎用的な処理はこの関数で行う
	;（사정ゲージの再計算や사정경험の上昇と共通部分の表示）
	CALL SAMEN_SHOOT
ENDIF
;조수를범한다で사정したフラグ
SIF E == 2
	TFLAG:7 = 2
SIF E == 1
	TFLAG:7 = 1

;-------------------------------------------------
;사정チェック（助손）
;-------------------------------------------------
CALL SAMEN_CHECK(ASSI)

;사정時の処理
IF E >= 1
	;사정先タイプの設定
	TFLAG:703 = 6
	;사정先選択の可・不可
	TFLAG:704 = 0
	;汎用的な処理はこの関数で行う
	;（사정ゲージの再計算や사정경험の上昇と共通部分の表示）
	CALL SAMEN_SHOOT
ENDIF


;-------------------------------------------------
;汚れの処理
;-------------------------------------------------
;奴隷のＰ⇔助손のＡの汚れが移動
;페니스밴드を使う場合、汚れは移動しない
IF PENIS(TARGET)
	CALL COM_STAIN
ENDIF

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
;助손が백버진かつ애널挿入だった場合の喪失
IF TALENT:ASSI:백버진 && TFLAG:705 == 1
	PRINTFORMW %CALLNAME:ASSI%が백버진喪失
	TALENT:ASSI:백버진 = 0
	EXP:ASSI:이상경험 += 1
	PRINTFORML %CALLNAME:ASSI%に%EXPNAME:50%＋１
	;異常な状態での후처녀喪失で이상경험を加算する
	;후처녀喪失の相손がにょたかちびの自分
		;にょたちびサーチ
		R = NO:ASSI
		L = NO:MASTER
		CALL GET_JIBUN
		IF A
			PRINTFORML %CALLNAME:MASTER%相손に백버진喪失で%EXPNAME:50%+1
			EXP:ASSI:이상경험 += 1
		ENDIF
	;후처녀喪失を撮影された
	IF TEQUIP:비디오카메라
		PRINTFORML 비디오촬영中に백버진喪失で%EXPNAME:50%+1
		EXP:ASSI:이상경험 += 1
	ENDIF
	IF (CFLAG:99 & 4) == 0
		IF RELATION:(NO:ASSI) > 100
			EXP:이상경험 += 1
			PRINTFORML %CALLNAME%に%EXPNAME:50%＋１
		ENDIF
		CFLAG:99 |= 4
	ENDIF
	;へたくしょん用面識判定の更新
	G = (2099 + NO:MASTER)
	H = (2099 + NO:ASSI)
	CFLAG:MASTER:H |= 256
	CFLAG:ASSI:G |= 512
	;백버진喪失相손の記録
	CSTR:ASSI:12 = %CALLNAME:MASTER%
ENDIF

;助손のA경험
EXP:ASSI:A경험 += 1

;助손が소인체형で調教者が말자지だった場合
IF TALENT:ASSI:소인체형 && TALENT:MASTER:말자지
	IF EXP:ASSI:A확장경험 == 0
		EXP:ASSI:이상경험 += 1
		PRINTFORML %CALLNAME:ASSI%の이상경험＋１
	ENDIF
	EXP:ASSI:A확장경험 += 1
	PRINTFORML %CALLNAME:ASSI%のA확장경험＋１
ENDIF

;성교경험
EXP:ASSI:성교경험 += 1
EXP:MASTER:성교경험 += 1
PRINTS EXPNAME:4
PRINTL +1

;삽입경험
EXP:MASTER:삽입경험 += 1

;게이경험とか加算
CALL COM_EXP(10)

;依存度ベクトル
TFLAG:33 = 5

T = NO:TARGET
A = NO:ASSI
;依存度補正値(助손との相性100↑かつ음란で1、창부で2）
IF TALENT:음란 && RELATION:A > 100
	TFLAG:30 += 1
ELSEIF TALENT:창부 && RELATION:A > 100
	TFLAG:30 += 2
ENDIF


RETURN 1

