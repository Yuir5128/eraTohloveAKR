;-------------------------------------------------
;커닐링구스
;Ｖ系調教コマンド
;-------------------------------------------------
@COM40

;-------------------------------------------------
;特殊派生処理
;-------------------------------------------------
;前回と今回の調教者が同じ
IF (ASSIPLAY && TFLAG:50) || (ASSIPLAY == 0 && TFLAG:50 == 0)
	;すでに삽입명령中は「サンドイッチ」
	IF PREVCOM == 405 || PREVCOM == 406
		CALL COM_ABLE515
		IF RESULT == 1
			TFLAG:40 = 4
			IF PREVCOM == 405
				TFLAG:41 = 1
			ELSE
				TFLAG:41 = 2
			ENDIF
			TFLAG:49 = 0
			JUMP COM515
		ENDIF
	;前回コマンドが촉수에범하게시킨다とき、「サンドイッチさせる」
	ELSEIF PREVCOM == 270 || PREVCOM == 271
		CALL COM_ABLE520
		IF RESULT == 1
			TFLAG:40 = 4
			IF PREVCOM == 270
				TFLAG:41 = 6
			ELSE
				TFLAG:41 = 7
			ENDIF
			TFLAG:49 = 0
			JUMP COM520
		ENDIF
	;前回の調教が펠라치오、커닐링구스강제、シックスナインだと、シックスナインに派生
	ELSEIF PREVCOM == 71 || PREVCOM == 222 || PREVCOM == 503
		CALL COM_ABLE503
		SIF RESULT == 1
			JUMP COM503
	;前回がサンドイッチか、サンドイッチさせる
	ELSEIF PREVCOM == 515 || PREVCOM == 520
		;助손が奴隷に挿入してれば再び「サンドイッチ」に
		IF TFLAG:41 == 1 || TFLAG:41 == 2
			CALL COM_ABLE515
			IF RESULT == 1
				TFLAG:40 = 4
				TFLAG:49 = 0
				JUMP COM515
			ENDIF
		;助손が主人に挿入してると「サンドイッチさせる」に
		ELSEIF TFLAG:41 == 6 || TFLAG:41 == 7
			CALL COM_ABLE520
			IF RESULT == 1
				TFLAG:40 = 4
				TFLAG:49 = 0
				JUMP COM520
			ENDIF
		ENDIF
	ENDIF
ENDIF

;페니스がない　かつ뇨의が高い、もしくは뇨의があって카테터装着中は음뇨한다に派生
IF !PENIS(TARGET) && (BASE:뇨의 >= 90 || (BASE:뇨의 >= 60 && TEQUIP:카테터))
	CALL COM_ABLE285
	SIF RESULT == 1
		JUMP COM285
ENDIF

;何も読まずにこっちに来たときはフラグ解除
TFLAG:40 = 0
TFLAG:41 = 0
TFLAG:49 = 0
TFLAG:42 = 0

PRINTL 커닐링구스
STR:0 = 커닐링구스
CALL KOJO_MESSAGE_COM

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費체력、기력
LOSEBASE:체력 += 5
LOSEBASE:기력 += 30

SOURCE:쾌V += 150
;舌を使う調教では、調教者の혀놀림によって、快感のソース増加
IF TALENT:PLAYER:혀놀림
	TIMES SOURCE:쾌V , 1.50
ELSEIF TALENT:PLAYER:고양이혀
	TIMES SOURCE:쾌V , 0.75
ENDIF
;着衣のとき
SIF TEQUIP:속옷（아래） || TEQUIP:특수2 == 5 || TEQUIP:특수2 == 6
	TIMES SOURCE:쾌V , 0.80

SOURCE:액체 += 60
SOURCE:노출 += 10
SOURCE:일탈 += 15
SOURCE:반발 += 15

;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;억압、저항持ちには、억울ソース追加
SIF TALENT:억압 || TALENT:저항
	SOURCE:울굴 += 20

;스킨십선호だと애정のソース追加
SIF TALENT:스킨십선호
	SOURCE:애정 += 50


;-------------------------------------------------
;汚れの処理
;-------------------------------------------------
CALL COM_STAIN

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
;게이경험とか加算
CALL COM_EXP(3)

;調教者が고양이혀持ちの場合は、調教者に가학쾌락경험を追加
IF TALENT:PLAYER:고양이혀
	EXP:PLAYER:가학쾌락경험 += 1
	PRINTFORML %CALLNAME:PLAYER%の가학쾌락경험＋１
ENDIF

;依存度ベクトル
TFLAG:33 = 1

RETURN 1


