;｢CALL EVENT_NEXTDAY_T｣｢CALL EVENTCHECK_M｣を追加

@EVENTFIRST

TARGET = -1
ASSI = -1

CALL CHARA_PRICE

PBAND = 12

;バージョンアップフラグ
FLAG:150 = 14
;調教時テキスト表示
FLAG:6 = 1
FLAG:7 = 2
;キャラ数の管理
FLAG:44 = 889
;사정先設定
FLAG:99 = 8

;猟奇コマンドフィルタ
SAVESTR:0 = /390/391/392/
CALL START_MODE

;チュートリアル表示フラグ
SIF FLAG:154
	CALL TUTORIAL
DRAWLINE


BEGIN SHOP

@START_MODE(ARG)
;モード
LOCAL = 0
;基本はキャラ名で表示する
LOCAL:1 = FLAG:43
;難易度
LOCAL:2 = 2
SIF FLAG:4
	LOCAL:2 = FLAG:4
;コマンドフィルタ
LOCAL:3 = 0
SIF FLAG:41
	LOCAL:3 = 1
;チュートリアル
LOCAL:4 = 0
SIF FLAG:41
	LOCAL:4 = 1
;처녀동정モード
LOCAL:5 = 0
;-----------------------------------------------------------
;表示部分
;-----------------------------------------------------------
PRINTL 　　　　　　　　　★★設定を選択してください★★
$INPUT_LOOP_2
WHILE 1
	DRAWLINE
	IF !ARG
		PRINT 　　　　　モード：
		SETCOLOR !LOCAL ? 0x8888FF # 0xA0A0A0
		PRINTBUTTON @"[ 0] START　 (100日期限で甲州金百万고슈금を稼ぐモード 周回プレイ有)", 0
		PRINTL 
		SETCOLOR LOCAL == 8 ? 0x8888FF # 0xA0A0A0
		PRINTL 　　　　　　　　　[ 1] FREE MISSION　　　　　　　　　　　　　　　　
		SETCOLOR LOCAL == 9 ? 0x8888FF # 0xA0A0A0
		PRINTL 　　　　　　　　　[ 2] EXTRA　 (期限も目標も無しのエンドレスモード)
		RESETCOLOR
		PRINTL 
	ENDIF
	
	;PRINT 　　キャラ名表示：
	;SETCOLOR !LOCAL:1 ? 0x8888FF # 0xA0A0A0
	;PRINTBUTTON @"[ 3] 通常　　(基本の名前で表示します　例：ライナ⇒ライナ)", 3
	;PRINTL 
	;SETCOLOR LOCAL:1 ? 0x8888FF # 0xA0A0A0
	;PRINTL 　　　　　　　　　[ 4] 本名　　(一部キャラを本名で表示します　例：ライナ⇒フェルナ)
	;RESETCOLOR
	;PRINTL 
	
	PRINT 　　　　　難易度：
	SETCOLOR LOCAL:2 == 1 ? 0x8888FF # 0xA0A0A0
	PRINTBUTTON @"[ 5] EASY　　(ヌルく遊んでみたい人向け)", 5
	PRINTL 
	SETCOLOR LOCAL:2 == 2 ? 0x8888FF # 0xA0A0A0
	PRINTL 　　　　　　　　　[ 6] NORMAL　(普通にプレイする人向け)
	SETCOLOR LOCAL:2 == 3 ? 0x8888FF # 0xA0A0A0
	PRINTL 　　　　　　　　　[ 7] HARD　　(難しめのプレイに燃える人向け)
	RESETCOLOR
	PRINTL 
	
	PRINT コマンドフィルタ：
	SETCOLOR !LOCAL:3 ? 0x8888FF # 0xA0A0A0
	PRINTBUTTON @"[ 8] シンプル(初めてeraをプレイする人向け 基本のコマンドのみ)", 8
	PRINTL 
	SETCOLOR LOCAL:3 == 1 ? 0x8888FF # 0xA0A0A0
	PRINTL 　　　　　　　　　[ 9] マニア　(eraに慣れてきた人向け 趣味のコマンドがたっぷり)
	SETCOLOR LOCAL:3 == 2 ? 0x8888FF # 0xA0A0A0
	PRINTL 　　　　　　　　　[10] 個別に設定(コマンド毎に使用するか設定できます)
	RESETCOLOR
	PRINTL 
	
	PRINT 　チュートリアル：
	SETCOLOR !LOCAL:4 ? 0x8888FF # 0xA0A0A0
	PRINTBUTTON @"[11] あり　　　　　　　　　　　　　　　　　",11
	PRINTL 
	SETCOLOR LOCAL:4 ? 0x8888FF # 0xA0A0A0
	PRINTL 　　　　　　　　　[12] なし　　　　　　　　　　　　　　　　　　　　　
	RESETCOLOR
	PRINTL 
	
	PRINT 　처녀동정モード：
	SETCOLOR LOCAL:5 ? 0x8888FF # 0xA0A0A0
	PRINTBUTTON @"[13] ON (あんな人もこんな人も처녀・동정に)　　　",13
	PRINTL 
	SETCOLOR !LOCAL:5 ? 0x8888FF # 0xA0A0A0
	PRINTL 　　　　　　　　　[14] OFF 　　　　　　　　　　　　　　　　　　　　　
	RESETCOLOR
	PRINTL 
	
	PRINTL 　　　　　　　　　[15] コンフィグ
	PRINTL 　　　　　　　　　[16] ヘルプ
	PRINTL 
	PRINT 　　　　　　　　　
	SETCOLOR 0x8888FF
	PRINTBUTTON @"　%UNICODE(0x27A0)%調教を始める               ", 17
	RESETCOLOR
	PRINTL 
;-----------------------------------------------------------
;入力処理部分
;-----------------------------------------------------------
	$INPUT_LOOP
	INPUT
	IF ARG && RESULT <= 2
		CLEARLINE 1
		GOTO INPUT_LOOP
	ENDIF
	SELECTCASE RESULT
		CASE 0
			LOCAL = 0
		CASE 1,2
			LOCAL = RESULT + 7
		CASE 3,4
			LOCAL:1 = !LOCAL:1
		CASE 5,6,7
			LOCAL:2 = RESULT - 4
		CASE 8,9,10
			LOCAL:3 = RESULT - 8
		CASE 11,12
			LOCAL:4 = !LOCAL:4
		CASE 13,14
			LOCAL:5 = !LOCAL:5
		CASE 15
			CALL OPTION
			LOCAL:1 = FLAG:43
		CASE 16
			CALL DICTIONARY_MENU
		CASE 17
			BREAK
		CASEELSE
			CLEARLINE 1
			GOTO INPUT_LOOP
	ENDSELECT
WEND
;-----------------------------------------------------------
;후処理部分
;-----------------------------------------------------------
IF !LOCAL:3
	SAVESTR:0 = /16/25/35/44/56/62/72/87/150/156/160/161/162/181/183/184/185/222/224/226/227/228/240/241/242/280/281/282/283/284/285/286/323/340/380/381/382/383/405/406/430/431/440/441/
ELSEIF LOCAL:3 == 1
	SAVESTR:0 = 
ENDIF
FLAG:43 = LOCAL:1
FLAG:4 = LOCAL:2
FLAG:154 = !LOCAL:4
FLAG:155 = LOCAL:5
DAY = 1
MONEY = 30000
SIF FLAG:5 == 9
	MONEY += 70000
SIF FLAG:5 == 0
	CALL OPENING
IF !LOCAL
	FLAG:5 = 0
	MASTER = -1
	CALL START_CHARA_SELECT
	CALL CHARA_BUY(1)
ELSEIF LOCAL == 8
	FLAG:5 = 8
	CALL FREE_MISSION_START
	SIF RESULT
		GOTO INPUT_LOOP_2
ELSE
	FLAG:5 = 9
	MASTER = -1
	CALL START_CHARA_SELECT
ENDIF

@EVENTCOM
#PRI
REPEAT 30
	TFLAG:COUNT = 0
REND
TFLAG:30 = 0
TFLAG:33 = 0
TFLAG:34 = 0
TFLAG:43 = 0
;입上の道具着脱分岐
TFLAG:54 = 0
TFLAG:58 = 0
TFLAG:59 = 0
TFLAG:68 = 0
TFLAG:75 = 0
TFLAG:100 = 0
TFLAG:160 = 0
TFLAG:161 = 0
TFLAG:162 = 0
TFLAG:163 = 0

@EVENTCOMEND
#PRI
IF TALENT:기절 && (SELECTCOM == 1 || SELECTCOM == 153 || SELECTCOM == 154)
	TALENT:기절 = 0
	BASE:기력 = 1
ENDIF
IF BASE:체력 <= 0
	PRINTFORMW ………………
	PRINTFORMW …………
	PRINTFORMW ……
	PRINTFORMW （激しい調教によって、%CALLNAME%は[기절]しました　調教を終了します）
	EXP:기절경험 += 1
	PRINTFORMW 기절경험+1
	IF EXP:기절경험 == 1
		EXP:이상경험 += 1
		PRINTFORMW 이상경험+1
	ENDIF
	;체력回復停止フラグ
	CFLAG:6 += 2
	SIF TALENT:불사신
		CFLAG:6 -= 1
	CFLAG:10 += 10
	BASE:체력 = 1
	BASE:기력 = 0
	TALENT:기절 = 4
	;순종ダウン
	IF ABL:순종 > 0
		ABL:순종 -= 1
		PRINTFORML %CALLNAME%の순종が１下がった
	ENDIF
	BEGIN AFTERTRAIN
ELSEIF BASE:체력 < 500
	PRINTW （체력が限界に来ています　調教を終了します）
	BEGIN AFTERTRAIN
ELSEIF TFLAG:74 < 0
	PRINTW （残り時間がなくなりました　調教を終了します）
	BEGIN AFTERTRAIN
ENDIF


@EVENTTURNEND
#PRI
;夜イベント呼び出し
IF TIME == 1
	CALL EVENT_NIGHT_SELF
	CALL EVENT_NIGHT_AFTER
ENDIF
REPEAT CHARANUM
	;기력の回復
	IF TALENT:COUNT:기절
		TALENT:COUNT:기절 -= 1
	ELSE
		BASE:COUNT:기력 = MAXBASE:COUNT:기력
	ENDIF
	
	;뇨의・변의・구역질ゲージを初期値に
	BASE:COUNT:뇨의 = 10
	BASE:COUNT:변의 = 10
	BASE:COUNT:구역질 = 10

	;労役中のキャラは체력回復処理無し
	SIF CFLAG:COUNT:12 > 0
		CONTINUE

	;체력回復停止処理
	IF CFLAG:COUNT:6 > 0
		CFLAG:COUNT:6 -= 1
		CONTINUE
	ENDIF

	;체력の回復（午후の調教후は夜の休みが入るので回復大きい）
	IF TIME == 0
		A = MAXBASE:COUNT:체력 / 15
	ELSE
		A = MAXBASE:COUNT:체력 / 5
	ENDIF

	;회복빠름、회복느림、발정기、불사신
	IF TALENT:COUNT:회복빠름
		TIMES A , 1.50
	ELSEIF TALENT:COUNT:회복느림 || TALENT:COUNT:발정기
		TIMES A , 0.75
	ELSEIF TALENT:COUNT:불사신
		TIMES A , 1.80
	ENDIF

	;일광욕、월광욕、흡혈귀
	IF TIME == 0
		SIF TALENT:COUNT:일광욕
			A += 100
	ELSE
		SIF TALENT:COUNT:흡혈귀
			TIMES A , 1.50
		SIF TALENT:COUNT:월광욕
			A += 100
	ENDIF

	;치료
	IF ASSI >= 0
		SIF TALENT:ASSI:치료
			A += 150
	ENDIF
	SIF TALENT:MASTER:치료
		A += 150
	
	;기절
	SIF TALENT:COUNT:기절
		TIMES A , 0.50

	;休憩フラグ（休憩フラグが経っている、もしくは調教対象でない）
	IF FLAG:0 || TARGET != COUNT
		A += 100
		CFLAG:COUNT:10 -= 1
	ENDIF

	;ショップで休憩を選択時
	IF FLAG:0 == 2
		TIMES A , 2.00
		CFLAG:COUNT:10 -= 1
	ENDIF

	BASE:COUNT:체력 += A
	SIF BASE:COUNT:체력 > MAXBASE:COUNT:체력
		BASE:COUNT:체력 = MAXBASE:COUNT:체력

	;ストレス値が0未満の場合は0にする
	SIF CFLAG:COUNT:10 < 0
		CFLAG:COUNT:10 = 0
	
	;禁欲ポイントの処理。
	;정조대してるキャラは多めに上げる。調教対象以外も徐々に上がっていく。
	IF TEQUIP:COUNT:속옷（아래） == 5
		IF TALENT:COUNT:자위광 || ABL:COUNT:자위중독 >= 3 || ABL:COUNT:욕망 >= 8 || TALENT:COUNT:발정기
			CFLAG:COUNT:602 += 15
		ELSEIF TALENT:COUNT:자위하기쉬움 || ABL:COUNT:자위중독 || ABL:COUNT:욕망 >= 5
			CFLAG:COUNT:602 += 10
		ELSE
			CFLAG:COUNT:602 += 5
		ENDIF
	ELSEIF TALENT:COUNT:발정기
		CFLAG:COUNT:602 += 10
	ELSE
		CFLAG:COUNT:602 += 5
	ENDIF
REND
CALL EVENTCHECK_ABL
FLAG:0 = 0

;午후なら次の日、午前なら午후にする
IF TIME == 1
	FLAG:81 += 1
	FLAG:82 += 1

	;日付変更時のイベント呼び出し
	CALL EVENT_NEXTDAY

	DAY = DAY+1
	PRINTL 
	PRINTW 一日が終わった……
	TIME = 0
	;バッドエンドの判定
	SIF DAY > 100 && FLAG:5 == 0
		CALL BADENDING_1
	SIF MONEY < 0 && FLAG:5 == 0
		CALL BADENDING_2

	;朝になった時のイベント呼び出し
	CALL EVENT_NEWDAY
	CALL EVENT_NEWDAY_SELF
ELSE
	;全体の임신判定の処理
	CALL IN_VAGINA_ANAL_ALL
	
	PRINTL 
	PRINTW 夜になりました
	TIME = 1
ENDIF

BEGIN SHOP



;────────────────────────────────────
;バージョンアップ
;────────────────────────────────────
@VERSION_UP
IF FLAG:150 == 0
	FOR LOCAL, 0, CHARANUM
		IF (TALENT:LOCAL:3 || TALENT:LOCAL:5 || TALENT:LOCAL:6 || TALENT:LOCAL:8) && CFLAG:LOCAL:41 <= 0
			CFLAG:LOCAL:41 = NO:MASTER
			CFLAG:LOCAL:40 = 1
		ENDIF
	NEXT
	FLAG:150 = 1
ENDIF
IF FLAG:150 < 2
	FOR LOCAL, 0, CHARANUM
		FOR LOCAL:1, 0, 100
			CFLAG:LOCAL:(1099 + LOCAL:1) = CFLAG:LOCAL:(899 + LOCAL:1)
			CFLAG:LOCAL:(899 + LOCAL:1) = 0
		NEXT
		SIF NO:LOCAL == 200
			FLAG:33++
		IF TALENT:LOCAL:3
			CFLAG:LOCAL:39 = 3
			CFLAG:LOCAL:40 = 1
		ELSEIF TALENT:LOCAL:4
			CFLAG:LOCAL:39 = 4
			CFLAG:LOCAL:40 = 1
		ELSEIF TALENT:LOCAL:5
			CFLAG:LOCAL:39 = 5
			CFLAG:LOCAL:40 = 1
		ELSEIF TALENT:LOCAL:6
			CFLAG:LOCAL:39 = 6
			CFLAG:LOCAL:40 = 1
		ELSEIF TALENT:LOCAL:7
			CFLAG:LOCAL:39 = 7
			CFLAG:LOCAL:40 = 1
		ELSEIF TALENT:LOCAL:8
			CFLAG:LOCAL:39 = 8
			CFLAG:LOCAL:40 = 1
		ENDIF
		IF TALENT:LOCAL:165
			CFLAG:LOCAL:39 = 165
			CFLAG:LOCAL:40 = 1
		ENDIF
	NEXT
	FLAG:150 = 2
ENDIF
IF FLAG:150 < 3
	FLAG:33 = 0
	FOR LOCAL,0,CHARANUM
		SIF NO:LOCAL > 150 && NO:LOCAL <= 160
			FLAG:33++
	NEXT
	FLAG:150 = 3
ENDIF
IF FLAG:150 < 4
	FLAG:84 = 0
	FLAG:150 = 4
ENDIF
IF FLAG:150 < 5
	FOR LOCAL, 0, CHARANUM
		IF TALENT:LOCAL:154
			IF CFLAG:LOCAL:114 < CHARANUM
				CFLAG:LOCAL:114 = NO:(CFLAG:LOCAL:114)
			ELSE
				FOR LOCAL:1, 0, CHARANUM
					SIF STRLENS(CSTR:(LOCAL:1):3)
						CFLAG:LOCAL:114 = NO:(LOCAL:1)
				NEXT
			ENDIF
		ENDIF
	NEXT
	FLAG:150 = 5
ENDIF
IF FLAG:150 < 6
	FLAG:44 = 100
	FLAG:150 = 6
ENDIF
IF FLAG:150 < 7
	FOR LOCAL, 0, CHARANUM
		;父、母のキャラ番号を登録番号→固有番号に
		SIF STRLENS(CSTR:LOCAL:2)
			CFLAG:LOCAL:1000 = NO:(CFLAG:LOCAL:1000)
		SIF STRLENS(CSTR:LOCAL:3)
			CFLAG:LOCAL:1001 = NO:(CFLAG:LOCAL:1001)
		SIF STRLENS(CSTR:LOCAL:4)
			CFLAG:LOCAL:1002 = NO:(CFLAG:LOCAL:1002)
		SIF STRLENS(CSTR:LOCAL:5)
			CFLAG:LOCAL:1003 = NO:(CFLAG:LOCAL:1003)
		SIF STRLENS(CSTR:LOCAL:6)
			CFLAG:LOCAL:1004 = NO:(CFLAG:LOCAL:1004)
		SIF STRLENS(CSTR:LOCAL:7)
			CFLAG:LOCAL:1005 = NO:(CFLAG:LOCAL:1005)
		;아이が成長するまでの非表示フラグで아이を検索
		IF CFLAG:LOCAL:26
			LOCAL:2 = 0
			LOCAL:3 = 0
			;母は子育てできるキャラか
			SIF TALENT:(NO2(CFLAG:LOCAL:1000)):붕괴 || NO:MASTER == CFLAG:LOCAL:1000 
				LOCAL:3 = 1
			;子育てしているキャラを検索
			FOR LOCAL:1, 0, CHARANUM
				;육아중かつ、育てている子フラグと아이が等しいか実母が子育て不可
				IF TALENT:(LOCAL:1):육아중 && (NO:LOCAL == CFLAG:(LOCAL:1):114 || LOCAL:3)
					CFLAG:LOCAL:115 = NO:(LOCAL:1)
					CFLAG:LOCAL:116 = CFLAG:(LOCAL:1):110
					LOCAL:2 = 1
				ENDIF
			NEXT
		ENDIF
	NEXT
	FLAG:150 = 7
ENDIF
IF FLAG:150 < 8
	SIF !CFLAG:MASTER:1
		CFLAG:MASTER:1 = 3
	FLAG:150 = 8
ENDIF
IF FLAG:150 < 9
	CLEARBIT FLAG:2, 6
	FLAG:150 = 9
ENDIF
IF FLAG:150 < 10
	FLAG:44 = 200
	FLAG:150 = 10
ENDIF
IF FLAG:150 < 11
	FLAG:1083 = 3000
	FLAG:150 = 11
ENDIF
IF FLAG:150 < 12
	FLAG:150 = 12
ENDIF
IF FLAG:150 < 13
	;CFLAG:4のデータの持ち方の互換処理
	REPEAT CHARANUM
		SIF CFLAG:COUNT:4 == 0
			CONTINUE
		
		;9桁以降を保持
		LOCAL:0 = CFLAG:COUNT:4 / POWER(10,8)
		;7桁までを保持
		LOCAL:1 = CFLAG:COUNT:4 % POWER(10,6)
		
		;8桁目を保持　鈍感敏感
		LOCAL:2 = (CFLAG:COUNT:4 % POWER(10,8)) / POWER(10,7)
		;7桁目を保持　部位
		LOCAL:3 = (CFLAG:COUNT:4 % POWER(10,7)) / POWER(10,6)
		
		;9桁目以降を11桁以降にずらす　7-10桁目は0が入っている
		LOCAL:4 = (LOCAL:0 * POWER(10,10)) + LOCAL:1
		
		SELECTCASE LOCAL:3
			CASE 1
				;Ｃ　7桁目に格納する
				LOCAL:4 += LOCAL:2 * POWER(10,6)
			CASE 2
				;Ｖ　8桁目に格納する
				LOCAL:4 += LOCAL:2 * POWER(10,7)
			CASE 3
				;Ａ　9桁目に格納する
				LOCAL:4 += LOCAL:2 * POWER(10,8)
			CASE 4
				;Ｂ　10桁目に格納する
				LOCAL:4 += LOCAL:2 * POWER(10,9)
		ENDSELECT
		
		CFLAG:COUNT:4 = LOCAL:4
	REND
	FLAG:150 = 13
ENDIF
IF FLAG:150 < 14
	REPEAT CHARANUM
		CFLAG:COUNT:4 = 0
	REND
	FLAG:150 = 14
ENDIF


@OPENING

;オープニング表示
$INPUT_LOOP_5
IF FLAG:5 == 0
	PRINTL オープニングストーリーを表示しますか？
	PRINTL [0] - 表示する
	PRINTL [1] - 表示しない
	INPUT
	IF RESULT == 0
		CALL OPENING_START
		DRAWLINE
	ELSEIF RESULT == 1
		DRAWLINE
	ELSE
		GOTO INPUT_LOOP_5
	ENDIF
ENDIF

;終了処理(フリーミッションクリア후に使用)
@GAME_END(ARG)
DRAWLINE
PRINTL ゲームを終了しますか？
PRINTC [0]ゲームを終了する
SIF ARG
	PRINTC [1]ゲームを継続する
PRINTC [2]タイトルに戻る
PRINTL

$INPUT_LOOP
INPUT

IF RESULT == 0
	QUIT
ELSEIF RESULT == 1 && ARG
	FLAG:5 = 9
	;FLAG:24 = フリーミッション番号はそのまま保持
ELSEIF RESULT == 2
	BEGIN TITLE
ELSE
	GOTO INPUT_LOOP
ENDIF


