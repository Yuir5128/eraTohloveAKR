;-------------------------------------------------
;EVENT_D.ERB、EVENT_DEPENDENCE、依存度システムの処理
;依存度はプラスに大きいと主人に対して慕う形で依存し、
;マイナスに大きいと主人を畏怖する形で依存していく
;依存度ベクトルは各調教コマンドごとで指示されており、増加と減少で処理は異なっている
;-------------------------------------------------
@MASTER_DEPENDENCE_CHECK

;依存度のベクトル処理
;0は関数終了、붕괴も依存度は変化しない
IF TFLAG:33 == 0 || TALENT:붕괴 == 1
	PRINTL <依存度変化無し>
	RETURN 0
;1は依存度が0以上で増加、0未満で減少
ELSEIF TFLAG:33 == 1
	SIF CFLAG:2 >= 0
		CALL MASTER_DEPENDENCE_PLUS
	SIF CFLAG:2 < 0
		CALL MASTER_DEPENDENCE_MINUS
;2は増加、但し복종、예속持ちは減少
ELSEIF TFLAG:33 == 2
	IF TALENT:복종 || TALENT:예속
		CALL MASTER_DEPENDENCE_MINUS
	ELSE
		CALL MASTER_DEPENDENCE_PLUS
	ENDIF
;3は減少、但し연모、친애持ちは増加
ELSEIF TFLAG:33 == 3
	IF TALENT:연모 || TALENT:친애
		CALL MASTER_DEPENDENCE_PLUS
	ELSE
		CALL MASTER_DEPENDENCE_MINUS
	ENDIF
;4は強制増加
ELSEIF TFLAG:33 == 4
	CALL MASTER_DEPENDENCE_PLUS
;5は強制減少
ELSEIF TFLAG:33 == 5
	CALL MASTER_DEPENDENCE_MINUS
ENDIF

RETURN 0


;依存度増加処理
@MASTER_DEPENDENCE_PLUS

R = 0

;ABL:순종をみる
R += ABL:순종*2

;ABL:봉사정신をみる
R += ABL:봉사정신

;難易度を見る
;EASY
IF FLAG:4 == 1
	R += 1
;NORMAL
ELSEIF FLAG:4 == 2
	R += 0
;HARD
ELSEIF FLAG:4 == 3
	R -= 1
ENDIF

;素質による増減
;陥落素質
;연모
SIF TALENT:연모
	R += 5
;친애
SIF TALENT:친애
	R += 7
;음란
M = NO:MASTER
SIF TALENT:음란 && RELATION:M > 100
	R += 3
;창부
SIF TALENT:창부 && RELATION:M > 100
	R += 5
;복종
SIF TALENT:복종
	R += 10
;예속
SIF TALENT:예속
	R += 20

;その他素質
;자위광
SIF TALENT:자위광
	R += 1
;음란구멍
SIF TALENT:음란구멍
	R += 1
;항문광
SIF TALENT:항문광
	R += 1
;음란가슴
SIF TALENT:음란가슴
	R += 1
;섹스광
SIF TALENT:섹스광 && (SEXPLAY(TARGET) || SELECTCOM == 122 || SELECTCOM == 225 || SELECTCOM == 226)
	R += 2
;맹신
SIF TALENT:맹신
	R += 2
;츤데레
SIF (TALENT:연모 || TALENT:친애 || TALENT:복종 || TALENT:예속) && TALENT:츤데레
	R += 1

;主人の매력
SIF TALENT:MASTER:매력
	R += 2
;主人の매혹
SIF TALENT:MASTER:매혹
	R += 3
;主人の수수께끼의매력
SIF TALENT:MASTER:수수께끼의매력
	R += 5
;主人の치료
SIF TALENT:MASTER:치료
	R += 2
;主人の고무적
SIF TALENT:MASTER:고무적
	R += 2
;主人の인기
SIF TALENT:MASTER:인기
	R += 3

;陥落前と陥落후で効果が変わる素質
IF !TALENT:연모 && !TALENT:친애 && !TALENT:복종 && !TALENT:예속
	;일선을넘지않음
	SIF TALENT:일선을넘지않음
		R -= 2
	;무관심で半分
	SIF TALENT:무관심
		R /= 2
ENDIF
;依存度補正を加算
R += TFLAG:30

;실의で半分になる
SIF TALENT:실의
	R /= 2
;멘헤라
SIF TALENT:멘헤라
	R += R/3

;結果が0未満の場合は0にする
SIF R < 0
	R = 0

CFLAG:2 += R

IF R > 0
	PRINTL <依存度増加>
ELSE
	PRINTL <依存度変化無し>
ENDIF

R = 0
TFLAG:30 = 0


RETURN 0



;依存度減少処理
@MASTER_DEPENDENCE_MINUS

R = 0

;ABL:순종をみる
R += ABL:순종*2

;ABL:마조끼をみる
R += ABL:마조끼

;難易度を見る
;EASY
IF FLAG:4 == 1
	R += 1
;NORMAL
ELSEIF FLAG:4 == 2
	R += 0
;HARD
ELSEIF FLAG:4 == 3
	R -= 1
ENDIF

;素質による増減
;陥落素質
;연모
SIF TALENT:연모
	R += 10
;친애
SIF TALENT:친애
	R += 20
;음란
M = NO:MASTER
SIF TALENT:음란 && RELATION:M > 100
	R += 3
;창부
SIF TALENT:창부 && RELATION:M > 100
	R += 5
;복종
SIF TALENT:복종
	R += 5
;예속
SIF TALENT:예속
	R += 7

;その他素質
;자위광
SIF TALENT:자위광
	R += 1
;음란구멍
SIF TALENT:음란구멍
	R += 1
;항문광
SIF TALENT:항문광
	R += 1
;음란가슴
SIF TALENT:음란가슴
	R += 1
;섹스광
SIF TALENT:섹스광 && (SEXPLAY(TARGET) || SELECTCOM == 122 || SELECTCOM == 225 || SELECTCOM == 226)
	R += 2
;새드
SIF TALENT:새드
	R -= 1
;마조
SIF TALENT:마조
	R += 1
;맹신
SIF TALENT:맹신
	R += 2
;츤데레
SIF (TALENT:연모 || TALENT:친애 || TALENT:복종 || TALENT:예속) && TALENT:츤데레
	R += 1

;主人の매력
SIF TALENT:MASTER:매력
	R += 2
;主人の매혹
SIF TALENT:MASTER:매혹
	R += 3
;主人の수수께끼의매력
SIF TALENT:MASTER:수수께끼의매력
	R += 5
;主人の치료
SIF TALENT:MASTER:치료
	R += 2
;主人の고무적
SIF TALENT:MASTER:고무적
	R += 2
;主人の인기
SIF TALENT:MASTER:인기
	R += 3
;陥落前と陥落후で効果が変わる素質
IF !TALENT:연모 && !TALENT:친애 && !TALENT:복종 && !TALENT:예속
	;일선을넘지않음
	SIF TALENT:일선을넘지않음
		R -= 2
	;무관심で半分
	SIF TALENT:무관심
		R /= 2
ENDIF

;依存度補正を加算
R += TFLAG:30

;실의で半分になる
SIF TALENT:실의
	R /= 2
;멘헤라
SIF TALENT:멘헤라
	R += R/3

;結果が0未満の場合は0にする
SIF R < 0
	R = 0

CFLAG:2 -= R

IF R > 0
	PRINTL <依存度減少>
ELSE
	PRINTL <依存度変化無し>
ENDIF

R = 0
TFLAG:30 = 0


RETURN 0

