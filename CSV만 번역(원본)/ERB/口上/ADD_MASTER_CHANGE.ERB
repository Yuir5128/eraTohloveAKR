;================================================================================================
;調教者変更機能用の関数群です
;CFLAG:39～41を利用しています
;================================================================================================

;思慕・畏怖対象の登録番号を調べる
;Rに検索対象の登録番号を代入してCALL
;思慕・畏怖対象のキャラ固有番号を「M」に、陥落素質を「N」に返す（【상사상애】の場合はLに1を返します）
;また、未陥落時には直前の調教者のキャラ固有番号を「M」に返す(Nは-1となるので未陥落分岐出来ます)
;登録番号が分からない時は先にぱんくしょんの「CALL NO」などで調べてください
;실의/붕괴の場合「N」に以前の陥落素質が入る
@MASTER_SEARCH
M = -1
N = -1
L = -1
SIF R < 0 || R > CHARANUM
	RETURN
;未陥落時/음란ルートは「直前の調教者」をMに返す
IF !CFLAG:R:40
	SIF CFLAG:R:41 < -99
		M = (CFLAG:R:41 + 100) * -1
	SIF CFLAG:R:39 > 0
		N = CFLAG:R:39
ELSE
	N = CFLAG:R:39
	SIF CFLAG:R:39 == 165
		L = 1
	M = CFLAG:R:41
ENDIF

@EVENTTRAIN
IF TARGET > -1
	CALL MASTER_CHANGE_ON
	;未陥落時/음란ルート
	IF !CFLAG:40
		M = -100 - (NO:MASTER)
		IF CFLAG:41 != M
			;前回調教者と主人が変わると依存度補正
			IF CFLAG:2 > 0
				;EASYで変動無し、NOMALで小変動、HARDで変動
				IF FLAG:4 == 2
					CFLAG:2 -= 20
				ELSEIF FLAG:4 == 3
					CFLAG:2 -= 50
				ENDIF
				SIF CFLAG:2 < 0
					CFLAG:2 = 0
			ELSEIF CFLAG:2 < 0
				;EASYで変動無し、NOMALで小変動、HARDで変動
				IF FLAG:4 == 2
					CFLAG:2 += 20
				ELSEIF FLAG:4 == 3
					CFLAG:2 += 50
				ENDIF
				SIF CFLAG:2 > 0
					CFLAG:2 = 0
			ENDIF
		ENDIF
	ENDIF
	;もし記録されている思慕・畏怖対象が現在のマスターではない場合、一時的に解除
	CALL MASTER_CHANGE_OFF
	;被조교경험の合計からマスター調教の経験を引いて助손조교경험を算出
	A = 0
	REPEAT 500
		B = COUNT + 1099
		A += CFLAG:B
	REND
	M = NO:MASTER + 1099
	A -= CFLAG:M
	;순종5以上で실의がなくなる
	IF TALENT:실의 && ABL:순종 > 4
		PRINTFORMW %CALLNAME:TARGET%は少しずつ心の傷を癒しているようだ……
		PRINTFORMW %CALLNAME:TARGET%は【실의】の状態から抜け出した
		TALENT:실의 = 0
	;실의の値が偶数のとき、依存値変動幅900以上or음란条件で실의が軽くなる
	;理論上、別の処理で실의の値が変動しない限り一回しか表示されなくなるはず
	ELSEIF TALENT:실의 && (TALENT:실의 % 2) == 0 && (CFLAG:2 > 900 || CFLAG:2 < -900 || ((ABL:욕망 >= 3 && ABL:C감각+ABL:V감각+ABL:A감각+ABL:B감각 >= 10 && MARK:쾌락각인 == 3 && MARK:굴복각인 == 3 && MARK:반발각인 == 0 && EXP:절정경험 >= 50) && (EXP:이상경험 >= 3 || (EXP:이상경험 >= 1 && A >= 1000) || A >= 2000)))
		PRINTFORMW %CALLNAME:TARGET%は少しずつ心の傷を癒しているようだ……
		TALENT:실의 /= 2
	ENDIF
ENDIF


;調教終了후、再び思慕・畏怖付与（再取得防止）
@EVENTTURNEND
#PRI
CALL MASTER_CHANGE_ON
IF TARGET > -1
	SIF !CFLAG:40
		CFLAG:41 = -100 - (NO:MASTER)
ENDIF

;日付変更イベント終了후、再び思慕・畏怖付与
@EVENTSHOP
CALL MASTER_CHANGE_ON

@MASTER_CHANGE_ON
IF TARGET > -1
	IF TALENT:붕괴 || TALENT:실의
		CFLAG:41 = -1
		CFLAG:40 = 0
	ENDIF
	IF CFLAG:40 && !TALENT:연모 && !TALENT:복종 && !TALENT:친애 && !TALENT:예속
		;상사상애のときの処理
		IF CFLAG:39 == 165
			TALENT:상사상애 = 1
			TALENT:친애 = 1
		ELSE
			TALENT:(CFLAG:39) = 1
		ENDIF
	ENDIF
ENDIF
;調教者の方の상사상애判定
IF CFLAG:MASTER:39 == 165
	TALENT:MASTER:상사상애 = 1
	TALENT:MASTER:친애 = 1
ENDIF

@MASTER_CHANGE_OFF
IF TARGET > -1
	;붕괴、실의
	IF TALENT:붕괴 || TALENT:실의
		CFLAG:41 = -1
		CFLAG:40 = 0
	ENDIF
	LOCAL = CFLAG:41
	IF NO:MASTER != LOCAL
		TALENT:연모 = 0
		TALENT:복종 = 0
		TALENT:친애 = 0
		TALENT:예속 = 0
		TALENT:상사상애 = 0
	ENDIF
ENDIF
;調教者の方の상사상애判定
IF TARGET > -1
	IF CFLAG:MASTER:41 != NO:TARGET && TALENT:MASTER:상사상애
		TALENT:MASTER:상사상애 = 0
		TALENT:MASTER:친애 = 0
	ENDIF
ENDIF

@SELECT_MASTER
DRAWLINE
PRINTFORML 現在【%NAME:MASTER%】が主人
PRINTL 今回は誰を主人にしますか？
DRAWLINE
CALL LIFE_LIST(1)
PRINTL  [1000]戻る

$INPUT_LOOP
INPUT
IF RESULT == 1000
	RETURN 0
ELSEIF RESULT < 0 || RESULT >= CHARANUM
	GOTO INPUT_LOOP
ELSEIF RESULT == MASTER
	GOTO INPUT_LOOP
ELSEIF CFLAG:RESULT:12 > 0
	GOTO INPUT_LOOP
ELSEIF CFLAG:RESULT:1 < 2
	GOTO INPUT_LOOP
ELSE
	C = RESULT
	CALL CHECK_CHILD_CARE
	SIF RESULT == 1
		GOTO INPUT_LOOP
ENDIF

;処理を抜けてきたとき、前マスターにCFLAG:1がついていない場合本来の主人とみなして「共犯者」の扱いとする
SIF CFLAG:MASTER:1 == 0
	CFLAG:MASTER:1 = 3

MASTER = C

SIF MASTER == TARGET
	TARGET = -1
SIF MASTER == ASSI
	ASSI = -1

;日常イベントにおける思慕・畏怖対象の判定
@DAILY_LIFE_MASTER_CHANGE
LOCAL = TARGET
REPEAT CHARANUM
	SIF COUNT == MASTER
		CONTINUE
	TARGET = COUNT
	CALL MASTER_CHANGE_ON
	SIF D
		CALL MASTER_CHANGE_OFF
REND
TARGET = LOCAL

@LIFE_LIST_MASTER_CHANGE
IF TALENT:X:반음반양 || TALENT:X:후타나리
	SETCOLOR 0x999999
	PRINT 双
ENDIF
IF TALENT:X:컨트보이
	SETCOLOR 0x8491c3
	PRINTFORM 上♂下♀
ELSEIF TALENT:X:쉬메일
	SETCOLOR 0xcc7eb1
	PRINTFORM 上♀下♂
ELSEIF MALE(X)
	SETCOLOR 0x8491c3
	PRINTFORM ♂ 
ELSE
	SETCOLOR 0xcc7eb1
	PRINTFORM ♀ 
ENDIF
RESETCOLOR
;처녀・동정・백버진保持者を特別扱いしてみる
IF ((VAGINA(X) && TALENT:X:처녀) || (PENIS(X) && TALENT:X:동정)) && TALENT:X:백버진
	PRINT 純潔 
;それ以外は付加価値の高い처녀優先で表示
ELSEIF TALENT:X:처녀 || TALENT:X:재생처녀
	PRINT 처녀 
ELSEIF TALENT:X:동정
	PRINT 동정 
ENDIF
SIF TALENT:X:붕괴
	PRINT 붕괴
IF CFLAG:X:41 >= 0 && CFLAG:X:40
	IF TALENT:X:(CFLAG:X:39)
		IF TALENT:X:상사상애
			PRINT 상사상애
		ELSEIF TALENT:X:연모
			PRINT 연모
		ELSEIF TALENT:X:친애
			PRINT 친애
		ELSEIF TALENT:X:복종
			PRINT 복종
		ELSE
			PRINT 예속
		ENDIF
		PRINTFORM (\@ CFLAG:X:39 == 165 ? ⇔ # ⇒ \@%CALLNAME:GETCHARA(CFLAG:X:41)%)
	ENDIF
ELSEIF TALENT:X:음란
	PRINT 음란
ELSEIF TALENT:X:창부
	PRINT 창부
ELSEIF CFLAG:X:1 == 2
	PRINT 売却・助손可能
ELSEIF CFLAG:X:1 == 1
	PRINT 売却可能
ENDIF
SIF CFLAG:X:0
	PRINT EX

;売却時
@SELLOUT_MASTER_CHANGE
CALL MASTER_CHANGE_OFF
M = 0
N = 0
O = 0
P = 0
Q = 0
R = 0
REPEAT CHARANUM
	N = COUNT
	IF CFLAG:N:41 == NO:TARGET && (CFLAG:N:39 == 3 || CFLAG:N:39 == 6 || CFLAG:N:39 == 165 || CFLAG:N:39 == 5 || CFLAG:N:39 == 8)
		P += 1
		IF CFLAG:N:39 == 3
			CFLAG:N:10 += 50
		ELSEIF CFLAG:N:39 == 5
			CFLAG:N:10 += 10
		ELSEIF CFLAG:N:39 == 6
			CFLAG:N:10 += 50
		ELSEIF CFLAG:N:39 == 165
			CFLAG:N:10 += 70
		ELSEIF CFLAG:N:39 == 8
			CFLAG:N:10 += 20
		ENDIF
		SIF CFLAG:N:10 > 99
			CFLAG:N:10 = 99
		SIF CFLAG:M:10 < CFLAG:N:10
			M = N
	ENDIF
REND
;売却キャラに思慕を寄せているキャラにストレス値加算
IF P
	PRINTL 
	PRINTFORM %CALLNAME%と仲の良かった%CALLNAME:M%
	SIF P > 1
		PRINT 達
	PRINTW の様子がおかしい
	PRINTFORM %CALLNAME%が売却されたという事実に、%CALLNAME:M%
	SIF P > 1
		PRINT 達
	PRINT の精神は
	SIF CFLAG:M:10 > 80
		PRINT とても
	PRINTW 不安定になっているようだ
	IF CFLAG:M:10 >= 100
		TFLAG:13 = 52
		;입上分岐フラグ
		TFLAG:53 = 1
		LOCAL = TARGET
		LOCAL:1 = ASSI
		ASSI = TARGET
		TARGET = M
		M = LOCAL
		CALL KOJO_MESSAGE_EVENT
		M = TARGET
		TARGET = LOCAL
		ASSI = LOCAL:1
		;부적があれば復活
		IF CFLAG:M:693 || CFLAG:M:694
			TFLAG:13 = 72
			CALL KOJO_MESSAGE_EVENT
			IF CFLAG:M:694
				CFLAG:M:10 = 0
				CFLAG:M:694 -= 1
			ELSE
				CFLAG:M:10 = 90
				CFLAG:M:693 -= 1
			ENDIF
		ELSE
			TALENT:M:친애 = 0
			TALENT:M:붕괴 = 1
			TALENT:M:상사상애 = 0
			CFLAG:M:40 = 0
			CFLAG:M:41 = -1
			CFLAG:M:10 = 0
		ENDIF
	ENDIF
ENDIF
M = 0
N = 0
;売却キャラを思慕・畏怖対象としていたキャラから素質消去と依存値リセット
REPEAT CHARANUM
	N = COUNT
	IF CFLAG:N:41 == NO:TARGET && (CFLAG:N:39 == 3 || CFLAG:N:39 == 6 || CFLAG:N:39 == 165 || CFLAG:N:39 == 5 || CFLAG:N:39 == 8)
		PRINTFORM %CALLNAME:N%は【
		IF CFLAG:N:39 == 3
			PRINT 연모
			M = 23
		ELSEIF CFLAG:N:39 == 5
			PRINT 복종
			M = 15
		ELSEIF CFLAG:N:39 == 6
			PRINT 친애
			M = 54
		ELSEIF CFLAG:N:39 == 165
			PRINT 상사상애
			M = 54
		ELSEIF CFLAG:N:39 == 8
			PRINT 예속
			M = 38
		ENDIF
		PRINTW 】を失い、【실의】を得た
		;EASYで変動無し、NOMALで小変動、HARDで変動
		IF FLAG:4 == 2
			M += 12
		ELSEIF FLAG:4 == 3
			M += 24
		ENDIF
		;애정경험
		EXP:N:애정경험 = 0
		;순종
		ABL:N:순종 = 0
		;【실의】
		TALENT:N:실의 = M
		;依存度
		CFLAG:N:2 = 0

		CFLAG:N:40 = 0
		CFLAG:N:41 = -1
		TALENT:N:연모 = 0
		TALENT:N:복종 = 0
		TALENT:N:친애 = 0
		TALENT:N:상사상애 = 0
		TALENT:N:붕괴 = 0
		;思慕対象のキャラ番号を記録
		CFLAG:N:38 = NO:TARGET
	ENDIF
REND
;共犯者から思慕・執心対象リセット
REPEAT CHARANUM
	N = COUNT
	IF CFLAG:N:41 == NO:TARGET && CFLAG:N:1 == 3
		CFLAG:N:41 = -1
		CFLAG:N:40 = 0
	ENDIF
REND


;共犯者の執心対象セット
@YEARN_FOR
;マスターの行った調教を算出する
A = 0
B = 0
C = 1099 + NO:MASTER
D = 0
;ターゲットの、マスターからの조교경험が1000以上でないとダメ
SIF CFLAG:TARGET:C < 1000
	RETURN
;すでに執着固定したキャラがいる（プロミスリングを贈っている）とダメ
SIF CFLAG:MASTER:140
	RETURN
REPEAT CHARANUM
	B = COUNT
	A += CFLAG:B:C
REND
D = A
;全조교경험の70%以上を占めると執心対象としてセットされる
TIMES A , 0.7
;全조교경험の50%以下になると執心対象が解除される
TIMES D , 0.50
B = -100 - (NO:TARGET)
;ターゲットに対しての調教が、マスターが行った全調教の70%以上である
IF A < CFLAG:TARGET:C && CFLAG:MASTER:41 <= 0 && CFLAG:MASTER:41 != B
	PRINTFORMW ……%CALLNAME%を見ていると、%CALLNAME:MASTER%の心は何故か騒ぐようだ
	PRINTL 
	PRINTL [0]思い過ごしだ
	PRINTL [1]認める
	$INPUT_LOOP
	INPUT
	;セットしないと、-(100+キャラ番号)が代入されて続けて同じキャラに選択肢が出ないようになる
	IF RESULT == 0
		PRINTFORMW %CALLNAME:MASTER%はその感情を忘れることにした……
		CFLAG:MASTER:41 = B
	;セットすると、キャラ番号が代入されて以降執心対象として扱われる（思慕・畏怖対象と扱いは同様）
	ELSEIF RESULT == 1
		PRINTFORMW %CALLNAME:MASTER%はその感情を認めることにした……
		CFLAG:MASTER:41 = NO:TARGET
		CFLAG:MASTER:40 = 1
		CFLAG:MASTER:39 = 6
		;調教者変更用の
		IF CFLAG:TARGET:39 == 6 && CFLAG:TARGET:41 == NO:MASTER && TALENT:MASTER:상사상애 == 0 && TALENT:상사상애 == 0
			PRINTL 
			TFLAG:13 = 53
			CALL KOJO_MESSAGE_EVENT
			SIF TARGET < 0
				RETURN 0
			PRINTFORMW %NAME:MASTER%と%NAME:TARGET%は【상사상애】になった
			TALENT:MASTER:상사상애 = 1
			TALENT:상사상애 = 1
			CFLAG:39 = 165
			CFLAG:MASTER:39 = 165
		ENDIF
	ELSE
		GOTO INPUT_LOOP
	ENDIF
ELSEIF D > CFLAG:TARGET:C && CFLAG:MASTER:41 == NO:TARGET && CFLAG:MASTER:1 == 3
	PRINTFORMW %CALLNAME:MASTER%は%CALLNAME%への執心を失った……
	CFLAG:MASTER:41 = -1
	CFLAG:MASTER:40 = 0
	;調教者変更用の
	IF CFLAG:TARGET:39 == 165 && CFLAG:TARGET:41 == NO:MASTER && TALENT:MASTER:상사상애 && TALENT:상사상애 
		PRINTL 
		SIF TARGET < 0
			RETURN 0
		PRINTFORMW %NAME:MASTER%と%NAME:TARGET%は【상사상애】を失った
		TALENT:MASTER:상사상애 = 0
		TALENT:상사상애 = 0
		CFLAG:39 = 6
		CFLAG:MASTER:39 = 0
	ENDIF
ENDIF

