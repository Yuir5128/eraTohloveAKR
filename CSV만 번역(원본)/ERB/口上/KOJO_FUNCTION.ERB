;;≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡
;입上向け関数ファイル
;多くはeraSQRから移植させていただきました。感謝
;≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡
;-------------------------------------------------
;未読判定関数@FIRSTTIME
;式中で使用する関数です。およそVARSIZE(LOCALS)^2個のイベントを登録出来ます
;ARGSはイベント番号、ARGは真だと初回判定を更新しない、ARG:1はキャラ登録番号（省略するとTARGET,-1ならMASTER）
;{イベント番号}/　という文字列をCSTR:20にぶっこんでいきます
;-------------------------------------------------
@FIRSTTIME(ARGS,ARG,ARG:1)
#FUNCTION
;ARG:1の変換
ARG:1 = ARG:1 ? ARG:1 # TARGET
ARG:1 = ARG:1 == -1 ? MASTER # ARG:1
SIF CSTR:(ARG:1):20 == ""
	CSTR:(ARG:1):20 = /

LOCALS = /%ARGS%/
IF STRCOUNT(CSTR:(ARG:1):20,LOCALS)
	RETURNF 0
ELSEIF ARG
	RETURNF 1
ENDIF
;初回判定更新
CSTR:(ARG:1):20 = %CSTR:(ARG:1):20%%ARGS%/
RETURNF 1

;-------------------------------------------------
;未読判定関数@ONCE
;式中で使用する関数です
;ARGSはイベント番号、ARGは真だと初回判定を更新しない、ARG:1は真だとフラグのオールリセットを行うARG:2はキャラ登録番号（省略するとTARGET,-1ならMASTER）
;{イベント番号}/　という文字列をCSTR:21にぶっこんでいきます
;-------------------------------------------------
@ONCE(ARGS,ARG,ARG:1,ARG:2)
#FUNCTION
;ARG:2の変換
ARG:2 = ARG:2 ? ARG:2 # TARGET
ARG:2 = ARG:2 == -1 ? MASTER # ARG:2
IF ARG:1
	CSTR:(ARG:2):21 = 
	RETURNF 0
ENDIF
SIF CSTR:(ARG:2):21 == ""
	CSTR:(ARG:2):21 = /

LOCALS = /%ARGS%/
IF STRCOUNT(CSTR:(ARG:2):21,LOCALS)
	RETURNF 0
ELSEIF ARG
	RETURNF 1
ENDIF
;初回判定更新
CSTR:(ARG:2):21 = %CSTR:(ARG:2):21%%ARGS%/
RETURNF 1
;フラグリセット処理
@EVENTTRAIN
FOR LOCAL,0,CHARANUM
	CSTR:LOCAL:21 = 
NEXT

;-------------------------------------------------
;페니스判定関数@PENIS
;式中で使用する関数です。페니스がある場合1を返します
;引数0は対象の登録番号。しょうもない中身
;-------------------------------------------------
@PENIS(ARG)
#FUNCTION
RETURNF TALENT:ARG:반음반양 || TALENT:ARG:후타나리 || TALENT:ARG:남자 || TALENT:ARG:쉬메일

;-------------------------------------------------
;페니스判定関数２@PENIS2
;式中で使用する関数です。페니스（페니스밴드可）がある場合1を返します
;引数0は対象の登録番号。しょうもない中身
;-------------------------------------------------
@PENIS2(ARG)
#FUNCTION
RETURNF TALENT:ARG:반음반양 || TALENT:ARG:후타나리 || TALENT:ARG:남자 || TALENT:ARG:쉬메일 || ITEM:PBAND

;-------------------------------------------------
;바기나判定関数@VAGINA
;式中で使用する関数です。바기나がある場合1を返します
;引数0は対象の登録番号。しょうもない中身
;-------------------------------------------------
@VAGINA(ARG)
#FUNCTION
RETURNF !TALENT:ARG:남자 && !TALENT:ARG:쉬메일

;-------------------------------------------------
;가슴判定関数@BREAST
;式中で使用する関数です。おっぱいがある場合1を返します
;引数0は対象の登録番号。しょうもない中身
;-------------------------------------------------
@BREAST(ARG)
#FUNCTION
RETURNF MALE(ARG) == 0 && !TALENT:ARG:절벽

;-------------------------------------------------
;長머리카락判定関数@RONGE
;式中で使用する関数です。長い머리카락を持っている場合1を返します
;引数0は対象の登録番号。헤어잡で使用しますが他に使い道ある？
;-------------------------------------------------
@RONGE(ARG)
#FUNCTION
SELECTCASE NO:ARG
	;小여우、이마노츠루기、にっかり、数珠丸、鯰尾、乱、村正、톤보키리、大般若、小竜、江雪、宗三、小夜、加州、安定、兼さん、蜂須賀、浦島、不動、시시오、코가라스마루、太郎、次郎、치요가네마루、네네키리마루、챠탄나키리、古今、日光、則宗、姫鶴、福島、칠성검
	CASE 5,11,17,19,27,45,63,65,75,77,79,81,83,85,87,91,101,103,120,122,124,132,134,156,162,170,182,188,198,202,204,206
		RETURNF 1
	;극
	;이마노츠루기극、にっかり극、鯰尾극、村正극、톤보키리극、江雪극、宗三극、小夜극、加州극、安定극、兼さん극、蜂須賀극、不動극、시시오극、치요가네마루극
	CASE 12,20,44,64,66,80,82,84,86,88,92,102,121,123,157
		RETURNF 1
	CASEELSE
		RETURNF 0
ENDSELECT

;-------------------------------------------------
;確率判定関数@PERCENT
;式中で使用する関数です。引数％の確率判定を行います。通れば1を返します
;-------------------------------------------------
@PERCENT(ARG)
#FUNCTION
RETURNF (ARG - RAND:100) > 0

;-------------------------------------------------
;関数名:BIND_CHECK_A
;概　要:拘束チェック関数、道具によるもののみ
;引　数:ARG…部位(0.四肢/1.目/2.입/??.鼻/??.耳/??.손/??.발)
;戻り値:該当部位が拘束されて使用不可になっているかどうか
;備  考:式中関数
;拘束をチェックする関数。ちょっと試作品
;拘束손段を후から追加する場合に、その都度입上側を更新するのが嫌なので作成
;テンプレで使うかどうかはしばらく様子見
;-------------------------------------------------
@BIND_CHECK_A(ARG)
#FUNCTION
SELECTCASE ARG
	;四肢
	CASE 0
		;밧줄、촉수구속
		RETURNF TEQUIP:밧줄
	;視覚
	CASE 1
		;아이마스크
		RETURNF TEQUIP:아이마스크
	;입
	CASE 2
		;볼개그、촉수구욕、助손입辱
		RETURNF TEQUIP:볼개그 || TEQUIP:촉수구욕
ENDSELECT
RETURNF 0

;-------------------------------------------------
;関数名:BIND_CHECK_B
;概　要:拘束チェック関数、四肢欠損を含む
;引　数:ARG…部位(0.四肢/1.目/2.입/??.鼻/??.耳/5.손/6.발)
;戻り値:該当部位が拘束されて使用不可になっているかどうか
;備  考:式中関数
;拘束をチェックする関数。ちょっと試作品
;拘束손段を후から追加する場合に、その都度입上側を更新するのが嫌なので作成
;テンプレで使うかどうかはしばらく様子見
;-------------------------------------------------
@BIND_CHECK_B(ARG)
#FUNCTION
SELECTCASE ARG
	;四肢
	CASE 0
		;밧줄、촉수구속、달마
		RETURNF TEQUIP:밧줄 || TALENT:달마
	;視覚
	CASE 1
		;아이마스크
		RETURNF TEQUIP:아이마스크
	;입
	CASE 2
		;볼개그、촉수구욕、助손입辱
		RETURNF TEQUIP:볼개그 || TEQUIP:촉수구욕
	;손
	CASE 5
		;밧줄、촉수구속、달마、양팔결손
		RETURNF TEQUIP:밧줄 || TALENT:달마 || TALENT:양팔결손
	;脚
	CASE 6
		;밧줄、촉수구속、달마、양팔결손
		RETURNF TEQUIP:밧줄 || TALENT:달마 || TALENT:양다리결손
ENDSELECT
RETURNF 0

;-------------------------------------------------
;関数名:CCHECK
;概　要:Ｃに気持ちいいものが付いてるか判定
;引　数:ARG…キャラ登録番号
;戻り値:
;備  考:式中関数
;-------------------------------------------------
@CCHECK(ARG)
#FUNCTION
RETURNF TEQUIP:ARG:클리캡 || TEQUIP:ARG:전동오나홀 || TEQUIP:ARG:음핵클립 || TEQUIP:ARG:카테터 || TEQUIP:ARG:요도벌룬

;-------------------------------------------------
;関数名:VCHECK
;概　要:Ｖに気持ちいいものが付いてるか判定
;引　数:ARG…キャラ登録番号
;戻り値:
;備  考:式中関数
;-------------------------------------------------
@VCHECK(ARG)
#FUNCTION
RETURNF TEQUIP:ARG:바이브 || TEQUIP:ARG:로터삽입 || TEQUIP:ARG:촉수질내산란

;-------------------------------------------------
;関数名:ACHECK
;概　要:Ａに気持ちいいものが付いてるか判定
;引　数:ARG…キャラ登録番号
;戻り値:
;備  考:式中関数
;-------------------------------------------------
@ACHECK(ARG)
#FUNCTION
RETURNF TEQUIP:ARG:애널바이브 || TEQUIP:ARG:애널비즈 || TEQUIP:ARG:로터A삽입 || TEQUIP:ARG:확장벌룬 || TEQUIP:ARG:촉수장내산란

;-------------------------------------------------
;関数名:BCHECK
;概　要:Ｂに気持ちいいものが付いてるか判定
;引　数:ARG…キャラ登録番号
;戻り値:
;備  考:式中関数
;-------------------------------------------------
@BCHECK(ARG)
#FUNCTION
RETURNF TEQUIP:ARG:니틀캡 || TEQUIP:ARG:유두클립 || TEQUIP:ARG:착유기 || TEQUIP:ARG:촉수유선삽입


;-------------------------------------------------
;関数名:ALLCHECK
;概　要:ＣＶＡＢどれかに気持ちいいものが付いてるか判定
;引　数:ARG…キャラ登録番号
;戻り値:
;備  考:式中関数
;-------------------------------------------------
@ALLCHECK(ARG)
#FUNCTION
RETURNF CCHECK(ARG) || VCHECK(ARG) || ACHECK(ARG) || BCHECK(ARG)


;-------------------------------------------------
;関数名:ZUIKI
;概　要:가려움증약が効果を発揮する場所についてるかチェック
;가슴、페니스、바기나、질내、애널のどこか
;引　数:ARG…キャラ登録番号
;戻り値:
;備  考:式中関数
;-------------------------------------------------
@ZUIKI(ARG)
#FUNCTION
RETURNF STAIN:ARG:가슴 & 1024 || STAIN:ARG:페니스 & 1024 || STAIN:ARG:바기나 & 1024 || STAIN:ARG:질내 & 1024 || STAIN:ARG:애널 & 1024


;-------------------------------------------------
;변의や뇨의があるかチェック
;引　数:ARG…種類(0.両方/1.뇨의のみ/2.변의のみ)
;戻り値:奴隷が변의や뇨의を感じているか
;備  考:式中関数
;-------------------------------------------------
@BASE46(ARG)
#FUNCTION
SELECTCASE ARG
	;どっちか
	CASE 0
		RETURNF BASE:뇨의 >= 60 || BASE:변의 >= 60
	;뇨의のみ
	CASE 1
		RETURNF BASE:뇨의 >= 60
	;변의のみ
	CASE 2
		RETURNF BASE:변의 >= 60
	;両方
	CASE 3
		RETURNF BASE:뇨의 >= 60 && BASE:변의 >= 60
ENDSELECT
RETURNF 0

;-------------------------------------------------
;関数名:A_BIND_CHECK
;概　要:변의がある状態でＡが塞がってるかどうか見る（排便可能か否かが判定可能）
;引　数:ARG…キャラ登録番号
;戻り値:
;備  考:式中関数
;-------------------------------------------------
@A_BIND_CHECK(ARG)
#FUNCTION
RETURNF BASE:ARG:변의 >= 60 && (TEQUIP:ARG:애널비즈 || TEQUIP:ARG:애널바이브 || TEQUIP:ARG:확장벌룬)

;-------------------------------------------------
;関数名:U_BIND_CHECK
;概　要:뇨의がある状態でＵが塞がってるかどうか見る（排尿可能か否かが判定可能）
;引　数:ARG…キャラ登録番号
;戻り値:
;備  考:式中関数
;-------------------------------------------------
@U_BIND_CHECK(ARG)
#FUNCTION
RETURNF BASE:ARG:뇨의 >= 60 && TEQUIP:ARG:요도벌룬


;-------------------------------------------------
;関数名:BOTTOMS
;概　要:하의の裾の形状を判定。
;	5はズボン状のもの。손が入らず、捲ることもできない。
;	4は短いズボン状のもの。손が入らず、捲ることもできないが、短いので太ももまで触れる。
;	3はボンテージスーツ限定。チャックで開閉できる。
;	2は似非スカート。손が入り、捲ることもできるが、スカートではない。
;	1はスカート。손が入り、捲ることもできる。
;引　数:ARG…キャラ登録番号
;戻り値:
;備  考:式中関数
;-------------------------------------------------
@BOTTOMS(ARG)
#FUNCTION
;そもそも하의を着てなければ判定しない
IF TEQUIP:ARG:하의 == 0
	RETURNF 0
ELSE
	SELECTCASE TEQUIP:ARG:하의
		;ズボン
		;ズボン、ジャージ、袴、オールインワン
		CASE 12,17,18,25
			RETURNF 5
		;ショートパンツ
		;ショートパンツ、ハーフパンツ、ブルマー、スパッツ
		CASE 13,14,15,16
			RETURNF 4
		;ボンテージスーツ
		CASE 10
			RETURNF 3
		;似非スカート
		;チャイナドレス、浴衣、着物、巫女服
		CASE 3,7,8,9
			RETURNF 2
		;スカート
		;ワンピース、カクテルドレス、ウェディングドレス、メイド服、ナース服、スカート
		CASE 1,2,4,5,6,11
			RETURNF 1
	ENDSELECT
ENDIF

;-------------------------------------------------
;関数名:TOPS
;概　要:전신상의の開きの形状を判定。1なら前は開かない
;引　数:ARG…キャラ登録番号
;戻り値:
;備  考:式中関数
;-------------------------------------------------
@TOPS(ARG)
#FUNCTION
;そもそも전신상의を着てなければ判定しない
IF TEQUIP:ARG:전신상의 == 0
	RETURNF 0
ELSE
	SELECTCASE TEQUIP:ARG:전신상의
		CASE 3,4,5,6
			RETURNF 1
	ENDSELECT
ENDIF

;-------------------------------------------------
;関数名:KIMONO
;概　要:着ているものが和服かどうか判定　1なら和服、0なら洋服
;引　数:ARG…キャラ登録番号
;戻り値:
;備  考:式中関数
;巫女服、浴衣、着物、作務衣、法衣、袴、和服、狩衣、胴衣
;-------------------------------------------------
@KIMONO(ARG)
#FUNCTION
;そもそも상의、하의どちらも着てなければ判定しない
IF TEQUIP:ARG:상의 == 0 && TEQUIP:ARG:하의 == 0
	RETURNF 0
ELSE
	SELECTCASE TEQUIP:ARG:상의
		;浴衣、着物、巫女服、和服、作務衣、狩衣
		CASE 7,8,9,18,20,21
			RETURNF 1
	ENDSELECT
	SELECTCASE TEQUIP:ARG:하의
		;浴衣、着物、巫女服、袴
		CASE 7,8,9,18
			RETURNF 1
	ENDSELECT
ENDIF

;-------------------------------------------------
;関数名:CODE_CHECK
;概　要:着ている상의が外出可能かどうか判定
;引　数:ARG…キャラ登録番号
;戻り値:
;備  考:式中関数
;-------------------------------------------------
@CODE_CHECK(ARG)
#FUNCTION
SIF TEQUIP:ARG:상의
	RETURNF 1
SELECTCASE TEQUIP:ARG:속옷（위）
	;インナー、タンクトップ、シャツ、Ｔシャツ、大きめのシャツ
	CASE 7,8,9,10,11
		RETURNF 1
ENDSELECT
SELECTCASE TEQUIP:ARG:전신상의
	;上着、パーカー、ローブ、カソック、法衣
	CASE 1,2,7,8,9
		RETURNF 1
ENDSELECT

;-------------------------------------------------
;関数名:TEQUIP_FEMALE
;概　要:着ているものが男物か、女物か判定。女物なら1を返す
;引　数:ARG…キャラ登録番号
;戻り値:
;備  考:式中関数
;-------------------------------------------------
@TEQUIP_FEMALE(ARG)
#FUNCTION
;そもそも상의、하의どちらも着てなければ判定しない
IF TEQUIP:ARG:상의 == 0 && TEQUIP:ARG:하의 == 0
	RETURNF 0
ELSE
	SELECTCASE TEQUIP:ARG:상의
		;ワンピース、カクテルドレス、チャイナドレス、ウェディングドレス、メイド服、ナース服、巫女服
		CASE 1,2,3,4,5,6,9
			RETURNF 1
	ENDSELECT
	SELECTCASE TEQUIP:ARG:하의
		;ワンピース、カクテルドレス、チャイナドレス、ウェディングドレス、メイド服、ナース服、巫女服、スカート
		CASE 1,2,3,4,5,6,9,11
			RETURNF 1
	ENDSELECT
ENDIF



;-------------------------------------------------
;関数名:MASTER_SEARCH2
;概　要:引数０のキャラの思慕・畏怖対象が引数１かどうかを判定する関数
;引　数:ARG:0,ARG:1…キャラ登録番号
;戻り値:
;備  考:式中関数
;-------------------------------------------------
@MASTER_SEARCH2(ARG:0,ARG:1)
#FUNCTION
SIF CFLAG:(ARG:0):40 && CFLAG:(ARG:0):41 == NO:(ARG:1)
	RETURNF 1
RETURNF 0

;-------------------------------------------------
;関数名:KANRAKU
;概　要:陥落ルートを調べる関数
;引　数:ARG:0…キャラ登録番号
;戻り値:0=未陥落1=연모ルート2=음란ルート3=복종ルート
;4=연모陥落で思慕対象以外との調教/被調教状態5=복종陥落で畏怖対象以外との調教/被調教状態
;備  考:式中関数
;-------------------------------------------------
@KANRAKU(ARG)
#FUNCTION
IF CFLAG:ARG:41 < 0 && CFLAG:ARG:39 != 4 && CFLAG:ARG:39 != 7
	RETURNF 0
ELSEIF TALENT:ARG:붕괴
	RETURNF 0
ELSE
	SELECTCASE CFLAG:ARG:39
		CASE 3,6,165
			IF !TALENT:ARG:연모 && !TALENT:ARG:친애
				RETURNF 4
			ELSE
				RETURNF 1
			ENDIF
		CASE 4,7
			RETURNF 2
		CASE 5,8
			IF !TALENT:ARG:복종 && !TALENT:ARG:예속
				RETURNF 5
			ELSE
				RETURNF 3
			ENDIF
	ENDSELECT
ENDIF
RETURNF 0

;-------------------------------------------------
;関数名:KANRAKU2
;概　要:陥落ルートを調べる関数（調教者が思慕・畏怖対象かは問わない）
;引　数:ARG:0…キャラ登録番号
;戻り値:0=未陥落1=연모ルート2=음란ルート3=복종ルート
;備  考:式中関数
;		eraelevenGOで追加した関数です
;-------------------------------------------------
@KANRAKU2(ARG)
#FUNCTION
IF CFLAG:ARG:41 < 0 && CFLAG:ARG:39 != 4 && CFLAG:ARG:39 != 7
	RETURNF 0
ELSEIF TALENT:ARG:붕괴
	RETURNF 0
ELSE
	SELECTCASE CFLAG:ARG:39
		CASE 3,6,165
			RETURNF 1
		CASE 4,7
			RETURNF 2
		CASE 5,8
			RETURNF 3
	ENDSELECT
ENDIF
RETURNF 0

;-------------------------------------------------
;関数名:SEXPLAY
;概　要:調教対象がしているセックス
;引　数:ARG:0…キャラ登録番号
;戻り値:1=セックス2=Ａセックス3=逆セックス中4=逆Ａセックス
;備  考:式中関数
;-------------------------------------------------
@SEXPLAY(ARG)
#FUNCTION
SELECTCASE SELECTCOM
	;セックス、乱れ牡丹、挿入Ｇスポ、挿入子宮입、二輪刺し、스팽킹セックス、스팽킹애널
	CASE 100 TO 106,506 TO 508,511,513,517
		RETURNF 1
	;애널セックス
	CASE 110 TO 116,512,514,518
		RETURNF 2
	;逆セックス
	CASE 260,261,262
		RETURNF 3
	;逆애널セックス
	CASE 263,264,265
		RETURNF 4
	;騎乗乳房자위、駅弁(TFLAG:705が0ならＶ、1ならＡ)
	CASE 509,510
		RETURNF TFLAG:705 + 1
	;サンドイッチ、連結、サンドイッチさせる、助손をサンドイッチ
	CASE 515,519,520,521
		IF TFLAG:40 == 1
			RETURNF 1
		ELSEIF TFLAG:40 == 2
			RETURNF 2
		ELSE
			RETURNF 0
		ENDIF
	CASEELSE
		RETURNF 0
ENDSELECT

;-------------------------------------------------
;関数名:DOUSEI
;概　要:二人が同性か判定する関数
;引　数:ARG,ARG:1…キャラ登録番号
;戻り値:同性なら1、異性なら0
;備  考:式中関数
;-------------------------------------------------
@DOUSEI(ARG,ARG:1)
#FUNCTION
SIF (MALE(ARG) && !MALE(ARG:1)) || (!MALE(ARG) && MALE(ARG:1))
	RETURNF 0
RETURNF 1

;-------------------------------------------------
;関数名:COMMON_KOJO_EX
;概　要:汎用コマンド입上の分類
;引　数:
;戻り値:1=2=3=4=
;備  考:
;-------------------------------------------------
@COMMON_KOJO_EX
SELECTCASE SELECTCOM
	;아무것도안한다
	CASE 2,310 TO 312,
		;快楽系道具が付いてるとき羞恥系
		IF CCHECK(TARGET) || VCHECK(TARGET) || ACHECK(TARGET) || BCHECK(TARGET)
			RETURN 7
		;その他
		ELSE
			RETURN 0
		ENDIF
	;薬　酒
	CASE 301 TO 306
		RETURN 13
	;場所移動
	CASE 313,315,320,323,207,321
		;場所移動(部屋に戻る)
		IF TFLAG:54
			RETURN 19
		;場所移動(部屋以外に移動)
		ELSE
			RETURN 18
		ENDIF
	;拘束
	CASE 131 TO 134
		;道具を外す(拘束)
		IF TFLAG:54
			RETURN 12
		;ついてなければその他
		ELSE
			RETURN 0
		ENDIF
	;밧줄、촉수구속
	CASE 130,351
		;道具を外す(拘束)
		IF TFLAG:54
			RETURN 12
		;快楽系道具が付いてるとき고통快楽系
		ELSEIF CCHECK(TARGET) || VCHECK(TARGET) || ACHECK(TARGET) || BCHECK(TARGET)
			RETURN 10
		;ついてなければ加虐系
		ELSE
			RETURN 8
		ENDIF
	;主人마조系
	CASE 280 TO 284
		RETURN 14
	;スカ系
	CASE 180 TO 185,285,286,352,527
		RETURN 15
	;猟奇
	CASE 156,383,390 TO 392
		RETURN 16
	;屈辱
	CASE 85 TO 87,224
		RETURN 17
	;Ｃ快感
	CASE 0,20 TO 23,30 TO 35,240 TO 242,246,342,343,380,400,401
		;道具を外す(快感)
		IF TFLAG:54
			RETURN 11
		ELSE
			RETURN 1
		ENDIF
	;Ｖ快感
	CASE 40 TO 43,344,362,363,403,405,500
		;道具を外す(快感)
		IF TFLAG:54
			RETURN 11
		ELSE
			RETURN 2
		ENDIF
	;Ａ快感
	CASE 50 TO 54,344,364,404,406,501
		;道具を外す(快感)
		IF TFLAG:54
			RETURN 11
		ELSE
			RETURN 3
		ENDIF
	;Ｂ快感
	CASE 10 TO 14,17,18,341,360,402,502
		;道具を外す(快感)
		IF TFLAG:54
			RETURN 11
		ELSE
			RETURN 4
		ENDIF
	;性交系
	CASE 100 TO 119,506 TO 510
		RETURN 5
	;逆性交系
	CASE 260 TO 265
		RETURN 6
	;羞恥
	CASE 3 TO 8,36,226,227,228,270,271,314,410,441,505
		RETURN 7
	;加虐
	CASE 72,140 TO 145,153 TO 155,157
		;快楽系道具が付いてるとき고통快楽系
		IF CCHECK(TARGET) || VCHECK(TARGET) || ACHECK(TARGET) || BCHECK(TARGET)
			RETURN 10
		;ついてなければ加虐系
		ELSE
			RETURN 8
		ENDIF
	;奉仕快楽
	CASE 73 TO 76,84,243,245,317,421,422,503,504,528
		RETURN 9
	;고통快楽
	CASE 15,16,24,25,44,55,56,60 TO 62,160 TO 163,322,361,365 TO 374,381,382,511 TO 514,517,518
		RETURN 10
	;奉仕
	CASE 70,71,77,80 TO 83,220 TO 223,225,420,516
		;快楽系道具が付いてるとき奉仕快楽系
		IF CCHECK(TARGET) || VCHECK(TARGET) || ACHECK(TARGET) || BCHECK(TARGET)
			RETURN 9
		;その他
		ELSE
			RETURN 20
		ENDIF
	CASEELSE
		RETURN 0
ENDSELECT

;====================================================================================
;@ACTIVECOM
;調教対象が能動的に行うコマンドかどうかを判定する関数です。個別汎用喘ぎの分岐用。分類は独断と偏見に基づいています。
;引　数:ARG:0…キャラ登録番号
;戻り値:1=問答無用　2=촉수系　3=助손命令系　4=逆レイプ系　5=奉仕快楽系　6=スカ系（する方）　7=자위系・放置系　8=奉仕系・嗜虐系　9=연모系
;備  考:式中関数
;えらいれ鬼道입上からお借りしました
;====================================================================================
@ACTIVECOM(ARG)
#FUNCTION
SELECTCASE ARG
	;問答無用
	CASE 0,1,10 TO 18,20 TO 25,30 TO 36,40 TO 44,50 TO 56,60 TO 62,72,81,83,84,100 TO 105,110 TO 115,130 TO 145,151,153 TO 163,181,240 TO 242,246,314,318,322,380 TO 383,390 TO 392,410,412,500 TO 502,505 TO 508,510 TO 515,517,518,527
		RETURNF 1
	;촉수系
	CASE 340 TO 374
		RETURNF 2
	;助손命令系
	CASE 400 TO 407
		RETURNF 3
	;逆レイプ系
	CASE 260 TO 265
		RETURNF 4
	;奉仕快楽系
	CASE 76,106,116,243 TO 245,422,430,431,503,504,509,528
		RETURNF 5
	;スカ系（する方）
	CASE 180,182,183
		RETURNF 6
	;자위系・放置系
	CASE 2 TO 8,226,227,228,270,271,285,286,300 TO 304,310 TO 313,315,316,320,321,323,411,440,441
		RETURNF 7
	;奉仕系・嗜虐系
	CASE 70,71,73 TO 75,77,80,82,85 TO 87,150,152,184,185,220 TO 225,280 TO 284,317,420,421,516
		RETURNF 8
	;コミュニケーション系
	CASE 200 TO 207,319
		RETURNF 9
	CASEELSE
		RETURNF 0
ENDSELECT

