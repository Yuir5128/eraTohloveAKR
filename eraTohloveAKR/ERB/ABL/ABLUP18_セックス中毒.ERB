;섹스중독가부
@ISABLUP18
;연모, 친애, 음란, 창부는 MAXLV10
IF ABL:섹스중독 >= 10
	RETURN 0
ELSEIF ABL:섹스중독 >= 5 && TALENT:연모 == 0 && TALENT:친애 == 0 && TALENT:음란 == 0 && TALENT:창부 == 0
	RETURN 0
ELSEIF EXP:성교경험  == 0
	RETURN 0
ENDIF

CALL DECIDE_ABLUP18

IF Z == 0
	RETURN 0
ELSE
	RETURN 1
ENDIF


;섹스중독
@ABLUP18
DRAWLINE
;연모, 친애, 음란, 창부는 MAXLV10
IF ABL:섹스중독 >= 10
	PRINTW 이미 MAX입니다
	RETURN 0
ELSEIF ABL:섹스중독 >= 5 && TALENT:연모 == 0 && TALENT:친애 == 0 && TALENT:음란 == 0 && TALENT:창부 == 0
	PRINTW 이미 MAX입니다
	RETURN 0
ENDIF

CALL DECIDE_ABLUP18


;LV2로부터 3에 올릴 때는이상경험필요(소질:[중독되기쉬움][섹스광]라면 무시할 수 있다)
IF ABL:섹스중독 == 2 && TALENT:중독되기쉬움 == 0 && TALENT:섹스광 == 0
	PRINTFORML %EXPNAME:50%있어, 한편
;LV3로부터 4에 올릴 때는이상경험 2 이상 필요(소질:[중독되기쉬움][섹스광]라면 무시할 수 있다)
ELSEIF ABL:섹스중독 == 3 && TALENT:중독되기쉬움 == 0 && TALENT:섹스광 == 0
	PRINTFORML %EXPNAME:50%2이상, 한편
;LV4로부터 5에 올릴 때는이상경험 3 이상 필요(소질:[중독되기쉬움][섹스광]라면 무시할 수 있다)
ELSEIF ABL:섹스중독 == 4 && TALENT:중독되기쉬움 == 0 && TALENT:섹스광 == 0
	PRINTFORML %EXPNAME:50%3이상, 한편
;LV8로부터 9에 올릴 때는이상경험 7 이상 필요(소질:[중독되기쉬움][섹스광]라면 무시할 수 있다)
ELSEIF ABL:섹스중독 == 8 && TALENT:중독되기쉬움 == 0 && TALENT:섹스광 == 0
	PRINTFORML %EXPNAME:50%7이상, 한편
ENDIF

;V감각이 필요
PRINTFORML %ABLNAME:4%{ABL:섹스중독 + 1}LV이상, 한편

PRINTFORM [0] - %PALAMNAME:6%의 구슬×{A}, %PALAMNAME:1%의 구슬×{B}, %PALAMNAME:7%의 구슬×{C}, %EXPNAME:4%{D}이상……

IF I == 0
	PRINT OK
ELSE
	SIF I & 1
		PRINT 구슬부족 
	SIF I & 2
		PRINT 경험부족 
	SIF I & 4
		PRINT 능력불발 
ENDIF
PRINTL 

;A감각이 필요
PRINTFORML %ABLNAME:5%{ABL:섹스중독 + 1}LV이상, 한편

PRINTFORM [1] - %PALAMNAME:6%의 구슬×{A}, %PALAMNAME:2%의 구슬×{B}, %PALAMNAME:7%의 구슬×{C}, %EXPNAME:4%{D}이상……
IF J == 0
	PRINT OK
ELSE
	SIF J & 1
		PRINT 구슬부족 
	SIF J & 2
		PRINT 경험부족 
	SIF J & 4
		PRINT 능력불발 
ENDIF
PRINTL 

;C감각이 필요
PRINTFORML %ABLNAME:3%{ ABL:섹스중독 + 1}LV이상, 한편

PRINTFORM [2] - %PALAMNAME:6%의 구슬×{A}, %PALAMNAME:0%의 구슬×{B}, %PALAMNAME:7%의 구슬×{C}, %EXPNAME:4%{D}이상……

IF K == 0
	PRINT OK
ELSE
	SIF K & 1
		PRINT 구슬부족 
	SIF K & 2
		PRINT 경험부족 
	SIF K & 4
		PRINT 능력불발 
ENDIF
PRINTL 

PRINTL [100] - 그만둔다


INPUT
IF (RESULT < 0 || RESULT > 2) && RESULT != 100
	RESTART
ELSEIF I != 0 && RESULT == 0
	PRINTL 조건을 채우고 있지 않습니다
	RESTART
ELSEIF J != 0 && RESULT == 1
	PRINTL 조건을 채우고 있지 않습니다
	RESTART
ELSEIF K != 0 && RESULT == 2
	PRINTL 조건을 채우고 있지 않습니다
	RESTART
ELSEIF RESULT == 100
	RETURN 0
ENDIF

ABL:섹스중독 += 1

IF RESULT == 0
	JUEL:욕정 -= A
	JUEL:쾌V -= B
	JUEL:굴복 -= C
ELSEIF RESULT == 1
	JUEL:욕정 -= A
	JUEL:쾌A -= B
	JUEL:굴복 -= C
ELSEIF RESULT == 2
	JUEL:욕정 -= A
	JUEL:쾌C -= B
	JUEL:굴복 -= C
ENDIF

PRINTFORMW %ABLNAME:18%의 레벨이{ABL:섹스중독}가 되었습니다

;섹스중독LVUP 판정
@DECIDE_ABLUP18
;조건별로 OK인가 안 되는가를 기록한다
I = 0
J = 0
K = 0
;능력 업 가능하면 1으로 한다
Z = 0

IF ABL:섹스중독 == 0
	A = 2500
	B = 6000
	C = 1000
	D = 10
ELSEIF ABL:섹스중독 == 1
	A = 5000
	B = 14000
	C = 2500
	D = 25
ELSEIF ABL:섹스중독 == 2
	A = 9000
	B = 27000
	C = 4800
	D = 80
ELSEIF ABL:섹스중독 == 3
	A = 17000
	B = 65000
	C = 10000
	D = 150
ELSEIF ABL:섹스중독 == 4
	A = 30000
	B = 80000
	C = 25000
	D = 250
ELSEIF ABL:섹스중독 == 5
	A = 35000
	B = 100000
	C = 30000
	D = 350
ELSEIF ABL:섹스중독 == 6
	A = 40000
	B = 120000
	C = 35000
	D = 500
ELSEIF ABL:섹스중독 == 7
	A = 45000
	B = 140000
	C = 40000
	D = 600
ELSEIF ABL:섹스중독 == 8
	A = 50000
	B = 160000
	C = 45000
	D = 700
ELSEIF ABL:섹스중독 == 9
	A = 60000
	B = 200000
	C = 50000
	D = 800
ENDIF

;일선을넘지않음
IF TALENT:일선을넘지않음 && (TALENT:연모 == 0 && TALENT:음란 == 0 && TALENT:복종 == 0 && TALENT:친애 == 0 && TALENT:창부 == 0 && TALENT:예속 == 0)
	TIMES A , 2.00
	TIMES B , 2.00
	TIMES C , 2.00
	TIMES D , 2.00
ENDIF
;중독되기쉬움
IF TALENT:중독되기쉬움
	TIMES A , 0.50
	TIMES B , 0.50
	TIMES C , 0.50
	TIMES D , 0.50
ENDIF

;LV2로부터 3에 올릴 때는이상경험필요(소질:[중독되기쉬움][섹스광]라면 무시할 수 있다)
IF ABL:섹스중독 == 2 && TALENT:중독되기쉬움 == 0 && TALENT:섹스광 == 0
	IF EXP:이상경험 == 0
		;이상경험가 불발
		I |= 2
		J |= 2
		K |= 2
	ENDIF
;LV3로부터 4에 올릴 때는이상경험 2 이상 필요(소질:[중독되기쉬움][섹스광]라면 무시할 수 있다)
ELSEIF ABL:섹스중독 == 3 && TALENT:중독되기쉬움 == 0 && TALENT:섹스광 == 0
	IF EXP:이상경험 < 2
		;이상경험가 불발
		I |= 2
		J |= 2
		K |= 2
	ENDIF
;LV4로부터 5에 올릴 때는이상경험 3 이상 필요(소질:[중독되기쉬움][섹스광]라면 무시할 수 있다)
ELSEIF ABL:섹스중독 == 4 && TALENT:중독되기쉬움 == 0 && TALENT:섹스광 == 0
	IF EXP:이상경험 < 3
		;이상경험가 불발
		I |= 2
		J |= 2
		K |= 2
	ENDIF
;LV8로부터 9에 올릴 때는이상경험 7 이상 필요(소질:[중독되기쉬움][섹스광]라면 무시할 수 있다)
ELSEIF ABL:섹스중독 == 8 && TALENT:중독되기쉬움 == 0 && TALENT:섹스광 == 0
	IF EXP:이상경험 < 7
		;이상경험가 불발
		I |= 2
		J |= 2
		K |= 2
	ENDIF
ENDIF

;V감각이 필요
SIF ABL:V감각 < ABL:섹스중독 + 1
	I |= 4
;A감각이 필요
SIF ABL:A감각 < ABL:섹스중독 + 1
	J |= 4
;C감각이 필요
SIF ABL:C감각 < ABL:섹스중독 + 1
	K |= 4


;욕정의 구슬로 올린다
IF JUEL:욕정 < A
	I |= 1
	J |= 1
	K |= 1
ENDIF
;쾌V의 구슬로 올린다
SIF JUEL:쾌V < B
	I |= 1
;쾌A의 구슬로 올린다
SIF JUEL:쾌A < B
	J |= 1
;쾌C의 구슬로 올린다
SIF JUEL:쾌C < B
	K |= 1
;굴복의 구슬로 올린다
IF JUEL:굴복 < C
	I |= 1
	J |= 1
	K |= 1
ENDIF
;성교경험가 필요
IF EXP:성교경험 < D
	I |= 2
	J |= 2
	K |= 2
ENDIF

IF I == 0 || J == 0 || K == 0
	Z = 1
ENDIF

RETURN 1

