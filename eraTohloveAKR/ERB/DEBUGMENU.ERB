@DEBUG_MENU_SHOP
DRAWLINE
IF TARGET >= 0
	PRINTFORM %CALLNAME:TARGET% 조교중　
	CALL LIFE_BAR
ENDIF
SIF ASSI >= 0
	PRINTFORM （조수:%CALLNAME:ASSI%）
DRAWLINE
;
PRINTL 무엇을 합니까?
PRINTL 

PRINTS "의존도 변화 처리　　　　　　"
SIF TARGET >= 0
	PRINTBUTTON "[ 0] 조교 대상", 0
PRINTS " "
PRINTBUTTON "[10] 주인", 10
PRINTL 
PRINTS "스트레스 변화 처리　　　　　"
SIF TARGET >= 0
	PRINTBUTTON "[ 1] 조교 대상", 1
PRINTS " "
PRINTBUTTON "[11] 주인", 11
PRINTL 
PRINTS "함락계 소질 착탈 처리　　　　"
SIF TARGET >= 0
	PRINTBUTTON "[ 2] 조교 대상", 2
PRINTS " "
PRINTBUTTON "[12] 주인", 12
PRINTL 
PRINTS "그 외의 소질 착탈 처리　　　"
SIF TARGET >= 0
	PRINTBUTTON "[ 3] 조교 대상", 3
PRINTS " "
PRINTBUTTON "[13] 주인", 13
PRINTL 
PRINTL [ 4] - 사모 대상자 체크
PRINT [ 5] - 임신확률
IF FLAG:151
	PRINTL 　　　　　　백발 백중
ELSE
	PRINTL 　　　　　　　　통상
ENDIF
PRINTS "주수변화 처리　　　　　　　"
SIF TARGET >= 0
	PRINTBUTTON "[ 6] 조교 대상", 6
PRINTS " "
PRINTBUTTON "[16] 주인", 16
PRINTL 
PRINTS "경험 변화 처리　　　　　　　"
SIF TARGET >= 0
	PRINTBUTTON "[ 7] 조교 대상", 7
PRINTS " "
PRINTBUTTON "[17] 주인", 17
PRINTL 
PRINTS "능력 변화 처리　　　　　　　"
SIF TARGET >= 0
	PRINTBUTTON "[ 8] 조교 대상", 8
PRINTS " "
PRINTBUTTON "[18] 주인", 18
PRINTL 
PRINTS "조교·피조교경험변화 처리"
SIF TARGET >= 0
	PRINTBUTTON "[ 9] 조교 대상", 9
PRINTS " "
PRINTBUTTON "[19] 주인", 19
PRINTL 
PRINTL [31] - FLAG를 표시·변경한다
PRINTS "CFLAG를 표시·변경한다　　 "
SIF TARGET >= 0
	PRINTBUTTON "[32] 조교 대상", 32
PRINTS " "
PRINTBUTTON "[42] 주인", 42
PRINTL 
PRINTS "CSTR를 표시·변경한다　　　"
SIF TARGET >= 0
	PRINTBUTTON "[33] 조교 대상", 33
PRINTS " "
PRINTBUTTON "[43] 주인", 43
PRINTL 
PRINTL [34] - 소지금을 변경한다
PRINTS "체력변화 처리　　　　　　　"
SIF TARGET >= 0
	PRINTBUTTON "[36] 조교 대상", 36
PRINTS " "
PRINTBUTTON "[46] 주인", 46
PRINTL 
PRINTS "각인 변화 처리　　　　　　　"
SIF TARGET >= 0
	PRINTBUTTON "[37] 조교 대상", 37
PRINTS " "
PRINTBUTTON "[47] 주인", 47
PRINTL 
PRINTL [38] - 일수를 경과시킨다
PRINTL [100] - 돌아온다

$INPUT_LOOP
INPUT
IF RESULT == 0 && TARGET >= 0
	CALL EDIT_FAVORABILITY(TARGET)
ELSEIF RESULT == 10
	CALL EDIT_FAVORABILITY(MASTER)
ELSEIF RESULT == 1 && TARGET >= 0
	CALL EDIT_STRESS(TARGET)
ELSEIF RESULT == 11
	CALL EDIT_STRESS(MASTER)
ELSEIF RESULT == 2 && TARGET >= 0
	CALL EDIT_TALENT_KANRAKU(TARGET)
ELSEIF RESULT == 12
	CALL EDIT_TALENT_KANRAKU(MASTER)
ELSEIF RESULT == 3
	CALL EDIT_TALENT(TARGET)
ELSEIF RESULT == 13
	CALL EDIT_TALENT(MASTER)
ELSEIF RESULT == 4 && TARGET >= 0
	CALL DEBUGCHECK_TARGET
ELSEIF RESULT == 5
	IF FLAG:151
		FLAG:151 = 0
	ELSE
		FLAG:151 = 1
	ENDIF
ELSEIF RESULT == 6 && TARGET >= 0
	CALL EDIT_JUEL(TARGET)
ELSEIF RESULT == 16
	CALL EDIT_JUEL(MASTER)
ELSEIF RESULT == 7 && TARGET >= 0
	CALL EDIT_EXP(TARGET)
ELSEIF RESULT == 17
	CALL EDIT_EXP(MASTER)
ELSEIF RESULT == 8 && TARGET >= 0
	CALL EDIT_ABL(TARGET)
ELSEIF RESULT == 18
	CALL EDIT_ABL(MASTER)
ELSEIF RESULT == 9 && TARGET >= 0
	CALL EDIT_HITYOUKYOU(TARGET)
ELSEIF RESULT == 19
	CALL EDIT_HITYOUKYOU(MASTER)
ELSEIF RESULT == 31
	CALL EDIT_FLAG
ELSEIF RESULT == 32 && TARGET >= 0
	CALL EDIT_CFLAG(TARGET)
ELSEIF RESULT == 42
	CALL EDIT_CFLAG(MASTER)
ELSEIF RESULT == 33 && TARGET >= 0
	CALL EDIT_CSTR(TARGET)
ELSEIF RESULT == 43
	CALL EDIT_CSTR(MASTER)
ELSEIF RESULT == 34
	CALL EDIT_MONEY(0)
ELSEIF RESULT == 36
	CALL EDIT_BASE(TARGET)
ELSEIF RESULT == 46
	CALL EDIT_BASE(MASTER)
ELSEIF RESULT == 37
	CALL EDIT_MARK(TARGET)
ELSEIF RESULT == 47
	CALL EDIT_MARK(MASTER)
ELSEIF RESULT == 38
	CALL EDIT_DAY
ELSEIF RESULT == 100
	RETURN 0
ELSE
	REUSELASTLINE 다시 입력해 주세요
	GOTO INPUT_LOOP
ENDIF
IF TARGET >= 0
	CLEARLINE 8
ELSE
	CLEARLINE 4
ENDIF
RESTART

;--------------------------------------------------
;의존도 변화 처리
;--------------------------------------------------
;주인 또는 현재 조교중의 캐릭터의 의존도를 좋아하는 값에 변화시킵니다
;연모등의 이벤트는 조교 종료시에 발생하므로 즉효과가 발휘할 것은 아니다
;너무 큰 값으로 하면 버그를 일으키는 일이 있습니다
@EDIT_FAVORABILITY(ARG)
DRAWLINE
PRINTFORML %CALLNAME:ARG%의 의존도:{CFLAG:ARG:2}
PRINTL 우선, 어느 값으로 하든가 입력해 주세요(0-999999/무효인 값으로 캔슬)
PRINTL [   0] 0
PRINTL [ 999] 999
PRINTL [2999] 2999
PRINTL [9999] 9999
PRINTL [10000000] 캔슬

INPUT
SIF RESULT < 0 || RESULT > 999999
	RETURN 0
CFLAG:ARG:2 = RESULT
SIF RESULT == 0
	RETURN 0
PRINTL 다음에, 값을 플러스로 할까 마이너스로 할까 선택해 주세요.
PRINTL [ 0] 플러스
PRINTL [ 1] 마이너스
$INPUT_LOOP
INPUT
IF RESULT == 0
	PRINTFORMW %CALLNAME:ARG%의 의존도를{CFLAG:ARG:2}로 변경합니다.
ELSEIF RESULT == 1
	CFLAG:2 *= -1
	PRINTFORMW %CALLNAME:ARG%의 의존도를{CFLAG:ARG:2}로 변경합니다.
ELSE
	GOTO INPUT_LOOP
ENDIF
RETURN 0

;--------------------------------------------------
;스트레스 변화 처리
;--------------------------------------------------
;현재 조교중의 캐릭터의 스트레스를 좋아하는 값에 변화시킵니다
;너무 큰 값으로 하면 버그를 일으키는 일이 있습니다
@EDIT_STRESS(ARG)
DRAWLINE
PRINTFORML %CALLNAME:ARG%의 스트레스:{CFLAG:ARG:10}
PRINTL 어느 값으로 하든가 입력해 주세요(0-999999/무효인 값으로 캔슬)
PRINTL [ 0] 0
PRINTL [99] 99
PRINTL [1000000] 캔슬

INPUT
SIF RESULT < 0 || RESULT > 999999
	RETURN 0
CFLAG:ARG:10 = RESULT
PRINTFORML %CALLNAME:ARG%의 스트레스를{CFLAG:ARG:65}로 변경했습니다
RETURN 0

;--------------------------------------------------
;함락계 소질 착탈 처리
;--------------------------------------------------
;함락계 소질을 부가/삭제시킵니다
@EDIT_TALENT_KANRAKU(ARG)
DRAWLINE
PRINTFORML %CALLNAME:ARG%에 부가/삭제하는 소질을 선택해 주세요
PRINT [  0] 소질[연모]를
IF TALENT:ARG:연모
	PRINTL 삭제한다
ELSE
	PRINTL 추가한다
ENDIF
PRINT [  1] 소질[친애]를
IF TALENT:ARG:친애
	PRINTL 삭제한다
ELSE
	PRINTL 추가한다
ENDIF
PRINT [  2] 소질[음란]를
IF TALENT:ARG:음란
	PRINTL 삭제한다
ELSE
	PRINTL 추가한다
ENDIF
PRINT [  3] 소질[창부]를
IF TALENT:ARG:창부
	PRINTL 삭제한다
ELSE
	PRINTL 추가한다
ENDIF
PRINT [  4] 소질[복종]를
IF TALENT:ARG:복종
	PRINTL 삭제한다
ELSE
	PRINTL 추가한다
ENDIF
PRINT [  5] 소질[예속]를
IF TALENT:ARG:예속
	PRINTL 삭제한다
ELSE
	PRINTL 추가한다
ENDIF
PRINT [  6] 소질[붕괴]를
IF TALENT:ARG:붕괴
	PRINTL 삭제한다
ELSE
	PRINTL 추가한다
ENDIF
PRINT [  7] 소질[새드]를
IF TALENT:ARG:새드
	PRINTL 삭제한다
ELSE
	PRINTL 추가한다
ENDIF
SIF TALENT:ARG:새드 == 1
	PRINTL [ 17] 소질[새드]를[도S]로 한다
PRINT [  8] 소질[마조]를
IF TALENT:ARG:마조
	PRINTL 삭제한다
ELSE
	PRINTL 추가한다
ENDIF
SIF TALENT:ARG:마조 == 1
	PRINTL [ 18] 소질[마조]를[도M]로 한다
PRINT [  9] 소질[실의]를
IF TALENT:ARG:실의
	PRINTL 삭제한다
ELSE
	PRINTL 추가한다
ENDIF
PRINT [ 10] 소질[상사상애]를
IF TALENT:ARG:상사상애
	PRINTL 삭제한다
ELSE
	PRINTL 추가한다
ENDIF
PRINT [100] 종료

$INPUT_LOOP
INPUT
IF RESULT == 0
	PRINT 소질[연모]를
	IF TALENT:ARG:연모
		PRINTW 삭제했습니다
		MARK:ARG:굴복각인 = 0
		TALENT:ARG:연모 = 0
		CFLAG:ARG:39 = 0
		CFLAG:ARG:41 = -1
		CFLAG:ARG:40 = 0
	ELSE
		PRINTW 추가했습니다
		MARK:ARG:굴복각인 = 3
		ABL:ARG:순종 = MAX(ABL:ARG:순종, 3)
		ABL:ARG:봉사정신 = MAX(ABL:ARG:봉사정신, 3)
		EXP:ARG:봉사쾌락경험 = MAX(EXP:ARG:봉사쾌락경험, 200)
		EXP:ARG:이상경험 = MIN(EXP:ARG:이상경험, 1)
		CFLAG:ARG:2 = MAX(CFLAG:ARG:2, 1000)
		TALENT:ARG:연모 = 1
		TALENT:ARG:음란 = 0
		TALENT:ARG:복종 = 0
		TALENT:ARG:친애 = 0
		TALENT:ARG:창부 = 0
		TALENT:ARG:예속 = 0
		TALENT:ARG:붕괴 = 0
		TALENT:ARG:실의 = 0
		PRINTL 사모 대상을 선택해 주세요.
		R = 0
		CALL LIFE_LIST
		$INPUT_LOOP1
		INPUT
		IF RESULT < 0 || RESULT >= CHARANUM || RESULT == ARG
			GOTO INPUT_LOOP1
		ELSE
			CFLAG:ARG:41 = NO:RESULT
			CFLAG:ARG:40 = 1
			CFLAG:ARG:39 = 3
			PRINTFORMW 사모 대상을 %CALLNAME:RESULT%로 지정했습니다.
		ENDIF
	ENDIF
ELSEIF RESULT == 1
	PRINT 소질[친애]를
	IF TALENT:ARG:친애
		PRINTW 삭제했습니다
		MARK:ARG:굴복각인 = 0
		TALENT:ARG:친애 = 0
		CFLAG:ARG:39 = 0
		CFLAG:ARG:41 = -1
		CFLAG:ARG:40 = 0
	ELSE
		PRINTW 추가했습니다
		MARK:ARG:굴복각인 = 3
		ABL:ARG:순종 = MAX(ABL:ARG:순종, 5)
		ABL:ARG:봉사정신 = MAX(ABL:ARG:봉사정신, 5)
		EXP:ARG:봉사쾌락경험 = MAX(EXP:ARG:봉사쾌락경험, 400)
		EXP:ARG:애정경험 = MAX(EXP:ARG:애정경험, 200)
		EXP:ARG:이상경험 = MIN(EXP:ARG:이상경험, 5)
		CFLAG:ARG:2 = MAX(CFLAG:ARG:2, 3000)
		TALENT:ARG:연모 = 0
		TALENT:ARG:음란 = 0
		TALENT:ARG:복종 = 0
		TALENT:ARG:친애 = 1
		TALENT:ARG:창부 = 0
		TALENT:ARG:예속 = 0
		TALENT:ARG:붕괴 = 0
		TALENT:ARG:실의 = 0
		PRINTL 사모 대상을 선택해 주세요.
		R = 0
		CALL LIFE_LIST
		$INPUT_LOOP2
		INPUT
		IF RESULT < 0 || RESULT >= CHARANUM || RESULT == ARG
			GOTO INPUT_LOOP2
		ELSE
			CFLAG:ARG:41 = NO:RESULT
			CFLAG:ARG:40 = 1
			CFLAG:ARG:39 = 6
			PRINTFORMW 사모 대상을 %CALLNAME:RESULT%로 지정했습니다.
		ENDIF
	ENDIF
ELSEIF RESULT == 10
	PRINT 소질[상사상애]를
	IF TALENT:ARG:상사상애
		PRINTW 삭제했습니다
		PRINTW 소질[친애]를 삭제했습니다
		MARK:ARG:굴복각인 = 0
		TALENT:GETCHARA(CFLAG:ARG:41):상사상애 = 0
		CFLAG:GETCHARA(CFLAG:ARG:41):39 = 6
		TALENT:ARG:친애 = 0
		TALENT:ARG:상사상애 = 0
		CFLAG:ARG:39 = 0
		CFLAG:ARG:41 = -1
		CFLAG:ARG:40 = 0
	ELSE
		PRINTW 추가했습니다
		MARK:ARG:굴복각인 = 3
		TALENT:ARG:연모 = 0
		TALENT:ARG:음란 = 0
		TALENT:ARG:복종 = 0
		TALENT:ARG:친애 = 1
		TALENT:ARG:창부 = 0
		TALENT:ARG:예속 = 0
		TALENT:ARG:붕괴 = 0
		TALENT:ARG:실의 = 0
		TALENT:ARG:상사상애 = 1
		PRINTL 사모 대상을 선택해 주세요.
		R = 0
		CALL LIFE_LIST
		$INPUT_LOOP5
		INPUT
		IF RESULT < 0 || RESULT >= CHARANUM || RESULT == ARG
			GOTO INPUT_LOOP5
		ELSE
			CFLAG:ARG:41 = NO:RESULT
			CFLAG:ARG:40 = 1
			CFLAG:ARG:39 = 165
			TALENT:RESULT:친애 = 1
			TALENT:RESULT:상사상애 = 1
			CFLAG:RESULT:41 = NO:ARG
			CFLAG:RESULT:40 = 1
			CFLAG:RESULT:39 = 165
			PRINTFORMW 사모 대상을 %CALLNAME:RESULT%로 지정했습니다.
		ENDIF
	ENDIF
ELSEIF RESULT == 2
	CFLAG:ARG:41 = -1
	PRINT 소질[음란]를
	IF TALENT:ARG:음란
		PRINTW 삭제했습니다
		MARK:ARG:굴복각인 = 0
		TALENT:ARG:음란 = 0
		CFLAG:ARG:39 = 0
	ELSE
		PRINTW 추가했습니다
		MARK:ARG:굴복각인 = 3
		MARK:ARG:쾌락각인 = 3
		ABL:ARG:욕망 = MAX(ABL:ARG:욕망, 3)
		ABL:ARG:C감각 += MIN(10 - ABL:C감각 - ABL:V감각 - ABL:A감각 - ABL:B감각, 0)
		IF ABL:ARG:C감각 > 10
			ABL:ARG:B감각 += ABL:ARG:C감각 - 10
			ABL:ARG:C감각 = 10
		ENDIF
		IF ABL:ARG:B감각 > 10
			ABL:ARG:A감각 += ABL:ARG:B감각 - 10
			ABL:ARG:B감각 = 10
		ENDIF
		EXP:ARG:절정경험 = MAX(EXP:ARG:절정경험, 50)
		EXP:ARG:이상경험 = MAX(EXP:ARG:이상경험, 3)
		TALENT:ARG:연모 = 0
		TALENT:ARG:음란 = 1
		TALENT:ARG:복종 = 0
		TALENT:ARG:친애 = 0
		TALENT:ARG:창부 = 0
		TALENT:ARG:예속 = 0
		TALENT:ARG:붕괴 = 0
		TALENT:ARG:실의 = 0
		CFLAG:ARG:39 = 4
	ENDIF
ELSEIF RESULT == 3
	CFLAG:ARG:41 = -1
	PRINT 소질[창부]를
	IF TALENT:ARG:창부
		PRINTW 삭제했습니다
		MARK:ARG:굴복각인 = 0
		TALENT:ARG:창부 = 0
		CFLAG:ARG:39 = 0
	ELSE
		PRINTW 추가했습니다
		MARK:ARG:굴복각인 = 3
		MARK:ARG:쾌락각인 = 3
		ABL:ARG:욕망 = MAX(ABL:ARG:욕망, 5)
		ABL:ARG:C감각 += MIN(20 - ABL:C감각 - ABL:V감각 - ABL:A감각 - ABL:B감각, 0)
		IF ABL:ARG:C감각 > 10
			ABL:ARG:B감각 += ABL:ARG:C감각 - 10
			ABL:ARG:C감각 = 10
		ENDIF
		IF ABL:ARG:B감각 > 10
			ABL:ARG:A감각 += ABL:ARG:B감각 - 10
			ABL:ARG:B감각 = 10
		ENDIF
		IF ABL:ARG:A감각 > 10
			ABL:ARG:V감각 += ABL:ARG:A감각 - 10
			ABL:ARG:A감각 = 10
		ENDIF
		EXP:ARG:절정경험 = MAX(EXP:ARG:절정경험, 200)
		EXP:ARG:이상경험 = MAX(EXP:ARG:이상경험, 6)
		TALENT:ARG:연모 = 0
		TALENT:ARG:음란 = 0
		TALENT:ARG:복종 = 0
		TALENT:ARG:친애 = 0
		TALENT:ARG:창부 = 1
		TALENT:ARG:예속 = 0
		TALENT:ARG:붕괴 = 0
		TALENT:ARG:실의 = 0
		CFLAG:ARG:39 = 7
	ENDIF
ELSEIF RESULT == 4
	PRINT 소질[복종]를
	IF TALENT:ARG:복종
		PRINTW 삭제했습니다
		MARK:ARG:굴복각인 = 0
		TALENT:ARG:복종 = 0
		CFLAG:ARG:39 = 0
		CFLAG:ARG:41 = -1
		CFLAG:ARG:40 = 0
	ELSE
		PRINTW 추가했습니다
		MARK:ARG:굴복각인 = 3
		ABL:ARG:순종 = MAX(ABL:ARG:순종, 3)
		ABL:ARG:마조끼 = MAX(ABL:ARG:마조끼, 3)
		ABL:ARG:C감각 -= MIN(10 - ABL:C감각 - ABL:V감각 - ABL:A감각 - ABL:B감각, 0)
		IF ABL:ARG:C감각 < 0
			ABL:ARG:B감각 += ABL:ARG:C감각
			ABL:ARG:C감각 = 0
		ENDIF
		IF ABL:ARG:B감각 < 0
			ABL:ARG:A감각 += ABL:ARG:B감각
			ABL:ARG:B감각 = 0
		ENDIF
		EXP:ARG:고통쾌락경험 += MAX(200 - EXP:ARG:고통쾌락경험 - EXP:ARG:속박경험, 0)
		EXP:ARG:이상경험 = MAX(EXP:ARG:이상경험, 2)
		CFLAG:ARG:2 = MIN(CFLAG:ARG:2, -1000)
		TALENT:ARG:연모 = 0
		TALENT:ARG:음란 = 0
		TALENT:ARG:복종 = 1
		TALENT:ARG:친애 = 0
		TALENT:ARG:창부 = 0
		TALENT:ARG:예속 = 0
		TALENT:ARG:붕괴 = 0
		TALENT:ARG:실의 = 0
		PRINTL 사모 대상을 선택해 주세요.
		R = 0
		CALL LIFE_LIST
		$INPUT_LOOP3
		INPUT
		IF RESULT < 0 || RESULT >= CHARANUM || RESULT == ARG
			GOTO INPUT_LOOP3
		ELSE
			CFLAG:ARG:41 = NO:RESULT
			CFLAG:ARG:40 = 1
			CFLAG:ARG:39 = 5
			PRINTFORMW 사모 대상을 %CALLNAME:RESULT%로 지정했습니다.
		ENDIF
	ENDIF
ELSEIF RESULT == 5
	PRINT 소질[예속]를
	IF TALENT:ARG:예속
		PRINTW 삭제했습니다
		MARK:ARG:굴복각인 = 0
		TALENT:ARG:예속 = 0
		CFLAG:ARG:39 = 0
		CFLAG:ARG:41 = -1
		CFLAG:ARG:40 = 0
	ELSE
		PRINTW 추가했습니다
		MARK:ARG:굴복각인 = 3
		ABL:ARG:순종 = MAX(ABL:ARG:순종, 5)
		ABL:ARG:마조끼 = MAX(ABL:ARG:마조끼, 5)
		ABL:ARG:C감각 -= MIN(15 - ABL:C감각 - ABL:V감각 - ABL:A감각 - ABL:B감각, 0)
		IF ABL:ARG:C감각 < 0
			ABL:ARG:B감각 += ABL:ARG:C감각
			ABL:ARG:C감각 = 0
		ENDIF
		IF ABL:ARG:B감각 < 0
			ABL:ARG:A감각 += ABL:ARG:B감각
			ABL:ARG:B감각 = 0
		ENDIF
		IF ABL:ARG:A감각 < 0
			ABL:ARG:V감각 += ABL:ARG:A감각
			ABL:ARG:A감각 = 0
		ENDIF
		EXP:ARG:고통쾌락경험 += MAX(500 - EXP:ARG:고통쾌락경험 - EXP:ARG:속박경험, 0)
		EXP:ARG:이상경험 = MAX(EXP:ARG:이상경험, 5)
		CFLAG:ARG:2 = MIN(CFLAG:ARG:2, -3000)
		TALENT:ARG:연모 = 0
		TALENT:ARG:음란 = 0
		TALENT:ARG:복종 = 0
		TALENT:ARG:친애 = 0
		TALENT:ARG:창부 = 0
		TALENT:ARG:예속 = 1
		TALENT:ARG:붕괴 = 0
		TALENT:ARG:실의 = 0
		PRINTL 사모 대상을 선택해 주세요.
		R = 0
		CALL LIFE_LIST
		$INPUT_LOOP4
		INPUT
		IF RESULT < 0 || RESULT >= CHARANUM || RESULT == ARG
			GOTO INPUT_LOOP4
		ELSE
			CFLAG:ARG:41 = NO:RESULT
			CFLAG:ARG:40 = 1
			CFLAG:ARG:39 = 8
			PRINTFORMW 사모 대상을 %CALLNAME:RESULT%로 지정했습니다.
		ENDIF
	ENDIF
ELSEIF RESULT == 6
	PRINT 소질[붕괴]를
	IF TALENT:ARG:붕괴
		PRINTW 삭제했습니다
		TALENT:ARG:붕괴 = 0
	ELSE
		PRINTW 추가했습니다
		TALENT:ARG:연모 = 0
		TALENT:ARG:음란 = 0
		TALENT:ARG:복종 = 0
		TALENT:ARG:친애 = 0
		TALENT:ARG:창부 = 0
		TALENT:ARG:예속 = 0
		TALENT:ARG:붕괴 = 1
		TALENT:ARG:실의 = 0
		CFLAG:ARG:41 = -1
		CFLAG:ARG:40 = 0
	ENDIF
ELSEIF RESULT == 7
	PRINT 소질[새드]를
	IF TALENT:ARG:새드
		PRINTW 삭제했습니다
		TALENT:ARG:새드 = 0
	ELSE
		PRINTW 추가했습니다
		TALENT:ARG:새드 = 1
		TALENT:ARG:마조 = 0
	ENDIF
ELSEIF RESULT == 17
	PRINTW 소질[새드]를[도S]로 했습니다
	TALENT:ARG:새드 = 2
	TALENT:ARG:마조 = 0
ELSEIF RESULT == 8
	PRINT 소질[마조]를
	IF TALENT:ARG:마조
		PRINTW 삭제했습니다
		TALENT:ARG:마조 = 0
	ELSE
		PRINTW 추가했습니다
		TALENT:ARG:새드 = 0
		TALENT:ARG:마조 = 1
	ENDIF
ELSEIF RESULT == 18
	PRINTW 소질[마조]를[도M]로 했습니다
	TALENT:ARG:마조 = 2
ELSEIF RESULT == 9
	PRINT 소질[실의]를
	IF TALENT:ARG:실의
		PRINTW 삭제했습니다
		TALENT:ARG:실의 = 0
	ELSE
		PRINTW 추가했습니다
		TALENT:ARG:연모 = 0
		TALENT:ARG:음란 = 0
		TALENT:ARG:복종 = 0
		TALENT:ARG:친애 = 0
		TALENT:ARG:창부 = 0
		TALENT:ARG:예속 = 0
		TALENT:ARG:붕괴 = 0
		TALENT:ARG:실의 = 23
		CFLAG:ARG:41 = -1
		CFLAG:ARG:40 = 0
	ENDIF
ELSEIF RESULT == 100
	RETURN 0
ELSE
	GOTO INPUT_LOOP
ENDIF
RETURN 0
;--------------------------------------------------
;그 외의 소질 착탈 처리
;--------------------------------------------------
;그 외의 소질을 부가/삭제시킵니다
@EDIT_TALENT(ARG)
#LOCALSIZE 2
VARSET LOCAL
DRAWLINE
PRINTFORML □ %CALLNAME:ARG%의 현재의 소질 □
FOR LOCAL, 0, 300
	SIF STRLENS(TALENTNAME:LOCAL) <= 0
		CONTINUE
	SIF TALENT:ARG:LOCAL
		PRINTFORM [%TALENTNAME:LOCAL%] 
NEXT
PRINTW 
DRAWLINE
FOR LOCAL, 0, 300
	SIF STRLENS(TALENTNAME:LOCAL) <= 0
		CONTINUE
	SIF (LOCAL >= 3 && LOCAL <= 9) || LOCAL == 83 || LOCAL == 84 || LOCAL == 162 || LOCAL == 165
		CONTINUE
	;개행 or표시 위치 열려라
	IF LOCAL:1 % 4 == 3
		PRINTFORML [{LOCAL, 3}] %TALENTNAME:LOCAL%
	ELSE
		PRINTFORM [{LOCAL, 3}] %TALENTNAME:LOCAL, 14, LEFT%
	ENDIF
	LOCAL:1 = LOCAL:1 + 1
NEXT
PRINTL 
PRINTFORML %CALLNAME:ARG%에 부가/삭제하는 소질을 선택해 주세요(무효인 값으로 캔슬)
PRINTL [1000] 캔슬

INPUT
SIF RESULT >= 1000
	RETURN 0
SELECTCASE RESULT
	CASE 3 TO 9, 83, 84, 162
		RETURN 0
	CASE 118
		LOCAL = RESULT
		PRINTL 어느 값으로 하든가 입력해 주세요(0-15/무효인 값으로 캔슬)
		PRINTL [0] 없음
		PRINTL [1] 천연
		PRINTL [2] 제모
		PRINTL [100] 캔슬
		INPUT
		SIF RESULT < 0 || RESULT > 2
			RETURN 0
		TALENT:ARG:LOCAL = RESULT
		PRINTFORMW [%TALENTNAME:LOCAL%]를\@ TALENT:ARG:LOCAL ? 부가 # 삭제\@했습니다
	CASE 159
		LOCAL = RESULT
		PRINTL 어느 값으로 하든가 입력해 주세요(0-15/무효인 값으로 캔슬)
		PRINTL [0] 없음
		PRINTL [1] 상흔 있어
		PRINTL [2] 문신 있어
		PRINTL [4] 소인 있어
		PRINTL [8] 피어스 있어
		PRINTL [15] 모두
		PRINTL [100] 캔슬
		INPUT
		SIF RESULT < 0 || RESULT > 15
			RETURN 0
		TALENT:ARG:LOCAL |= RESULT
		PRINTFORMW [%TALENTNAME:LOCAL%]를\@ TALENT:ARG:LOCAL ? 부가 # 삭제\@했습니다
	CASEELSE
		IF STRLENS(TALENTNAME:RESULT) <= 0
			RETURN 0
		ELSEIF TALENT:ARG:RESULT
			TALENT:ARG:RESULT = 0
			PRINTFORMW [%TALENTNAME:RESULT%]를 삭제했습니다
		ELSE
			TALENT:ARG:RESULT = 1
			PRINTFORMW [%TALENTNAME:RESULT%]를 부가했습니다
		ENDIF
	ENDSELECT
RETURN 0

;--------------------------------------------------
;호감도 체크
;--------------------------------------------------
@DEBUGCHECK_TARGET
#LOCALSIZE 7
VARSET LOCAL
;우선은 호감도일위의 캐릭터를 조사하면서, 조수조교경험를 산출해 본다
FOR LOCAL:1, 0, 900
	SIF GETCHARA(LOCAL:1) == -1
		CONTINUE
	LOCAL = LOCAL:1 + 1099
	LOCAL:2 = CFLAG:LOCAL
	LOCAL:3 += CFLAG:LOCAL
	IF LOCAL:4 < LOCAL:2
		LOCAL:4 = LOCAL:2
		LOCAL:5 = LOCAL
	ENDIF
NEXT
LOCAL:3 -= CFLAG:(LOCAL:5)
SIF LOCAL:5
	LOCAL:5 -= 1099
IF CFLAG:2 < 0
	CFLAG:2 *= -1
	LOCAL:6 = 1
ENDIF
PRINTFORM %CALLNAME:TARGET%는
IF CFLAG:2 < 250 || (LOCAL:3 > 1000 && !TALENT:음란 && !TALENT:창부) || TALENT:붕괴 || !LOCAL:4
	PRINTL 누구에게도 관심이 없는 것 같습니다
ELSE
	PRINTFORM %CALLNAME:GETCHARA(LOCAL:5)%
	IF TALENT:연모 || TALENT:친애
		PRINTL 를 연모하고 있습니다
	ELSEIF TALENT:음란 || TALENT:창부
		PRINTL 가 마음에 드는 같습니다
	ELSEIF TALENT:복종 || TALENT:예속
		PRINTL 에복종를 맹세하고 있습니다
	ELSEIF CFLAG:2 < 500
		IF LOCAL:6
			PRINTL 를 무서워하고 있는 것 같습니다
		ELSE
			PRINTL 가 신경이 쓰이고 있는 것 같습니다
		ENDIF
	ELSEIF CFLAG:2 < 750
		IF LOCAL:6
			PRINTL 에 압도 되고 있는 것 같습니다
		ELSE
			PRINTL 를 좋아하는 것 같습니다
		ENDIF
	ELSE
		IF LOCAL:6
			PRINTL 에 전율 하고 있는 것 같습니다
		ELSE
			PRINTL 를 사랑해 버린 것 같습니다
		ENDIF
	ENDIF
ENDIF
PRINTW 
SIF LOCAL:6
	CFLAG:2 *= -1
RETURN 0

;--------------------------------------------------
;주수변화 처리
;--------------------------------------------------
;주인 또는 현재 조교중의 캐릭터의 주수를 좋아하는 값에 변화시킵니다
;너무 큰 값으로 하면 버그를 일으키는 일이 있습니다
@EDIT_JUEL(ARG)
#LOCALSIZE 3
DRAWLINE
PRINTFORML □ %CALLNAME:ARG%의 주수 □
LOCAL:2 = 0
FOR LOCAL:1, 0 ,13
	IF LOCAL:1 == 4
		CONTINUE
	ELSEIF LOCAL:1 == 12
		LOCAL = 100
	ELSE
		LOCAL = LOCAL:1
	ENDIF
	PRINTFORM [{LOCAL, 3}] %PALAMNAME:LOCAL%의 구슬:{JUEL:ARG:LOCAL, 8}
	;개행 or표시 위치 열려라
	IF LOCAL:2 % 3 == 2
		PRINTL 
	ELSE
		PRINT 　
	ENDIF
	LOCAL:2 = LOCAL:2 + 1
NEXT
PRINTL 
PRINTLC [98] 모두 0으로 한다
PRINTLC [99] 모두 9999999로 한다
PRINTLC [1000] 캔슬
DRAWLINE
PRINTL 우선, 어느 구슬로 하든가 입력해 주세요(무효인 값으로 캔슬)
INPUT
IF RESULT < 0 || (RESULT > 14 && RESULT != 100 && RESULT != 98 && RESULT != 99)
	RETURN 0
ELSEIF RESULT == 98 || RESULT == 99
	FOR LOCAL:1, 0 ,13
		IF LOCAL:1 == 4
			CONTINUE
		ELSEIF LOCAL:1 == 12
			LOCAL = 100
		ELSE
			LOCAL = LOCAL:1
		ENDIF
		IF RESULT == 98
			JUEL:ARG:LOCAL = 0
		ELSE
			JUEL:ARG:LOCAL = 9999999
		ENDIF
		
	NEXT
	RETURN 0
ENDIF
LOCAL = RESULT
PRINTL 다음에, 어느 값으로 하든가 입력해 주세요(0-9999999/무효인 값으로 캔슬)
PRINTL [0] 0
PRINTL [9999999] 9999999
PRINTL [10000000] 캔슬
INPUT
SIF RESULT < 0 || RESULT > 9999999
	RETURN 0
JUEL:ARG:LOCAL = RESULT
RETURN 0

;--------------------------------------------------
;경험 변화 처리
;--------------------------------------------------
;주인 또는 현재 조교중의 캐릭터의 경험을 좋아하는 값에 변화시킵니다
;너무 큰 값으로 하면 버그를 일으키는 일이 있습니다
@EDIT_EXP(ARG)
#LOCALSIZE 2
DRAWLINE
PRINTFORML □ %CALLNAME:ARG%의 경험 □
LOCAL:1 = 0
FOR LOCAL, 0, 71
	SIF !STRLENS(EXPNAME:LOCAL)
		CONTINUE
	PRINTFORM [{LOCAL, 2}] %EXPNAME:LOCAL, 12%:{EXP:ARG:LOCAL, 5}
	IF ++LOCAL:1 == 3
		PRINTL 
		LOCAL:1 = 0
	ELSE
		PRINT 　
	ENDIF
NEXT
PRINTL 
PRINTLC [98] 모두 0으로 한다
PRINTLC [99] 모두 9999로 한다
PRINTL [100] 캔슬
DRAWLINE
PRINTL 우선, 어느 경험으로 하든가 입력해 주세요
INPUT
IF RESULT != 98 && RESULT != 99 && (RESULT < 0 || RESULT > 70 || !STRLENS(EXPNAME:RESULT))
	RETURN 0
ELSEIF RESULT == 98 || RESULT == 99
	FOR LOCAL, 0, 71
		SIF !STRLENS(EXPNAME:LOCAL)
			CONTINUE
		IF RESULT == 98
			EXP:ARG:LOCAL = 0
		ELSE
			EXP:ARG:LOCAL = 9999
		ENDIF
	NEXT
	RETURN 0
ENDIF
LOCAL = RESULT
PRINTL 다음에, 어느 값으로 하든가 입력해 주세요(0-9999/무효인 값으로 캔슬)
PRINTL [   0] 0
PRINTL [ 100] 100
PRINTL [9999] 9999
PRINTL [10000] 100늘린다
PRINTL [10001] 100줄인다
PRINTL [10002] 캔슬
INPUT
IF RESULT < 0 || RESULT > 10001
	RETURN 0
ELSEIF RESULT == 10000
	EXP:ARG:LOCAL += 100
	RETURN 0
ELSEIF RESULT == 10001
	EXP:ARG:LOCAL -= 100
	RETURN 0
ENDIF
SIF RESULT < 0 || RESULT > 10001
	RETURN 0
EXP:ARG:LOCAL = RESULT
RETURN 0

;--------------------------------------------------
;능력 변화 처리
;--------------------------------------------------
;주인 또는 현재 조교중의 캐릭터의 능력을 좋아하는 값에 변화시킵니다
;너무 큰 값으로 하면 버그를 일으키는 일이 있습니다
@EDIT_ABL(ARG)
#LOCALSIZE 2
DRAWLINE
PRINTFORML □ %CALLNAME:ARG%の能力 □
LOCAL:1 = 0
FOR LOCAL, 0, 23
	SIF !STRLENS(ABLNAME:LOCAL)
		CONTINUE
	PRINTFORM [{LOCAL, 2}] %ABLNAME:LOCAL, 12%:{ABL:ARG:LOCAL, 2}
	IF ++LOCAL:1 == 3
		PRINTL 
		LOCAL:1 = 0
	ELSE
		PRINT 　
	ENDIF
NEXT
PRINTL 
PRINTLC [98] 모두 0으로 한다
PRINTLC [99] 모두 10으로 한다
PRINTL [100] 캔슬
DRAWLINE
PRINTL 우선, 어느 능력으로 하든가 입력해 주세요(무효인 값으로 캔슬)
INPUT
IF RESULT != 98 && RESULT != 99 && (RESULT < 0 || RESULT > 23 || !STRLENS(ABLNAME:RESULT))
	RETURN 0
ELSEIF RESULT == 98 || RESULT == 99
	FOR LOCAL, 0, 23
		SIF !STRLENS(ABLNAME:LOCAL)
			CONTINUE
		IF RESULT == 98
			ABL:ARG:LOCAL = 0
		ELSE
			ABL:ARG:LOCAL = 10
		ENDIF
	NEXT
	RETURN 0
ENDIF
LOCAL = RESULT
PRINTL 다음에, 어느 값으로 하든가 입력해 주세요(0-10/무효인 값으로 캔슬)
PRINTL [ 0] 0
PRINTL [ 5] 5
PRINTL [10] 10
SIF ABL:ARG:LOCAL != 10
	PRINTL [98] 1늘린다
SIF ABL:ARG:LOCAL != 0
	PRINTL [99] 1줄인다
PRINTL [100] 캔슬
INPUT
IF RESULT != 98 && RESULT != 99 && (RESULT < 0 || RESULT > 10)
	RETURN 0
ELSEIF RESULT == 98
	SIF ABL:ARG:LOCAL != 10
		ABL:ARG:LOCAL += 1
	RETURN 0
ELSEIF RESULT == 99
	SIF ABL:ARG:LOCAL != 0
		ABL:ARG:LOCAL -= 1
	RETURN 0
ENDIF
ABL:ARG:LOCAL = RESULT
RETURN 0

;--------------------------------------------------
;조교경험·피조교경험변화 처리
;--------------------------------------------------
;주인 또는 현재 조교중의 캐릭터의 능력을 좋아하는 값에 변화시킵니다
;너무 큰 값으로 하면 버그를 일으키는 일이 있습니다
@EDIT_HITYOUKYOU(ARG)
#LOCALSIZE 5
VARSET LOCAL
DRAWLINE
PRINTL 표시·변경하는 값을 선택해 주세요.
PRINTFORML [ 0] %CALLNAME:ARG%의조교경험
PRINTFORML [ 1] %CALLNAME:ARG%의 피조교경험
PRINTFORML [ 2] %CALLNAME:ARG%의 조수조교경험를 표시한다
$INPUT_LOOP1
INPUT
IF RESULT == 0
	PRINTFORML □ %CALLNAME:ARG%의조교경험 □
	FOR LOCAL:3, 0, CHARANUM
		LOCAL:2 = NO:ARG + 1099
		IF CFLAG:(LOCAL:3):(LOCAL:2)
			PRINTFORM [{LOCAL:3, 3}] %NATION_OR_H(NO:(LOCAL:3)), 10%：{CFLAG:(LOCAL:3):(LOCAL:2), 5}
			IF (LOCAL:4 % 3) == 2
				PRINTL 
			ELSE
				PRINT 　
			ENDIF
			LOCAL:4++
		ENDIF
	NEXT
	PRINTL 
	PRINTL [1000] 캔슬
	PRINTL 
	PRINTL 상대의 캐릭터 등록 번호를 입력해 주세요(무효인 값으로 캔슬)
	INPUT
	IF RESULT < 0 || RESULT >= CHARANUM || RESULT == ARG
		RETURN 0
	ELSE
		LOCAL = RESULT
		LOCAL:1 = LOCAL:2
	ENDIF
ELSEIF RESULT == 1
	PRINTFORML □ %CALLNAME:ARG%의 피조교경험 □
	FOR LOCAL:3, 0, 900
		LOCAL:2 = LOCAL:3 + 1099
		IF CFLAG:ARG:(LOCAL:2)
			PRINTFORM [{LOCAL:3, 3}] %NATION_OR_H(LOCAL:3), 16%：{CFLAG:ARG:(LOCAL:2), 6}
			IF (LOCAL:4 % 3) == 2
				PRINTL 
			ELSE
				PRINT 　
			ENDIF
			LOCAL:4++
		ENDIF
	NEXT
	PRINTL 
	PRINTL [1000] 캔슬
	PRINTL 
	PRINTL 상대의 캐릭터 고유 번호를 입력해 주세요(무효인 값으로 캔슬)
	INPUT
	IF EXISTCSV(RESULT , 0) && (RESULT != NO:ARG)
		LOCAL = ARG
		LOCAL:1 = RESULT + 1099
	ELSE
		RETURN 0
	ENDIF
ELSEIF RESULT == 2
	;조수조교경험를 산출
	FOR LOCAL:1, 0, 900
		LOCAL = LOCAL:1 + 1099
		LOCAL:2 = CFLAG:LOCAL
		LOCAL:3 += CFLAG:LOCAL
	NEXT
	LOCAL:3 -= CFLAG:(NO:MASTER + 1099)
	PRINTFORMW %CALLNAME:ARG%의 조수조교경험는{LOCAL:3}입니다
	RETURN 0
ELSE
	GOTO INPUT_LOOP1
ENDIF
PRINTL 
PRINTL 어느 값으로 하든가 입력해 주세요(0-999999/무효인 값으로 캔슬)
PRINTL [    0] 0
PRINTL [  100] 100
PRINTL [ 1000] 1000
PRINTL [10000] 10000
PRINTL [1000000] 캔슬
PRINTL 
$INPUT_LOOP
INPUT
SIF RESULT < 0 || RESULT > 999999
	RETURN 0
CFLAG:LOCAL:(LOCAL:1) = RESULT
RETURN 0



;--------------------------------------------------
;FLAG 표시·변경 처리
;--------------------------------------------------
;현재의 FLAG를 좋아하는 값에 변화시킵니다
;무심코 만지작거리면 에러 초월하는 일이 있습니다
@EDIT_FLAG
#LOCALSIZE 3
VARSET LOCAL
DRAWLINE
PRINTL (주의!)
PRINTL 어디까지나 디버그용입니다. 서투르게 만지작거리면 에러나 플레이 불능을 일으킬 가능성이 있기 (위해)때문에,
PRINTL 여기를 조작했을 경우, 세이브는 하지 말아 주세요.
PRINTFORML □ 현재의 FLAG:0~5000 □
FOR LOCAL:1, 0, 5001
	IF FLAG:(LOCAL:1)
		PRINTFORM [{LOCAL:1, 4}] FLAG:{LOCAL:1, 4} = {FLAG:(LOCAL:1), 6}
		IF (LOCAL:2 % 3) == 2
			PRINTL 
		ELSE
			PRINT 　
		ENDIF
		LOCAL:2++
	ENDIF
NEXT
PRINTL 
PRINTL [10000] 캔슬
PRINTL 변경하는 FLAG 번호를 입력해 주세요(0-5000/무효인 값으로 캔슬)

INPUT
SIF RESULT < 0 || RESULT > 5000
	RETURN 0
LOCAL = RESULT
PRINTL 
PRINTL 어느 값으로 하든가 입력해 주세요(0-999999/무효인 값으로 캔슬)
PRINTL [0] 0
PRINTL [1] 1
PRINTL [1000000] 캔슬

INPUT
SIF RESULT < 0 || RESULT > 999999
	RETURN 0
FLAG:LOCAL = RESULT
PRINTFORMW FLAG:{LOCAL}를{FLAG:LOCAL}로 변경했습니다
RETURN 0

;--------------------------------------------------
;CFLAG 표시·변경 처리
;--------------------------------------------------
;주인 또는 현재 조교중의 캐릭터의 CFLAG를 좋아하는 값에 변화시킵니다
;무심코 만지작거리면 에러 초월하는 일이 있습니다
@EDIT_CFLAG(ARG)
#LOCALSIZE 3
VARSET LOCAL
DRAWLINE
PRINTL (주의!)
PRINTL 어디까지나 디버그용입니다. 서투르게 만지작거리면 에러나 플레이 불능을 일으킬 가능성이 있기 (위해)때문에,
PRINTL 여기를 조작했을 경우, 세이브는 하지 말아 주세요.
PRINTFORML □ %CALLNAME:ARG%의 CFLAG:0~6000 □
FOR LOCAL:1, 0, 6001
	IF CFLAG:ARG:(LOCAL:1)
		PRINTFORM [{LOCAL:1, 4}] CFLAG:{LOCAL:1, 4} = {CFLAG:ARG:(LOCAL:1), 4}
		IF (LOCAL:2 % 3) == 2
			PRINTL 
		ELSE
			PRINT 　
		ENDIF
		LOCAL:2++
	ENDIF
NEXT
PRINTL 
PRINTL [10000] 캔슬
PRINTL 변경하는 CFLAG 번호를 입력해 주세요(0-6000/무효인 값으로 캔슬)

INPUT
SIF RESULT < 0 || RESULT > 6000
	RETURN 0
LOCAL = RESULT
PRINTL 
PRINTL 어느 값으로 하든가 입력해 주세요(－9999999999-9999999999/무효인 값으로 캔슬)
PRINTL [0] 0
PRINTL [1] 1
PRINTL [2] 2
PRINTL [3] 3
PRINTL [10000000000] 캔슬

INPUT
SIF RESULT < -9999999999 || RESULT > 9999999999
	RETURN 0
CFLAG:ARG:LOCAL = RESULT
PRINTFORMW %CALLNAME:ARG%의 CFLAG:{LOCAL}를{CFLAG:ARG:LOCAL}로 변경했습니다
RETURN 0


;--------------------------------------------------
;CSTR 표시·변경 처리
;--------------------------------------------------
;주인 또는 현재 조교중의 캐릭터의 CSTR를 표시·변경합니다
@EDIT_CSTR(ARG)
#LOCALSIZE 3
VARSET LOCAL
DRAWLINE
PRINTFORML □ %CALLNAME:ARG%의 CSTR:0~60 □
FOR LOCAL:1, 0, 61
	IF CSTR:ARG:(LOCAL:1) != ""
		PRINTFORM [{LOCAL:1, 2}] CSTR:{LOCAL:1, 2} = %CSTR:ARG:(LOCAL:1)%
		IF (LOCAL:2 % 3) == 2
			PRINTL 
		ELSE
			PRINT 　
		ENDIF
		LOCAL:2++
	ENDIF
NEXT
PRINTL 
PRINTL [100] 캔슬
PRINTL 변경하는 CSTR 번호를 입력해 주세요(0-60/무효인 값으로 캔슬)

INPUT
SIF RESULT < 0 || RESULT > 60
	RETURN 0
LOCAL = RESULT
PRINTL 
PRINTL 캐릭터 라인을 입력해 주세요
PRINTBUTTON "[] 공백을 대입한다", ""
PRINTL 
PRINTBUTTON "[캔슬] ", "캔슬"

INPUTS
SIF RESULTS == "캔슬"
	RETURN 0
CSTR:ARG:LOCAL = %RESULTS%
PRINTFORMW %CALLNAME:ARG%의 CSTR:{LOCAL}를 %CSTR:ARG:LOCAL%로 변경했습니다
RETURN 0

;--------------------------------------------------
;소지금 변경 처리
;--------------------------------------------------
;현재의 소지금을 좋아하는 값에 변화시킵니다
;너무 큰 값으로 하면 버그를 일으키는 일이 있습니다
@EDIT_MONEY(ARG)
DRAWLINE
PRINTFORML 현재의 소지금${MONEY}
PRINTL 어느 값으로 하든가 입력해 주세요(0-99999999/무효인 값으로 캔슬)
PRINTL [ 0] 0
PRINTL [1000] 1000
PRINTL [10000] 10000
PRINTL [100000] 10만
PRINTL [1000000] 100만
PRINTL [10000000] 1000만
PRINTL [100000000] 캔슬

INPUT
SIF RESULT < 0 || RESULT > 99999999
	RETURN 0
MONEY:ARG = RESULT
PRINTFORMW 소지금을${MONEY}로 변경했습니다
RETURN 0

;--------------------------------------------------
;체력변화 처리
;--------------------------------------------------
;주인 또는 현재 조교중의 캐릭터의체력를 좋아하는 값에 변화시킵니다
;너무 큰 값으로 하면 버그를 일으키는 일이 있습니다
@EDIT_BASE(ARG)
DRAWLINE
PRINTFORML %CALLNAME:ARG%의 현재의체력:{BASE:ARG:체력, 4}/{MAXBASE:ARG:체력}
PRINTFORML 어느 값으로 하든가 입력해 주세요(1-{MAXBASE:ARG:체력}/무효인 값으로 캔슬)
PRINTL [   1]   1
PRINTL [ 300] 300
PRINTL [ 500] 500
PRINTL [ 800] 800
PRINTL [1000]1000
PRINTFORML [{MAXBASE:ARG:체력}]{MAXBASE:ARG:체력}
PRINTL [100000] 캔슬
PRINTL [100001] 최대체력를 변경한다

INPUT
IF RESULT == 100001
	PRINTFORML 어느 값으로 하든가 입력해 주세요(1-99999/무효인 값으로 캔슬)
	PRINTL [1000]1000
	PRINTL [1500]1500
	PRINTL [2000]2000
	PRINTL [2500]2500
	PRINTL [3000]3000
	PRINTL [100000] 500늘린다
	PRINTL [100001] 1000늘린다
	PRINTL [100002] 캔슬
	INPUT
	IF RESULT < 501 || RESULT > 100001
		RETURN 0
	ELSEIF RESULT == 100000
		MAXBASE:ARG:체력 += 500
	ELSEIF RESULT == 100001
		MAXBASE:ARG:체력 += 1000
	ELSEIF RESULT < 501 || RESULT > 100001
		MAXBASE:ARG:체력 = RESULT
	ENDIF
	PRINTFORMW %CALLNAME:ARG%의 최대체력를{MAXBASE:ARG:체력}로 변경했습니다
	RETURN 0
ELSEIF RESULT < 501 || RESULT > MAXBASE:ARG:체력
	RETURN 0
ENDIF
BASE:ARG:체력 = RESULT
PRINTFORMW %CALLNAME:ARG%의 현재의체력를{BASE:ARG:체력}로 변경했습니다
RETURN 0

;--------------------------------------------------
;각인 변화 처리
;--------------------------------------------------
;주인 또는 현재 조교중의 캐릭터의 각인을 좋아하는 값에 변화시킵니다
@EDIT_MARK(ARG)
#LOCALSIZE 2
DRAWLINE
PRINTFORML □ %CALLNAME:ARG%의 각인 □
LOCAL:1 = 0
FOR LOCAL, 0, 5
	PRINTFORM [{LOCAL}] %MARKNAME:LOCAL, 12%:{MARK:ARG:LOCAL}
	IF ++LOCAL:1 == 3
		PRINTL 
		LOCAL:1 = 0
	ELSE
		PRINT 　
	ENDIF
NEXT
PRINTL 
PRINTLC [98] 모두 0으로 한다
PRINTLC [99] 모두 3으로 한다
PRINTL [100] 캔슬
DRAWLINE
PRINTL 우선, 어느 각인으로 하든가 입력해 주세요(무효인 값으로 캔슬)
INPUT
IF RESULT < 0 || (RESULT > 4 && RESULT != 98 && RESULT != 99)
	RETURN 0
ELSEIF RESULT == 98 || RESULT == 99
	FOR LOCAL, 0, 5
		IF RESULT == 98
			MARK:ARG:LOCAL = 0
		ELSE
			MARK:ARG:LOCAL = 3
		ENDIF
	NEXT
	RETURN 0
ENDIF
LOCAL = RESULT
PRINTL 다음에, 어느 값으로 하든가 입력해 주세요(0-10/무효인 값으로 캔슬)
PRINTL [0] 0
PRINTL [1] 1
PRINTL [2] 2
PRINTL [3] 3
PRINTL [100] 캔슬
INPUT
SIF RESULT < 0 || RESULT > 3
	RETURN 0
MARK:ARG:LOCAL = RESULT
RETURN 0

;--------------------------------------------------
;일수 경과 처리
;--------------------------------------------------
;임의의 일수후에 점프 합니다
;사이에 이벤트가 끼이면 불편이 일어나므로 신중하게 조작해 주세요
;너무 큰 값으로 하면 버그를 일으키는 일이 있습니다
@EDIT_DAY
DRAWLINE
PRINTL 임의의 일수후에 점프 합니다. 점프 한 사이에 일어나는 이벤트는 스킵 되므로,
PRINTL 이벤트를 일으키고 싶은 경우는 얌전하게 매크로를 사용하는 편이 좋습니다.
PRINTL 덧붙여 캘린더적인 의미에서의 일자를 조작하고 싶은 경우는 FLAG를 조작할 필요가 있습니다.
PRINTL 
PRINTL (주의!)
PRINTL 어디까지나 디버그용입니다. 서투르게 만지작거리면 에러나 플레이 불능을 일으킬 가능성이 있기 (위해)때문에,
PRINTL 여기를 조작했을 경우, 세이브는 하지 말아 주세요.
PRINTL 
PRINTFORML 현재의 일수:{DAY}일째
PRINTL 며칠 경과시키든가 입력해 주세요(－{DAY}-{9999 - DAY}/무효인 값으로 캔슬)
PRINTL [1] 1일후
PRINTL [2] 2일후(3일 렌탈 대기 따위)
PRINTL [4] 4일후(5일 렌탈 대기 따위)
PRINTL [9] 9일후(10일 렌탈 대기 따위)
PRINTL [11] 11일후(임신확정 대기 따위)
PRINTL [19] 19일후(육아 대기 따위)
PRINTL [50] 50일후(임월 대기 따위)
PRINTL [10000] 캔슬

INPUT
SIF DAY + RESULT < 0 || DAY + RESULT > 9999
	RETURN 0
DAY += RESULT
PRINTFORMW 현재의 일수를{DAY}일째로 변경했습니다
RETURN 0
