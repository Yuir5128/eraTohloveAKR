;조교 대상 선택(eratohoR+ 1.31)
@CHANGE_TARGET
IF TARGET >= 0
	PRINT 현재
	PRINTFORML %NAME:TARGET%를 조교중
ENDIF
PRINTL 이번은 누구를 조교합니까?
R = 1
CALL LIFE_LIST
PRINTFORML  [1000]돌아온다

$INPUT_LOOP
INPUT
IF RESULT == 1000
	RETURN 0
ELSEIF RESULT < 0 || RESULT >= CHARANUM || MASTER == RESULT
	GOTO INPUT_LOOP
ELSE
	SIF CFLAG:RESULT:1 == 3
		GOTO INPUT_LOOP
	SIF CFLAG:RESULT:12 > 0
		GOTO INPUT_LOOP
	C = RESULT
	CALL CHECK_CHILD_CARE
	SIF RESULT == 1
		GOTO INPUT_LOOP
ENDIF

SIF C != 0
	TARGET = C
SIF TARGET == ASSI
	ASSI = -1

@SELECT_ASSI
PRINT 현재
IF ASSI >= 0
	PRINTFORML   [{ASSI}]%NAME:ASSI%가 조수
ELSE
	PRINTFORML 조수는 없습니다
ENDIF
PRINTL 누구를 조수로 합니까? (현재의 조수는☆)
DRAWLINE
CALL LIFE_LIST(1)
PRINTFORML   [999]조수없음
PRINTFORML  [1000]돌아온다

$INPUT_LOOP
INPUT
IF RESULT == 1000
	RETURN 0
ELSEIF RESULT == 999
	ASSI = -1
	RETURN 0
ELSEIF RESULT == MASTER
	ASSI = -1
	RETURN 0
ELSEIF RESULT < 0 || RESULT >= CHARANUM
	GOTO INPUT_LOOP
ELSEIF CFLAG:RESULT:12 > 0
	GOTO INPUT_LOOP
ELSEIF CFLAG:RESULT:1 < 2
	GOTO INPUT_LOOP
ELSEIF CFLAG:RESULT:26
	GOTO INPUT_LOOP
ELSE
	C = RESULT
	CALL CHECK_CHILD_CARE
	SIF RESULT == 1
		GOTO INPUT_LOOP
ENDIF

ASSI = C
CALL ASSI_CHANGE
ISASSI:ASSI = 1
SIF ASSI == TARGET
	TARGET = -1


@LIFE_LIST(ARG)
DRAWLINE
REPEAT CHARANUM
	IF CFLAG:COUNT:1 == 3 && CFLAG:COUNT:12 == 0
		IF COUNT == MASTER
			SETCOLOR 0x8888FF
			PRINTFORM %UNICODE(0x27A0)%
		ELSEIF COUNT == ASSI
			SETCOLOR 0x8888FF
			PRINT 조
		ELSE
			PRINT 　
		ENDIF
		RESETCOLOR
		PRINTFORM [{COUNT,2}]%CALLNAME:COUNT, 40, LEFT%
		;조교자 변경 때의 인터페이스
		X = COUNT
		CALL LIFE_LIST_MASTER_CHANGE
		COUNT = X
		PRINTL 
	ENDIF
REND
DRAWLINE
REPEAT CHARANUM
	;공범자 플래그유의 캐릭터는 배제
	SIF CFLAG:COUNT:1 == 3
		CONTINUE
	;노역중의 캐릭터는 제외
	SIF CFLAG:COUNT:12 > 0
		CONTINUE
	;성장전의아이는 제외
	SIF CFLAG:COUNT:26
		CONTINUE
	;육아 방으로 이동하고 있는 캐릭터는 제외
	C = COUNT
	CALL CHECK_CHILD_CARE
	SIF R == 1 && RESULT == 1
		CONTINUE
	SIF ARG && CFLAG:COUNT:1 < 2
		CONTINUE
	IF ARG
		IF CFLAG:COUNT:1 < 2
			PRINT ×
		ENDIF
	ELSEIF COUNT == TARGET
		SETCOLOR 0x8888FF
		PRINT ☆
	ELSEIF COUNT == ASSI
		SETCOLOR 0x8888FF
		PRINT 조
	ELSE
		PRINT 　
	ENDIF
	RESETCOLOR
	IF COUNT<10
		PRINTFORM [ {COUNT}]
	ELSE
		PRINTFORM [{COUNT}]
	ENDIF
	SIF TALENT:COUNT:기절
		SETCOLOR 120,120,120
	PRINTFORM %CALLNAME:COUNT,20,LEFT%
	RESETCOLOR
	SIF TALENT:COUNT:기절
		PRINT 기절
	IF ARG
		PRINTFORM 기교Lv{ABL:COUNT:기교} 
		SIF ABL:COUNT:기교 < 10
			PRINT  
	ELSE
		PRINTFORM %UNICODE(0x208D)%
		CALL PRINT_COLORBAR,BASE:COUNT:체력,MAXBASE:COUNT:체력,8,UNICODE(0x2583),UNICODE(0x2583),BARCOLORSET("赤"),RESULT:1, 327685
		PRINTFORM %UNICODE(0x208E)%
		PRINTFORM %UNICODE(0x208D)%
		CALL PRINT_COLORBAR,BASE:COUNT:기력,MAXBASE:COUNT:기력,8,UNICODE(0x2583),UNICODE(0x2583),BARCOLORSET("青"),RESULT:1, 327685
		PRINTFORM %UNICODE(0x208E)%
	ENDIF
	;조교자 변경 때의 인터페이스
	X = COUNT
	CALL LIFE_LIST_MASTER_CHANGE
	COUNT = X
	PRINTL 
REND
PRINTL 
IF E >= 1
	SETCOLOR 0x8888FF
	PRINTFORM 　　　　%UNICODE(0x27A0)%
	PRINTD :조교자　
	PRINT ☆
	PRINTD :조교중　
	PRINT 조
	PRINTDL :조수를 나타내고 있습니다
	RESETCOLOR
ENDIF

;스테이터스 표시
@SHOW_CHARADATA
R = 0
CALL LIFE_LIST
PRINTFORML  [1000]돌아온다

$INPUT_LOOP
INPUT
IF RESULT == 1000
	RETURN 0
ELSEIF RESULT < 0 || RESULT >= CHARANUM
	GOTO INPUT_LOOP
ENDIF

CALL PRINT_STATUS(RESULT)
RESTART

;-------------------------------------------------
;캐릭터 구입
;-------------------------------------------------
@CHARA_BUY(ARG, ARG:1)
VARSET LOCAL
CALL CHARA_PRICE
IF MASTER == -1
	PRINTL 　★★플레이어를 변경할 수 있습니다★★
	PRINT 　★★캐릭터를 선택해 주세요★★
ELSEIF ARG
	PRINTL 　★★공범자를 추가할 수 있습니다★★
	PRINT 　★★캐릭터를 선택해 주세요★★
ELSEIF ARG:1
	PRINTL 　★★최초로 노예를 1명 받을 수 있습니다★★
	PRINTL 　★★캐릭터를 선택해 주세요★★
ELSE
	PRINTFORML {DAY}일째 \@ TIME ? 야 # 주 \@ 나머지 자금(%MONEY_FORM(MONEY)%고슈금)
	PRINT 　★★구입하는 캐릭터를 선택해 주세요★★
ENDIF
SETCOLOR 0x8888FF
PRINTL 　대사 있어
SETCOLOR 140, 140, 140
DRAWLINE
RESETCOLOR
LOCAL:1 = 0
FOR LOCAL, 1, FLAG:44
	IF EXISTCSV(LOCAL, 0)
		IF ITEMSALES:(199 + LOCAL) == 1 && CHARA_BUY_PRE(LOCAL)
			LOCALS = [{LOCAL}] 
			TRYCCALLFORM SELECTER_K{LOCAL}()
				SETCOLOR 0x8888FF
			CATCH
			ENDCATCH
			LOCALS = %LOCALS%%NATION_OR_H(LOCAL)%
			SIF MASTER > -1 && !ARG && !ARG:1
				LOCALS = %LOCALS%(%MONEY_FORM(ITEMPRICE:(199+LOCAL))%고슈금)
			IF STRLENS(LOCALS) > 27
				LOCAL:1++
				SIF (LOCAL:1 % 3) == 0
					PRINTL 
				PRINTFORM %LOCALS, 54,LEFT%
				PRINT  
				SIF (LOCAL:1 % 3) == 0
					LOCAL:1++
			ELSE
				PRINTFORM %LOCALS,27,LEFT%
			ENDIF
			RESETCOLOR
			LOCAL:1++
			IF LOCAL:1 % 3 == 0
				PRINTL 
			ELSE
				PRINT  
			ENDIF
		ENDIF
	ENDIF
NEXT
SIF LOCAL:1 % 3!= 0
	PRINTL 
SIF (MASTER == -1 || ARG || ARG:1 || MONEY > 1) && LOCAL:1 > 0
	PRINTFORML [998]누구라도 좋은\@ MASTER == -1 || ARG || ARG:1 ?  　　　 # (시가 특가)\@　　　
SETCOLOR 140,140,140
DRAWLINE
RESETCOLOR
IF MASTER == -1
	PRINTL [999]"당신"로 한다(소질 선택 가능)
ELSEIF ARG
	PRINTL [999]공범자는 필요없다
ELSEIF ARG:1
	PRINTL [999]노예는 필요없다
ELSE
	PRINTL [999]돌아온다
ENDIF
$INPUT_LOOP
INPUT
LOCAL = RESULT
;"당신"/돌아온다/필요없다
IF RESULT == 999
	IF MASTER == -1
		MASTER = 0
		CALL ANATA_TALENT_SET(MASTER)
	ENDIF
	CALL CHARA_BUY_AFTER
	RETURN 0
;누구라도 전제 조건 있는 캐릭터도 불린다
ELSEIF RESULT == 998 && (MASTER == -1 || ARG || ARG:1 || MONEY > 1) && LOCAL:1 > 0
	$RANDOMSELECT_LOOP
	LOCAL = 1 + RAND:(FLAG:44)
	SIF LOCAL > 799 || ITEMSALES:(199+LOCAL) != 1
		GOTO RANDOMSELECT_LOOP
	LOCAL:3 = 1
;캐릭터가 없는가 전제 조건을 채우지 않으면 재입력
ELSEIF ITEMSALES:(199+LOCAL) != 1 || !CHARA_BUY_PRE(LOCAL)
	CLEARLINE 1
	GOTO INPUT_LOOP
ENDIF
DRAWLINE
;캐릭터 해설을 부른다
IF RESULT == 998 && (MASTER > -1 && !ARG && !ARG:1)
ELSEIF MASTER == -1 || ARG
	CALL CHARA_MANUAL(LOCAL,2)
ELSE
	CALL CHARA_MANUAL(LOCAL,1)
ENDIF
SIF MASTER > -1 && !ARG && !ARG:1
	PRINTFORML 가격\@ LOCAL:3 ? {MONEY/2} # {ITEMPRICE:(199+LOCAL)} \@고슈금 나머지 자금{MONEY}고슈금
DRAWLINE
PRINT [0] 돌아온다　　　　　　　　
IF MASTER == -1
	PRINTL [9] 플레이어에 선택한다
ELSEIF ARG
	PRINTL [9] 공범자로서 맞아들인다
ELSEIF ARG:1
	PRINTL [9] 이 노예를 갖고 싶다　　　
ELSE
	PRINTFORML [9] \@ LOCAL:3 ? # %NATION_OR_H(LOCAL)%를\@구입한다
ENDIF
$INPUT_LOOP_2
INPUT
IF RESULT == 0
	RESTART
ELSEIF RESULT != 9
	CLEARLINE 1
	GOTO INPUT_LOOP_2
ENDIF
IF !LOCAL:3 && MASTER > -1 && !ARG && !ARG:1 && MONEY < ITEMPRICE:(199+LOCAL)
	PRINTW 돈이 모자랍니다
	PRINTL 
	RESTART
ENDIF
ITEM:(199 + LOCAL) = 1
ADDCHARA LOCAL
CALL CHARANAME_PRINT_NATION
IF MASTER == -1
	DELCHARA 0
	MASTER = 0
	CFLAG:MASTER:1 = 3
	PRINTFORMW 플레이어를 「%NAME:MASTER%」로 설정합니다
	LOCAL:1 = 0
	LOCAL:2 = MASTER
ELSEIF ARG
	ASSI = CHARANUM - 1
	CFLAG:ASSI:1 = 3
	PRINTFORMW 「%NAME:ASSI%」가 공범이 되었습니다
	LOCAL:1 = ARG - 1
	LOCAL:2 = ASSI
	

ELSE
	IF !ARG:1
		IF LOCAL:3
			MONEY -= MONEY / 2
		ELSE
			MONEY -= ITEMPRICE:(199+LOCAL)
		ENDIF
	ENDIF
	SIF MONEY < 0
		MONEY = 0
	TARGET = CHARANUM - 1
	PRINTFORMW 「%NAME:TARGET%」를 노예로서 물러갑니다
	LOCAL:1 = 0
	LOCAL:2 = TARGET
ENDIF
CALL VIRGIN_MODE(LOCAL:2)
ITEMSALES:(199+LOCAL) = 0
CALL CHARA_BUY_AFTER(LOCAL:1,LOCAL:2)
;개체차 처리
CALL DIFF_MAIN(LOCAL:2)

@CHARA_BUY_AFTER(ARG:0, ARG:1)
;즉각함락를 붙이는 처리
SIF FLAG:(1000 + NO:(ARG:1)) & 32
	TALENT:(ARG:1):즉각함락 = 1
;ITEMSALES는 립 더할 수 있는 없음으로 해 둔다(관망중인 것으로, 지우지 않고 comment out)
;REPEAT FLAG:44
;	A = COUNT + 199
;	ITEMSALES:A = 0
;	;여기 TFLAG의 처리 필요없잖아…?
;	;ITEMSALES:COUNT = TFLAG:COUNT
;	;TFLAG:COUNT = 0
;REND
SIF ARG
	CALL CHARA_BUY(ARG)

@CHARA_PRICE
REPEAT FLAG:44
	
	;CSV가 없는 캐릭터는 스킵
	SIF !EXISTCSV(COUNT, 0)
		CONTINUE
	
	;구입이 끝난 캐릭터는 스킵
	;다만 최초의 1회는 ITEMSALES를 대입할 필요가 있으므로, 캐릭터가 「당신」밖에 없는 경우는 스킵 하지 않는다
	;ITEMPRICE:구입 금액
	IF ITEMPRICE:(199+COUNT) == 0 || (ITEMSALES:(199 + COUNT) == 0 && CHARANUM > 1)
		CONTINUE
	ELSEIF ITEMPRICE:(199 + COUNT)
		ITEMSALES:(199 + COUNT) = 1
	ENDIF
REND
;-------------------------------------------------
;PC를 선택식으로 해 본다
;-------------------------------------------------
@START_CHARA_SELECT
K = 0
PRINTFORML ★★플레이어를 설정합니다★★
MASTER = 0
CALL ANATA_TALENT_SET(MASTER)

IF FLAG:5 == 9
	CALL KYOHAN_CHARA_SELECT
	CALL CHARA_BUY(0,1)
ENDIF

;-------------------------------------------------
;공범자를 추가할 수 있도록(듯이)해 본다
;-------------------------------------------------
@KYOHAN_CHARA_SELECT
PRINTFORML ★★공범자를 추가할 수 있습니다★★
PRINTFORML 
IF FLAG:5 == 9
	PRINTL [0] 2 인선
	PRINTL [1] 1 인선
	PRINTL [2] 선택하지 않는다
ELSE
	PRINTL [1] 선택한다
	PRINTL [2] 선택하지 않는다
ENDIF
$INPUT_LOOP
INPUT
IF RESULT == 0
	PRINTFORML -공범자 추가─
	FLAG:42 = 2
	CALL CHARA_BUY(2)
ELSEIF RESULT == 1
	PRINTFORML -공범자 추가─
	FLAG:42 = 1
	CALL CHARA_BUY(1)
ELSEIF RESULT == 2
	FLAG:42 = 0
ELSE 
	GOTO INPUT_LOOP
ENDIF

;-------------------------------------------------
;강해서 뉴 게임해 본다
;-------------------------------------------------
;강해서 뉴 게임의 처리는 여기서 실행
;보너스 포인트로 여러가지 구입한다
@TSUYOKUTE_GAME
PRINTFORML 보너스 포인트를 얻어 뉴 게임을 개시합니다. 좋습니까?
PRINTL [0] 네
PRINTL [1] 아니오

$INPUT_LOOP_TSUYOKUTE
INPUT
IF RESULT == 0
	FLAG:5 = 0
	FLAG:40 = 0
	
	;일자, 시간, 자금, 조교 대상이나 조수의 리셋트
	DAY = 1
	TIME = 0
	MONEY = 30000
	TARGET = -1
	ASSI = -1
	
	;노예 소거 처리
	DELALLCHARA
	
	;플래그 리셋트
	REPEAT 300
		ITEM:COUNT = 0
	REND
	REPEAT 999
		TFLAG:COUNT = 0
	REND
	FLAG:23 = 0
	FLAG:30 = 0
	FLAG:31 = 0
	FLAG:33 = 0
	FLAG:32 = 0
	FLAG:42 = 0
	CALL CHARA_PRICE
	FLAG:500 = 0
	;신주인의 선택
	ADDCHARA 0
	MASTER = -1
	
	;난이도 설정
	CALL START_MODE(1)
	
	;보너스 취득
	Z = FLAG:41
	CALL TSUYOKUTE_BONUS
	
ELSEIF RESULT == 1
	RETURN 1
ELSE
	GOTO INPUT_LOOP_TSUYOKUTE
ENDIF

;-------------------------------------------------
;강해서 뉴 게임의 보너스 취득
;-------------------------------------------------
@TSUYOKUTE_BONUS
IF Z > 0
	PRINTFORML 나머지 보너스 포인트 {Z}
	PRINTL 
	SIF ABL:MASTER:기교 < 10
		PRINTL [0]   - 기교Lv＋3
	PRINTL [1]   - 자금＋30000
	PRINTL [2]   - 공범자를 부른다
	SIF TALENT:MASTER:금지된지식 == 0
		PRINTL [3]   - 【금단의지식】를 몸에 익힌다
	SIF TALENT:MASTER:조제지식 == 0
		PRINTL [4]   - 【조제지식】를 몸에 익힌다
	SIF TALENT:MASTER:새드 == 0
		PRINTL [5]   - 【새드】를 몸에 익힌다
	SIF TALENT:MASTER:마조 == 0
		PRINTL [6]   - 【마조】를 몸에 익힌다
	SIF TALENT:MASTER:수수께끼의매력 == 0 && B > 1
		PRINTL [7]   - 【수수께끼의매력】를 몸에 익힌다(BP2)
	PRINTL [100] - 보너스 취득을 그만둔다
	
	$INPUT_LOOP
	INPUT
	IF RESULT == 0 && ABL:MASTER:기교 < 10
		IF ABL:MASTER:기교 > 7
			ABL:MASTER:기교 = 10
		ELSE
			ABL:MASTER:기교 += 3
		ENDIF
		PRINTFORML %CALLNAME:MASTER%의기교가{ABL:MASTER:기교}:LV가 되었다
		Z -= 1
	ELSEIF RESULT == 1
		MONEY += 30000
		PRINTFORML 소지 코슈금이{MONEY}고슈금이 되었다
		Z -= 1
	ELSEIF RESULT == 2
		PRINTL -공범자 추가─
		FLAG:42 += 1
		Z -= 1
		CALL CHARA_BUY(1)
	ELSEIF RESULT == 3 && TALENT:MASTER:금지된지식 == 0
		PRINTFORML %CALLNAME:MASTER%는【금단의지식】를 몸에 익혔다
		TALENT:MASTER:금지된지식 = 1
		Z -= 1
	ELSEIF RESULT == 4 && TALENT:MASTER:조제지식 == 0
		PRINTFORML %CALLNAME:MASTER%는【조제지식】를 몸에 익혔다
		TALENT:MASTER:조제지식 = 1
		Z -= 1
	ELSEIF RESULT == 5 && TALENT:MASTER:새드 == 0
		PRINTFORM %CALLNAME:MASTER%는
		IF TALENT:MASTER:마조
			TALENT:MASTER:마조 = 0
			PRINT 【마조】를 잃어
		ENDIF
		PRINTL 【새드】를 몸에 걸쳤다
		TALENT:MASTER:새드 = 1
		Z -= 1
	ELSEIF RESULT == 6 && TALENT:MASTER:마조 == 0
		PRINTFORM %CALLNAME:MASTER%는
		IF TALENT:MASTER:새드
			TALENT:MASTER:새드 = 0
			PRINT 【새드】를 잃어
		ENDIF
		PRINTL 【마조】를 몸에 걸쳤다
		TALENT:MASTER:마조 = 1
		Z -= 1
	ELSEIF RESULT == 7 && TALENT:MASTER:수수께끼의매력 == 0 && B > 1
		PRINTFORML %CALLNAME:MASTER%는【수수께끼의매력】를 몸에 익혔다
		TALENT:MASTER:수수께끼의매력 = 1
		Z -= 2
	ELSEIF RESULT == 100
		PRINTL 보너스 취득을 그만두어 게임을 개시합니다. 좋습니까?
		PRINTL [1] 네
		PRINTL [2] 아니오
		$INPUT_LOOP_2
		INPUT
		IF RESULT == 1
			Z = 0
		ELSEIF RESULT == 2
		ELSE
			GOTO INPUT_LOOP_2
		ENDIF
	ELSE
		GOTO INPUT_LOOP
	ENDIF
	
	CALL TSUYOKUTE_BONUS
ENDIF

