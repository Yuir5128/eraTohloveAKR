;=============================================================================
;매각후에피소드 관련의 함수
;=============================================================================
;엔딩시에 매각한 캐릭터의 에피소드를 흘린다
;엔딩후에 불린다. 돌아온후에 게임 종료 OR 강해서 뉴 게임
;-------------------------------------------------
;엔딩후에피소드를 표시시킬까 선택
;-------------------------------------------------
@SHOW_AFTERWARDS
;FLAG:500 =에피소드를 표시하는 노예수
;한사람도 에피소드가 없으면 아무것도 하지 않고 종료
SIF FLAG:500 == 0
	RETURN 0

;선택지
PRINTFORML 매각된%CALLNAME%의 그후를……
PRINTC [0]본다
PRINTC [1]보지 않는다
$INPUT_LOOP
INPUT
IF RESULT == 1
	GOTO AFTERWARDS_RESET
ELSEIF RESULT != 0
	GOTO INPUT_LOOP
ENDIF

;기록할 수 있는 인원수를 넘었을 경우,낡은 것으로부터 순서에 표시하도록(듯이)하고 싶었던 것 같다
A = FLAG:500
IF A > 10
	A = 10
	W:1 = FLAG:500 % 10
ELSE
	W:1 = 0
ENDIF

REPEAT A
	W = 501 + (COUNT + W:1) % 10
	X = FLAG:W % 1000
	Z = FLAG:W / 1000
	CALL SHOW_SLAVE_AFTERWARDS
REND

$AFTERWARDS_RESET
;에피소드 플래그 소거
W = 501
REPEAT 10
	FLAG:W = 0
	W += 1
REND
;에피소드를 표시하는 노예수리셋트
FLAG:500 = 0
RETURN 0

;-------------------------------------------------
;매각시의 처리
;-------------------------------------------------
@SET_AFTERWARDS
;매각 캐릭터 에피소드를 세트
X = NO:TARGET
Z = 0
CALL SET_SLAVE_AFTERWARDS
;표시하지 않는 경우는 기록하지 않는다
SIF Z == 0
	RETURN 0
;%10+581의 의미:에피소드 관련으로 사용하는 변수가 FLAG:501에서 10 인분이니까
Y:0 = FLAG:500 % 10 + 501
Y:1 = Z * 1000 + NO:TARGET
FLAG:Y = Y:1
;에피소드를 표시하는 노예수를 카운트
FLAG:500 += 1

RETURN 0

