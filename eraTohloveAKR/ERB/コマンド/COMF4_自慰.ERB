;-------------------------------------------------
;자위
;자위계 커맨드
;-------------------------------------------------
@COM4

;바이브, 비디오촬영때에 내용 변화
;비디오촬영중이라면공개자위
;야외플레이중이라면 야외 자위
;샤워사용중이라면샤워자위
;바이브장비로바이브자위
;오나홀 장비로 오나홀 자위
LOCALS = 

SIF TEQUIP:비디오카메라
	LOCALS =공개
SIF TEQUIP:야외플레이
	LOCALS =야외

IF TEQUIP:바이브
	LOCALS = %LOCALS%바이브
	TFLAG:56 = 1
ELSEIF TEQUIP:로터삽입
	LOCALS = %LOCALS%로터
	TFLAG:56 = 1
ELSEIF TEQUIP:전동오나홀
	LOCALS = %LOCALS%오나홀
	TFLAG:56 = 0
ELSEIF TEQUIP:샤워
	LOCALS =  %LOCALS%샤워
	TFLAG:56 = 0
ELSE
	;페니스가 있어, 원이남자라면페니스를 우선해 사용한다
	IF PENIS(TARGET) && MALE2(TARGET)
		TFLAG:56 = 0
	;바기나를 사용할 수 있는 경우는 손가락을 넣는다
	ELSEIF (!TALENT:처녀 && VAGINA(TARGET)) || EXP:V경험 >= 20
		TFLAG:56 = 1
	ELSE
		TFLAG:56 = 0
	ENDIF
ENDIF

IF TEQUIP:비디오카메라 || TEQUIP:야외플레이 || TEQUIP:바이브 || TEQUIP:로터삽입 || TEQUIP:전동오나홀 || TEQUIP:샤워
	LOCALS = %LOCALS%자위
ELSE
	LOCALS = %LOCALS%자위
ENDIF

STR:0 = %LOCALS%
PRINTFORML %STR:0%

;-------------------------------------------------
;실행할 수 있을까의 판정
;-------------------------------------------------
;모든 명령에 공통의 요소를 고려
;(순종가 높다고 명령에 따르기 쉬운 등)
CALL COM_ORDER

;실행 판정 조작
CALL KOJO_DENIAL_PLUS

;합계를 표시(33이상으로 실행)
PRINT  = 
PRINTV A

;난이도 상승
;공개로+10, 바이브로+5, 샤워로＋3
V = 33
SIF TEQUIP:비디오카메라
	V += 10
SIF TEQUIP:야외플레이
	V += 10

SIF TEQUIP:바이브
	V += 5
SIF TEQUIP:로터삽입
	V += 3
SIF TEQUIP:전동오나홀
	V += 3
SIF TEQUIP:샤워
	V += 3

SIF A < V
	PRINT  < 
SIF A == V
	PRINT  = 
SIF A > V
	PRINT  > 
PRINT 실행치
PRINTV V

;실행 불가대사/실행 판정 조작
SIF A < V
	CALL KOJO_DENIAL

WAIT

;실행 할 수 없다
SIF A < V
	RETURN 0

CALL KOJO_MESSAGE_COM


;실행자가 노예인 플래그
TFLAG:28 = 1

;스타킹 찌릿찌릿 플래그
IF TEQUIP:특수2 == 5 || TEQUIP:특수2 == 6
	TFLAG:55 = 1
ENDIF


;-------------------------------------------------
;소스 추가 처리
;-------------------------------------------------
;소비체력, 기력
LOSEBASE:체력 += 10
LOSEBASE:기력 += 50

;기본의 쾌감
SOURCE:쾌C += 80
SIF TFLAG:56 == 1
	SOURCE:쾌V += 80

IF TEQUIP:바이브 || TEQUIP:로터삽입
	TIMES SOURCE:쾌C , 0.70

	SIF TEQUIP:바이브
		TIMES SOURCE:쾌V , 1.50
	SIF TEQUIP:로터삽입
		TIMES SOURCE:쾌V , 1.20

	SIF TEQUIP:바이브
		SOURCE:통증 += 50
	SIF TEQUIP:로터삽입
		SOURCE:통증 += 20

	;V에의고통는 먼저 처리해 둔다
	;EXP:V경험를 본다(처녀의반감는 별도 처리필 보고인 것으로 생략)
	IF EXP:V경험 < EXPLV:1
		TIMES SOURCE:통증, 2.50
	ELSEIF EXP:V경험 < EXPLV:2
		TIMES SOURCE:통증, 0.80
		TIMES SOURCE:반발, 1.20
	ELSEIF EXP:V경험 < EXPLV:3
		TIMES SOURCE:통증, 0.40
	ELSEIF EXP:V경험 < EXPLV:4
		TIMES SOURCE:통증, 0.10
	ELSEIF EXP:V경험 < EXPLV:5
		TIMES SOURCE:통증, 0.00
	ELSEIF EXP:V경험 >= EXPLV:5
		TIMES SOURCE:통증, 0.00
	ENDIF
	
	;PALAM:윤활를 본다
	IF PALAM:윤활 < PALAMLV:1
		TIMES SOURCE:통증, 1.20
		TIMES SOURCE:반발, 1.50
	ELSEIF PALAM:윤활 < PALAMLV:2
		TIMES SOURCE:통증, 0.70
		TIMES SOURCE:반발, 1.20
	ELSEIF PALAM:윤활 < PALAMLV:3
		TIMES SOURCE:통증, 0.40
	ELSEIF PALAM:윤활 < PALAMLV:4
		TIMES SOURCE:통증, 0.10
	ELSEIF PALAM:윤활 >= PALAMLV:4
		TIMES SOURCE:통증, 0.10
	ENDIF

	SOURCE:굴복 += 100
	SOURCE:반발 += 100
ELSEIF TEQUIP:전동오나홀
	TIMES SOURCE:쾌C , 1.50
ELSEIF TEQUIP:샤워
	TIMES SOURCE:쾌C , 1.20
ENDIF

SOURCE:성행동 += 100
SOURCE:두려움 += 200
SOURCE:중독충족 += 200
SOURCE:노출 += 400
SOURCE:굴복 += 500
SOURCE:일탈 += 300
SOURCE:반발 += 250

;비디오 or야외
IF TEQUIP:비디오카메라 || TEQUIP:야외플레이
	SOURCE:액체 += 50
	SOURCE:욕정 += 100
	SOURCE:반발 += 200
ENDIF

;-------------------------------------------------
;특수소스 처리
;-------------------------------------------------
;중독에 의한중독충족의 소스는 먼저 처리해 둔다
;자위중독를 본다
IF ABL:자위중독 == 1
	TIMES SOURCE:쾌C , 1.00
	TIMES SOURCE:쾌V , 1.00
	TIMES SOURCE:중독충족, 0.00
ELSEIF ABL:자위중독 == 2
	TIMES SOURCE:쾌C , 1.10
	TIMES SOURCE:쾌V , 1.10
	TIMES SOURCE:중독충족, 0.30
ELSEIF ABL:자위중독 == 3
	TIMES SOURCE:쾌C , 1.20
	TIMES SOURCE:쾌V , 1.20
	TIMES SOURCE:중독충족, 0.60
ELSEIF ABL:자위중독 == 4
	TIMES SOURCE:쾌C , 1.30
	TIMES SOURCE:쾌V , 1.30
	TIMES SOURCE:중독충족, 0.90
ELSEIF ABL:자위중독 == 5
	TIMES SOURCE:쾌C , 1.50
	TIMES SOURCE:쾌V , 1.50
	TIMES SOURCE:중독충족, 1.20
ELSEIF ABL:자위중독 == 6
	TIMES SOURCE:쾌C , 1.70
	TIMES SOURCE:쾌V , 1.70
	TIMES SOURCE:중독충족, 1.50
ELSEIF ABL:자위중독 == 7
	TIMES SOURCE:쾌C , 2.00
	TIMES SOURCE:쾌V , 2.00
	TIMES SOURCE:중독충족, 1.80
ELSEIF ABL:자위중독 == 8
	TIMES SOURCE:쾌C , 2.30
	TIMES SOURCE:쾌V , 2.30
	TIMES SOURCE:중독충족, 2.30
ELSEIF ABL:자위중독 == 9
	TIMES SOURCE:쾌C , 2.50
	TIMES SOURCE:쾌V , 2.50
	TIMES SOURCE:중독충족, 3.00
ELSEIF ABL:자위중독 >= 10
	TIMES SOURCE:쾌C , 3.50
	TIMES SOURCE:쾌V , 3.50
	TIMES SOURCE:중독충족, 5.00
ENDIF

;억압, 저항소유에는, 억울소스 추가
SIF TALENT:억압 || TALENT:저항
	SOURCE:울굴 += 400

;-------------------------------------------------
;더러움의 처리
;-------------------------------------------------
;노예의손(장갑)⇔노예의 CorVor질내
;바이브, 로터, 오나홀을 사용하고 있을 때는 더러움은 어디에도 이동하지 않는다
IF TEQUIP:바이브 || TEQUIP:로터삽입 || TEQUIP:전동오나홀
;샤워자위의 경우 더러움을 리셋트, 윤활 더욱 반.
ELSEIF TEQUIP:샤워
	STAIN:손 = 0
	STAIN:페니스 = 2
	STAIN:바기나 = 1
	STAIN:질내 = 1
	STAIN:애널 = 8
	STAIN:발 = 0
	STAIN:장갑 = 256
	STAIN:전신상의 = 256
	STAIN:하의 = 256
	STAIN:속옷（아래） = 256
	STAIN:발장식 = 256
	STAIN:신발 = 256
	PALAM:윤활 /= 2
ELSE
	CALL COM_STAIN
ENDIF

;-------------------------------------------------
;경험의 처리
;-------------------------------------------------
IF TFLAG:56 == 1
	EXP:V경험 += 1
	PRINTL V경험＋1
ENDIF

;비디오촬영시는 경험에 플러스
IF TEQUIP:비디오카메라
	EXP:자위경험 += 2
	PRINTL 자위경험＋2
	EXP:조교자위경험 += 2
	PRINTL 조교자위경험＋2
	
	IF !EXP:공개자위 && !TALENT:음란 && !TALENT:창부
		EXP:이상경험 += 1
		PRINTL 공개자위첫경험으로이상경험＋1
	ENDIF
	EXP:공개자위 += 1
	PRINTL 공개자위경험＋1
ELSE
	EXP:자위경험 += 1
	PRINTL 자위경험＋1
	EXP:조교자위경험 += 1
	PRINTL 조교자위경험＋1
ENDIF

;촬영시의 조교 내용 체크
IF TEQUIP:바이브 || TEQUIP:로터삽입
	TFLAG:90 = 991
ELSEIF TEQUIP:샤워 && !PENIS(TARGET)
	TFLAG:90 = 994
ELSE
	TFLAG:90 = 0
ENDIF

;게이경험라든지 가산
CALL COM_EXP(3)

;의존도 벡터
TFLAG:33 = 3

;의존도 보정치(복종로 1, 예속로 2)
IF TALENT:복종 && ASSIPLAY == 0
	TFLAG:30 += 1
ELSEIF TALENT:예속 && ASSIPLAY == 0
	TFLAG:30 += 2
ENDIF


TFLAG:56 = 0

RETURN 1


