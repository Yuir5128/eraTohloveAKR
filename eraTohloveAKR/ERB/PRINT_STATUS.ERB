;-------------------------------------------------
;스테이터스 표시
;-------------------------------------------------
;1 P, 이름, 체력기력, 소질, 연모·공포 대상, 상실상대
@PRINT_STATUS(ARG,ARG:1)
DRAWLINE
PRINTFORM ■ 
SIF TALENT:ARG:붕괴
	PRINTFORM ＜접혔다＞
PRINTFORML %NAME:ARG%의 스테이터스 ■
IF CFLAG:ARG:1 < 3
	;조교자 변경 대응
	LOCAL:1 = 0
	FOR LOCAL, 0, CHARANUM
		SIF CFLAG:LOCAL:40 && CFLAG:LOCAL:41 == NO:ARG
			LOCAL:1++
	NEXT
	SIF LOCAL:1 > 0
		PRINTFORM (떨어뜨린 인원수:{LOCAL:1}명)　
	SIF TALENT:MASTER:천리안
		PRINTFORML (의존도:{CFLAG:ARG:2} 스트레스{CFLAG:ARG:10} 금욕치{CFLAG:ARG:602})
	SIF LOCAL:1 > 0 && !TALENT:MASTER:천리안
		PRINTL 
	VARSET T
ELSEIF ARG == MASTER
	;조교자 변경 대응
	LOCAL:1 = 0
	FOR LOCAL, 0, CHARANUM
		SIF CFLAG:LOCAL:40 && CFLAG:LOCAL:41 == NO:ARG
			LOCAL:1++
	NEXT
	PRINTFORM (떨어뜨린 인원수:{LOCAL:1}명)　
	IF FLAG:4
		PRINT 난이도:
		IF FLAG:4 == 1
			PRINTL 역
		ELSEIF FLAG:4 == 2
			PRINTL 보통
		ELSEIF FLAG:4 == 3
			PRINTL 난
		ENDIF
	ELSE
		PRINTL 
	ENDIF
ENDIF
PRINTFORM 체력%UNICODE(0x208D)%
CALL PRINT_COLORBAR,BASE:ARG:체력,MAXBASE:ARG:체력,32,UNICODE(0x2583),UNICODE(0x2583),BARCOLORSET("赤"),RESULT:1
PRINTFORM %UNICODE(0x208E)%
PRINTFORML  {BASE:ARG:체력}/{MAXBASE:ARG:체력} 

PRINTFORM 기력%UNICODE(0x208D)%
CALL PRINT_COLORBAR,BASE:ARG:기력,MAXBASE:ARG:기력,32,UNICODE(0x2583),UNICODE(0x2583),BARCOLORSET("紺"),RESULT:1
PRINTFORM %UNICODE(0x208E)%
PRINTFORML  {BASE:ARG:기력}/{MAXBASE:ARG:기력} 
PRINTL 
;조교자 변경 확장 대응
IF CFLAG:ARG:40
	R = CFLAG:ARG:41
	M = NO2(R)
	IF KANRAKU(ARG) == 1 || KANRAKU(ARG) == 4
		PRINT □ 사모
	ELSEIF KANRAKU(ARG) == 3 || KANRAKU(ARG) == 5
		PRINT □ 공포
	;공범자의 집착 대상 설정이 유효
	ELSEIF CFLAG:ARG:1 == 3
		;적어도 상대가연모계통에 기울고 있어, 한편 사모 대상이 그 상호일 때
		IF CFLAG:M:41 == NO:ARG && CFLAG:M:2 > 1000
			PRINT ■ 사모
		;일방통행일지도 모른다
		ELSE
			PRINT ■ 집착
		ENDIF
	ENDIF
	PRINTFORML 대상… %NAME:M%
ENDIF
CALL NEW_PRINT_TALENT(ARG)
CALL NEW_PRINT_EXP(ARG)
CALL NEW_PRINT_ABL(ARG)
PRINTL □ 각인 □---------------------------------------------------------------------
CALL SHOW_INFO_MARK(ARG)
PRINTL 
PRINTL 　　　　　　　　　　[100] - 돌아오는[102] - 차의 페이지
$INPUT_LOOP
INPUT
IF RESULT != 100 && RESULT != 102
	CLEARLINE 1
	GOTO INPUT_LOOP
ENDIF
SIF RESULT == 102
	CALL PRINT_STATUS2(ARG)
SIF RESULT == 100
	RETURN 0
RESTART

;2 P째, 경험, 능력, 구슬, 각인
@PRINT_STATUS2(ARG)
PRINTL 
PRINTL 
PRINTL 
CALL SHOW_INFO_JUEL(ARG)
PRINTL 
CALL PRINT_LOSTVIRGIN(ARG)
CALL PRINT_RELATION(ARG)
CALL PRINT_HITYOUKYOU(ARG)
PRINTL [101] - 전의 페이지 [100] - 돌아온다
$INPUT_LOOP
INPUT
IF RESULT != 100 && RESULT != 101
	CLEARLINE 1
	GOTO INPUT_LOOP
ENDIF
RETURN RESULT

;3 P째, 떨어뜨린 상대


@PRINT_LOSTVIRGIN(ARG)
PRINTL □ 처녀계 소질 상실상대 □-------------------------------------------------------
PRINTFORM %CALLNAME:ARG,16,RIGHT%의 처녀상실상대:
STRLENS CSTR:ARG:10
IF TALENT:ARG:처녀 && VAGINA(ARG)
	PRINTFORML %CALLNAME:ARG%는처녀입니다
ELSEIF RESULT
	PRINTFORML %CSTR:ARG:10%
ELSE
	PRINTL ――――――――――――
ENDIF
PRINTFORM 　　　　　　　　　동정상실상대:
STRLENS CSTR:ARG:11
IF TALENT:ARG:동정 && PENIS(ARG)
	PRINTFORML %CALLNAME:ARG%는동정입니다
ELSEIF RESULT
	PRINTFORML %CSTR:ARG:11%
ELSE
	PRINTL ――――――――――――
ENDIF
PRINTFORM 　　　　백버진상실상대:
STRLENS CSTR:ARG:12
IF TALENT:ARG:백버진
	PRINTFORML %CALLNAME:ARG%는백버진입니다
ELSEIF RESULT
	PRINTFORML %CSTR:ARG:12%
ELSE
	PRINTL ――――――――――――
ENDIF
IF TALENT:ARG:낙인
	STRLENS CSTR:ARG:1
	SIF RESULT
		PRINTFORML 　　　　%CALLNAME:ARG%에낙인를 남긴 것은 %CSTR:ARG:1%입니다
ENDIF
IF (CFLAG:ARG:112 + CFLAG:ARG:113) > 0
	PRINTL 
	PRINTL □ 자 □-----------------------------------------------------------------------
	IF CFLAG:ARG:112
		PRINTFORML 　　　출산한아이의 수:{CFLAG:ARG:112}명
	ENDIF
	IF CFLAG:ARG:113
		PRINTFORML 　　　임신하게 한아이의 수:{CFLAG:ARG:113}명
	ENDIF
	IF STRLENS(CSTR:ARG:13)
		PRINTFORML %CALLNAME:ARG,16,RIGHT%의아이：%CSTR:ARG:13%
	ENDIF
ENDIF
IF STRLENS(CSTR:ARG:3)
	PRINTL 
	PRINTL □ 부모 □-----------------------------------------------------------------------
	PRINTFORM %CALLNAME:ARG, 16, RIGHT%의 부친:
	IF STRLENS(CSTR:ARG:2)
		PRINTFORML %CSTR:ARG:2%
	ELSE
		PRINTL 불명
	ENDIF
	PRINTFORML 　　　　　　　　　모친:%CSTR:ARG:3%
	IF STRLENS(CSTR:ARG:4) || STRLENS(CSTR:ARG:5)
		PRINTFORM 　　　　　　　　아버지의 아버지:
		IF STRLENS(CSTR:ARG:4)
			PRINTFORML %CSTR:ARG:4%
		ELSE
			PRINTL 불명
		ENDIF
		PRINTFORML 　　　　　　　　아버지의 어머니:%CSTR:ARG:5%
	ENDIF
	IF STRLENS(CSTR:ARG:6) || STRLENS(CSTR:ARG:7)
		PRINTFORM 　　　　　　　　어머니의 아버지:
		IF STRLENS(CSTR:ARG:6)
			PRINTFORML %CSTR:ARG:6%
		ELSE
			PRINTL 불명
		ENDIF
		PRINTFORML 　　　　　　　　어머니의 어머니:%CSTR:ARG:7%
	ENDIF
ENDIF
PRINTL 

@PRINT_HITYOUKYOU(ARG)
VARSET LOCAL
PRINTL □ 조교경험 □-----------------------------------------------------------------
FOR LOCAL:1, 0, CHARANUM
	LOCAL = NO:ARG + 1099
	IF CFLAG:(LOCAL:1):LOCAL
		PRINTFORM %NATION_OR_H(NO:(LOCAL:1)), 16%：{CFLAG:(LOCAL:1):LOCAL,6}
		IF (LOCAL:2 % 3) == 2
			PRINTL 
		ELSE
			PRINT 　
		ENDIF
		LOCAL:2++
	ENDIF
NEXT
SIF (LOCAL:2 % 3) != 0
	PRINTL 
PRINTL 
LOCAL:2 = 0
PRINTL □ 피조교경험 □---------------------------------------------------------------
FOR LOCAL:1, 0, 500
	LOCAL = LOCAL:1 + 1099
	IF CFLAG:ARG:LOCAL
		PRINTFORM %NATION_OR_H(LOCAL:1), 16%：{CFLAG:ARG:LOCAL,6}
		IF (LOCAL:2 % 3) == 2
			PRINTL 
		ELSE
			PRINT 　
		ENDIF
		LOCAL:2++
	ENDIF
NEXT
SIF (LOCAL:2 % 3) != 0
	PRINTL 
PRINTL 

@NEW_PRINT_EXP(ARG)
PRINTL □ 경험 □---------------------------------------------------------------------
;표시순서의 설정
; 0,　　　V경험　 1,　　　A경험　 2,　　절정경험　 3,　　사정경험
; 4,　　성교경험　 5,　　질내사정경험　 6,　　　U경험　 7,　　　M경험
;10,　　자위경험 11, 조교자위경험 12, 공개자위 20,　　정액경험
;21, 봉사쾌락경험 22, 펠라경험 23,　　애정경험 24, 정음절정경험
;25, 허공사정경험 26, V사정경험 27, A사정경험 28, B사정경험
;30, 고통쾌락경험 31,　　배뇨경험 32, 가학쾌락경험 33,　　스캇경험
;34,　　기절경험 35,　　관장경험 36, 야외방뇨경험 37,　　식분경험
;38,　　전극경험 39, 채내배뇨경험 40,　　레즈경험 41,　　게이경험
;42, 강제정음절정 43,　　윤간경험 49, U확장경험 50,　　이상경험
;51,　　속박경험 52, V확장경험 53, A확장경험 54,　　분유경험
;55,　　촉수경험 56,　　수간경험 57, 뱀장어탕경험 58, 곤충탕경험
;59,　　산란경험 60,　　출산경험 61,　　가사경험 62,　　촬영경험
;63,　　피사경험 64,　　가창경험 65, 창관봉공경험 70,　　조교경험
LOCAL:2 = 0
FOR LOCAL, 0, 100
	;[공]때 VA관련 경험은 비표시
	IF TALENT:ARG:공
		SELECTCASE LOCAL
			CASE 0,1,26,27,52,53
				CONTINUE
		ENDSELECT
	ENDIF
	IF EXP:ARG:LOCAL
		PRINTFORM %EXPNAME:LOCAL, 12%:{EXP:ARG:LOCAL, 5}
		LOCAL:2++
		IF LOCAL:2 % 4 == 0
			PRINTL 
		ELSE
			PRINT 　
		ENDIF
	ENDIF
NEXT
;최후의 루프로 개행하고 있지 않았으면 개행
SIF LOCAL:2 % 4 != 0
	PRINTL 

@NEW_PRINT_ABL(ARG)
PRINTL □ 능력 □---------------------------------------------------------------------
;표시순서의 설정
; 0,　　순종　 1,　　욕망　 2,　　기교　 7, 봉사정신
; 3, C감각　 4, V감각　 5, A감각　 6, B감각
; 8, 노출벽 12, 요리기능 13, 촬영기능 14, 가창기능
; 9, 마조끼 10, 새드끼 11, 레즈끼 20, 게이끼
;15, 자위중독 16, 정액중독 17, 레즈중독 18, 섹스중독
;19, 착유중독 21, 게이중독 22, 스캇중독　
LOCAL:2 = 0
FOR LOCAL, 0, 23
	;[공]때 VA관련 능력은 비표시
	IF TALENT:ARG:공
		SELECTCASE LOCAL
			CASE 4, 5
				CONTINUE
		ENDSELECT
	ENDIF
	IF ABL:ARG:LOCAL
		PRINTFORM 　%ABLNAME:LOCAL,12%Lv{ABL:ARG:LOCAL,2}
		LOCAL:2++
		IF LOCAL:2 % 4 == 0
			PRINTL 
			LOCAL:2 = 0
		ELSE
			PRINT 　
		ENDIF
	ENDIF
NEXT
;최후의 루프로 개행하고 있지 않았으면 개행
SIF LOCAL:2 % 4 != 0
	PRINTL 


@NEW_PRINT_TALENT(ARG)
PRINTL □ 소질 □---------------------------------------------------------------------
;SIF FLAG:2002
;	PRINTFORML %PERSONA(ARG)%
;통상 표시
;IF !FLAG:2003
;	LOCAL:1 = 0
;	FOR LOCAL, 0, 300
;		;비표시 소질
;		SIF !TALENT_TYPE(LOCAL, ARG)
;			CONTINUE
;		IF TALENT:ARG:LOCAL
;			LOCALS = %GET_TALENTNAME(ARG, LOCAL)%
;			LOCAL:2 = STRLENS(LOCALS)
;			LOCAL:1 += LOCAL:2 + 3
;			;그다지 채우지 않는다
;			IF LOCAL:1 > 70
;				PRINTL 
;				LOCAL:1 = LOCAL:2 + 3
;			ENDIF
;			PRINTFORM [%LOCALS%] 
;		ENDIF
;	NEXT
;	PRINTL 
;;소질 정돈 표시
;ELSE
	;루프를 200×n회윤간의 것은 과연 조금 무거운 것이 아닌가… 라고 생각했으므로 이런 방식에.
	;TALENT는 사용해도 겨우 200종 정도라고 생각하므로 소지하고 있는 번호만을, 조금 강행인손입를 사용해 LOCAL만으로 보유.
	;지금의 방식이라면 각 타입에 대해 99종 밖에 취급할 수 없지만, 그 이전에 소질을 99종 가지는 것도 어렵다고 생각하므로 반드시 괜찮아.
	;안되게 되면 안되게 되었을 때에 어떻게든 해 주세요. 덧붙여 현재의 사양에서는 소질 분류는 9까지 밖에 취급할 수 없습니다.
	
	;LOCAL 초기화
	VARSET LOCAL
	
	;소질 분류 처리의 루프
	;LOCAL:0		루프 카운터(소질 번호)
	;LOCAL:1		소질 분류 번호
	;LOCAL:X00		그 계통의 TALENT를 몇개 보유하고 있을까(X=소질 분류 번호)
	;LOCAL:X01~X99	소지하고 있는 TALENT의 번호를 순서에 격납해 나간다(X=소질 분류 번호)
	FOR LOCAL, 0, 300
		;소질이 없으면 처리하지 않는다. 쓸데없게 함수 부르는 것도 싫은 것으로
		SIF !TALENT:ARG:LOCAL
			CONTINUE
		LOCAL:1 = TALENT_TYPE(LOCAL, ARG)
		;비표시는 표시하지 않는다. 성별은 별처리를 위해 여기에서는 취급하지 않는다
		SIF LOCAL:1 > 1
			LOCAL:(100 * LOCAL:1 + 1 + LOCAL:(100 * LOCAL:1)++) = LOCAL
	NEXT
	
	;성별은 특수한 것으로 별처리. 여기서 표시한다
	PRINTFORML %TALENT_TYPENAME(1)%：[%GET_SEX(ARG)%]\@ TALENT:ARG:처녀 && VAGINA(ARG) ? %" "%[%GET_TALENTNAME(ARG, 0)%] # \@\@ TALENT:ARG:동정 && PENIS(ARG) ? %" "%[%GET_TALENTNAME(ARG, 160)%] # \@\@ TALENT:ARG:백버진 ? %" "%[%GET_TALENTNAME(ARG, 161)%] # \@\@ TALENT:ARG:수 ? %" "%[%GET_TALENTNAME(ARG, 157)%] # \@\@ TALENT:ARG:공 ? %" "%[%GET_TALENTNAME(ARG, 158)%] # \@\@ TALENT:ARG:오메가 ? %" "%[%GET_TALENTNAME(ARG, 170)%] #  \@
	
	;표시의 루프
	;LOCAL:0	루프 카운터 1(소질 분류 번호)
	;LOCAL:1	처리중의 행 오프셋(offset)(자리수 위치)
	;LOCAL:2	타이틀 부분의 문자수( 『00:』의 부분)
	;LOCAL:3	루프 카운터 2(그 만큼류의 소지 소질수만큼 루프)
	;LOCAL:4	소질명의 길이
	;LOCAL:5	그 만큼류의 소질 소지수가 격납되고 있는 LOCAL의 번호
	;LOCALS:0	범용의 캐릭터 라인 격납역(소질 분류명/소질명)
	FOR LOCAL, 2, 8
		;소질 분류 번호를 배열의 첨자에 변환
		LOCAL:5 = LOCAL * 100
		;그 만큼류의 소질을 1개도 소지하고 있지 않으면아무것도안한다
		SIF !LOCAL:(LOCAL:5)
			CONTINUE
		;소질 분류명을 취득→출력
		LOCALS = %TALENT_TYPENAME(LOCAL)%
		PRINTFORM %LOCALS%:
		LOCAL:1 = 0
		LOCAL:2 = STRLENS(LOCALS) + 2
		;소질을 소지하고 있는 수만큼 루프
		FOR LOCAL:3, 0, LOCAL:(LOCAL:5)
			;소질명을 취득
			LOCALS = %GET_TALENTNAME(ARG, LOCAL:(LOCAL:5 + 1 + LOCAL:3))%
			LOCAL:4 = STRLENS(LOCALS)
			LOCAL:1 += LOCAL:4 + 3
			;개행 처리
			IF LOCAL:1 > 78 - LOCAL:2
				PRINTL 
				PRINTS " " * LOCAL:2
				LOCAL:1 = LOCAL:2 + 3 + LOCAL:4
			ENDIF
			;소질명의 출력
			PRINTFORM [%LOCALS%] 
		NEXT
		PRINTL 
	NEXT
;ENDIF

@PRINT_RELATION(ARG)
PRINTL □ 궁합 □---------------------------------------------------------------------
LOCAL:1 = 0
FOR LOCAL, 0, 500
	IF RELATION:(ARG):LOCAL != 0 && RELATION:(ARG):LOCAL != 100
		PRINTFORM 　%NATION_OR_H(LOCAL), 16%×
		IF RELATION:(ARG):LOCAL > 100
			SETCOLOR 0xcc7eb1
		ELSE
			SETCOLOR 0x8491c3
		ENDIF
		PRINTFORM {RELATION:(ARG):LOCAL, 3}
		RESETCOLOR
		PRINT %
		IF (LOCAL:1 % 3) == 2
			PRINTL 
		ELSE
			PRINT 　
		ENDIF
		LOCAL:1++
	ENDIF
NEXT
SIF !LOCAL:1
	PRINTL 　특별히 이루어
SIF (LOCAL:1 % 3) != 0
	PRINTL 
PRINTL 

